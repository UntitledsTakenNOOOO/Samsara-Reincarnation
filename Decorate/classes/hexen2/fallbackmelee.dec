ACTOR Hexen2Sickle : Weapon
{
  Weapon.SelectionOrder 1500
  Weapon.Kickback 100
  Weapon.SlotNumber 0
  Weapon.BobRangeX 0
  Weapon.BobRangeY 0.75
  Weapon.BobSpeed 1.4
  Weapon.BobStyle Alpha
  Inventory.RestrictedTo "Demoness"
  Tag "Sickle"
  +WEAPON.WIMPY_WEAPON
  +WEAPON.MELEEWEAPON
  +WEAPON.NOALERT
  +INVENTORY.UNDROPPABLE
  States
  {
	Spawn:
	  HX00 A -1
	  Stop
	Ready:
	  HX00 A 0 A_JumpIfInventory("Hexen2PowerWeaponLevel2",1,"ReadyPowered")
	  HX00 A 0 A_JumpIfInventory("SamsaraHasPermaWeaponLevel2",1,"ReadyPowered")
	  HX04 JIHGFEDCBA 1
	  Goto WeaponIdle
	ReadyPowered:
	  HX04 JIHGFEDCBA 2 Bright
	  Goto WeaponIdle
	WeaponIdle:
	  HX00 A 0 A_JumpIfInventory("Hexen2PowerWeaponLevel2",1,"WeaponIdlePowered")
	  HX00 A 0 A_JumpIfInventory("SamsaraHasPermaWeaponLevel2",1,"WeaponIdlePowered")
	  HX00 A 1 A_WeaponReady
      Loop
	WeaponIdlePowered:
	  HX00 A 1 Bright A_WeaponReady
      Goto WeaponIdle
	Select:
	  TNT1 A 0 A_GiveInventory("Hexen2Weapon1",1)
	  TNT1 A 0 A_TakeInventory("Hexen2Weapon2",1)
	  TNT1 A 0 A_TakeInventory("Hexen2Weapon3",1)
	  TNT1 A 0 SetPlayerProperty(0,1,2)
	  TNT1 A 1 A_Raise
	  Wait
	Deselect:
	  TNT1 A 0 A_TakeInventory("Hexen2Weapon1",1)
	  HX04 A 0 A_JumpIfInventory("Hexen2PowerWeaponLevel2",1,"DeselectPowered")
	  HX00 A 0 A_JumpIfInventory("SamsaraHasPermaWeaponLevel2",1,"DeselectPowered")
	  HX04 ABCDEFGHIJ 1
      TNT1 A 1 A_Lower
      Wait
	DeselectPowered:
	  HX04 ABCDEFGHIJ 2 Bright
      TNT1 A 1 A_Lower
      Wait
	Fire:
	  HX01 A 0 A_PlaySound("Hexen2/gaunt1", CHAN_WEAPON)
	  HX01 A 0 A_JumpIfInventory("Hexen2AttackCounter", 2, "Fire2")
	  HX01 A 0 A_GiveInventory("Hexen2AttackCounter", 1)
	  HX00 A 0 A_JumpIfInventory("Hexen2PowerWeaponLevel2",1,"FirePowered")
	  HX00 A 0 A_JumpIfInventory("SamsaraHasPermaWeaponLevel2",1,"FirePowered")
      HX01 AB 2
      HX01 C 0 A_JumpIfInventory("CoopModeOn",1,2)
	  HX01 C 2 A_FireCustomMissile("Hexen2SickleProjectile",0,0,0)
      Goto FireEnd
	  HX01 C 2 A_FireCustomMissile("Hexen2SickleProjectileCoop",0,0,0)
      Goto FireEnd
    FireEnd:
      HX01 DEFGHI 2
      Goto WeaponIdle
	Fire2:
	  HX02 A 0 A_TakeInventory("Hexen2AttackCounter", 1024)
	  HX02 A 0 A_GiveInventory("Hexen2AttackCounter", 1)
	  HX00 A 0 A_JumpIfInventory("Hexen2PowerWeaponLevel2",1,"Fire2Powered")
	  HX00 A 0 A_JumpIfInventory("SamsaraHasPermaWeaponLevel2",1,"Fire2Powered")
	  HX02 ABC 2
      HX02 C 0 A_JumpIfInventory("CoopModeOn",1,2)
	  HX02 D 2 A_FireCustomMissile("Hexen2SickleProjectile",0,0,0)
      Goto Fire2End
      HX02 D 2 A_FireCustomMissile("Hexen2SickleProjectileCoop",0,0,0)
      Goto Fire2End
    Fire2End:
      HX02 EFGHIJ 2
      Goto WeaponIdle
	FirePowered:
	  HX01 AB 2 Bright
      HX01 C 0 Bright A_JumpIfInventory("CoopModeOn",1,2)
	  HX01 C 2 Bright A_FireCustomMissile("Hexen2SickleProjectilePowered",0,0,0)
      Goto FirePoweredEnd
      HX01 C 2 Bright A_FireCustomMissile("Hexen2SickleProjectilePoweredCoop",0,0,0)
      Goto FirePoweredEnd
    FirePoweredEnd:
      HX01 DEFGHI 2 Bright
      Goto WeaponIdle
	Fire2Powered:
	  HX02 ABC 2 Bright
      HX02 D 0 Bright A_JumpIfInventory("CoopModeOn",1,2)
	  HX02 D 2 Bright A_FireCustomMissile("Hexen2SickleProjectilePowered",0,0,0)
      Goto Fire2PoweredEnd
      HX02 D 2 Bright A_FireCustomMissile("Hexen2SickleProjectilePoweredCoop",0,0,0)
	  Goto Fire2PoweredEnd
    Fire2PoweredEnd:
      HX02 EFGHIJ 2 Bright
      Goto WeaponIdle
	NoFrames:
	  HX00 A 0 bright A_Stop HX01 A 0 bright A_Stop HX02 A 0 bright A_Stop HX03 A 0 bright A_Stop HX04 A 0 bright A_Stop HX05 A 0 bright A_Stop 
	  HX06 A 0 bright A_Stop HX07 A 0 bright A_Stop HX08 A 0 bright A_Stop HX09 A 0 bright A_Stop HX10 A 0 bright A_Stop HX11 A 0 bright A_Stop 
	  HX12 A 0 bright A_Stop HX13 A 0 bright A_Stop HX14 A 0 bright A_Stop HX15 A 0 bright A_Stop HX16 A 0 bright A_Stop HX17 A 0 bright A_Stop 
	  stop
  }
}

Actor Hexen2SickleProjectile : FastProjectile
{
	PROJECTILE
	+BLOODSPLATTER
	+NOEXTREMEDEATH
	Radius 6
	Height 8
	Speed 48
	Damage (CallACS("Hexen2_StrengthMultiplier",10))
	Scale 0.5
	DamageType "Hexen2Damage"
	States
	{
	Spawn:
		TNT1 A 1
		stop
	Death:
	Crash:
		TNT1 A 0 A_PlaySound("Hexen2/HitWall",CHAN_BODY)
		TNT1 A 1
		HX00 ABCDEFGHIJ 2 Bright
		stop
	XDeath:
		TNT1 A 0 A_PlaySound("Hexen2/Slash",CHAN_BODY)
		TNT1 A 0 A_JumpIf(CallACS("Hexen2_Decorate",29,1) != 0,"Drain")
		TNT1 A 1 
		stop
	Drain:
		TNT1 A 0 A_PlaySound("Hexen2/Drain",CHAN_BODY)
		TNT1 A 1 A_GiveInventory("SamsaraHexen2SickleHealthDrain",1,AAPTR_TARGET)
		stop
	}
}

Actor Hexen2SickleProjectileCoop : Hexen2SickleProjectile { +THRUSPECIES Species "Player" }

actor SamsaraHexen2SickleHealthDrain : Trigger
{
    States
    {
      Pickup:
        TNT1 A 0 A_JumpIf(CallACS("Hexen2_Decorate", 30) > 0, 1)
        stop
        TNT1 A 0 ACS_NamedExecuteWithResult("SamsaraPlayerHealer", CallACS("Hexen2_Decorate", 30))
        stop
    }
}

Actor Hexen2SickleProjectilePowered : Hexen2SickleProjectile
{
	Speed 48
	Damage (CallACS("Hexen2_StrengthMultiplier",20+random(0,30)))
	Renderstyle Translucent
	Alpha 0.5
	+HITTRACER
	DamageType "Hexen2Damage"
	States
	{
	Spawn:
		TNT1 A 1
		stop
	Death:
	Crash:
		TNT1 A 0 ACS_NamedExecuteAlways("Hexen2_Decorate",0,9)
		TNT1 A 0 A_PlaySound("Hexen2/HitWall",CHAN_BODY)
		HX01 BCDEFJ 2 Bright
		stop
	XDeath:
		TNT1 A 0 ACS_NamedExecuteAlways("Hexen2_Decorate",0,9)
		TNT1 A 0 A_PlaySound("Hexen2/Slash",CHAN_BODY)
		TNT1 A 0 A_JumpIf(CallACS("Hexen2_Decorate",29,1) != 0,"Drain")
		HX01 BCDEFJ 2 Bright
		stop
	Drain:
		TNT1 A 0 A_PlaySound("Hexen2/Drain",CHAN_BODY)
		TNT1 A 0 A_GiveInventory("SamsaraHexen2SicklePoweredHealthDrain",1,AAPTR_TARGET)
		HX01 BCDEFJ 2 Bright
		stop
	}
}

Actor Hexen2SickleProjectilePoweredCoop : Hexen2SickleProjectilePowered { +THRUSPECIES Species "Player" }

actor SamsaraHexen2SicklePoweredHealthDrain : SamsaraHexen2SickleHealthDrain {}
