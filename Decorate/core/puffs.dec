// silent, invisible, non-damaging bullet puff made specifically for classes that otherwise have no hitscans to speak of, such as Everhail or Security Officer.
// it doesn't make anything bleed, goes through everything, as a useless and nonsensical damage type that is overridden to always exactly zero damage no matter what
// so if it somehow hits something it never does any damage. all to make sure it does absolutely nothing but activate switches
// this is necessary because some maps require hitscans in order to progress, and some classes only use projectile-based weapons that would otherwise make those maps impossible.
// such is life

actor SamsaraBlankPuff
{
	Radius 1
	Height 1
	DamageType "NotImportant"
	Mass 0
	//+ALLOWTHRUFLAGS // doesn't exist in zandronum yet
	+BLOODLESSIMPACT
	+CLIENTSIDEONLY
	+DONTBLAST
	+DONTREFLECT
	+DONTSPLASH
	+MTHRUSPECIES
	+NOBLOCKMAP
	+NODAMAGETHRUST
	+NODECAL
	+NOEXTREMEDEATH
	+NOTELEPORT
	+NOTONAUTOMAP
	+PAINLESS
	+THRUACTORS
	States
	{
		Spawn:
			TNT1 A 1
			stop
	}
}

DamageType NotImportant
{
	Factor 0
	ReplaceFactor
}

//a bulletpuff that doesn't get affected by settings
ACTOR BulletPuffSettingless
{
	+NOBLOCKMAP
	+NOGRAVITY
	+ALLOWPARTICLES
	+RANDOMIZE
	VSpeed 1
	Mass 5
	States
	{
		Spawn:
			PUFF A 4 Bright
			PUFF B 4
			// Intentional fall-through
		Melee:
			PUFF CD 4
			Stop
	}
}

// bullet puff which calls an ACS script to store x,y,z coordinates of puff. Used for scripts relying
// on where the player is aiming

actor SamsaraLineTracePuff
{
	Radius 1
	Height 1
	+ALWAYSPUFF
	+BLOODLESSIMPACT
	+CLIENTSIDEONLY
	+DONTBLAST
	+DONTREFLECT
	+DONTSPLASH
	+HITTRACER
	+NOBLOCKMAP
	+NODAMAGETHRUST
	+NODECAL
	+NOEXTREMEDEATH
	+NOINTERACTION
	+NOTELEPORT
	+NOTIMEFREEZE
	+NOTONAUTOMAP
	+NOTRIGGER
	+PAINLESS
	+PUFFGETSOWNER
	+PUFFONACTORS
	+SKYEXPLODE
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 A 1 ACS_NamedExecuteWithResult("SamsaraSetPlayerLineTraceCoords")
			Stop
	}
}

actor SamsaraLineTracePuffCoop : SamsaraLineTracePuff { +MTHRUSPECIES }
