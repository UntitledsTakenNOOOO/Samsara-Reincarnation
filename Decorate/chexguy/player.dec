actor ChexWarrior : PlayerPawn
{
    Player.DisplayName "ChexWarrior"
    Player.StartItem "Mini-Zorcher", 1
    Player.StartItem " Bootspoon ", 1
    Player.StartItem "ChexClass", 1
    Player.StartItem "IsSamsaraClass", 1
    Player.StartItem "Clip", 50
	Player.StartItem "TauntButton", 1
    Player.Face "CHX"
    Player.Soundclass "ChexWarrior"
    Player.JumpZ 9//Default is 8
    BloodType "ChexBlood" //+NOBLOOD
	BloodColor "Green"
	+NOBLOODDECALS
    +NOSKIN
    +DONTGIB
    -DONTBLAST
    -THRUSPECIES
    Species "Player"
	DamageFactor "RebelMissile", 0
	DamageFactor "DynamiteCoop", 0.0	
    DamageFactor "Telefrag", 2.0
	DamageFactor "SWFlameCoop", 0.0	
    DamageFactor "PoisonCloud", 1.65
    DamageFactor "Wraithverge", 0.9
    DamageFactor "CorvusCrossbow", 0.9
    DamageFactor "DragonClaw", 0.9
    DamageFactor "MarathonPistol", 0.8
	DamageFactor "SWStickyCoop", 0.0	
    DamageFactor "Pistol", 0.8
    DamageFactor "DoomFist", 0.8
    DamageFactor "ZorchPistol", 0.8
    DamageFactor "BFG", 0.8
    DamageFactor "BFGSpray", 0.8
    DamageFactor "Normal", 0.8
    DamageFactor "Zorch", 0.8
    DamageFactor "PhasingZorch", 0.8
    DamageFactor "RMR-BigPlasma", 0.7			
    DamageFactor "Ice", 0.8
    DamageFactor "Fire", 0.8
    DamageFactor "Slime", 0.8
    DamageFactor "Disintegrate", 0.8
    DamageFactor "Poison", 0.8
    DamageFactor "WolfenFlamethrower", 0.8
    DamageFactor "DukeDamage", 0.8
    DamageFactor "DukeExplosive", 0.8
    DamageFactor "DukeSpectral", 0.8
    DamageFactor "MightyBoot", 1.3
    DamageFactor "DukeIce", 0.8
    DamageFactor "DukePistol", 0.8
    DamageFactor "Explosive", 0.8
    DamageFactor "MarathonFusion", 0.8
    DamageFactor "DoomPlasma", 0.8
    DamageFactor "CorvusCrossbow", 0.8
    DamageFactor "CorvusHellstaff", 0.8
	DamageFactor "CorvusHellstaffTomed", 0.8
    DamageFactor "MarathonPlasma", 0.8
    DamageFactor "BJSpear", 0.8
    DamageFactor "DoomCacodemon", 0.8
    DamageFactor "DoomHellNoble", 0.8
    DamageFactor "DoomSpiderPlasma", 0.8
    DamageFactor "DoomTheseGuysAreTheBest", 0.8
    DamageFactor "HereNitrogolem", 0.8
    DamageFactor "HereAxeGreen", 0.8
    DamageFactor "HereAxeRed", 0.8
    DamageFactor "Wizard", 0.8
    DamageFactor "HereLichWind", 0.8
    DamageFactor "HereDsparil", 0.8
    DamageFactor "HexBishop", 0.8
    DamageFactor "HexSlaughtaur", 0.8
    DamageFactor "HexGreenFire", 0.8
    DamageFactor "HexStalkerShot", 0.8
    DamageFactor "HexHeresiarchPurple", 0.8
    DamageFactor "WMCBeam", 0.55
    DamageFactor "WMCNade", 0.55
    DamageFactor "QuakePistol", 0.8
    DamageFactor "NotQuadded", 0.8
    DamageFactor "SpectralNoQuad", 0.8
	DamageFactor "MarathonStun", 1.0
    DamageFactor "QuakeFire", 0.8 
	DamageFactor "FireExplosive", 0.8
    DamageFactor "Kamikaze", 1.33333333333
    DamageFactor "ZorchKamikaze", 1.33333333333
	DamageFactor "EnemyInfightDamage", 0.0
	DamageFactor "CMDMGSHIT", 0.0
	Damagefactor "RottExplosive", 0.8
	DamageFactor "RottFire", 0.8
	DamageFactor "RottBullet", 0.8
	DamageFactor "BlakePlasma", 0.8
	DamageFactor "BlakeBullet", 0.8
	DamageFactor "Dynamite", 0.8
	DamageFactor "LifeLeech", 0.8
	DamageFactor "Tesla", 0.8
	DamageFactor "CalebBullet", 0.8
	DamageFactor "CalebNapalm", 0.8
	DamageFactor "CalebFire", 0.8
	DamageFactor "PhosFireCoop", 0.0
	DamageFactor "EradCoopExplosive2", 0.0
    DamageFactor "EradCoopGreenFire", 0.0
    DamageFactor "C7MineBoom", 0.0	
	DamageFactor "PunchDagger", 0.7
	DamageFactor "ElectricBolt", 0.9
	DamageFactor "PoisonBolt", 0.8
	DamageFactor "MiniExplosive", 0.8
	DamageFactor "StrifeExplosive", 0.8
	DamageFactor "StrifeRebelBullet", 0.8
	DamageFactor "BCannonProj", 0.75
	DamageFactor "Disruptoruniversaldamageshit4", 0.8
	DamageFactor "Disruptoruniversaldamageshit3", 0.8
	DamageFactor "Disruptoruniversaldamageshit2",	0.8
	DamageFactor "Disruptoruniversaldamageshit1", 0.8
	DamageFactor "DisruptorWall", 0.7
	DamageFactor "SWNukeCoop", 0.0		
	DamageFactor "SWRailgunCoop", 0.0
	DamageFactor "SWFlame2Coop", 0.0
    RadiusDamageFactor 0.75
    Player.DamageScreenColor "Yellow"
    Player.ColorRange 192, 207
    States
    {
      Spawn:
        CHEX A 1
        Loop
      See:
        CHEX AAAABBBBCCCCDDDD 1
        Loop
      Missile:
        CHEX E 12 
        Goto Spawn
      Melee:
        CHEX F 6 BRIGHT
        Goto Missile
      Pain:
        CHEX G 4 
        CHEX G 4 A_Pain
        Goto Spawn
	  Pain.Flem:
	  Pain.Slime:
	    CHEX G 4 A_SetBlend("60 b0 58",0.9,10)
		CHEX G 4 A_Pain
		goto Spawn
      Pain.Chickenator:
        CHEX G 1
        goto Spawn
	  Pain.LifeLeech:
		TNT1 A 0 A_GiveToTarget("SerpentHealth", random(1,2))
		TNT1 A 0 A_GiveToTarget("HealthBonus",1)
		Goto Pain
      Death:
        CHEX H 10
        CHEX I 10 A_PlayerScream
        CHEX J 10 A_NoBlocking
        CHEX KLM 10
        CHEX N 20
        CHEX X -1
        Stop
	  Death.RottExplosive:
      XDeath:
		FLEE A 3
		TNT1 AA 0 A_SpawnItemEx("Chexbit1",random(-16,16),random(-16,16),random(24,54),random(-450,450)/100.00,random(-450,450)/100.00,random(550,850)/100.00,random(-180,180),160)
		TNT1 AA 0 A_SpawnItemEx("Chexbit2",random(-20,16),random(-16,16),random(24,54),random(-450,450)/100.00,random(-450,450)/100.00,random(550,850)/100.00,random(-180,180),160)
		TNT1 AA 0 A_SpawnItemEx("Chexbit3",random(-16,16),random(-16,16),random(24,54),random(-450,450)/100.00,random(-450,450)/100.00,random(550,850)/100.00,random(-180,180),160)
		TNT1 AA 0 A_SpawnItemEx("Chexbit4",random(-16,16),random(-16,16),random(24,54),random(-450,450)/100.00,random(-450,450)/100.00,random(550,850)/100.00,random(-180,180),160)
		TNT1 AAAAAAAAAA 0 A_SpawnItemEx("ChexCrumb",random(-16,16),random(-16,16),random(24,54),random(-450,450)/100.00,random(-450,450)/100.00,random(550,850)/100.00,random(-180,180),160)
		FLEE B 20 A_PlaySound("chex/munch",0,1.0)
		FLEE C 5 A_PlaySound("chex/boop",0,0.7)
		FLEE B 5
		FLEE D 10
		TNT1 A 0 A_PlaySound("chex/selfzorch",0,0.7)
		FLEE EFGH 5
		ACLO E -1
        stop
      Death.CorvusGrimBallista:
        CHEX O 5
        CHEX P 5 A_PlaySoundEx("*falling",CHAN_VOICE,0,0)
        CHEX Q 5 A_NoBlocking
        CHEX RSTUV 5
        CHEX W 20
        CHEX X -1
        Stop
      Death.ZorchPistol:
      Death.Zorch:
      Death.ZorchKamikaze:
      Death.PhasingZorch:
      Death.ZorchSpectral:
        CHEX G 0 A_Stop
        TNT1 A 0 A_PlaySoundEx("chex/zorch","soundslot5",0,0)
        CHEX G 3 A_SpawnItem("ZorchEffectSmall")
        CHEX G 3 A_SetTranslucent(0.9,0)
        CHEX G 0 A_PlayerScream
        CHEX G 3 A_SetTranslucent(0.8,0)
        CHEX G 0 A_NoBlocking
        CHEX G 3 A_SetTranslucent(0.7,0)
        CHEX G 3 A_SetTranslucent(0.6,0)
        CHEX G 3 A_SetTranslucent(0.5,0)
        CHEX G 3 A_SetTranslucent(0.4,0)
        CHEX G 3 A_SetTranslucent(0.3,0)
        CHEX G 3 A_SetTranslucent(0.2,0)
        CHEX G 3 A_SetTranslucent(0.1,0)
        ACLO E 1 A_SetTranslucent(1,0)
        ACLO E -1
        stop
      CoopModeOn:
        TNT1 A 0
        TNT1 A 0 A_ChangeFlag("THRUSPECIES",1)
        TNT1 A 0 A_ChangeFlag("DONTBLAST",1)
        CHEX A 1
        goto Spawn
      Death.DukeExplosive:
      Death.DukeSpectral:
      Death.DukeDamage:
      Death.DukePistol:
        TNT1 A 0 A_Jump(218,"Death")
        TNT1 A 0 A_GiveToTarget("DukeDMTaunt1",1)
        goto Death
      Death.DukeIce:
        TNT1 A 0 A_Jump(218,"Ice")
        TNT1 A 0 A_GiveToTarget("DukeDMTaunt1",1)
        goto Ice
      Ice:
      Death.HexenIce:
      Death.Ice:
	    TNT1 A 0 A_PlayWeaponSound("misc/freeze")
        CHEX Z 70 A_NoBlocking
        CHEX Z 0 A_SetTranslucent(0,0)
        TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx("IceChunk2",random(-1,1),random(-1,1),random(8,48),random(-2,2),random(-2,2),random(1,6))
        CHEX Z 1 A_PlayWeaponSound("misc/icebreak")
        CHEX Z -1
        stop
      Pain.MightyBoot:
	    CHEX G 1 ACS_ExecuteAlways(312, 0, 25, 5, 24)
        CHEX G 1 A_PlaySoundEx("duke/kickhit","body",0,0)
        TNT1 A 0 A_Recoil(12)
        CHEX G 6 A_Pain
        Goto See
	  Pain.MarathonStun:
        TNT1 A 0 A_Stop
	    CHEX G 1 ACS_ExecuteAlways(312, 0, 25, 5, 100)
		TNT1 A 0 A_GiveInventory("NothingSpeed")
		TNT1 A 0 A_SetBlend("White",0.9,105)
        CHEX G 1 A_PlaySoundEx("marathon/tech50/stun","body",0,0)
        CHEX G 6 A_Pain
        Goto See
	  Death.CalebNapalm:
	  Death.CalebFire:
		TNT1 A 0 A_Jump(218, "Burn")
		TNT1 A 0 A_GiveToTarget("CalebDMTaunt1", 1)
		Goto Burn	
	  Death.Dynamite:
	  Death.LifeLeech:
	  Death.CalebBullet:	  
	  Death.Tesla:
	  Death.Vodoo:
		TNT1 A 0 A_Jump(218, "Death")
		TNT1 A 0 A_GiveToTarget("CalebDMTaunt1", 1)
		Goto Death		
	  Death.EradGreenFire:
	  Death.EradCoopGreenFire:
		TNT1 A 0 A_Jump(218, "Burn")
		TNT1 A 0 A_GiveToTarget("EleenaDMTaunt1", 1)
		Goto Burn
	  Death.EradDisc:
	  Death.EradShock:
	  Death.EradPiercingDart:
	  Death.EradExplosive:
	  Death.EradNova:
	  Death.EradExplosive2:	  
		TNT1 A 0 A_Jump(218, "Death")
		TNT1 A 0 A_GiveToTarget("EleenaDMTaunt1", 1)
		Goto Death
	  Death.HexGreenFire:
      Death.QuakeFire:
      Death.FireExplosive:
      Burn:
		CWBN A 3 A_PlaySoundEx("*burndeath","voice",0,0)
		CWBN BCDEFGHI 3
		CWBN J 20
		CWBN J 0 A_PlaySoundEx("chex/burndeath2","body",0,0)
		CWBN KMNOP 3
		CWBN Q -1
		stop		
      Disintegrate:
        TNT1 A 0 A_GiveInventory("EnemyZorched",1)
        TNT1 A 0 A_NoBlocking
        TNT1 A 0 A_ChangeFlag("DONTGIB",1)
        TNT1 A 0 A_SpawnItemEx("ChexterDisintegrate",0,0,5,0,0,0,0,32)
        ACLO E 5 A_PlayerScream
        ACLO E -1
        stop	
	  Death.Nuke:
		ACLO E 0 A_UnsetSolid
		ACLO E 0 A_PlayerScream
		ACLO E 0 A_NoBlocking
		ACLO E 5 A_SpawnItemEx("ChexNuke")
		ACLO E -1
		stop
    }
}

Actor ChexNuke
{
	-SOLID
	+NOBLOCKMAP
	+CLIENTSIDEONLY
	states
	{
	Spawn:
		NK19 ABCDEFG 5 bright
		stop
	}
}

actor ChexterDisintegrate : ZombiemanDisintegrate 
{
   States
   {
      Spawn:
        TNT1 A 0
        CHEX H 3 A_SpawnItem("DisintEffectRound",0,20)
        CHEX H 3 A_SetTranslucent(0.9,0)
        CHEX H 3 A_SetTranslucent(0.8,0)
        CHEX H 3 A_SetTranslucent(0.7,0)
        CHEX I 3 A_SetTranslucent(0.6,0)
        CHEX I 3 A_SetTranslucent(0.5,0)
        CHEX I 2 A_SetTranslucent(0.4,0)
        CHEX I 2 A_SetTranslucent(0.3,0)
        CHEX J 2 A_SetTranslucent(0.2,0)
        CHEX J 2 A_SetTranslucent(0.1,0)
        Stop
   }
}

actor ChexClass : inventory { -INVBAR +INVENTORY.UNDROPPABLE inventory.maxamount 1 }

actor ChexPuff : BulletPuff
{
    +PUFFONACTORS
    +BLOODLESSIMPACT
    +CLIENTSIDEONLY
	+THRUGHOST
    DamageType "Zorch"
    RenderStyle Add
    Alpha 0.4
    Scale 0.22
    VSpeed 0
    States
    {
      Spawn:
        CMIS DCB 4 bright
        stop
    }
}

actor ChexBullet : FastProjectile
{
    Damage (Random(1,3)*5)
    Radius 1
    Height 1
    Speed 320
    Projectile
    Renderstyle "Normal"
    Species "Player"
    DamageType "Zorch"
    -BLOODSPLATTER
    +BLOODLESSIMPACT
    +THRUSPECIES
    +NOTIMEFREEZE
	+THRUGHOST
    Alpha 1.0
    States
    {
      Spawn:
        TNT1 A 1
        loop

      Death:
        TNT1 A 1 A_SpawnItem("ChexPuff")
        stop
    }
}

actor ChexUnsolid: Trigger
{
    States
    {
      Pickup:
        TNT1 A 0 A_UnsetSolid
        stop
    }
}

actor ChexSolid: Trigger
{
    States
    {
      Pickup:
        TNT1 A 0 A_SetSolid
        stop
    }
}

// XDeath Stuff

ACTOR Chexbit1 // Borrowed from DemonSteele's Steel Beast code
{
   Radius 2
   Height 2
   BounceFactor 0.55
   BounceCount 5
   Mass 10
   Speed 8
   +DOOMBOUNCE
   PROJECTILE
   +NOTELEPORT
   -NOGRAVITY
   -SOLID
   -SKYEXPLODE
   +NOBLOCKMAP
   +MOVEWITHSECTOR
   +CLIENTSIDEONLY
   States
   {
      Spawn:
       CERE ABCD 5
        loop
      Death:
       TNT1 A 0 A_Jump(256,"Death1","Death2")
      Death1:
       CERE A -1
        stop
      Death2:
       CERE C -1
        stop
   }
}

ACTOR Chexbit2 : Chexbit1
{
   States
   {
      Spawn:
       CERE EFGH 5
        loop
      Death:
       TNT1 A 0 A_Jump(256,"Death1","Death2")
      Death1:
       CERE E -1
        stop
      Death2:
       CERE G -1
        stop
   }
}

ACTOR Chexbit3 : Chexbit1
{
   States
   {
      Spawn:
       CERE IJKL 5
        loop
      Death:
       TNT1 A 0 A_Jump(256,"Death1","Death2")
      Death1:
       CERE I -1
        stop
      Death2:
       CERE K -1
        stop
   }
}

ACTOR Chexbit4 : Chexbit1
{
   States
   {
      Spawn:
       CERE MNOP 5
        loop
      Death:
       TNT1 A 0 A_Jump(256,"Death1","Death2")
      Death1:
       CERE M -1
        stop
      Death2:
       CERE O -1
        stop
   }
}

ACTOR Chexcrumb
{
   Radius 2
   Height 2
   Mass 10
   Speed 18
   Scale 0.75
   Gravity 0.75
   PROJECTILE
   +NOTELEPORT
   -NOGRAVITY
   -SOLID
   -SKYEXPLODE
   +NOBLOCKMAP
   +MOVEWITHSECTOR
   +CLIENTSIDEONLY
   States
   {
      Spawn:
       TNT1 A 0 
       TNT1 A 10 A_Jump(256,"Crumb1","Crumb2","Crumb3")
        loop
      Crumb1:
       CERE Q 10
        loop
      Crumb2:
       CERE R 10
        loop
      Crumb3:
       CERE S 10
        loop
      Death:
       TNT1 A 1
        stop
   }
}

// Unique Chex Blood - Family Friendly!
// Code taken from Reelism which was adapted from Psychic
// so... thanks Xaser!

ACTOR ChexBlood
{
	Mass 1
	Radius 1
	Height 1
	Scale 0.5
	Alpha 1
	Gravity .5
	+NOBLOCKMAP
	+FLOORCLIP
	+NOTELEPORT
	+CLIENTSIDEONLY
	+DONTTRANSLATE
	+CORPSE
	speed .1
	VSpeed 64
	States
	{
	Spawn:
		CERE AB 2
		CERE C 2 A_SpawnDebris("ChexBloodSpurt",1)
		CERE D 2 
	Live:
		CERE ABCDEFGH 2
		loop
	Crash:
		CERE A 0
		stop
	}
}

ACTOR ChexBloodSpurt
{
	Health 4
	radius 3
	height 6
	speed .1
	Scale .75
	Mass 0
	BounceType Doom
	+FLOORCLIP
	+DONTSPLASH
	+NOTELEPORT
	+NOBLOCKMAP
	+CLIENTSIDEONLY
	States
	{
	Spawn:
		SPRY A 0
		CERE QRSQRSQRSQRS 1
		CERE Q 1 A_SetTranslucent(.8)
		CERE R 1 A_SetTranslucent(.6)
		CERE S 1 A_SetTranslucent(.4)
		CERE Q 1 A_SetTranslucent(.2)
		Stop
	}
}