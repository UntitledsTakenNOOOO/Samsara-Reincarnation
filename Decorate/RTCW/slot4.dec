Actor RTCW_FG42 : RTCW_Weapon
{
	Weapon.SlotNumber 4
	Weapon.SelectionOrder 750
	Weapon.AmmoUse1 1
	Weapon.AmmoUse2 0
	Weapon.AmmoType1 "RTCW_FG42Magazine"
	Weapon.AmmoType2 "Clip"
	Weapon.AmmoGive1 0
	Weapon.AmmoGive2 40
	Obituary "%o was killed (fg42) by %k."
	Inventory.Pickupmessage "FG42"
	Inventory.PickupSound "RTCW/WeaponPickup"
	Tag "FG-42"
	+WEAPON.NOALERT
	+WEAPON.AMMO_OPTIONAL
	States
	{
		Spawn:
			RW98 A -1
			Stop
		Deselect:
			RW00 V 0 A_TakeInventory("RTCW_Scoped",1)
			RW00 V 0 A_TakeInventory("RTCW_ZoomFactor",90)
			RW00 V 0 A_ZoomFactor(1.0)
			RW00 V 0 A_PlaySound("RTCW/Change",CHAN_7)
			RW00 FGHIJKL 2
			Goto Super::Deselect
		Ready:
			RW00 A 0 A_TakeInventory("RTCW_WeaponToken",256)
			RW00 A 0 A_GiveInventory("RTCW_WeaponToken",12)
			RW00 LMNOP 2
		ReadyLoop:
			RW00 A 0 A_JumpIfInventory("RTCW_Kicking",1,"Kicking")
			RW00 A 0 A_JumpIfInventory("RTCW_Scoped",1,"LoopScope")
			RW00 A 1 A_WeaponReady(WRF_ALLOWRELOAD)
			Loop
		LoopScope:
			TNT1 A 0 A_JumpIfInventory("RTCW_Kicking",1,"Kicking")
			TNT1 A 0 A_JumpIfInventory("RTCW_Scoped",1,1)
			Goto ReadyLoop
			TNT1 A 1 A_WeaponReady(WRF_ALLOWRELOAD)
			Loop
		NoAmmo:
			RW00 A 0 A_PlaySound("RTCW/NoAmmo",CHAN_5)
			RW00 A 17
			TNT1 A 0 A_SelectWeapon("RTCW_Knife")
			Goto Deselect
		NoAmmoScoped:
			TNT1 A 0 A_PlaySound("RTCW/NoAmmo",CHAN_5)
			TNT1 A 17
			TNT1 A 0 A_SelectWeapon("RTCW_Knife")
			Goto Deselect
		Fire:
			RW00 B 0 A_JumpIfInventory("RTCW_FG42Magazine",1,1)
			Goto Reload
			RW00 B 0 A_GiveInventory("RTCW_FiringToken",1)
			RW00 B 0 A_FireCustomMissile("RTCW_Casing",-random(85,105),0,8,4,0,-4)
			RW00 B 0 A_TakeInventory("RTCW_FG42Magazine",1)
			RW00 B 0 A_GiveInventory("RTCW_BlazkoWeaponAlert",1)	
			RW00 B 0 A_GiveInventory("RTCW_FG42Spread",1)
			RW00 B 0 A_PlaySound("RTCW/FG42/Fire",CHAN_Weapon)
			RW00 B 0 A_JumpIfInventory("RTCW_Scoped",1,"FireScoped")
			RW00 B 0 ACS_NamedExecuteWithResult("RTCW_Recoil",1)
			RW00 B 0 A_FireCustomMissile("RTCW_FG42Tracer",CallACS("RTCW_Decorate",1,1)*frandom(-1.0,1.0),0,0,4,0,CallACS("RTCW_Decorate",1,1)*frandom(-1.0,1.0))
			RW00 B 0 A_GunFlash("FireFlash")
			RW00 BCD 2
			RW00 E 1
			RW00 F 0 A_Refire("Fire")
			RW00 F 0 A_TakeInventory("RTCW_FG42Spread",4)
			Goto ReadyLoop
		FireFlash:
			RW03 B 2 Bright
			Stop
		FireScoped:
			TNT1 A 0 A_GiveInventory("RTCW_FiringToken",1)
			TNT1 A 0 A_FireCustomMissile("RTCW_FG42Tracer",CallACS("RTCW_Decorate",1,1)*frandom(-0.25,0.25),0,0,4,0,CallACS("RTCW_Decorate",1,1)*frandom(-0.25,0.25))
			TNT1 A 0 A_SetPitch(pitch-0.5)
			TNT1 A 0 A_SetAngle(angle+(2*random(0,1)-1)*frandom(0.5,1.0))
			TNT1 A 0 A_GiveInventory("RTCW_AimSpread",1048576)
			TNT1 A 7
			TNT1 A 0 A_Refire("Fire")
			TNT1 A 0 A_TakeInventory("RTCW_FG42Spread",4)
			Goto ReadyLoop
		AltFire:
			RW00 A 0 A_PlaySound("RTCW/Change",CHAN_7)
			RW00 A 0 A_JumpIfInventory("RTCW_Scoped",1,"LowerScope")
			RW00 A 0 A_GiveInventory("RTCW_Scoped",1)
			RW00 A 0 ACS_NamedExecuteWithResult("RTCW_Scope")
			TNT1 A 10 A_ZoomFactor(1.63)
			Goto ReadyLoop
		LowerScope:
			RW00 A 0 A_TakeInventory("RTCW_Scoped",1)
			RW00 A 0 A_ZoomFactor(1.0)
			Goto Ready
		Reload:
			TNT1 A 0 A_JumpIfInventory("RTCW_Scoped",1,"ScopedNoAmmo")
			TNT1 A 0 A_JumpIfInventory("RTCW_FG42Magazine",0,"ReadyLoop")
			TNT1 A 0 A_JumpIfInventory("Clip",2,"ReloadSuccess")
			TNT1 A 0 A_JumpIfInventory("RTCW_FG42Magazine",1,1)
			goto NoAmmo
			TNT1 A 0
			goto ReadyLoop
		ScopedNoAmmo:
			TNT1 A 0 A_JumpIfInventory("RTCW_FG42Magazine",0,"ReadyLoop")
			TNT1 A 0 A_JumpIfInventory("Clip",2,1)
			Goto NoAmmoScoped
			TNT1 A 0 A_JumpIfInventory("RTCW_FG42Magazine",1,"ReloadLowerScope")
			TNT1 A 0 A_PlaySound("RTCW/NoAmmo",CHAN_6)
			TNT1 A 17
		ReloadLowerScope:
			RW00 A 0 A_PlaySound("RTCW/Change",CHAN_7)
			Goto ReloadSuccess+1
		ReloadSuccess:
			RW00 A 0 A_JumpIfInventory("RTCW_Scoped",1,"ReloadLowerScope")
			RW00 A 0 A_TakeInventory("RTCW_ZoomFactor",90)
			RW00 A 0 A_TakeInventory("RTCW_Scoped",1)
			RW00 A 0 A_ZoomFactor(1.0)
			TNT1 A 0 //A_JumpIfInventory("RTCW_FG42Magazine",1,1)
			goto ReloadNoAmmo			
			RW00 A 0 A_PlaySound("RTCW/FG42/Reload",CHAN_6)
			RW00 "QRSTUVWXYZ[\]" 2
			RW01 ABCDEFGHIJKLMN 2
			RW02 DEFG 2
			RW00 A 0
			goto ReloadAmmo
		ReloadNoAmmo:
			RW00 A 0 A_PlaySound("RTCW/FG42/Reload",CHAN_6)
			RW00 "QRSTUVWXYZ[\]" 2
			RW01 ABCDEFGHIJKLMNOPQR 2
			RW01 S 0 A_PlaySound("RTCW/FG42/Reload2",CHAN_6)
			RW01 "STUVWXYZ[\]" 2
			RW02 ABCDEFG 1
			RW00 A 0
			goto ReloadAmmo		
		ReloadAmmo:
			TNT1 A 0 A_JumpIfInventory("RTCW_FG42Magazine",0,"ReadyLoop")
			TNT1 A 0 A_JumpIfInventory("Clip",2,1)
			goto ReadyLoop
			TNT1 A 0 A_GiveInventory("RTCW_FG42Magazine",1)
			TNT1 A 0 A_TakeInventory("Clip",2,TIF_NOTAKEINFINITE)
			Loop
		Kicking:
			TNT1 A 0 A_Gunflash("KickingFoot")
		KickingDelay:
			RW00 A 0 A_JumpIfInventory("RTCW_Scoped",1,"KickingScopedDelay")
			RW00 A 18
			RW00 A 0 A_TakeInventory("RTCW_Kicking",1)
			Goto ReadyLoop
		KickingScopedDelay:
			RW00 L 18
			TNT1 A 0 A_TakeInventory("RTCW_Kicking",1)
			Goto ReadyLoop
		KickingFoot:
			RW99 ABCD 2
			RW99 E 0 A_FireCustomMissile("RTCW_KickProjectile",0,0,0,-8)
			RW99 EFGHI 2
			Stop
	}
}

//Was 15, now 30
Actor RTCW_FG42Tracer : RTCW_Tracer
{ 
	States
	{
		Crash:
		Death:
			TNT1 A 0 A_Explode(CallACS("RTCW_BulletDamage",35),8,!XF_HURTSOURCE,0,8)
			Goto Super::Death
		XDeath:
			TNT1 A 0 A_Explode(CallACS("RTCW_BulletDamage",35),8,!XF_HURTSOURCE,0,8)
			Goto Super::XDeath
	}
}

Actor RTCW_FG42Spread : Inventory { +INVENTORY.UNDROPPABLE Inventory.MaxAmount 8 Inventory.InterHubAmount 0  }

Actor RTCW_FG42Magazine : Ammo
{
	Inventory.MaxAmount 20
	Ammo.BackpackMaxAmount 20
}