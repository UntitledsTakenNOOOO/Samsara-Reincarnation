// Tokens

actor DGStrHasPlasmaRepeater : Boolean {}
actor SamsaraDoomguyStrRepeaterHeatAmount : Counter { Inventory.MaxAmount 50 }
actor SamsaraDoomguyStrRepeaterPoweredAttackAmount : Counter { Inventory.MaxAmount 8 }

// Weapon

actor "Plasma Repeater" : Weapon
{
	Inventory.PickupMessage "You got the plasma repeater!"
	Inventory.RestrictedTo "DoomguyPlayer"
	Weapon.SlotNumber 8
	Weapon.AmmoType "Cell"
	//Weapon.AmmoGive 20
	Weapon.AmmoUse 1
	Obituary "%k disintegrated %o with the repeater."
	Tag "Plasma Repeater"
	+EXPLOSIVE
	+NOAUTOFIRE
	+UNDROPPABLE
	States
	{
	Spawn:
		WREP A -1
		stop

	Ready:
		TNT1 A 0 A_TakeInventory("SamsaraDoomguyStrRepeaterHeatAmount")
		REPG A 1 A_WeaponReady
		loop

	// Unpowered
	Deselect:
		TNT1 A 0 A_TakeInventory("SamsaraDoomguyStrRepeaterHeatAmount")
		TNT1 A 0 A_JumpIfInventory("PowerSamsaraDoomguyStrWeaponPower",1,"DeselectPowered")
		TNT1 A 0 A_JumpIfInventory("SamsaraHasPermaWeaponLevel2",1,"DeselectPowered")
	DeselectContinue:
		TNT1 A 0 A_Lower
		REPG A 1 A_Lower
		goto Deselect

	Select:
		TNT1 A 0 A_TakeInventory("SamsaraDoomguyStrRepeaterHeatAmount")
		TNT1 A 0 A_JumpIfInventory("PowerSamsaraDoomguyStrWeaponPower",1,"SelectPowered")
		TNT1 A 0 A_JumpIfInventory("SamsaraHasPermaWeaponLevel2",1,"SelectPowered")
	SelectContinue:
		TNT1 A 0 A_Raise
		REPG A 1 A_Raise
		goto Select

	Fire:
		REPG A 4 A_PlaySound("doomguy/stronghold/repidl")
		REPG BCD 4
		REPG ABCD 3
		REPG ABCD 2
	Hold:
		TNT1 A 0 A_JumpIfInventory("PowerSamsaraDoomguyStrWeaponPower",1,"HoldPowered1")
		TNT1 A 0 A_JumpIfInventory("SamsaraHasPermaWeaponLevel2",1,"HoldPowered1")
		TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"Hold1Attack")
		TNT1 A 0 A_JumpIfInventory("Cell",1,"Hold1Attack")
		goto Hold1

	Hold1Attack:
		TNT1 A 0 A_GunFlash
	Hold1:
		TNT1 A 0 A_GiveInventory("SamsaraDoomguyStrRepeaterAttackHandler")
		REPG EF 2 Bright
		TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"Hold2Attack")
		TNT1 A 0 A_JumpIfInventory("Cell",1,"Hold2Attack")
		goto Hold2

	Hold2Attack:
		TNT1 A 0 A_GunFlash
	Hold2:
		TNT1 A 0 A_GiveInventory("SamsaraDoomguyStrRepeaterAttackHandler")
		REPG GH 2 Bright
	HoldReFire:
		TNT1 A 0 A_ReFire
	HoldFinish:
		TNT1 A 0 A_PlaySound("doomguy/stronghold/repstp")
		TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrRepeaterHeatAmount",0,"HoldFinishHeat")
		REPG A 2 A_TakeInventory("SamsaraDoomguyStrRepeaterHeatAmount")
		REPG BCD 2
		REPG ABCD 3
		REPG ABCD 4
		goto HoldFinish2

	HoldFinishHeat:
		TNT1 A 0 A_TakeInventory("SamsaraDoomguyStrRepeaterHeatAmount")
		REPG ABCD 2 A_FireCustomMissile("SamsaraDoomguyStrRepSteam",random(-2,2),false)
		REPG ABCD 3 A_FireCustomMissile("SamsaraDoomguyStrRepSteam",random(-2,2),false)
		REPG ABCD 4 A_FireCustomMissile("SamsaraDoomguyStrRepSteam",random(-2,2),false)
		goto HoldFinish2

	HoldFinish2:
		REPG ABCD 5
		TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"HoldFinishAmmo")
		TNT1 A 0 A_JumpIfInventory("Cell",1,"HoldFinishAmmo")
		goto HoldFinishNoAmmo

	HoldFinishNoAmmo: // if there's no ammo, don't switch weapon to retain original behaviour
		TNT1 A 0 A_ClearReFire
		goto Ready

	HoldFinishAmmo:
		TNT1 A 0 A_ReFire("Fire") // needed because of +NOAUTOFIRE
		goto Ready

	Flash:
		TNT1 A 2 A_Light2
		TNT1 A 2 A_Light1
		goto LightDone

	// Powered
	DeselectPowered:
		TNT1 A 0 A_Lower
		goto DeselectContinue

	SelectPowered:
		TNT1 A 0 A_Raise
		goto SelectContinue

	HoldPowered1:
		TNT1 A 0 A_TakeInventory("SamsaraDoomguyStrRepeaterPoweredAttackAmount") // reset
	HoldPowered1Loop:
		TNT1 A 0 A_GiveInventory("SamsaraDoomguyStrRepeaterPoweredAttackAmount")
		TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"HoldPowered1LoopAttack")
		TNT1 A 0 A_JumpIfInventory("Cell",1,"HoldPowered1LoopAttack")
		goto HoldPowered1LoopContinue

	HoldPowered1LoopAttack:
		TNT1 A 0 A_GunFlash
	HoldPowered1LoopContinue:
		TNT1 A 0 A_TakeInventory("SamsaraTakingAmmo")
		REPG E 1 Bright A_GiveInventory("SamsaraDoomguyStrRepeaterPoweredAttackHandler1")
		TNT1 A 0 A_TakeInventory("SamsaraTakingAmmo")
		REPG F 1 Bright A_GiveInventory("SamsaraDoomguyStrRepeaterPoweredAttackHandler1")
		TNT1 A 0 A_TakeInventory("SamsaraTakingAmmo")
		REPG G 1 Bright A_GiveInventory("SamsaraDoomguyStrRepeaterPoweredAttackHandler1")
		TNT1 A 0 A_GiveInventory("SamsaraTakingAmmo")
		REPG H 1 Bright A_GiveInventory("SamsaraDoomguyStrRepeaterPoweredAttackHandler1")
		TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrRepeaterPoweredAttackAmount",0,"HoldPoweredReFire")
		goto HoldPowered1Loop

	HoldPowered2: // no need to do ammo calls since this is only called from HoldPowered1's refire
		TNT1 A 0 A_GunFlash
		TNT1 A 0 A_TakeInventory("SamsaraTakingAmmo")
		REPG E 1 Bright A_GiveInventory("SamsaraDoomguyStrRepeaterPoweredAttackHandler2")
		TNT1 A 0 A_TakeInventory("SamsaraTakingAmmo")
		REPG F 1 Bright A_GiveInventory("SamsaraDoomguyStrRepeaterPoweredAttackHandler2")
		TNT1 A 0 A_TakeInventory("SamsaraTakingAmmo")
		REPG G 1 Bright A_GiveInventory("SamsaraDoomguyStrRepeaterPoweredAttackHandler2")
		TNT1 A 0 A_GiveInventory("SamsaraTakingAmmo")
		REPG H 1 Bright A_GiveInventory("SamsaraDoomguyStrRepeaterPoweredAttackHandler2")
	HoldPoweredReFire:
		TNT1 A 0 A_JumpIfInventory("PowerSamsaraDoomguyStrWeaponPower",1,2)
		TNT1 A 0 A_JumpIfInventory("SamsaraHasPermaWeaponLevel2",1,1)
		goto HoldReFire
		TNT1 A 0 A_ReFire("HoldPowered2")
		goto HoldFinish
	}
}

// Unpowered attack handler

actor SamsaraDoomguyStrRepeaterAttackHandler : Trigger
{
	States
	{
	// Ammo checking/taking stuff
	Pickup:
		TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
		TNT1 A 0 A_JumpIfInventory("Cell",1,1)
		stop
		TNT1 A 0 A_TakeInventory("Cell",1)
		goto PickupAttack

	// Attack stuff
	PickupAttack:
		TNT1 A 0 A_PlaySound("doomguy/stronghold/repfir",CHAN_WEAPON)
		TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
		TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
	PickupAttackDM:
		TNT1 A 0 A_FireBullets(4,4,2,9,"SamsaraDoomguyStrRepPuff",FBF_NOFLASH)
		goto PickupFinish

	PickupAttackCoop:
		TNT1 A 0 A_FireBullets(4,4,2,9,"SamsaraDoomguyStrRepPuffCoop",FBF_NOFLASH)
		goto PickupFinish

	// Finishing stuff
	PickupFinish:
		TNT1 A 0 A_GiveInventory("SamsaraDoomguyStrRepeaterHeatAmount")
		stop
	}
}

// Powered attack handlers

actor SamsaraDoomguyStrRepeaterPoweredAttackHandler1 : Trigger
{
	States
	{
	// Ammo checking/taking stuff
	Pickup:
		TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
		TNT1 A 0 A_JumpIfInventory("Cell",1,1)
		stop
		TNT1 A 0 A_JumpIfInventory("SamsaraTakingAmmo",1,1)
		goto PickupAttack
		TNT1 A 0 A_TakeInventory("Cell",1)
		goto PickupAttack

	// Attack stuff
	PickupAttack:
		TNT1 A 0 A_PlaySound("doomguy/stronghold/repfir",CHAN_WEAPON)
		TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
		TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
	PickupAttackDM:
		TNT1 A 0 A_FireBullets(10,6,-1,20,"SamsaraDoomguyStrRepPuffNoDamage",FBF_NOFLASH)
		goto PickupFinish

	PickupAttackCoop:
		TNT1 A 0 A_FireBullets(10,6,-1,20,"SamsaraDoomguyStrRepPuffNoDamageCoop",FBF_NOFLASH)
		goto PickupFinish

	// Finishing stuff
	PickupFinish:
		TNT1 A 0 A_GiveInventory("SamsaraDoomguyStrRepeaterHeatAmount")
		stop
	}
}

actor SamsaraDoomguyStrRepeaterPoweredAttackHandler2 : Trigger
{
	States
	{
	// Ammo checking/taking stuff
	Pickup:
		TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
		TNT1 A 0 A_JumpIfInventory("Cell",1,1)
		stop
		TNT1 A 0 A_JumpIfInventory("SamsaraTakingAmmo",1,1)
		goto PickupAttack
		TNT1 A 0 A_TakeInventory("Cell",1)
		goto PickupAttack

	// Attack stuff
	PickupAttack:
		TNT1 A 0 A_PlaySound("doomguy/stronghold/repfir",CHAN_WEAPON)
		TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
		TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
	PickupAttackDM:
		TNT1 A 0 A_FireBullets(20,10,2,10,"SamsaraDoomguyStrRepPuffNoDamage",FBF_NOFLASH)
		goto PickupFinish

	PickupAttackCoop:
		TNT1 A 0 A_FireBullets(20,10,2,10,"SamsaraDoomguyStrRepPuffNoDamageCoop",FBF_NOFLASH)
		goto PickupFinish

	// Finishing stuff
	PickupFinish:
		TNT1 A 0 A_GiveInventory("SamsaraDoomguyStrRepeaterHeatAmount")
		stop
	}
}

// Puff

actor SamsaraDoomguyStrRepPuff
{
	Radius 3
	Height 3
	DamageType "DoomDamageTypeRepeater"
	RenderStyle Add
	Alpha 0.75
	SeeSound "doomguy/stronghold/rephit"
	AttackSound "doomguy/stronghold/rephit"
	Decal "MummyScorch"
	+ALWAYSPUFF
	//+BLOODLESSIMPACT
	+NOBLOCKMAP
	+NOGRAVITY
	+NOTIMEFREEZE
	+PUFFGETSOWNER
	+PUFFONACTORS
	States
	{
	Spawn:
		RPFF A 0 Bright NoDelay Light("RepeaterBlast1") A_Explode(32,88)
	SpawnFinish:
		RPFF A 3 Bright Light("RepeaterBlast1") A_ChangeFlag("NOTIMEFREEZE",false)
		RPFF B 3 Bright Light("RepeaterBlast2")
		RPFF C 3 Bright Light("RepeaterBlast3")
		RPFF D 3 Bright Light("RepeaterBlast4")
		RPFF E 3 Bright Light("RepeaterBlast5")
		RPFF F 3 Bright Light("RepeaterBlast6")
		stop
	}
}

actor SamsaraDoomguyStrRepPuffCoop : SamsaraDoomguyStrRepPuff { +MTHRUSPECIES }

actor SamsaraDoomguyStrRepPuffNoDamage : SamsaraDoomguyStrRepPuff
{
	DamageType "DoomDamageTypeRepeaterNoSelfDamage"
	States
	{
	Spawn:
		RPFF A 0 Bright NoDelay Light("RepeaterBlast1") A_GiveToTarget("SamsaraDoomguyNoSelfDamage")
		TNT1 A 0 A_Explode(32,88)
		TNT1 A 0 A_TakeFromTarget("PowerSamsaraDoomguyNoSelfDamage")
		goto SpawnFinish
	}
}

actor SamsaraDoomguyStrRepPuffNoDamageCoop : SamsaraDoomguyStrRepPuffNoDamage { +MTHRUSPECIES }

// Steam

actor SamsaraDoomguyStrRepSteam
{
	Projectile
	Radius 0
	Height 1
	Speed 25
	RenderStyle "Translucent"
	Alpha 0.35
	SeeSound "doomguy/stronghold/repstm"
	+CLIENTSIDEONLY
	+DONTBLAST
	+DONTREFLECT
	+DONTSPLASH
	+NOTONAUTOMAP
	+NOTRIGGER
	+THRUACTORS
	States
	{
	Spawn:
		TNT1 A 3
		WSMK ABCDE 2
		stop
	}
}
