Actor DeusEx_GlockMagazine : Ammo
{
	+INVENTORY.IGNORESKILL
	Inventory.Amount 1
	Inventory.MaxAmount 6
}

Actor DeusEx_10mmPistol : DeusExWeapon
{
	Weapon.SelectionOrder 1700
	Weapon.SlotNumber 1
	Weapon.SlotPriority 1
	Weapon.AmmoType1 "DeusEx_GlockMagazine"
	Weapon.AmmoUse1 1
	Weapon.AmmoType2 "Clip"
	Weapon.AmmoUse2 0
	Tag "Pistol"
	Weapon.UpSound "DeusEx/10mm/Select"
	Inventory.PickupMessage "You found a Pistol."
	Obituary "%o was gatted with %k's 10mm Pistol."
	States
	{
	  Select:
			TNT1 AAAAAAAAAAAAAAAAAAAA 0 A_Raise
			TNT1 A 1 A_Raise
			loop
	  Ready:
			DX00 B 0 A_GiveInventory("DeusEx_GlockEquipped", 1)
			DX00 BCDEFGHIJKLMNOP 1
			DX00 P 0 A_TakeInventory("DeusEx_Reloading", 1)
			Goto ReadyLoop
	  ReadyLoop:
			DX00 Q 1 A_WeaponReady(WRF_ALLOWRELOAD) DX00 Q 0 A_Jump(3, "Idle1", "Idle2", "Idle3")
			Loop
	  Idle1:
			DX01 "XYZ[\]" 5 A_WeaponReady(WRF_ALLOWRELOAD)
			DX02 ABC 5 A_WeaponReady(WRF_ALLOWRELOAD)
			Goto ReadyLoop
	  Idle2:
			DX02 DEFGHIJKLMNO 5 A_WeaponReady(WRF_ALLOWRELOAD)
			Goto ReadyLoop
	  Idle3:
			DX02 PQRSTUVWXY 5 A_WeaponReady(WRF_ALLOWRELOAD)
			Goto ReadyLoop
	  Deselect:
			DX01 U 0 A_TakeInventory("DeusEx_GlockEquipped", 1)
			DX01 U 0 A_TakeInventory("DeusEx_GlockSpread", 120)
			DX01 U 0 A_GiveInventory("DeusEx_Reloading", 1)
			DX01 UV 2
			Goto Super::InstantDeselect
		Fire:
			DX00 R 0 BRIGHT A_JumpIfInventory("DeusEx_GlockMagazine", 1, "Fire2")
			Goto FireReload
		Fire2:
			DX00 R 0 BRIGHT A_GiveInventory("DeusEx_GlockSpread", 8)
			DX00 R 0 A_FireBullets(0,0,1,0,"SamsaraBlankPuff",FBF_NORANDOM|FBF_NOFLASH)
			DX00 R 0 A_JumpIfInventory("CoopModeOn",1,2)
			DX00 R 3 BRIGHT A_FireCustomMissile("DeusEx_GlockBullet",CallACS("DeusEx_Decorate", 5, 0, 1)*frandom(-1.0,1.0), 1, 4, -1, 1, CallACS("DeusEx_Decorate", 5, 1, 1)*FRandom(-0.5,0.5))
			Goto FireEnd
			DX00 R 3 BRIGHT A_FireCustomMissile("DeusEx_GlockBulletCoop",CallACS("DeusEx_Decorate", 5, 0, 1)*frandom(-1.0,1.0), 1, 4, -1, 1, CallACS("DeusEx_Decorate", 5, 1, 1)*FRandom(-0.5,0.5))
			Goto FireEnd
		FireEnd:
			DX00 S 0 A_PlaySound("DeusEx/10mm/Fire", CHAN_WEAPON)
			DX00 S 0 A_SpawnItemEx("DeusEx_ShellCasing1", 8, -16, 39, Random(1,3), 0, 3, -90, SXF_NOCHECKPOSITION|SXF_MULTIPLYSPEED|SXF_TRANSFERPITCH)
			DX00 S 4 A_AlertMonsters(1000)
			DX00 T 4
			DX00 UV 3
			DX00 W 2 A_Refire
			Goto ReadyLoop
		Reload:
			DX00 Z 0 A_JumpIfInventory("DeusEx_GlockMagazine", 0, "ReadyLoop")
			DX00 Z 0 A_JumpIfInventory("Clip", 1, "ReloadSuccess")
			Goto ReadyLoop
		ReloadSuccess:
			DX00 Z 0 A_GiveInventory("DeusEx_Reloading", 1)
			DX00 "Z[\]" 2
			DX01 A 2
			DX01 B 0 A_PlaySound("DeusEx/10mm/Reload", CHAN_WEAPON)
			DX01 BCDEFGHIJKL 2
			DX01 M 0 A_JumpIfInventory("DeusEx_PistolSkill", 4, 12)
			DX01 M 0 A_JumpIfInventory("DeusEx_PistolSkill", 3, 7)
			DX01 M 0 A_JumpIfInventory("DeusEx_PistolSkill", 2, 4)
			DX01 M 0 A_JumpIfInventory("DeusEx_PistolSkill", 1, 1)
			DX01 MNOMNOMNO 5 A_WeaponReady(WRF_NOFIRE)
			goto ReloadAmmo
		ReloadAmmo:
			TNT1 A 0 A_JumpIfInventory("DeusEx_GlockMagazine",0,"ReloadFinish")
			//TNT1 A 0 A_JumpIfInventory("Clip",1,1)
			//goto ReloadFinish
			TNT1 A 0 A_GiveInventory("DeusEx_GlockMagazine",1)
			//TNT1 A 0 A_TakeInventory("Clip",1,TIF_NOTAKEINFINITE)
			loop 
		ReloadFinish:
			DX01 P 0 A_TakeInventory("DeusEx_Reloading", 1)
			DX01 PQRST 2 A_WeaponReady(WRF_NOFIRE)
			Goto ReadyLoop
		FireReload:
			DX00 Q 0 A_PlaySound("DeusEx/DryFire", CHAN_WEAPON)
			DX00 QQQ 1
			DX00 Q 1 A_ClearRefire
			Goto Reload
	}
}

Actor DeusEx_GlockSpread : Ammo { +INVENTORY.UNDROPPABLE Inventory.MaxAmount 120 Inventory.InterHubAmount 0 Ammo.BackPackAmount 0 Ammo.BackPackMaxAmount 120 }

Actor DeusEx_GlockBullet : DeusExBullet
{
	Damage(CallACS("DeusEx_WeaponSkillDamage", 1, 14))
	States
	{
		Spawn:
			TNT1 A 0 NODELAY //A_SetUserVar(user_damage, CallACS("DeusEx_WeaponSkillDamage", 1, 14))
			TNT1 A 1 //A_SetUserVar(user_damage, CallACS("DeusEx_WeaponSkillDamage", 1, 14))
			Goto Spawn2
		Crash:
			TNT1 A 0 A_PlaySound("DeusEx/Bullet/HitRobot", CHAN_BODY)
			TNT1 A 1 A_Explode(CallACS("DeusEx_DamageModifiers",CallACS("DeusEx_WeaponSkillDamage", 1, 14)),8,!XF_HURTSOURCE,0,8)
			stop
		XDeath:
			//TNT1 A 0 A_PlaySound("DeusEx/Bullet/HitFlesh", CHAN_BODY)
			TNT1 A 1 A_Explode(CallACS("DeusEx_DamageModifiers",CallACS("DeusEx_WeaponSkillDamage", 1, 14)),8,!XF_HURTSOURCE,0,8)
			stop
	}
}

Actor DeusEx_GlockBulletCoop : DeusEx_GlockBullet { +THRUSPECIES Species "Player" }

Actor DeusEx_StealthMagazine : Ammo
{
	+INVENTORY.IGNORESKILL
	Inventory.Amount 1
	Inventory.MaxAmount 10
}

Actor DeusEx_StealthPistol : DeusEx_10mmPistol
{
	Weapon.AmmoType1 "DeusEx_StealthMagazine"
	Weapon.AmmoUse1 1
	Weapon.SlotPriority 3
	Tag "Pistol"
	Weapon.UpSound "DeusEx/Stealth/Select"
	Inventory.PickupMessage "You found a Stealth Pistol."
	Obituary "%o was wetworked with %k's Stealth Pistol."
	+WEAPON.CHEATNOTWEAPON
	States
	{
		Spawn:
			DX03 A -1
			Stop
		Ready:
			DX00 B 0 A_GiveInventory("DeusEx_StealthEquipped", 1)
			DX00 BCDE 2 
			DX00 E 0 A_TakeInventory("DeusEx_Reloading", 1)
			Goto ReadyLoop
		Fire:
			DX00 R 0 BRIGHT A_JumpIfInventory("DeusEx_StealthMagazine", 1, "Fire2")
			Goto FireReload
		Fire2:
			DX00 F 0 A_GiveInventory("DeusEx_StealthSpread", 5)
			DX00 F 0 A_FireBullets(0,0,1,0,"SamsaraBlankPuff",FBF_NORANDOM|FBF_NOFLASH)
			DX00 F 0 A_JumpIfInventory("CoopModeOn",1,2)
			DX00 F 3 A_FireCustomMissile("DeusEx_StealthPistolBullet",CallACS("DeusEx_Decorate", 5, 5, 1)*frandom(-1.0,1.0), 1, 12, 0, 1, CallACS("DeusEx_Decorate", 5, 5, 1)*FRandom(-0.5,0.5))
			Goto FireEnd
			DX00 F 3 A_FireCustomMissile("DeusEx_StealthPistolBulletCoop",CallACS("DeusEx_Decorate", 5, 5, 1)*frandom(-1.0,1.0), 1, 12, 0, 1, CallACS("DeusEx_Decorate", 5, 5, 1)*FRandom(-0.5,0.5))
			Goto FireEnd
		FireEnd:
			DX00 F 0 A_SpawnItemEx("DeusEx_ShellCasing1", 8, -16, 39, Random(1,3), 0, 3, -90, SXF_NOCHECKPOSITION|SXF_MULTIPLYSPEED|SXF_TRANSFERPITCH)
			DX00 G 0 A_PlaySound("DeusEx/Stealth/Fire", CHAN_WEAPON)
			DX00 G 3
			DX00 HI 3
			DX00 J 2 A_Refire("Fire")
			Goto ReadyLoop
		Reload:
			DX00 M 0 A_JumpIfInventory("DeusEx_StealthMagazine", 0, "ReadyLoop")
			DX00 M 0 A_JumpIfInventory("Clip", 1, "ReloadSuccess")
			Goto ReadyLoop
		ReloadSuccess:
			DX00 M 0 A_PlaySound("DeusEx/Stealth/Reload1", CHAN_WEAPON)
			DX00 MNOPQ 4 A_WeaponReady(WRF_NOFIRE)
			DX00 R 0 A_JumpIfInventory("DeusEx_PistolSkill", 4, "ReloadAmmo")
			DX00 R 0 A_JumpIfInventory("DeusEx_PistolSkill", 3, "ReloadAmmo")
			DX00 R 0 A_JumpIfInventory("DeusEx_PistolSkill", 2, 12)
			DX00 R 0 A_JumpIfInventory("DeusEx_PistolSkill", 1, 6)
			DX00 RSTUVRSTUVRSTUV 6 A_WeaponReady(WRF_NOFIRE)
			Goto ReloadAmmo
		ReloadAmmo:
			TNT1 A 0 A_JumpIfInventory("DeusEx_StealthMagazine",0,"ReloadFinish")
			TNT1 A 0 A_JumpIfInventory("Clip",1,1)
			goto ReloadFinish
			TNT1 A 0 A_GiveInventory("DeusEx_StealthMagazine",1)
			TNT1 A 0 A_TakeInventory("Clip",1,TIF_NOTAKEINFINITE)
			loop
		ReloadFinish:
			DX00 V 0 A_PlaySound("DeusEx/Stealth/Reload2", CHAN_WEAPON)
			DX00 WXY 4
			Goto ReadyLoop
		Deselect:
			DX00 Z 0 A_TakeInventory("DeusEx_GlockEquipped", 1)
			DX00 Z 0 A_TakeInventory("DeusEx_StealthSpread", 900)
			DX00 Z 0 A_GiveInventory("DeusEx_Reloading", 1)
			DX00 "Z[\" 3
			Goto Super::InstantDeselect
		ReadyLoop:
			DX00 "]" 1 A_WeaponReady(WRF_ALLOWRELOAD)  DX00 "]" 0 A_Jump(3, "Idle1", "Idle2")
			Loop
		Idle1:
			DX01 ABCDEF 6 A_WeaponReady(WRF_ALLOWRELOAD)
			Goto ReadyLoop
		Idle2:
			DX01 GHIJKLMNOP 6 A_WeaponReady(WRF_ALLOWRELOAD)
			DX01 QRST 6 A_WeaponReady(WRF_ALLOWRELOAD)
			Goto ReadyLoop
		FireReload:
			DX00 E 0 A_PlaySound("DeusEx/DryFire", CHAN_WEAPON)
			DX00 EEE 1
			DX00 E 1 A_ClearRefire
			Goto Reload
		}
}

Actor DeusEx_StealthPistolBullet : DeusExBullet
{
	Damage(CallACS("DeusEx_WeaponSkillDamage", 1, 8))
	States
	{
		Spawn:
			TNT1 A 0 NODELAY //A_SetUserVar(user_damage, CallACS("DeusEx_WeaponSkillDamage", 1, 14))
			TNT1 A 1 //A_SetUserVar(user_damage, CallACS("DeusEx_WeaponSkillDamage", 1, 14))
			Goto Spawn2
		Crash:
			TNT1 A 0 A_PlaySound("DeusEx/Bullet/HitRobot", CHAN_BODY)
			TNT1 A 1 A_Explode(CallACS("DeusEx_DamageModifiers",CallACS("DeusEx_WeaponSkillDamage", 1, 8)),8,!XF_HURTSOURCE,0,8)
			stop	
		XDeath:
			//TNT1 A 0 A_PlaySound("DeusEx/Bullet/HitFlesh", CHAN_BODY)
			TNT1 A 1 A_Explode(CallACS("DeusEx_DamageModifiers",CallACS("DeusEx_WeaponSkillDamage", 1, 8)),8,!XF_HURTSOURCE,0,8)
			stop
	}
}
Actor DeusEx_StealthPistolBulletCoop : DeusEx_StealthPistolBullet { +THRUSPECIES Species "Player" }

Actor DeusEx_StealthSpread : Ammo { +INVENTORY.UNDROPPABLE Inventory.MaxAmount 115 Inventory.InterHubAmount 0 Ammo.BackPackAmount 0 Ammo.BackPackMaxAmount 115 }