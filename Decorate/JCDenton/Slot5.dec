Actor DeusEx_GEPGunLockTimer : Inventory { Inventory.MaxAmount 20 +Inventory.UNDROPPABLE -INVBAR }
Actor DeusEx_RocketLoaded : Boolean {}
Actor DeusEx_WPLoaded : Boolean {}
Actor DeusEx_RocketFired : Boolean {}

Actor DeusEx_GEPGun : DeusExWeapon
{
	Weapon.SelectionOrder 2100
	Weapon.SlotNumber 5
	Weapon.SlotPriority 1
	Weapon.AmmoType "RocketAmmo"
	Weapon.AmmoUse 1
	Weapon.AmmoGive 4
	-WEAPON.AMMO_OPTIONAL
	Tag "Guided Explosive Projectile Gun"
	Weapon.UpSound "DeusEx/GEPGun/Select"
	Inventory.PickupMessage "You found a GEP Gun."
	Obituary "%o couldn't outrun %k's GEPs."
	States
	{
		Spawn:
			DX03 A -1
			Stop
		Select:
			TNT1 AAAAAAAAAAAAAAAAAAAA 0 A_Raise
			TNT1 A 1 A_Raise
			loop
	    Ready:
			//DX00 C 0 A_JumpIfInventory("DeusEx_HeavyWeaponSkill", 2, 2)
			DX00 C 0 A_GiveInventory("DeusEx_GEPGunEquipped", 1) //ACS_NamedExecuteAlways("DeusEx_Decorate", 0, 3)
			DX00 CDEFGHIJ 4
			DX00 J 0 A_TakeInventory("DeusEx_Reloading", 1)
			Goto ReadyLoop
		IdleFlash:
			TNT1 A 6 A_FireBullets(1,1,1,0, "DeusEx_GEPTargeterPuff", FBF_NORANDOM|FBF_NOFLASH|FBF_EXPLICITANGLE)
			Stop
		ReadyLoop:
			DX01 K 0 A_GunFlash("IdleFlash")
			DX01 K 6 A_WeaponReady(WRF_ALLOWRELOAD) DX01 K 0 A_Jump(18, "Idle1", "Idle2", "Idle3")
			Loop
		IdleFlash2:
			TNT1 AAAAAAAA 6 A_FireBullets(1,1,1,0, "DeusEx_GEPTargeterPuff", FBF_NORANDOM|FBF_NOFLASH|FBF_EXPLICITANGLE)
			Stop
		Idle1:
			DX01 K 0 A_GunFlash("IdleFlash2")
			DX01 KLMNOPQ 6 A_WeaponReady
			Goto ReadyLoop
		Idle2:
			DX01 Q 0 A_GunFlash("IdleFlash3")
			DX01 QRSTUVWXY 6 A_WeaponReady
			Goto ReadyLoop
		IdleFlash3:
			TNT1 AAAAAAAAAA 6 A_FireBullets(1,1,1,0, "DeusEx_GEPTargeterPuff", FBF_NORANDOM|FBF_NOFLASH|FBF_EXPLICITANGLE)
			Stop
		Idle3:
			DX01 Z 0 A_GunFlash("IdleFlash4")
			DX01 "Z[\]" 6 A_WeaponReady
			DX02 AB 6 A_WeaponReady
			Goto ReadyLoop
		IdleFlash4:
			TNT1 AAAAAA 6 A_FireBullets(1,1,1,0, "DeusEx_GEPTargeterPuff", FBF_NORANDOM|FBF_NOFLASH|FBF_EXPLICITANGLE)
			Stop
		Fire:
			DX00 K 0 A_JumpIfInventory("DeusEx_RocketFired", 1, "CheckRockets")
			DX00 K 0 A_JumpIfInventory("DeusEx_WPLoaded", 1, "FireWP")
			DX00 K 0 A_AlertMonsters(1200)
			DX00 K 0 A_JumpIfInventory("CoopModeOn",1,2)
			DX00 K 5 A_FireCustomMissile("DeusEx_GEPRocket", 0, 1, 14, 3)
			Goto FireContinue
			DX00 K 5 A_FireCustomMissile("DeusEx_GEPRocketCoop", 0, 1, 14, 3)
			Goto FireContinue
		FireWP:
			DX00 K 0 A_JumpIfInventory("DeusEx_RocketFired", 1, "CheckRockets")
			DX00 K 0 A_AlertMonsters(1200)
			DX00 K 0 A_JumpIfInventory("CoopModeOn",1,2)
			DX00 K 5 A_FireCustomMissile("DeusEx_WPRocket", 0, 1, 14, 3)
			Goto FireContinue
			DX00 K 5 A_FireCustomMissile("DeusEx_WPRocketCoop", 0, 1, 14, 3)
			Goto FireContinue
		FireContinue:
			DX00 L 0 A_PlaySound("DeusEx/GEPGun/Fire", CHAN_WEAPON)
			DX00 L 0 A_GiveInventory("DeusEx_RocketFired", 1)
			DX00 L 4
			DX00 MN 3
			Goto CheckRockets
		Reload:
			DX00 K 0 A_JumpIfInventory("DeusEx_RocketFired", 1, "CheckRockets")
			Goto ReadyLoop
		CheckRockets:
			TNT1 A 0 A_JumpIfInventory("RocketAmmo", 1, "FireEnd")
			Goto ReadyLoop
		FireEnd:
			DX00 Y 0 A_PlaySound("DeusEx/GEPGun/Reload", CHAN_BODY)
			DX00 "YZ[\]" 6 A_WeaponReady(WRF_NOFIRE)
			DX01 ABCDE 5 A_WeaponReady(WRF_NOFIRE)
			DX01 F 0 A_TakeInventory("DeusEx_RocketFired", 999)
			DX01 FGHIJ 7 A_WeaponReady(WRF_NOFIRE)
			Goto ReadyLoop
		Deselect:
			DX00 O 0 A_TakeInventory("DeusEx_GEPGunLockTimer", 999)
			DX00 O 0 A_TakeInventory("DeusEx_RocketFired", 1)
			DX00 O 0 A_TakeInventory("Unreal_TargetLocked", 999)
			DX00 O 0 A_GiveInventory("DeusEx_Reloading", 1)
			DX00 OPQRSTUV 4
			DX00 W 4 A_TakeInventory("DeusEx_GEPGunEquipped", 999) //ACS_NamedExecuteAlways("DeusEx_Decorate", 0, 4)
			Goto Super::InstantDeselect
		AltFire:
			DX00 Y 0 A_JumpIfInventory("DeusEx_RocketFired", 1, "CheckRockets")
			DX00 Y 0 A_JumpIfInventory("DeusEx_WPLoaded", 1, "LoadRocket")
			DX00 Y 0 A_JumpIfInventory("RocketAmmo", 1, "Altfire1Success")
			Goto ReadyLoop
		AltFireSuccess:
			DX00 Y 0 A_GiveInventory("DeusEx_Reloading", 1)
			DX00 Y 0 A_PlaySound("DeusEx/GEPGun/Reload", CHAN_BODY)
			DX00 "YZ[\]" 6 A_WeaponReady(WRF_NOFIRE)
			DX01 ABCDE 5 A_WeaponReady(WRF_NOFIRE)
			DX01 FGHIJ 7 A_WeaponReady(WRF_NOFIRE)
			DX01 J 0 A_JumpIfInventory("DeusEx_WPLoaded", 1, "ChangeToRocket")
			Goto ChangeToWP
		ChangeToWP:
			TNT1 A 0 A_TakeInventory("DeusEx_RocketLoaded", 999)
			TNT1 A 0 A_GiveInventory("DeusEx_WPLoaded", 1)
			TNT1 A 0 A_Print("GEP Gun now has WP Rockets loaded")
			Goto ReadyLoop
		LoadRocket:
			DX00 Y 0 A_JumpIfInventory("RocketAmmo", 1, "AltfireSuccess")
			Goto ReadyLoop
		ChangeToRocket:
			TNT1 A 0 A_TakeInventory("DeusEx_WPLoaded", 999)
			TNT1 A 0 A_GiveInventory("DeusEx_RocketLoaded", 1)
			TNT1 A 0 A_Print("GEP Gun now has Rockets loaded")
			Goto ReadyLoop
	}
}

Actor DeusEx_GEPRocket
{
	Projectile
	Radius 8
	Height 6
	Damage (0)
	Speed 20
	Scale 0.5
	+FORCERADIUSDMG
	DamageType "DeusExExploded"
	ActiveSound "DeusEx/GEP/Approach"
	States
	{
		Spawn:
			DX00 A 0 A_LoopActiveSound
			DX00 A 0 A_JumpIfInTargetInventory("Unreal_TargetLocked", 1, "SpawnLocked")
			Goto SpawnLoop
		SpawnLoop:
			DX00 A 1 BRIGHT A_SpawnItemEx("DeusEx_Smoke",0,0,0,0,0,0,0, SXF_NOCHECKPOSITION)
			Loop
		SpawnLocked:
			DX00 A 1 BRIGHT ACS_NamedExecuteAlways("rocketFollow",0,0,0,0)
			DX00 A 1 BRIGHT
			Goto SpawnLoop
		Death:
			TNT1 A 0 A_PlaySound("DeusEx/GEPGun/Explode", CHAN_BODY)
			TNT1 A 0 A_SpawnItemEX("DeusEx_ExplosionMid", 0,0,0,0,0,0,0, SXF_NOCHECKPOSITION|SXF_CLIENTSIDE)
			TNT1 A 0 A_SpawnItemEX("DeusEx_ShockWaveX", 0,0,0,0,0,0,0, SXF_NOCHECKPOSITION|SXF_CLIENTSIDE)
			TNT1 A 0 A_SpawnItemEX("DeusEx_ShockWaveY", 0,0,0,0,0,0,0, SXF_NOCHECKPOSITION|SXF_CLIENTSIDE)
			TNT1 A 0 A_AlertMonsters(3000)
			TNT1 A 2 A_Explode(CallACS("DeusEx_WeaponSkillDamage", 5, 120), 38, 1, 1, 16)
			TNT1 A 2 A_Explode(CallACS("DeusEx_WeaponSkillDamage", 5, 120), 77, 1, 1, 16)
			TNT1 A 2 A_Explode(CallACS("DeusEx_WeaponSkillDamage", 5, 120), 115, 1, 1, 16)
			TNT1 A 2 A_Explode(CallACS("DeusEx_WeaponSkillDamage", 5, 120), 154, 1, 1, 16)
			TNT1 A 2 A_Explode(CallACS("DeusEx_WeaponSkillDamage", 5, 120), 192, 1, 1, 24)
			Stop
	}
}

Actor DeusEx_GEPRocketCoop : DeusEx_GEPRocket { Species "Player" +THRUSPECIES }

Actor DeusEx_WPRocket : DeusEx_GEPRocket
{
	DamageType "DeusExBurned"
	PoisonDamageType "DeusExFlamed"
	PoisonDamage 5, 30, 35
	States
	{
		Death:
			TNT1 A 0 A_PlaySound("DeusEx/GEPGun/Explode", CHAN_BODY)
			TNT1 A 0 A_SpawnItemEX("DeusEx_ExplosionMid", 0,0,0,0,0,0,0, SXF_NOCHECKPOSITION|SXF_CLIENTSIDE)
			TNT1 A 0 A_SpawnItemEX("DeusEx_ShockWaveX", 0,0,0,0,0,0,0, SXF_NOCHECKPOSITION|SXF_CLIENTSIDE)
			TNT1 A 0 A_SpawnItemEX("DeusEx_ShockWaveY", 0,0,0,0,0,0,0, SXF_NOCHECKPOSITION|SXF_CLIENTSIDE)
			TNT1 A 0 A_AlertMonsters(3000)
			TNT1 A 2 A_Explode(CallACS("DeusEx_WeaponSkillDamage", 5, 60), 102)
			TNT1 AAAAAA 0 A_CustomMissile("DeusEx_WPProjectile1", 32, 0, Random(0,359), CMF_AIMOFFSET|CMF_TRACKOWNER)
			TNT1 A 2 A_Explode(CallACS("DeusEx_WeaponSkillDamage", 5, 60), 204)
			TNT1 AAAAAA 0 A_CustomMissile("DeusEx_WPProjectile2", 32, 0, Random(0,359), CMF_AIMOFFSET|CMF_TRACKOWNER)
			TNT1 A 2 A_Explode(CallACS("DeusEx_WeaponSkillDamage", 5, 60), 306)
			TNT1 AAAAAA 0 A_CustomMissile("DeusEx_WPProjectile3", 32, 0, Random(0,359), CMF_AIMOFFSET|CMF_TRACKOWNER)
			TNT1 A 2 A_Explode(CallACS("DeusEx_WeaponSkillDamage", 5, 60), 408)
			TNT1 AAAAAA 0 A_CustomMissile("DeusEx_WPProjectile4", 32, 0, Random(0,359), CMF_AIMOFFSET|CMF_TRACKOWNER)
			TNT1 A 2 A_Explode(CallACS("DeusEx_WeaponSkillDamage", 5, 60), 512)
			TNT1 AAAAAA 0 A_CustomMissile("DeusEx_WPProjectile5", 32, 0, Random(0,359), CMF_AIMOFFSET|CMF_TRACKOWNER)
			TNT1 A 1
			Stop
	}
}

Actor DeusEx_WPRocketCoop : DeusEx_WPRocket { Species "Player" +THRUSPECIES }

Actor DeusEx_WPProjectile1 : FastProjectile
{
	Projectile
	Speed 51
	Damage (0)
	Height 4
	Radius 2
	PoisonDamage 5, 30, 35
	DamageType "DeusExFlamed"
	PoisonDamageType "DeusExFlamed"
	Obituary "%k bathed %o in flames."
	+NOEXTREMEDEATH
	+NODAMAGETHRUST
	-BLOODSPLATTER
	+BLOODLESSIMPACT
	+NODAMAGETHRUST
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 A 1
			Stop
		Crash:
		Death:
		XDeath:
			TNT1 A 1
			Stop
	}
}

Actor DeusEx_WPProjectile2 : DeusEx_WPProjectile1 { Speed 102 }
Actor DeusEx_WPProjectile3 : DeusEx_WPProjectile1 { Speed 153 }
Actor DeusEx_WPProjectile4 : DeusEx_WPProjectile1 { Speed 204 }
Actor DeusEx_WPProjectile5 : DeusEx_WPProjectile1 { Speed 256 }

Actor DeusEx_GEPTargeterPuff
{
	Scale 0.5
	+BLOODLESSIMPACT
	+NOBLOCKMAP
    +NOGRAVITY
	-SKYEXPLODE
	+FORCEXYBILLBOARD
	+CLIENTSIDEONLY
	+ALWAYSPUFF
    +PUFFONACTORS
	+PUFFGETSOWNER
    +DONTSPLASH
	+FIXMAPTHINGPOS
	+FORCEXYBILLBOARD
	renderstyle add
	alpha 1.0
	Mass 0
    Radius 1
    Height 1
	states
	{
	Spawn:
	XDeath:
	Melee:
		TNT1 A 0
		TNT1 A 0 A_GiveToTarget("DeusEx_GEPGunLockTimer", 3)
		TNT1 A 0 A_JumpIfInTargetInventory("DeusEx_GEPGunLockTimer", 0, "LockON")
		TNT1 A 1 A_GiveToTarget("DeusEx_TrackSound", 1)
		Stop
	Crash:
		TNT1 A 0
		TNT1 A 0 //A_TakeFromTarget("Unreal_TargetLocked", 999)
		TNT1 A 0 A_TakeFromTarget("DeusEx_GEPGunLockTimer", 5)
		TNT1 A 1 A_JumpIfInTargetInventory("DeusEx_GEPGunLockTimer", 1, 1)
		Goto LoseLock
		TNT1 A 1
		Stop
	LockOn:
		TNT1 A 0
		TNT1 A 0 //A_JumpIfInTargetInventory("Unreal_TargetLocked", 1, 2)
		TNT1 A 0 A_GiveToTarget("DeusEx_LockOnSound", 1)
		TNT1 A 0 A_GiveToTarget("Unreal_TargetLocked", 1)
		TNT1 A 0 A_TakeFromTarget("Unreal_TargetLost", 999)
		TNT1 A 0 A_SpawnItemEx("Unreal_SeekerActor",0,0,0,0,0,0,0, SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS) //A_GiveInventory("Unreal_MarkedForDeath", 24, AAPTR_TRACER)
		TNT1 A 1
		Stop
	LoseLock:
		TNT1 A 0
		//TNT1 A 0 A_JumpIfInTargetInventory("Unreal_TargetLost", 1, 2)
		TNT1 A 0 A_GiveToTarget("Unreal_TargetLost", 1)
		TNT1 A 0 A_TakeFromTarget("Unreal_TargetLocked", 999)
		TNT1 A 1
		Stop
	}
}

Actor DeusEx_LockOnSound : CustomInventory
{
	-COUNTITEM
	+INVENTORY.ALWAYSPICKUP
	+INVENTORY.AUTOACTIVATE
	Inventory.MaxAmount 1
	Inventory.PickupMessage ""
	Inventory.PickupSound ""
	States
	{
		Pickup:
			TNT1 A 0
			TNT1 A 0
		Use:
			TNT1 A 0
			//TNT1 A 0 ACS_NamedExecuteAlways("Unreal_EightballReticle", 0)
			TNT1 A 0 A_PlaySound("DeusEx/GEPGun/Lock", CHAN_BODY, 1.0)
			TNT1 A 1
			Stop
	}
}

Actor DeusEx_TrackSound : CustomInventory
{
	-COUNTITEM
	+INVENTORY.ALWAYSPICKUP
	+INVENTORY.AUTOACTIVATE
	Inventory.MaxAmount 1
	Inventory.PickupMessage ""
	Inventory.PickupSound ""
	States
	{
		Pickup:
			TNT1 A 0
			TNT1 A 0
		Use:
			TNT1 A 0
			TNT1 A 0 A_PlaySound("DeusEx/GEPGun/Track", CHAN_BODY, 1.0)
			TNT1 A 1
			Stop
	}
}