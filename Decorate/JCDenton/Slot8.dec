Actor DeusEx_LAWAmmo : Ammo
{
	+INVENTORY.IGNORESKILL
	Inventory.Amount 1
	Inventory.MaxAmount 3
	Inventory.Icon AMSMO0
}	

Actor DeusEx_LAW : DeusExWeapon
{
	Weapon.SelectionOrder 2400
	Weapon.SlotNumber 8
	Weapon.AmmoType "DeusEx_LAWAmmo"
	Weapon.AmmoUse 1
	Weapon.AmmoGive 1
	Tag "Light Anti-Tank Weapon"
	Weapon.UpSound "DeusEx/LAW/Select"
	Inventory.PickupMessage "You found a LAW."
	Obituary "%o fought the LAW and the LAW won."
	States
	{
		Spawn:
			DX03 A -1
			Stop
		Select:
			TNT1 AAAAAAAAAAAAAAAAAAAA 0 A_Raise
			TNT1 A 1 A_Raise
			loop
	    Ready:
			DX00 B 0 A_JumpIfInventory("DeusEx_LAWAmmo", 1, 1)
			Goto ReadyNoAmmo
//			DX00 B 0 A_JumpIfInventory("DeusEx_HeavyWeaponSkill", 2, 2)
			DX00 B 0 A_GiveInventory("DeusEx_LAWEquipped", 1) //ACS_NamedExecuteAlways("DeusEx_Decorate", 0, 3)
			DX00 BCDEFGHIJK 4
			DX00 K 0 A_TakeInventory("DeusEx_Reloading", 1)
			Goto ReadyLoop
		SelectNoAmmo:
			TNT1 AAAA 2
			Goto ReadyNoAmmo
		ReadyLoop:
			DX00 L 1 A_WeaponReady DX00 L 0 A_Jump(3, "Idle1", "Idle2", "Idle3")
			Loop
		Idle1:
			DX01 "XYZ[\]" 6 A_WeaponReady
			DX02 ABC 6 A_WeaponReady
			Goto ReadyLoop
		Idle2:
			DX02 DEFGHIJKLM 6 A_WeaponReady
			Goto ReadyLoop
		Idle3:
			DX02 NOPQRS 6 A_WeaponReady
			Goto ReadyLoop
		ReadyNoAmmo:
			TNT1 A 0 A_JumpIfInventory("DeusEx_LAWAmmo", 1, "GrabAnother")
			TNT1 A 1 A_WeaponReady
			Loop
		Fire:
			DX00 L 0 A_JumpIfInventory("DeusEx_LAWAmmo", 1, 1)
			Goto DropLAW
			DX00 M 0 BRIGHT A_JumpIfInventory("CoopModeOn",1,2)
			DX00 M 5 BRIGHT A_FireCustomMissile("DeusEx_LAWMissile", Random(-3,3), 1, 16, 5)
			Goto FireEnd
			DX00 M 5 BRIGHT A_FireCustomMissile("DeusEx_LAWMissileCoop", Random(-3,3), 1, 16, 5)
			Goto FireEnd
		FireEnd:
			DX00 N 4 BRIGHT A_PlaySound("DeusEx/LAW/Fire", CHAN_WEAPON)
			DX00 OPQRSTUVWXYZ 4 BRIGHT
			DX00 "[\]" 4 
			Goto DropLAW
		DropLAW:
			DX01 A 0 A_GiveInventory("DeusEx_Reloading", 1)
			DX01 ABCDEFGHIJKL 2
			DX01 M 2 A_SpawnItemEx("DeusEx_LAWDrop", 8, -16, 24, 4, 0, 3, -90, SXF_NOCHECKPOSITION|SXF_MULTIPLYSPEED|SXF_TRANSFERPITCH)
			Goto CheckAmmo
		CheckAmmo:
			DX00 B 0 A_JumpIfInventory("DeusEx_LAWAmmo", 1, 1)
			Goto DeselectNoAmmo
			TNT1 A 0 A_JumpIfInventory("DeusEx_HeavyWeaponSkill", 4, 3)
			TNT1 A 0 A_JumpIfInventory("DeusEx_HeavyWeaponSkill", 3, 1)
			TNT1 AAA 35
			Goto GrabAnother
		GrabAnother:
			DX00 B 0 A_PlaySound("DeusEx/LAW/Select", CHAN_WEAPON)
			DX00 BCDEFGHIJK 4
			DX00 K 0 A_TakeInventory("DeusEx_Reloading", 1)
			Goto ReadyLoop
		Deselect:
			DX00 W 0 A_JumpIfInventory("DeusEx_LAWAmmo", 1, 1)
			Goto DeselectNoAmmo
			DX01 O 0 A_GiveInventory("DeusEx_Reloading", 1)
			DX01 OPQRSTU 4
			DX01 V 4 A_TakeInventory("DeusEx_LAWEquipped", 999) //ACS_NamedExecuteAlways("DeusEx_Decorate", 0, 4)
			Goto Super::InstantDeselect
		DeselectNoAmmo:
			TNT1 A 2
			Goto Super::InstantDeselect
	}
}

Actor DeusEx_LAWMissile 
{
	Projectile
	Radius 8
	Height 6
	Damage (0)
	Speed 15
	Scale 1.75
	+FORCERADIUSDMG
	DamageType "DeusExExploded"
	ActiveSound "DeusEx/LAW/Approach"
	States
	{
		Spawn:
			DX00 A 0 A_LoopActiveSound
			Goto SpawnLoop
		SpawnLoop:
			DX00 A 1 BRIGHT A_SpawnItemEx("DeusEx_Smoke",0,0,0,0,0,0,0, SXF_NOCHECKPOSITION)
			Loop
		Death:
			TNT1 A 0 A_PlaySound("DeusEx/LAW/Explode", CHAN_BODY, 1.5, false, ATTN_NONE)
			TNT1 A 0 A_SpawnItemEX("DeusEx_ExplosionLarge", 0,0,0,0,0,0,0, SXF_NOCHECKPOSITION|SXF_CLIENTSIDE)
			TNT1 A 0 A_SpawnItemEX("DeusEx_ShockWaveXHuge", 0,0,0,0,0,0,0, SXF_NOCHECKPOSITION|SXF_CLIENTSIDE)
			TNT1 A 0 A_SpawnItemEX("DeusEx_ShockWaveYHuge", 0,0,0,0,0,0,0, SXF_NOCHECKPOSITION|SXF_CLIENTSIDE)
			TNT1 A 0 A_AlertMonsters(3000)
			TNT1 A 2 A_Explode(CallACS("DeusEx_WeaponSkillDamage", 5, 300), 154, 1, 1, 154)
			TNT1 A 2 A_Explode(CallACS("DeusEx_WeaponSkillDamage", 5, 300), 308, 1, 1, 308)
			TNT1 A 2 A_Explode(CallACS("DeusEx_WeaponSkillDamage", 5, 300), 462, !XF_HURTSOURCE, 1, 462)
			TNT1 A 2 A_Explode(CallACS("DeusEx_WeaponSkillDamage", 5, 300), 616, !XF_HURTSOURCE, 1, 616)
			TNT1 A 2 A_Explode(CallACS("DeusEx_WeaponSkillDamage", 5, 300), 768, !XF_HURTSOURCE, 1, 768)
			Stop
	}
}

Actor DeusEx_LAWMissileCoop : DeusEx_LAWMissile { Species "Player" +THRUSPECIES }