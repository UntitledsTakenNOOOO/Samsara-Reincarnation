ACTOR SoF_Slugger : SoF_Weapon
{
	Weapon.SlotNumber 6
	Weapon.SelectionOrder 300
	Weapon.AmmoUse1 0
	Weapon.AmmoGive1 0
	Weapon.AmmoType1 "SoF_SluggerMagazine"
	Weapon.AmmoUse2 0
	Weapon.AmmoGive2 40
	Weapon.AmmoType2 "Cell"
	Obituary "%o was sawn in half by %k's slugthrower"
	Inventory.Pickupmessage "Repeating Slug Thrower"
	Tag "'Heavy Hitter' H-24"
	States
	{
		Ready:
			TNT1 A 0
			SF08 A 4 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			SF08 B 0 A_PlaySound("SoF/Weapons/autoshot/Ready",CHAN_AUTO)
			SF08 BCDEFGH 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			SF09 A 0 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		READY_IDLE:
			//Source doesn't do this, but it's weird without it
			SF09 A 0 A_PlaySound("SoF/Weapons/ipistol/Throw",CHAN_WEAPON,0.75)
			SF09 ABCDEFGHIJKLMNOPQRST 2 A_WeaponReady(WRF_ALLOWRELOAD)
			SF09 U 0 A_StopSound(CHAN_WEAPON)
			SF09 UVWX 2 A_WeaponReady(WRF_ALLOWRELOAD)
		ReadyLoop:
		IDLE:	
			SF03 A 0 A_Jump(25,"IDLEFINGER_A","IDLEFINGER_B")
			SF03 ABCDEFGHIJKLMNOPQRSTUVW 2 A_WeaponReady(WRF_ALLOWRELOAD)
			Loop
		IDLEFINGER_A:
			SF04 ABCDEFGHIJK 2 A_WeaponReady(WRF_ALLOWRELOAD)
			SF04 L 0 A_PlaySound("SoF/Weapons/Autoshot/Spin",CHAN_AUTO,0.75)
			SF04 "LMNOPQRSTUVWXYZ[\]" 2 A_WeaponReady(WRF_ALLOWRELOAD)
			SF05 "ABCDEFGHIJKLMNOPQRSTUVWXYZ[\]" 2 A_WeaponReady(WRF_ALLOWRELOAD)
			SF06 AB 2 A_WeaponReady(WRF_ALLOWRELOAD)
			Goto ReadyLoop
		IDLEFINGER_B:
			SF07 ABCDEFGHIJKLMNO 2 A_WeaponReady(WRF_ALLOWRELOAD)
			Goto ReadyLoop
		Deselect:
			SF00 A 0 A_StopSound(CHAN_WEAPON)
			SF00 ABCD 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)	
			SF00 H 0 A_PlaySound("SoF/Weapons/Autoshot/Done",CHAN_AUTO)
			Goto DeselectLoop
		Fire:
			"####" "#" 0 A_StopSound(CHAN_WEAPON)
			"####" "#" 0 A_JumpIfInventory("SoF_SluggerMagazine",1,1)
			Goto DryFire
		FIRE1:
			SF01 A 0 A_GunFlash("FLASH1",GFF_NOEXTCHANGE)
			SF01 A 0 A_GiveInventory("SoF_Slugger_FireToken")
			SF01 A 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			SF01 B 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			SF01 C 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			SF01 D 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			SF01 E 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			Goto ReadyLoop
		FLASH1:
			SFF1 A 1 Bright
			TNT1 A 1 //Skip frame
			SFF1 B 1 Bright
			Stop
		Altfire:
			"####" "#" 0 A_StopSound(CHAN_WEAPON)
			"####" "#" 0 A_JumpIfInventory("SoF_SluggerMagazine",2,1)
			Goto DryFire
		FIRE2:
			SF02 A 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			SF02 B 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			SF02 C 0 A_GiveInventory("SoF_Slugger_AltFireToken")
			SF02 C 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			SF02 D 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			SF02 E 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			SF02 F 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			SF02 G 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			SF02 H 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			SF02 I 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			SF02 J 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			SF02 K 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			SF02 L 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			SF02 M 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			SF02 N 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			SF02 O 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			SF02 P 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			SF02 Q 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			SF02 R 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			SF02 S 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			SF02 T 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			SF02 U 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			SF02 V 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			SF02 W 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			SF02 X 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			Goto ReadyLoop
		Reload:
			"####" "#" 0 A_JumpIfInventory("SoF_SluggerMagazine",0,"ReadyLoop")
			"####" "#" 0 A_JumpIfInventory("Cell",2,1)
			Goto ReadyLoop
		RELOAD_LOOP:
			SF0A A 0 A_JumpIfInventory("Cell",2,1)
			Goto RELOAD_FINISH
			SF0A A 0 A_GiveInventory("SoF_SluggerMagazine",1)
			SF0A A 0 A_TakeInventory("Cell",2,TIF_NOTAKEINFINITE)
			SF0A A 0 A_JumpIfInventory("SoF_SluggerMagazine",0,"RELOAD_FINISH")
			Loop
		RELOAD_FINISH:
			SF0A ABCDEFGHIJKLMNOP 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			SF0A M 0 A_PlaySound("SoF/Weapons/Autoshot/PullOut",CHAN_AUTO)
			SF0A MNOPQRSTU 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			SF0A V 0 A_PlaySound("SoF/Weapons/Autoshot/PutIn",CHAN_AUTO)
			SF0A "VWXYZ[\]" 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			SF0B ABCDEFGHI 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			Goto ReadyLoop
		DryFire:
			"####" "#" 0 A_PlaySound("SoF/Weapons/gpistol/Empty",CHAN_AUTO)
			SF07 ABC 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			Goto Reload
		Spawn:
			SFPU A -1
			Stop
	}
}

Actor SoF_Slugger_FireToken : Trigger
{
	States
	{
		Pickup:
			TNT1 A 0 A_AlertMonsters
			TNT1 A 0 A_TakeInventory("SoF_SluggerMagazine",1)
			TNT1 A 0 A_SpawnItemEx("SoF_MuzzleSmoke",cos(pitch)*8,1,39-(sin(pitch)*8),0.125*sin(pitch),0,abs(0.125*cos(pitch)))
			TNT1 A 0 A_PlaySound("SoF/Weapons/Autoshot/Fire",CHAN_WEAPON)	
			TNT1 A 0 ACS_NamedExecuteWithResult("GlobalRecoil",2,60)
			TNT1 A 0 A_FireCustomMissile("SoF_Slug",frandom(-1,1),0,5,4,0,frandom(-1,1))
			TNT1 A 0 A_Quake(1,4,0,16,"")
			Stop
	}
}

Actor SoF_Slugger_AltFireToken : Trigger
{
	States
	{
		Pickup:
			TNT1 A 0 A_AlertMonsters
			TNT1 A 0 A_TakeInventory("SoF_SluggerMagazine",2)
			TNT1 A 0 A_SpawnItemEx("SoF_MuzzleSmoke",cos(pitch)*8,1,39-(sin(pitch)*8),0.125*sin(pitch),0,abs(0.125*cos(pitch)))
			TNT1 A 0 A_PlaySound("SoF/Weapons/Autoshot/Fire2",CHAN_WEAPON)	
			TNT1 A 0 ACS_NamedExecuteWithResult("GlobalRecoil",10,60)
			TNT1 A 0 A_FireCustomMissile("SoF_SlugGrenade",frandom(-1,1),0,5,4,0,frandom(-1,1))
			TNT1 A 0 A_Quake(1,4,0,16,"")
			Stop
	}
}

Actor SoF_SluggerMagazine : Ammo
{
	Inventory.Amount 20
	Inventory.MaxAmount 20
}

Actor SoF_Slug : FastProjectile
{
	Height 8
	Radius 4
	Speed 80
	Damage (80)
	DamageType "SoFSlug"
	Decal "SoFExplosionScorchSmall"
	Renderstyle Add
	+BLOODLESSIMPACT
	+FORCERADIUSDMG
	+NODAMAGETHRUST
	+HITTRACER
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 A 0 ACS_NamedExecuteWithResult("SoF_Decorate",12)
			TNT1 A 2
		SpawnLoop:
			SF00 A 1 Bright
			Loop
		Death:
			TNT1 AAAAAA 0 A_SpawnItemEx("SoF_SlugExplosionSpark",0,0,0,frandom(-6,6),0,frandom(-6,6),frandom(0,360))
			TNT1 AAAA 0 A_SpawnItemEx("SoF_SlugExplosionLargeSpark",frandom(-8,8),frandom(-8,8),frandom(-8,8))
			TNT1 A 0 A_SpawnItemEx("SoF_SlugExplosionSmoke",0,0,0,frandom(-1,1),frandom(-1,1),frandom(1,2),frandom(0,360))
			TNT1 A 0 A_SpawnItemEx("SoF_SlugShockwave")
			TNT1 A 0 A_SpawnItemEx("SoF_SlugExplosionFlare")
			TNT1 A 0 A_PlaySound("SoF/Weapons/autoshot/SlugExp",CHAN_AUTO,0.75)
			TNT1 A 0 A_Explode(50,80,!XF_HURTSOURCE,true)
			TNT1 A 0 A_Explode(10,80,0,true)
			TNT1 A 0 A_Quake(3,17,0,175,"")
			TNT1 A 1 A_SpawnItemEx("SoF_ExplosionScorchSmall")
			Stop
		XDeath:
			TNT1 A 0 ACS_NamedExecuteWithResult("SoF_BloodGenerator_Offsets")
			TNT1 A 0 A_GiveInventory("SoF_GibberSeverGenerator",1,AAPTR_TRACER)
			goto Death
	}
}

Actor SoF_SlugShockwave : SoF_ExplosionShockwave
{
	Scale 0.25
	States
	{
		Spawn:
			TNT1 A 0
			SF00 J -1 Bright ACS_NamedExecuteWithResult("SoF_FX",4,10,16384)
			Stop
	}
}

Actor SoF_SlugExplosionFlare
{
	Height 2
	Radius 2
	Renderstyle Add
	Scale 0.5
	+FORCEXYBILLBOARD
	+NOINTERACTION
	+CLIENTSIDEONLY
	States
	{
		Spawn:
			TNT1 A 0
			SF00 B -1 Bright ACS_NamedExecuteWithResult("SoF_FX",4,10)
			Stop
	}
}

Actor SoF_SlugParticle
{
	Height 2
	Radius 2
	Renderstyle Add
	Scale 0.125
	+FORCEXYBILLBOARD
	+NOINTERACTION
	+CLIENTSIDEONLY
	States
	{
		Spawn:
			TNT1 A 0
			SF00 K -1 Bright
			Stop
	}
}

Actor SoF_SlugFlare
{
	Height 2
	Radius 2
	Renderstyle Translucent
	Scale 0.5
	+FORCEXYBILLBOARD
	+NOINTERACTION
	+CLIENTSIDEONLY
	States
	{
		Spawn:
			TNT1 A 0
			SF00 B -1 Bright
			Stop
	}
}

Actor SoF_SlugExplosionSpark
{
	Height 2
	Radius 2
	Scale 0.25
	Renderstyle Add
	+FORCEXYBILLBOARD
	+NOINTERACTION
	+CLIENTSIDEONLY
	States
	{
		Spawn:
			SF00 A 0
			SF00 A 6 Bright
			Stop
	}
}

Actor SoF_SlugExplosionLargeSpark
{
	Height 2
	Radius 2
	Scale 0.25
	Renderstyle Add
	+FORCEXYBILLBOARD
	+NOINTERACTION
	+CLIENTSIDEONLY
	States
	{
		Spawn:
			SF02 A 0
			SF02 A 6 Bright ACS_NamedExecuteWithResult("SoF_FX",4,10)
			Stop
	}
}

Actor SoF_SlugExplosionSmoke : SoF_MuzzleSmoke
{	
	Scale 0.5
	Alpha 0.5
	States
	{
		Spawn:
			SF00 E 0
			SF00 E -1 ACS_NamedExecuteWithResult("SoF_FX",4,20,16000)
			Stop
	}
}

Actor SoF_SlugGrenade
{
	Height 8
	Radius 4
	Speed 24
	Damage (0)
	Projectile
	BounceType "Doom"
	BounceFactor 0.33
	DamageType "SoFStunGrenade"
	+BLOODLESSIMPACT
	-NOGRAVITY
	+NOEXTREMEDEATH
	+FORCERADIUSDMG
	+FORCEPAIN
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 A 0 ThrustThingZ(0,32,0,1)
		SpawnLoop:
			SF00 A 1 A_SpawnItemEx("SoF_SlugGrenadeTrail")
			Loop
		//Since this uses 4 cells it needs to be buffed
		Death:
			TNT1 A 0 A_PlaySound("SoF/Weapons/autoshot/GrenExp",CHAN_AUTO,0.75)
			TNT1 A 0 A_SpawnItemEx("SoF_SlugGrenadeFlare")
			TNT1 A 0 A_SpawnItemEx("SoF_SlugGrenadeExplosion")
			TNT1 AAAAAAAAAAAAAAAA 0 A_SpawnItemEx("SoF_ExplosionSpark",0,0,0,frandom(-6,6),0,frandom(-2,2),frandom(0,360))
			TNT1 AAAAAAAAAAAAAAAA 0 A_SpawnItemEx("SoF_ExplosionSpark",0,0,0,frandom(-2,2),0,frandom(-6,6),frandom(0,360))
			TNT1 AAAAA 0 A_SpawnItemEx("SoF_ExplosionSmoke",0,0,0,frandom(-1,1),frandom(-1,1),frandom(1,2),frandom(0,90))
			TNT1 A 0 A_Explode(100,210,!XF_HURTSOURCE,true)
			TNT1 A 0 A_Explode(20,210,0,true)
			TNT1 A 1
			Stop
	}
}

Actor SoF_SlugGrenadeTrail
{	
	Height 2
	Radius 2
	Alpha 0.5
	Scale 0.125
	Renderstyle Translucent
	+FORCEXYBILLBOARD
	+NOINTERACTION
	+CLIENTSIDEONLY
	States
	{
		Spawn:
			SF00 F 0
			SF00 F -1 Bright ACS_NamedExecuteWithResult("SoF_FX",3,10,4096)
			Stop
	}
}

Actor SoF_SlugGrenadeFlare
{
	Height 2
	Radius 2
	Renderstyle Translucent
	+FORCEXYBILLBOARD
	+NOINTERACTION
	+CLIENTSIDEONLY
	States
	{
		Spawn:
			TNT1 A 0
			SF00 B -1 Bright ACS_NamedExecuteWithResult("SoF_FX",4,10,65536)
			Stop
	}
}

Actor SoF_SlugGrenadeExplosion
{	
	Height 2
	Radius 2
	Renderstyle Add
	XScale 4.0
	YScale 2.0
	+FORCEXYBILLBOARD
	+NOINTERACTION
	+CLIENTSIDEONLY
	States
	{
		Spawn:
			SF00 H 0
			SF00 H -1 Bright ACS_NamedExecuteWithResult("SoF_FX",4,10,65536)
			Stop
	}
}