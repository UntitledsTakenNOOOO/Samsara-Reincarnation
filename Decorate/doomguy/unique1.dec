// Tokens

actor DGStrHasHomingRocketLauncher : Boolean {}
actor SamsaraDoomguyStrHRLSecondaryPoweredAttackAmount : Counter { Inventory.MaxAmount 4 }

// Weapon

actor "Homing Rocket Launcher" : Weapon
{
	Inventory.PickupMessage "You got the homing rocket launcher!"
	Inventory.RestrictedTo "DoomguyPlayer"
	Weapon.SlotNumber 8
	Weapon.AmmoType "RocketAmmo"
	//Weapon.AmmoGive 4
	Weapon.AmmoUse 1
	Tag "Homing Rocket Launcher"
	+ALT_USES_BOTH // only one ammo type with same ammo amount use, so this makes alt fire behave as intended
	+NOAUTOFIRE
	+UNDROPPABLE
	+WEAPON.EXPLOSIVE
	States
	{
	  Spawn:
		WHRL A -1
		stop

	  // Unpowered
	  Ready:
		TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrWeaponPower",1,"ReadyPowered")
		TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrWeaponPowerPerma",1,"ReadyPowered")
		HRLG A 1 A_WeaponReady(WRF_NOSECONDARY)
		loop

	  Deselect:
		TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrWeaponPower",1,"DeselectPowered")
		TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrWeaponPowerPerma",1,"DeselectPowered")
	  DeselectContinue:
		TNT1 A 0 A_Lower
		HRLG A 1 A_Lower
		goto Deselect

	  Select:
		TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrWeaponPower",1,"SelectPowered")
		TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrWeaponPowerPerma",1,"SelectPowered")
	  SelectContinue:
		TNT1 A 0 A_Raise
		HRLG A 1 A_Raise
		goto Select

	  Fire:
		HRLG A 4
		HRLG C 5 Bright A_GunFlash
		HRLG D 6 Bright A_GiveInventory("SamsaraDoomguyStrHRLAttackHandler")
		HRLG E 5 Bright
		HRLG F 4 Bright
		HRLG B 3 A_ReFire
		goto Ready

	  Flash:
		TNT1 A 5 A_Light1
		TNT1 A 6 A_Light2
	  FlashFinish:
		TNT1 A 5 A_Light1
		goto LightDone

	  // Powered
	  ReadyPowered:
		HRLG A 1 A_WeaponReady
		goto Ready

	  DeselectPowered:
		TNT1 A 0 A_Lower
		goto DeselectContinue

	  SelectPowered:
		TNT1 A 0 A_Raise
		goto SelectContinue

	  AltFire:
		HRLG A 4
		HRLG C 5 Bright A_GunFlash
		TNT1 A 0 A_TakeInventory("SamsaraDoomguyStrHRLSecondaryPoweredAttackAmount") // reset
	  AltFirePoweredLoop:
		TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrHRLSecondaryPoweredAttackAmount",0,"AltFireContinue")
		TNT1 A 0 A_GiveInventory("SamsaraDoomguyStrHRLSecondaryPoweredAttackAmount")
		TNT1 A 0 A_GunFlash("AltFlash2")
		HRLG D 4 Bright A_GiveInventory("SamsaraDoomguyStrHRLPoweredSecondaryAttackHandler")
		loop

	  AltFireContinue:
		HRLG E 5 Bright
		HRLG F 4 Bright
		TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrWeaponPower",1,"AltFireFinishReFire") // so you can't exploit it
		TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrWeaponPowerPerma",1,"AltFireFinishReFire")
		TNT1 A 0 A_CheckReload
	  AltFireFinish:
		HRLG B 3
		goto Ready

	  AltFireFinishReFire:
		TNT1 A 0 A_ReFire
		goto AltFireFinish

	  AltFlash:
		TNT1 A 5 A_Light1
	  AltFlash2:
		TNT1 A 4 A_Light2
		goto FlashFinish
	}
}

// Primary attack handler

actor SamsaraDoomguyStrHRLAttackHandler : Trigger
{
    States
    {
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrWeaponPower",1,"PickupPowered")
        TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrWeaponPowerPerma",1,"PickupPowered")
      // Unpowered
      PickupUnpowered:
      // Ammo checking/taking stuff (right)
      PickupAmmoCheckUnpoweredRight:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttackUnpoweredRight")
        TNT1 A 0 A_JumpIfInventory("RocketAmmo",1,1)
        goto PickupAmmoCheckUnpoweredLeft
        TNT1 A 0 A_TakeInventory("RocketAmmo",1)
        goto PickupAttackUnpoweredRight

      // Attack stuff (right)
      PickupAttackUnpoweredRight:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackUnpoweredRightCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackUnpoweredRightCoop")
      PickupAttackUnpoweredRightDM:
        TNT1 A 0 A_FireCustomMissile("SamsaraDoomguyStrHomRocket1",-4,false)
        goto PickupAmmoCheckUnpoweredLeft

      PickupAttackUnpoweredRightCoop:
        TNT1 A 0 A_FireCustomMissile("SamsaraDoomguyStrHomRocket1Coop",-4,false)
        goto PickupAmmoCheckUnpoweredLeft

      // Ammo checking/taking stuff (left)
      PickupAmmoCheckUnpoweredLeft:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttackUnpoweredLeft")
        TNT1 A 0 A_JumpIfInventory("RocketAmmo",1,1)
        stop
        TNT1 A 0 A_TakeInventory("RocketAmmo",1)
        goto PickupAttackUnpoweredLeft

      // Attack stuff (left)
      PickupAttackUnpoweredLeft:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackUnpoweredLeftCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackUnpoweredLeftCoop")
      PickupAttackUnpoweredLeftDM:
        TNT1 A 0 A_FireCustomMissile("SamsaraDoomguyStrHomRocket1",4,false)
        stop

      PickupAttackUnpoweredLeftCoop:
        TNT1 A 0 A_FireCustomMissile("SamsaraDoomguyStrHomRocket1Coop",4,false)
        stop

      // Powered
      PickupPowered:
      // Ammo checking/taking stuff (right)
      PickupAmmoCheckPoweredRight:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttackPoweredRight")
        TNT1 A 0 A_JumpIfInventory("RocketAmmo",1,1)
        goto PickupAmmoCheckPoweredLeft
        TNT1 A 0 A_TakeInventory("RocketAmmo",1)
        goto PickupAttackPoweredRight

      // Attack stuff (right)
      PickupAttackPoweredRight:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackPoweredRightCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackPoweredRightCoop")
      PickupAttackPoweredRightDM:
        TNT1 A 0 A_FireCustomMissile("SamsaraDoomguyStrPowerHomRocket1",-4,false,0,4.5)
        goto PickupAmmoCheckPoweredLeft

      PickupAttackPoweredRightCoop:
        TNT1 A 0 A_FireCustomMissile("SamsaraDoomguyStrPowerHomRocket1Coop",-4,false,0,4.5)
        goto PickupAmmoCheckPoweredLeft

      // Ammo checking/taking stuff (left)
      PickupAmmoCheckPoweredLeft:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttackPoweredLeft")
        TNT1 A 0 A_JumpIfInventory("RocketAmmo",1,1)
        stop
        TNT1 A 0 A_TakeInventory("RocketAmmo",1)
        goto PickupAttackPoweredLeft

      // Attack stuff (left)
      PickupAttackPoweredLeft:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackPoweredLeftCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackPoweredLeftCoop")
      PickupAttackPoweredLeftDM:
        TNT1 A 0 A_FireCustomMissile("SamsaraDoomguyStrPowerHomRocket1",4,false,0,4.5)
        stop

      PickupAttackPoweredLeftCoop:
        TNT1 A 0 A_FireCustomMissile("SamsaraDoomguyStrPowerHomRocket1Coop",4,false,0,4.5)
        stop
    }
}

// Secondary (powered) attack handler

actor SamsaraDoomguyStrHRLPoweredSecondaryAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("RocketAmmo",1,1)
        stop
        TNT1 A 0 A_TakeInventory("RocketAmmo",1)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireCustomMissile("SamsaraDoomguyStrPowerHomRocket1",random(-600,600)/100.0,false,0,4.5,0,random(-150,150)/100.0)
        stop

      PickupAttackCoop:
        TNT1 A 0 A_FireCustomMissile("SamsaraDoomguyStrPowerHomRocket1Coop",random(-600,600)/100.0,false,0,4.5,0,random(-150,150)/100.0)
        stop
    }
}

// Homing rocket

actor SamsaraDoomguyStrHomRocket1 : Rocket
{
	Speed 30
	Damage 15
	DamageType "DoomDamageType"
	SeeSound "doomguy/stronghold/hrlfir"
	DeathSound "doomguy/stronghold/hrlexp"
	Decal "Scorch"
	Obituary "%k destroyed %o with the homing rocket launcher."
	-DEHEXPLOSION
	-RANDOMIZE
	States
	{
	  Spawn:
		HMIS A -1 Bright Light("ROCKET")
		stop

	  Death:
		TNT1 A 0 A_SetTranslucent(0.67,1)
		DMSL B 8 Bright Light("ROCKET_X1") A_Explode
		DMSL C 6 Bright Light("ROCKET_X2")
		DMSL D 4 Bright Light("ROCKET_X3")
		stop
	}
}

actor SamsaraDoomguyStrHomRocket1Coop : SamsaraDoomguyStrHomRocket1
{
	Species "Player"
	+SEEKERMISSILE
	+THRUSPECIES
	States
	{
	  Spawn:
		HMIS A 4 Bright Light("ROCKET") A_SeekerMissile(10,25,SMF_LOOK)
		loop
	}
}

// Powered homing rocket

actor SamsaraDoomguyStrPowerHomRocket1 : SamsaraDoomguyStrHomRocket1
{
	Radius 11
	Height 8
	Speed 42
	Damage (80)
	Scale 1.25
	+FORCERADIUSDMG
	States
	{
	  Spawn:
		DMSL E -1 Bright Light("POWERHOMROCKET")
		stop

	  Death:
		TNT1 A 0 A_SetTranslucent(0.67,1)
		TNT1 A 0 A_Explode(80,240,0)
		TNT1 A 0 A_ChangeFlag("FORCERADIUSDMG",false)
		DMSL B 8 Bright Light("POWERHOMROCKET_X1") A_Explode(80,160)
		DMSL C 6 Bright Light("POWERHOMROCKET_X2")
		DMSL D 4 Bright Light("POWERHOMROCKET_X3")
		stop
	}
}

actor SamsaraDoomguyStrPowerHomRocket1Coop : SamsaraDoomguyStrPowerHomRocket1
{
	Species "Player"
	+SEEKERMISSILE
	+THRUSPECIES
	States
	{
	  Spawn:
		DMSL E 2 Bright Light("POWERHOMROCKET") A_SeekerMissile(25,35,SMF_LOOK)
		loop
	}
}
