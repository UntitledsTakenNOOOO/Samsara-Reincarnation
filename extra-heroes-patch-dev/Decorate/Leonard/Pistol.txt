Actor " .454 Casull Pistol " : Weapon
{
    Inventory.PickupSound "RR/Wpickup"
	+WEAPON.WIMPY_WEAPON
	+INVENTORY.UNDROPPABLE
	Weapon.SelectionOrder 8000
	Weapon.SlotNumber 1
	Weapon.SlotPriority 1
	Weapon.AmmoType ""
	Weapon.AmmoUse 0
	Weapon.AmmoGive 0
	Inventory.PickupMessage "Casull Pistol"	
	Obituary "%k made %o leave his land."
	Weapon.UpSound "Leonard/PistolU"
	Inventory.RestrictedTo "Leonard"	
	States
	{
		Spawn:
			RRP_ A -1 Bright
			Stop
		Select:
			TNT1 A 0 A_Raise
			RRP_ B 1 A_Raise
			Loop
		Deselect:
			TNT1 A 0 A_Lower
			RRP_ B 1 A_Lower
			Loop
		Ready:
			TNT1 A 0 A_JumpIfInventory("RRDrunkToken", 1, "SetDrunkView")
			Goto SetSoberView
			TNT1 A 0 A_JumpIfInventory("LeonardWaiting", 15, "WaitingTaunt")
			TNT1 A 0 A_JumpIfInventory("LeonardReady",1,"IntroTaunt")		
			RRP_ B 1 A_WeaponReady
			Loop
		Fire:
			TNT1 A 0 A_JumpIfInventory("RRDrunkToken", 1, "SetDrunkViewFire")
			Goto SetSoberViewFire
			TNT1 A 0 A_JumpIfInventory("RRReloadShit",6,"Reload")
			RRP_ C 1 Bright
			RRP_ C 1 Bright A_PlaySound("Leonard/PistolF",CHAN_WEAPON)
			RRP_ C 0 A_JumpIfInventory("CoopModeOn",1,2)
			RRP_ D 1 Bright A_FireBullets(2,2,1,20,"RRPuff",FBF_NORANDOM)
			Goto A
			RRP_ D 1 Bright A_FireCustomMissile("RRBullet1",frandom(1.20,-1.20),1,0,0,0,frandom(0.70,-0.70))
		A:	
			RRP_ DD 1 Bright
			RRP_ EEEFFFGGGHHHHGGGFFE 1 Bright
			RRP_ I 1 Bright A_GiveInventory("RRReloadShit",1)
			TNT1 A 0 A_JumpIfInventory("RRReloadShit",6,"Reload")
			RRP_ B 1 Bright 
			RRP_ B 1 Bright
			Goto Ready
		Reload:
			TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,"ReloadCheck")
		KeepReloading:
			RRP_ II 1 Bright A_WeaponReady(WRF_NOBOB|WRF_NOFIRE|WRF_NOSWITCH)
			TNT1 A 0 A_PlaySound("Leonard/PistolR",CHAN_WEAPON)
			RRP_ J 1 Bright A_WeaponReady(WRF_NOBOB|WRF_NOFIRE|WRF_NOSWITCH)
			RRP_ J 2 offset(-8,50) Bright A_WeaponReady(WRF_NOBOB|WRF_NOFIRE|WRF_NOSWITCH)
			RRP_ J 2 offset(-13,63) Bright A_WeaponReady(WRF_NOBOB|WRF_NOFIRE|WRF_NOSWITCH)
			RRP_ K 2 offset(-18,76) Bright A_WeaponReady(WRF_NOBOB|WRF_NOFIRE|WRF_NOSWITCH)
			RRP_ KK 1 offset(-23,89) Bright A_WeaponReady(WRF_NOBOB|WRF_NOFIRE|WRF_NOSWITCH)
			RRP_ K 2 offset(-18,76) Bright A_WeaponReady(WRF_NOBOB|WRF_NOFIRE|WRF_NOSWITCH)			
			RRP_ J 2 offset(-13,63) Bright A_WeaponReady(WRF_NOBOB|WRF_NOFIRE|WRF_NOSWITCH)
			RRP_ J 2 offset(-8,50) Bright A_WeaponReady(WRF_NOBOB|WRF_NOFIRE|WRF_NOSWITCH)		
			RRP_ J 1 offset(0,37) Bright A_WeaponReady(WRF_NOBOB|WRF_NOFIRE|WRF_NOSWITCH)			
			RRP_ II 1 Bright A_WeaponReady(WRF_NOBOB|WRF_NOFIRE|WRF_NOSWITCH)			
			TNT1 A 0 A_TakeInventory("RRReloadShit",6)			
			TNT1 A 0 A_TakeInventory("PistolAmmoCounterDummyShit",6)			
			TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,"ReloadAmmo")
//			RRP_ B 1 Bright A_WeaponReady(WRF_NOBOB|WRF_NOFIRE|WRF_NOSWITCH)
			Goto Ready
      ReloadCheck:
			TNT1 A 0 A_JumpIfInventory("Clip",1,"KeepReloading")
			Goto Ready
	  ReloadAmmo:
			TNT1 A 0 A_JumpIfInventory("Clip",1,1)
			Goto Ready
      ReloadAmmo2:
			TNT1 A 0 A_TakeInventory("Clip",1)
			TNT1 A 0 A_GiveInventory("PistolAmmoCounterDummyShit",1)
			TNT1 A 0 A_JumpIfInventory("PistolAmmoCounterDummyShit",6,"Ready")
			TNT1 A 0 A_JumpIfInventory("Clip",1,"ReloadAmmo2")
			Goto Ready
	  IntroTaunt:
			TNT1 A 0 A_JumpIfInventory("LeonardBallgag",1,2)
			TNT1 A 0 A_PlaySoundEx("Leonard/Enter", "SoundSlot7", 0, 0)
			TNT1 A 0 A_TakeInventory("LeonardReady", 1)
			TNT1 A 0 A_GiveInventory("LeonardTauntCooldown",5)
			TNT1 A 0 ACS_ExecuteAlways(706, 0)
			Goto Ready
	  WaitingTaunt:
			TNT1 A 0 A_JumpIfInventory("LeonardBallgag",1,2)
			TNT1 A 0 A_PlaySoundEx("Leonard/Bored", "SoundSlot7", 0, 0)
			TNT1 A 0 A_TakeInventory("LeonardWaiting", 15)
			TNT1 A 0 A_GiveInventory("LeonardTauntCooldown",5)
			TNT1 A 0 ACS_ExecuteAlways(706, 0)
			Goto Ready
	  SetDrunkView:
			TNT1 A 0 A_ZoomFactor(0.55)
			Goto Ready+1
	  SetDrunkViewFire:
			TNT1 A 0 A_ZoomFactor(0.55)
			Goto Fire+1
	  SetSoberView:
			TNT1 A 0 A_ZoomFactor(1.0)
			Goto Ready+1
	  SetSoberViewFire:
			TNT1 A 0 A_ZoomFactor(1.0)
			Goto Fire+1
	}
}

Actor RRBullet1 : FastProjectile
{
  Speed 300
  Damage (20)
  Radius 1
  Height 1
  Species "Player"  
  Projectile
  +BLOODSPLATTER
  +NOTIMEFREEZE
  +NOEXTREMEDEATH
  +THRUSPECIES
  +DONTHARMCLASS
  DamageType "LeonardDamage0"  
  +DONTHARMSPECIES
  +MTHRUSPECIES
  Decal RedneckRampageBulletChip
  States
  {
  Spawn:
    TNT1 A 1
    Loop
  Death:
  Crash:  
   	TNT1 A 0 A_PlaySound("RR/RICOCHET")  
    TNT1 A 1 A_SpawnItemEx("RRPuff",0,0,0,0,0,1,0,0)        
    stop
  XDeath:
    TNT1 A 1
    stop
    }
}

Actor RRReloadShit : Inventory { Inventory.Amount 1 Inventory.MaxAmount 6 }