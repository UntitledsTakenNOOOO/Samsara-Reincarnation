actor Firestorm : CWeapFlame
{
    Weapon.SelectionOrder 1000
    Weapon.SlotNumber 6
    Weapon.AmmoUse 0
    Weapon.AmmoGive 25
    Weapon.KickBack 150
    Weapon.YAdjust 10
    Weapon.AmmoType1 "Cell"
    Inventory.PickupMessage "You got the Firestorm!"
    Inventory.PickupSound "hexen/weaponget"
    Obituary "$SAMSARA_HEXEN_OB_SLOT6"
	Inventory.RestrictedTo "HexenPlayer"
    +INVENTORY.UNDROPPABLE
    +BRIGHT
    States
    {
      Spawn:
        WCFM ABCDEFGH 4 Bright
        Loop

      Select:
		TNT1 A 0 A_JumpIfInventory("HexenClassMode", 2, "SelectFighter")
		TNT1 A 0 A_JumpIfInventory("HexenClassMode", 1, "SelectMage")
        CFLM A 1 A_Raise
        Loop

	  SelectMage:
		H109 A 1 A_Raise
		Loop
		
	  SelectFighter:
		H105 E 1 A_Raise
		Loop

      Deselect:
		TNT1 A 0 A_JumpIfInventory("HexenClassMode", 2, "DeselectFighter")
		TNT1 A 0 A_JumpIfInventory("HexenClassMode", 1, "DeselectMage")
        CFLM A 1 A_Lower
        Loop
		
	  DeselectMage:
		H109 A 1 A_Lower
		Loop
		
	  DeselectFighter:
		H105 E 1 A_Lower
		Loop

      Ready:
		TNT1 A 0 A_JumpIfInventory("HexenClassMode", 2, "ReadyFighter")
		TNT1 A 0 A_JumpIfInventory("HexenClassMode", 1, "ReadyMage")
        CFLM AAAABBBBCCCC 1 A_WeaponReady
        Loop
		
	  ReadyMage:
		H109 AAAAA 1 BRIGHT A_WeaponReady
		H109 A 1 BRIGHT A_WeaponReady TNT1 A 0 A_Jump(96, 2)
		H109 B 0 BRIGHT A_PlaySound("MageLightningReady", CHAN_WEAPON)
		H109 BBBBB 1 BRIGHT A_WeaponReady
		H109 B 1 BRIGHT A_WeaponReady TNT1 A 0 A_Jump(96, 2)
		H109 C 0 BRIGHT A_PlaySound("MageLightningReady", CHAN_WEAPON)
		H109 CCCCC 1 BRIGHT A_WeaponReady
		H109 C 1 BRIGHT A_WeaponReady TNT1 A 0 A_Jump(96, 2)
		H109 B 0 BRIGHT A_PlaySound("MageLightningReady", CHAN_WEAPON)
		H109 BBBBB 1 BRIGHT A_WeaponReady
		Goto Ready
	
	  ReadyFighter:
		H105 FFFFFFGGGGGGHHHHHH 1 Bright A_WeaponReady
		Loop
		
      Fire:
		TNT1 A 0 A_JumpIfInventory("HexenClassMode", 2, "FireFighter")
		TNT1 A 0 A_JumpIfInventory("HexenClassMode", 1, "FireMage")
		TNT1 A 0 A_JumpIfInventory("Cell", 4, 1)
		Goto SwitchWeapon
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"SPFire")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"SPFire")
        CFLM A 2 Offset (0, 40)
        CFLM D 2 Offset (0, 50)
        CFLM D 2 Offset (0, 36)
        CFLM E 4 Bright
        TNT1 A 0 A_PlayWeaponSound("ClericFlameFire")
        TNT1 A 0 A_FireCustomMissile("CFlameMissileDM")
		TNT1 A 0 Bright A_TakeInventory("Cell", 4, TIF_NOTAKEINFINITE)
        CFLM F 4 Bright //A_CFlameAttack
        CFLM E 4 Bright
        CFLM G 2 Offset (0, 40)
        CFLM G 2
        CFLM G 1 A_Refire
        Goto Ready
		
	  FireMage:
		H109 D 0 Bright A_JumpIfInventory("Cell", 5, 1)
		Goto SwitchWeapon
		H109 DE 3 Bright
		H109 F 0 BRIGHT A_PlaySound("MageLightningFire", CHAN_WEAPON)
		H109 F 4 Bright A_FireCustomMissile("LightningFloor2", 0,0,0,0)
		H109 F 0 Bright A_FireCustomMissile("LightningCeiling2", 0,0,0,0)
		H109 F 0 Bright A_TakeInventory("Cell", 5, TIF_NOTAKEINFINITE)
		H109 G 4 Bright
		H109 HI 3 Bright
		H109 I 6 Bright Offset(0, 199)
		H109 C 2 Bright Offset(0, 55)
		H109 B 2 Bright Offset(0, 50)
		H109 B 2 Bright Offset(0, 45)
		H109 B 2 Bright Offset(0, 40)
		Goto Ready	
		
	  FireFighter:
		TNT1 A 0 A_JumpIfInventory("Cell", 8, 1)
		Goto SwitchWeapon
		H105 F 2 Bright A_PlaySound("hexen/weapons/gauntletlightning",CHAN_WEAPON,1.0)
		H105 I 5 Bright
		H105 J 7 Bright
		H105 K 0 A_FireCustomMissile("HexenGauntletPlasma",-5,0,-7)
		H105 K 0 A_FireCustomMissile("HexenGauntletPlasma",-3,0,-7)
		H105 K 0 A_FireCustomMissile("HexenGauntletPlasma",3,0,7)
		H105 K 0 A_FireCustomMissile("HexenGauntletPlasma",5,0,7)
		H105 K 0 Bright A_TakeInventory("Cell", 8, TIF_NOTAKEINFINITE)
		H105 K 6 Bright
		H105 J 4 Bright
		H105 I 3 Bright
		Goto Ready

      SPFire:
        CFLM A 2 Offset (0, 40)
        CFLM D 2 Offset (0, 50)
        CFLM D 2 Offset (0, 36)
        CFLM E 4 Bright
        CFLM F 4 Bright A_FireCustomMissile("CFlameMissile2")
		TNT1 A 0 Bright A_TakeInventory("Cell", 4, TIF_NOTAKEINFINITE)
        CFLM E 4 Bright
        CFLM G 2 Offset (0, 40)
        CFLM G 2
        CFLM G 1 A_Refire
        Goto Ready
		
	  SwitchWeapon:
		TNT1 A 0 A_JumpIf(CallACS("Hexen_SwitchPriority")==1,1)
		TNT1 A 0
		Goto Ready
    }
}

actor CFlameMissileDM : CFlameMissile
{
    Speed 150
    States
    {
      Spawn:
        CFFX A 4 Bright
        CFFX A 1 A_CFlamePuff
        Goto Death

      Crash:
      XDeath:
        TNT1 B 0 A_SpawnItemEx("CircleFlame1",40,0,-10,0,0,0,0,28,0)
        TNT1 B 0 A_SpawnItemEx("CircleFlame1",40,0,-10,0,0,0,45,28,0)
        TNT1 B 0 A_SpawnItemEx("CircleFlame1",40,0,-10,0,0,0,90,28,0)
        TNT1 B 0 A_SpawnItemEx("CircleFlame1",40,0,-10,0,0,0,135,28,0)
        TNT1 B 0 A_SpawnItemEx("CircleFlame1",40,0,-10,0,0,0,180,28,0)
        TNT1 B 0 A_SpawnItemEx("CircleFlame1",40,0,-10,0,0,0,225,28,0)
        TNT1 B 0 A_SpawnItemEx("CircleFlame1",40,0,-10,0,0,0,270,28,0)
        TNT1 B 0 A_SpawnItemEx("CircleFlame1",40,0,-10,0,0,0,315,28,0)

      Death:
        CFFX A 1 Bright A_CFlamePuff
        CFFX ABC 3 Bright
        CFFX D 4 Bright
        CFFX E 3 Bright
        CFFX F 4 Bright
        CFFX G 3 Bright
        CFFX H 4 Bright
        CFFX I 3 Bright
        CFFX J 4 Bright
        CFFX K 3 Bright
        CFFX L 4 Bright
        CFFX M 3 Bright
        Stop
    }
}

actor CircleFlame1 : CircleFlame
{
    Obituary "$SAMSARA_HEXEN_OB_SLOT6"
    Damage 2//Originally 2
    +STRIFEDAMAGE
    States
    {
      Death:
        CFCF QR 3 Bright
        CFCF S 3 Bright A_Explode(5, 20, 0)
        CFCF TUVWXYZ 3 Bright
        Stop
    }
}

actor CFlameMissile2 : CFlameMissile replaces CFlameMissile
{
    +THRUSPECIES
    Species "Player"
}

actor CircleFlame2 : CircleFlame replaces CircleFlame
{
    Obituary "$SAMSARA_HEXEN_OB_SLOT6"
    Damage 2
    +THRUSPECIES
    Species "Player"
    States
    {
      Death:
        CFCF QR 3 Bright
        CFCF S 3 Bright A_Explode(32, 64, 0)
        CFCF TUVWXYZ 3 Bright
        Stop
    }
}