actor Ranger_PlasmaSelected : Boolean { Inventory.InterHubAmount 0 }
actor SamsaraQuakeLaserCannonSide : Boolean {}

actor "Laser Cannon" : SamsaraQuakeWeapon
{
    Inventory.PickupMessage "You got the Laser Cannon."
    Weapon.SelectionOrder 100
    Weapon.AmmoType "Cell"
    Weapon.AmmoGive 60
    Weapon.AmmoUse 1
    Scale 0.75
    Tag "Laser Cannon"
    States
    {
      Spawn:
        QLSC Z -1
        stop

	  Ready:
        QLSC A 1 A_WeaponReady
        loop

      Select:
        TNT1 A 0 A_TakeInventory("Ranger_AxeSelected")
        TNT1 A 0 A_TakeInventory("Ranger_GunSelected")
        TNT1 A 0 A_TakeInventory("Ranger_ExplosiveSelected")
        TNT1 A 0 A_TakeInventory("Ranger_NailSelected")
        TNT1 A 0 A_GiveInventory("Ranger_PlasmaSelected")
        goto Super::Select

      Fire:
        QLSC B 0 A_TakeInventory("SamsaraQuakeLaserCannonSide")
      FireSideCheck:
        QLSC B 0 A_JumpIfInventory("SamsaraQuakeLaserCannonSide",1,"FireUp")
        goto FireDown

      FireDown:
        QLSC B 0 A_GiveInventory("SamsaraQuakeWeaponAlert")
        QLSC B 0 A_GunFlash
        QLSC B 0 A_GiveInventory("SamsaraQuakeLaserCannonAttackHandler")
        QLSC B 0 A_JumpIfInventory("SamsaraUsingSmoothAnims",1,"FireDownSmooth")
      FireDownVanilla:
        QLSC B 4 Bright A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        goto FireFinish

      FireDownSmooth:
        QLSC BC 2 Bright A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        goto FireFinish

      FireUp:
        QLSC D 0 A_GiveInventory("SamsaraQuakeWeaponAlert")
        QLSC D 0 A_GunFlash
        QLSC D 0 A_GiveInventory("SamsaraQuakeLaserCannonAttackHandler")
        QLSC B 0 A_JumpIfInventory("SamsaraUsingSmoothAnims",1,"FireUpSmooth")
      FireUpVanilla:
        QLSC D 0 A_JumpIfInventory("SamsaraHasDoubleFiringSpeed",1,2)
        QLSC D 3 Bright A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        goto FireFinish
        QLSC D 2 Bright A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        goto FireFinish

      FireUpSmooth:
        QLSC D 0 A_JumpIfInventory("SamsaraHasDoubleFiringSpeed",1,"FireUpSmoothRage")
        QLSC D 1 Bright A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        QLSC E 2 Bright A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        goto FireFinish

      FireUpSmoothRage:
        QLSC D 2 Bright A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        goto FireFinish

      FireFinish:
        QLSC A 0 A_ReFire("FireSideCheck")
        goto Ready

      Flash:
        TNT1 A 2 A_Light1
        goto LightDone
    }
}

actor SamsaraQuakeLaserCannonAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupPlaySound")
        TNT1 A 0 A_JumpIfInventory("Cell",1,1)
        stop
        TNT1 A 0 A_TakeInventory("Cell",1)
        goto PickupPlaySound

      // Sound stuff
      PickupPlaySound:
        TNT1 A 0 A_JumpIfInventory("SamsaraAlternateSoundToken",1,"PickupPlaySoundRerelease")
      // Quake I (Original) sounds
      PickupPlaySoundOriginal:
        TNT1 A 0 A_PlaySound("quakeweps/lasershot",CHAN_WEAPON)
        goto PickupAttack

      // Quake I (Rerelease) sounds
      PickupPlaySoundRerelease:
        TNT1 A 0 A_PlaySound("quakeweps/rerelease/lasershot",CHAN_WEAPON)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_JumpIfInventory("SamsaraQuakeLaserCannonSide",1,"PickupAttackUp")
      // Down
      PickupAttackDown:
        TNT1 A 0 A_JumpIfInventory("SamsaraAlternateSoundToken",1,"PickupAttackDownRerelease")
      // Quake I (Original) sounds
      PickupAttackDownOriginal:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackDownOriginalCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackDownOriginalCoop")
      PickupAttackDownOriginalDM:
        TNT1 A 0 A_FireCustomMissile("LaserCannonBolt",0,false,3,-2)
        TNT1 A 0 A_FireCustomMissile("LaserCannonBolt",0,false,-3,-2)
        goto PickupFinishDown

      PickupAttackDownOriginalCoop:
        TNT1 A 0 A_FireCustomMissile("LaserCannonBoltCoop",0,false,3,-2)
        TNT1 A 0 A_FireCustomMissile("LaserCannonBoltCoop",0,false,-3,-2)
        goto PickupFinishDown

      // Quake I (Rerelease) sounds
      PickupAttackDownRerelease:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackDownRereleaseCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackDownRereleaseCoop")
      PickupAttackDownRereleaseDM:
        TNT1 A 0 A_FireCustomMissile("LaserCannonBoltSoundRerelease",0,false,3,-2)
        TNT1 A 0 A_FireCustomMissile("LaserCannonBoltSoundRerelease",0,false,-3,-2)
        goto PickupFinishDown

      PickupAttackDownRereleaseCoop:
        TNT1 A 0 A_FireCustomMissile("LaserCannonBoltCoopSoundRerelease",0,false,3,-2)
        TNT1 A 0 A_FireCustomMissile("LaserCannonBoltCoopSoundRerelease",0,false,-3,-2)
        goto PickupFinishDown

      // Up
      PickupAttackUp:
        TNT1 A 0 A_JumpIfInventory("SamsaraAlternateSoundToken",1,"PickupAttackUpRerelease")
      // Quake I (Original) sounds
      PickupAttackUpOriginal:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackUpOriginalCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackUpOriginalCoop")
      PickupAttackUpOriginalDM:
        TNT1 A 0 A_FireCustomMissile("LaserCannonBoltSecond",0,false,0,5)
        goto PickupFinishUp

      PickupAttackUpOriginalCoop:
        TNT1 A 0 A_FireCustomMissile("LaserCannonBoltSecondCoop",0,false,0,5)
        goto PickupFinishUp

      // Quake I (Rerelease) sounds
      PickupAttackUpRerelease:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackUpRereleaseCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackUpRereleaseCoop")
      PickupAttackUpRereleaseDM:
        TNT1 A 0 A_FireCustomMissile("LaserCannonBoltSecondSoundRerelease",0,false,0,5)
        goto PickupFinishUp

      PickupAttackUpRereleaseCoop:
        TNT1 A 0 A_FireCustomMissile("LaserCannonBoltSecondCoopSoundRerelease",0,false,0,5)
        goto PickupFinishUp

      // Finishing stuff
      // Down
      PickupFinishDown:
        TNT1 A 0 A_GiveInventory("SamsaraQuakeLaserCannonSide")
        TNT1 A 0 A_JumpIfInventory("SamsaraHasDoubleFiringSpeed",1,2)
        TNT1 A 0 ACS_NamedExecuteAlways("SamsaraRecoil", 0, -50, 1, 3)
        stop
        TNT1 A 0 ACS_NamedExecuteAlways("SamsaraRecoil", 0, 1, 0, 2)
        stop

      // Up
      PickupFinishUp:
        TNT1 A 0 A_TakeInventory("SamsaraQuakeLaserCannonSide")
        TNT1 A 0 A_JumpIfInventory("SamsaraHasDoubleFiringSpeed",1,2)
        TNT1 A 0 ACS_NamedExecuteAlways("SamsaraRecoil", 0, -50, 1, 2)
        stop
        TNT1 A 0 ACS_NamedExecuteAlways("SamsaraRecoil", 0, -50, 0, 1)
        stop
    }
}

actor LaserCannonBolt
{
    Radius 2
    Height 2
    Speed 50
    Projectile
    Damage (7)
    DamageType "QuakeExplosive"
    BounceType "Hexen"
    +BLOODSPLATTER
    BounceCount 3
    Scale 0.55
    BounceSound "quakeweps/laserbounce"
    DeathSound "quakeweps/laserhit"
    Obituary "$SAMSARA_RANGER_OB_UNIQUE"
    //SelfObituary "%o checks if %p weapon is loaded." // had randomized obituary in SOA, use this one for differentiation
    States
    {
      Spawn:
        LSCL A 0 Bright
        TNT1 A 0 A_Jump(16,"Spawn2")
        LSCL A 1 Bright A_Explode(2,1,1)
        loop

      Spawn2:
        TNT1 A 0 A_ChangeFlag("ALLOWBOUNCEONACTORS",false)
        TNT1 A 0 A_ChangeFlag("BOUNCEONACTORS",false)
        TNT1 A 0 A_ChangeFlag("BOUNCEONCEILINGS",false)
        TNT1 A 0 A_ChangeFlag("BOUNCEONFLOORS",false)
        TNT1 A 0 A_ChangeFlag("BOUNCEONWALLS",false)
        TNT1 A 0 A_ChangeFlag("USEBOUNCESTATE",false)
      Spawn3:
        LSCL A 1 Bright A_Explode(2,1,1)
        loop

      Death:
        LSCL A 1 Bright
      Crash:
        TNT1 A 1 A_SpawnItemEx("QuakeBulletPuff")
        stop

      XDeath:
        TNT1 A 1
        stop
    }
}

actor LaserCannonBoltCoop : LaserCannonBolt
{
    Damage (10)
    +THRUSPECIES
    Species "Player"
}

actor LaserCannonBoltSecond : LaserCannonBolt
{
    Damage (14)
    States
    {
      Spawn:
        LSCL A 0 Bright
        TNT1 A 0 A_Jump(16,"Spawn2")
        LSCL A 1 Bright A_Explode(4,1,1)
        loop

      Spawn2:
        TNT1 A 0 A_ChangeFlag("ALLOWBOUNCEONACTORS",false)
        TNT1 A 0 A_ChangeFlag("BOUNCEONACTORS",false)
        TNT1 A 0 A_ChangeFlag("BOUNCEONCEILINGS",false)
        TNT1 A 0 A_ChangeFlag("BOUNCEONFLOORS",false)
        TNT1 A 0 A_ChangeFlag("BOUNCEONWALLS",false)
        TNT1 A 0 A_ChangeFlag("USEBOUNCESTATE",false)
      Spawn3:
        LSCL A 1 Bright A_Explode(4,1,1)
        loop
    }
}

actor LaserCannonBoltSecondCoop : LaserCannonBoltSecond
{
    Damage (20)
    +THRUSPECIES
    Species "Player"
}

actor LaserCannonBoltSoundRerelease : LaserCannonBolt
{
    BounceSound "quakeweps/rerelease/laserbounce"
    DeathSound "quakeweps/rerelease/laserhit"
}

actor LaserCannonBoltCoopSoundRerelease : LaserCannonBoltSoundRerelease
{
    Damage (10)
    +THRUSPECIES
    Species "Player"
}

actor LaserCannonBoltSecondSoundRerelease : LaserCannonBoltSecond
{
    BounceSound "quakeweps/rerelease/laserbounce"
    DeathSound "quakeweps/rerelease/laserhit"
}

actor LaserCannonBoltSecondCoopSoundRerelease : LaserCannonBoltSecondSoundRerelease
{
    Damage (20)
    +THRUSPECIES
    Species "Player"
}
