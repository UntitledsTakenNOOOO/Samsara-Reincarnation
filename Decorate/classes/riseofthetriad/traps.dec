ACTOR RoTTBlades
{
   Radius 20
   Height 56
   Speed 0
   Mass 9999999
   Scale 0.50
   Tag "Blade Trap"
   +FLOORCLIP
   +INVULNERABLE
   +RIPPER
   +RANDOMIZE
   +EXTREMEDEATH
   +SOLID
   +FOILINVUL
   +SHOOTABLE
   +NOBLOOD
   +NODAMAGE
   +NODROPOFF
   +CANTLEAVEFLOORPIC
   +ISMONSTER
   -COUNTKILL
   +NEVERTARGET
   +NOBLOCKMONST
   +NODAMAGETHRUST
   MaxDropOffHeight 0
   MaxStepHeight 0
   DamageType Explosive
   Obituary "%o was sliced by a blade trap."
   States
   {
		Spawn:
			TNT1 A 0
			TNT1 A 0 A_GiveInventory("RoTTIsBladeTrap",1)
			TNT1 A 0 ACS_NamedExecuteWithResult("RoTTBladeGenerate")
		SpawnLoop:
			RT40 A 2 A_Explode(4,28,XF_NOTMISSILE,0,20)
			RT40 B 2
			RT40 C 2 A_Explode(4,28,XF_NOTMISSILE,0,20)
			RT40 D 2
			RT40 E 2 A_Explode(4,28,XF_NOTMISSILE,0,20)
			RT40 F 2
			RT40 G 2 A_Explode(4,28,XF_NOTMISSILE,0,20)
			RT40 H 2
			RT40 I 2 A_Explode(4,28,XF_NOTMISSILE,0,20)
			RT40 A 0 A_PlaySound("gen/blades", 0, 1.0, 0, ATTN_STATIC)
			Loop
	}
}

Actor RoTTBladesTester
{
	Height 2
	Radius 1
	States
	{
		Spawn:
			TNT1 A 5
			stop
	}
}

Actor RoTTIsBladeTrap : Boolean {}
Actor RoTTBladesTurnAround : Boolean {}

Actor RoTTBladesBlockTrigger : Trigger
{
	States
	{
		Pickup:
			TNT1 A 0 A_CheckLOF("TurnAround",CLOFF_JUMP_ON_MISS|CLOFF_ALLOWNULL,28,0,0,0,51)
			TNT1 A 0 A_CheckLOF("TurnAround",CLOFF_JUMP_ON_MISS|CLOFF_ALLOWNULL,28,0,0,0,1)
			TNT1 A 0 A_RadiusGive("RoTTBladesBlockTriggerStatic",48,RGF_MONSTERS|RGF_NOSIGHT,1)
			stop
		TurnAround:
			TNT1 A 0 A_GiveInventory("RoTTBladesTurnAround",1)
			stop
	}
}

Actor RoTTBladesBlockTriggerStatic : Trigger
{
	States
	{
		Pickup:
			TNT1 A 0 A_JumpIfInventory("RoTTIsBladeTrap",1,1)
			Goto Failure
			TNT1 A 0 A_RadiusGive("RoTTBladesTurnAround",48,RGF_MONSTERS|RGF_NOSIGHT,1)
			stop
		Failure:
			TNT1 A 0
			stop
	}
}

ACTOR RoTTBlades2 : RoTTBlades
{
   States
   {
		Spawn:
			TNT1 A 0
			TNT1 A 0 A_GiveInventory("RoTTIsBladeTrap",1)
			Goto SpawnLoop
	}
}

ACTOR RoTTBladesRoomChecker : RoTTBlades
{
   Radius 40
   States
   {
		Spawn:
			TNT1 A 0
			Stop
	}
}

ACTOR RoTTFireJet
{
   Radius 24
   Height 52
   Speed 0
   Scale 0.5
   Mass 9999999
   Tag "Fire Jet"
   +FLOORHUGGER
   //+NOGRAVITY
   +FLOORCLIP
   +RANDOMIZE
   +ISMONSTER
   -COUNTKILL
   +NEVERTARGET
   +NODAMAGETHRUST
   DamageType RoTTFire
   Obituary "%o was roasted by a fire jet."
   States
	{
		Spawn:
			RT41 A 30
			RT41 A 2 Bright A_PlaySound("gen/Firejet")
			RT41 BCDEFGHIJKLMNOPRSTUV 2 Bright A_Explode(4,24,XF_NOTMISSILE,0,4)
			RT41 VW 2 Bright
			Loop
	}
}

ACTOR RoTTUpSpikes
{
   Radius 20
   Height 56
   Speed 0
   Mass 9999999
   Scale 0.50
   Tag "Spiketrap"
   +FLOORCLIP
   +INVULNERABLE
   +EXTREMEDEATH
   +RANDOMIZE
   +SHOOTABLE
   +NOBLOOD
   +NODAMAGE
   +ISMONSTER
   -COUNTKILL
   +NEVERTARGET
   +NODAMAGETHRUST
   DamageType Explosive
   Obituary "%o was impaled by a spiketrap."
   States
   {
   Spawn:
       RT42 G 30
       RT42 HI 2
       RT42 A 0 A_ChangeFlag("Shootable", 1)
       RT42 J 2 A_PlaySound("gen/spear")
       RT42 K 2 A_SetSolid
       RT42 L 2 A_Explode(8,20,XF_NOTMISSILE,0,4)
       RT42 M 2
       RT42 N 2 A_Explode(8,20,XF_NOTMISSILE,0,4)
       RT42 O 2
       RT42 P 2 A_Explode(8,20,XF_NOTMISSILE,0,4)
       RT42 A 2
       RT42 B 2 A_Explode(8,20,XF_NOTMISSILE,0,4)
       RT42 C 2
       RT42 D 2 A_Explode(8,20,XF_NOTMISSILE,0,4)
       RT42 EF 2
       RT42 G 0 A_UnsetSolid
       RT42 A 0 A_ChangeFlag("Shootable", 0)
       LOOP
   }
}
