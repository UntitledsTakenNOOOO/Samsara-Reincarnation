ACTOR Painkiller_Stakegun : PainkillerWeapon
{
	Weapon.SelectionOrder 800
	Weapon.SlotNumber 1
	Weapon.AmmoType1 "Shell"
	Weapon.AmmoUse1 1
	Weapon.AmmoGive1 20
	Weapon.BobRangeX 0
	Weapon.BobRangeY 1.2
	Weapon.AmmoType2 "RocketAmmo"
	Weapon.AmmoUse2 1
	Weapon.AmmoGive2 5
	Obituary "%o was impaled by %k's stakegun."
	Inventory.Pickupmessage "Stakegun"
	Inventory.PickupSound "Painkiller/Stakegun/Pickup"
	Tag "Stakegun/Grenade Launcher"
	-WEAPON.CHEATNOTWEAPON
	States
	{
		Select:
			TNT1 A 0 A_TakeInventory("Painkiller_HoldingShotgun",1)
			TNT1 A 0 A_TakeInventory("Painkiller_HoldingRifle",1)
			TNT1 A 0 A_TakeInventory("Painkiller_HoldingChaingun",1)
			TNT1 A 0 A_TakeInventory("Painkiller_HoldingElectro",1)
			TNT1 A 0 A_TakeInventory("Painkiller_HoldingBolt",1)
			TNT1 A 0 A_TakeInventory("Painkiller_HoldingDemon",1)
			TNT1 A 0 A_GiveInventory("Painkiller_HoldingStake",1)
			Goto Super::Select
		Deselect:
			TNT1 AAAAAAAAAAAAAAAAAAAA 0 A_Lower
			TNT1 A 1 A_Lower
			TNT1 A 0 A_TakeInventory("Painkiller_HoldingStake",1)
			Loop
		Ready:
			PK00 ABCDEFGHI 10 A_WeaponReady
			Loop
		Fire:
			PK01 A 0 A_PlaySound("Painkiller/Stakegun/Shoot",CHAN_5)
			PK01 A 2
			PK01 B 0 A_JumpIfInventory("CoopModeOn",1,2)
			PK01 B 0 A_FireCustomMissile("Painkiller_StakeProjectile",0,1)
			Goto FireEnd
			PK01 B 0 A_FireCustomMissile("Painkiller_StakeProjectileCoop",0,1)
			Goto FireEnd
		FireEnd:
			PK01 B 0 A_TakeInventory("Painkiller_StakeAmmo",1,TIF_NOTAKEINFINITE)
			PK01 BC 2
			PK01 DEFGHJKMN 4
			PK01 PQR 3 
			Pk01 STU 3 A_WeaponReady
			Goto Ready
		AltFire:
			PK02 A 0 A_PlaySound("Painkiller/Grenade/Shoot",CHAN_5)
			PK02 A 0 A_PlaySound("Painkiller/Grenade/Reload",CHAN_6)
			PK02 A 0 A_JumpIfInventory("CoopModeOn",1,2)
			PK02 A 0 A_FireCustomMissile("Painkiller_StakeGrenade",0,1,0,0,0,10)
			Goto AltFireEnd
			PK02 A 0 A_FireCustomMissile("Painkiller_StakeGrenadeCoop",0,1,0,0,0,10)
			Goto AltFireEnd
		AltFireEnd:
			PK02 A 0 A_TakeInventory("Painkiller_GrenadeAmmo",1,TIF_NOTAKEINFINITE)
			PK02 ABCDEFGH 2	
			PK02 JL 2 
			PK02 NP 2 A_WeaponReady
			Goto Ready
		Spawn:
			PK99 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 1 A_SpawnItemEx("Painkiller_Stake_Icon",0,0,40)
			PK99 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 1 A_SpawnItemEx("Painkiller_Stake_Icon2",0,0,40)
			Loop
	}
}

Actor Painkiller_Stake_Icon : Painkiller_Weapon_Icon
{
	States
	{
		Spawn:
			PK07 T 2
			Stop
	}
}

Actor Painkiller_Stake_Icon2 : Painkiller_Weapon_Icon
{
	States
	{
		Spawn:
			PK07 W 2
			Stop
	}
}

Actor Painkiller_StakeProjectile : PainkillerProjectile
{
	-NOGRAVITY
	+HITTRACER
	+NODAMAGETHRUST
	+FORCERADIUSDMG
	+NOEXTREMEDEATH
	//+THRUACTORS
	Decal "PainkillerStakeHole"
	DamageType "PainkillerStake"
	Gravity 0.40
	Speed 56
	Scale 0.5
	Damage (130)
	Height 4
	Radius 2
	var int user_timer;
	States
	{
		Spawn:
			PK00 A 0 NoDelay A_JumpIfInventory("Painkiller_WeaponModifier_Perma",1,2,AAPTR_TARGET)
			PK00 A 0 A_JumpIfInventory("Painkiller_WeaponModifier_Token",1,1,AAPTR_TARGET)
			Goto SpawnLoop
			PK00 A 0 A_ChangeFlag("NOGRAVITY",1)
			PK00 A 0 A_SetUserVar(user_timer,user_timer+5)
			Goto SpawnLoop
		SpawnLoop:
			PK00 A 0 A_JumpIf(ACS_NamedExecuteWithResult("SamsaraDecorate",48) == 0, 2)
			PK00 A 0 A_SetDamageType("Painkiller")
			PK00 A 0 A_GiveInventory("Painkiller_StakeFlew",1)
			PK00 A 0 ACS_NamedExecuteWithResult("Painkiller_ProjectilePitch")
			PK00 A 0 A_SetUserVar(user_timer,user_timer+1)
			PK00 A 0 A_JumpIf(user_timer < 8,3)
			PK00 A 0 A_GiveInventory("Painkiller_StakeFlaming", 1)
			PK00 A 0 A_SpawnItemEx("Painkiller_StakeFire",0,0,16,frandom(-0.25,0.25),frandom(-0.25,0.25),frandom(0.25,0.5),0,SXF_NOCHECKPOSITION)
			PK00 A 2 A_SpawnItemEx("Painkiller_StakeTrail",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH)
			Loop
		Death:
			PK00 B 0 A_Playsound("Painkiller/Stake/Default",CHAN_6,0.5)
			PK00 B 0 A_ChangeFlag("NOGRAVITY",true)
			PK00 B 0 A_JumpIfInventory("Painkiller_StakeFlew",1,2)
			PK00 B 0 A_SetPitch(-CallACS("Painkiller_ShooterPitch")/182)
			PK00 B 0 A_SpawnItemEx("Painkiller_StakeHitWallEffect1",0,0,0,0,0,-0.15,0,SXF_NOCHECKPOSITION)
			PK00 B 0 A_SpawnItemEx("Painkiller_StakeHitWallEffect2",0,0,0,0,0,frandom(0.15,0.5),0,SXF_NOCHECKPOSITION)
			PK00 B 105 A_GiveInventory("Painkiller_DamageCredit",1,AAPTR_TRACER)
			PK00 BBBBBBBBB 2 A_Fadeout(0.1)
			Stop
		XDeath:
			PK00 B 0 A_JumpIfInventory("Painkiller_StakeFlaming", 1, 1)
			Goto XDeath+2
			PK00 B 0 A_Explode(70, 6, 0,0, 6)
			PK00 B 0 A_JumpIf(CallACS("Painkiller_StakeShatter")==2,"Remove")
			PK00 B 0 A_Playsound("Painkiller/Stake/Body",CHAN_5,0.5)
			PK00 B 0 A_JumpIf(CallACS("Painkiller_StakeShatter")==1,"SpawnAnother")
			PK00 B 0 A_JumpIfInventory("Painkiller_StakeFlew",1,2)
			PK00 B 0 A_SetPitch(-CallACS("Painkiller_ShooterPitch")/182)
			PK00 B 0 A_SpawnItemEx("Painkiller_StakeDebris1",0,0,0,frandom(-2,2),frandom(-2,2),frandom(-2,2),0,SXF_NOCHECKPOSITION)
			PK00 B 0 A_SpawnItemEx("Painkiller_StakeDebris2",0,0,0,frandom(-2,2),frandom(-2,2),frandom(-2,2),0,SXF_NOCHECKPOSITION)
			PK00 B 0 A_SpawnItemEx("Painkiller_StakeDebris3",0,0,0,frandom(-2,2),frandom(-2,2),frandom(-2,2),0,SXF_NOCHECKPOSITION)
			PK00 B 0 A_SpawnItemEx("Painkiller_StakeDebris4",0,0,0,frandom(-2,2),frandom(-2,2),frandom(-2,2),0,SXF_NOCHECKPOSITION)
			PK00 B 0 A_SpawnItemEx("Painkiller_StakeDebris5",0,0,0,frandom(-2,2),frandom(-2,2),frandom(-2,2),0,SXF_NOCHECKPOSITION)
			TNT1 A 2 A_GiveInventory("Painkiller_DamageCredit",1,AAPTR_TRACER)
			Stop
		SpawnAnother:
			PK00 A 0 ACS_NamedExecuteWithResult("Painkiller_ForceStakeBodyRemoval")
			TNT1 A 1
			TNT1 A 0 A_CustomMissile("Painkiller_StakeIntermediate",0,0,0,CMF_AIMDIRECTION|CMF_SAVEPITCH,-pitch)
			TNT1 A 5
			Stop
		Remove:
			TNT1 A 5
			Stop
	}
}

Actor Painkiller_StakeProjectileCoop : Painkiller_StakeProjectile { +THRUSPECIES Species "Player" }

Actor Painkiller_StakeIntermediate
{
	+ISMONSTER
	-COUNTKILL
	-SOLID
	+THRUACTORS
	-SHOOTABLE
	Speed 0
	Height 0
	Radius 0
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 A 0 A_Stop
			TNT1 A 0 A_SetPitch(-CallACS("Painkiller_ShooterPitch")/182)
			TNT1 A 0 A_CustomMissile("Painkiller_StakeProjectilePenetrate",0,0,0,CMF_AIMDIRECTION|CMF_TRACKOWNER,-pitch)
			TNT1 A 3
			Stop
	}
}

Actor Painkiller_StakeProjectilePenetrate : Painkiller_StakeProjectile
{
	+THRUACTORS
	States
	{
		Spawn:
			PK00 A 0
			PK00 A 0 A_TransferPointer(AAPTR_TARGET,AAPTR_DEFAULT,AAPTR_TARGET,AAPTR_TARGET)
			PK00 A 0 A_TransferPointer(AAPTR_TARGET,AAPTR_DEFAULT,AAPTR_TRACER,AAPTR_TRACER)
			PK00 A 0 A_TransferPointer(AAPTR_TARGET,AAPTR_DEFAULT,AAPTR_TARGET,AAPTR_TARGET)
			PK00 A 0 ACS_NamedExecuteWithResult("Painkiller_StakeRetrieveTracer")
			PK00 A 0 A_GiveInventory("Painkiller_StakeFlew",1)
			PK00 A 0 A_JumpIfInventory("Painkiller_WeaponModifier_Perma",1,2,AAPTR_TARGET)
			PK00 A 0 A_JumpIfInventory("Painkiller_WeaponModifier_Token",1,1,AAPTR_TARGET)
			Goto SpawnLoop
			PK00 A 0 A_ChangeFlag("NOGRAVITY",1)
			Goto SpawnLoop
		SpawnLoop:
			PK00 A 0 A_GiveInventory("Painkiller_StakeFlew",1)
			PK00 A 0 ACS_NamedExecuteWithResult("Painkiller_ProjectilePitch")
			PK00 A 2 A_SpawnItemEx("Painkiller_StakeTrail",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH)
			Loop
		Death:
			PK00 B 0 A_Playsound("Painkiller/Stake/Default",CHAN_6,0.5)
			PK00 B 0 A_ChangeFlag("NOGRAVITY",true)
			PK00 B 0 A_JumpIfInventory("Painkiller_StakeFlew",1,6)
			PK00 A 0 A_TransferPointer(AAPTR_TARGET,AAPTR_DEFAULT,AAPTR_TARGET,AAPTR_TARGET)
			PK00 A 0 A_SetPitch(CallACS("Painkiller_ShooterPitch")/182)
			PK00 A 0 A_TransferPointer(AAPTR_TARGET,AAPTR_DEFAULT,AAPTR_TRACER,AAPTR_TRACER)
			PK00 A 0 A_TransferPointer(AAPTR_TARGET,AAPTR_DEFAULT,AAPTR_TARGET,AAPTR_TARGET)
			PK00 A 0 ACS_NamedExecuteWithResult("Painkiller_StakeRetrieveTracer")
			PK00 B 0 A_SpawnItemEx("Painkiller_StakeHitWallEffect1",0,0,0,0,0,-0.15,0,SXF_NOCHECKPOSITION)
			PK00 B 0 A_SpawnItemEx("Painkiller_StakeHitWallEffect2",0,0,0,0,0,frandom(0.15,0.5),0,SXF_NOCHECKPOSITION)
			PK00 B 105 A_GiveInventory("Painkiller_DamageCredit",1,AAPTR_TRACER)
			PK00 BBBBBBBBB 2 A_Fadeout(0.1)
			Stop
		XDeath:
			PK00 B 0 A_JumpIfInventory("Painkiller_StakeFlew",1,2)
			PK00 B 0 A_SetPitch(-CallACS("Painkiller_ShooterPitch")/182)
			PK00 B 105 A_GiveInventory("Painkiller_DamageCredit",1,AAPTR_TRACER)
			PK00 BBBBBBBBB 2 A_Fadeout(0.1)
			Stop
		SpawnAnother:
			PK00 B 105
			PK00 BBBBBBBBB 2 A_Fadeout(0.1)
			Stop
	}
}

Actor Painkiller_StakeProjectileLaunch : PainkillerProjectile
{
	-NOGRAVITY
	+HITTRACER
	+NODAMAGETHRUST
	+NOEXTREMEDEATH
	//+THRUACTORS
	DamageType "PainkillerExplosive"
	Gravity 0.5
	Speed 50
	Scale 0.5
	Damage (0)
	Height 4
	Radius 2
	States
	{
		Spawn:
			PK00 A 0
			//PK00 A 1
			//PK00 A 0 A_ChangeFlag("THRUACTORS",0)
			Goto SpawnLoop
		SpawnLoop:
			PK00 A 0 A_GiveInventory("Painkiller_StakeFlew",1)
			PK00 A 0 ACS_NamedExecuteWithResult("Painkiller_ProjectilePitch")
			PK00 A 0 A_SpawnItemEx("Painkiller_FlamethrowerFire",0,0,16,frandom(-0.25,0.25),frandom(-0.25,0.25),frandom(0.25,0.5),0,SXF_NOCHECKPOSITION)
			PK00 A 2 A_SpawnItemEx("Painkiller_StakeTrail",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH)
			Loop
		Death:
			PK00 B 0 A_PlaySound("Painkiller/Grenade/Explode",CHAN_BODY)
			PK00 B 0 A_Explode(200,96)
			PK00 B 0 A_RadiusGive("Painkiller_DamageCredit",96,RGF_MONSTERS|RGF_NOSIGHT,1)
			PK00 B 0 A_SpawnItemEx("Painkiller_StakeDebris1",0,0,0,frandom(-2.0,2.0),frandom(-2.0,2.0),frandom(-2.0,2.0),0,SXF_NOCHECKPOSITION)
			PK00 B 0 A_SpawnItemEx("Painkiller_StakeDebris2",0,0,0,frandom(-2.0,2.0),frandom(-2.0,2.0),frandom(-2.0,2.0),0,SXF_NOCHECKPOSITION)
			PK00 B 0 A_SpawnItemEx("Painkiller_StakeDebris3",0,0,0,frandom(-2.0,2.0),frandom(-2.0,2.0),frandom(-2.0,2.0),0,SXF_NOCHECKPOSITION)
			PK00 B 0 A_SpawnItemEx("Painkiller_StakeDebris4",0,0,0,frandom(-2.0,2.0),frandom(-2.0,2.0),frandom(-2.0,2.0),0,SXF_NOCHECKPOSITION)
			PK00 B 0 A_SpawnItemEx("Painkiller_StakeDebris5",0,0,0,frandom(-2.0,2.0),frandom(-2.0,2.0),frandom(-2.0,2.0),0,SXF_NOCHECKPOSITION)
			PK00 B 0 A_GiveInventory("Painkiller_DamageCredit",1,AAPTR_TRACER)
			TNT1 A 1 A_SpawnItemEx("Painkiller_GrenadeSmokeSpawner",0,0,0,0,0,0,0,SXF_CLIENTSIDE|SXF_NOCHECKPOSITION)
			Stop
	}
}

Actor Painkiller_StakeProjectileLaunchCoop : Painkiller_StakeProjectileLaunch { +THRUSPECIES Species "Player" }

Actor Painkiller_StakeFlew : PainkillerBoolean {}
Actor Painkiller_StakeFlaming : PainkillerBoolean {}

Actor Painkiller_StakeDebris1 : Painkiller_Debris
{
	Scale 3.0
	Gravity 0.5
	States
	{
		Spawn:
			PK00 AA 0
			Goto SpawnLoop
		SpawnLoop:
			PK00 B 0 A_SetAngle(random(0,360))
			PK00 B 70
			PK00 BBBBBBBBBBBBBBBB 3 A_Fadeout(0.05)
			Stop
	}
}

Actor Painkiller_StakeDebris2 : Painkiller_StakeDebris1 {}
Actor Painkiller_StakeDebris3 : Painkiller_StakeDebris1 {}
Actor Painkiller_StakeDebris4 : Painkiller_StakeDebris1 {}
Actor Painkiller_StakeDebris5 : Painkiller_StakeDebris1 {}

Actor Painkiller_StakeTrail : Painkiller_Trail {}

Actor Painkiller_StakeGrenade : PainkillerProjectile
{
	-NOGRAVITY
	+NODAMAGETHRUST
	+THRUSPECIES
	+FORCERADIUSDMG
	+HITTRACER
	-SKYEXPLODE
	DamageType "PainkillerExplosive"
	BounceType "Doom"
	BounceCount 30
	BounceFactor 0.6
	WallBounceFactor 0.5
	BounceSound "Painkiller/Grenade/Bounce"
	Species "PainkillerGrenade"
	//+THRUACTORS
	Scale 10.0
	Gravity 0.35
	Speed 16
	Damage (0)
	Height 4
	Radius 2
	var int user_pitch;
	States
	{
		Spawn:
			PK00 A 0
			PK00 A 0 A_SetUserVar("user_pitch",-CallACS("Painkiller_ShooterPitch")/182)
			//PK00 A 0 A_JumpIf(CallACS("Painkiller_GrenadeTidToken")==1,1)
			PK00 B 1 ACS_NamedExecuteWithResult("Painkiller_SpawnGrenadeHitBox",1)
			//PK00 A 0 A_SpawnItemEx("Painkiller_GrenadeHitBox",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS)
			//PK00 A 1
			//PK00 A 0 A_ChangeFlag("THRUACTORS",0)
			Goto SpawnLoop
		SpawnLoop:
			//PK00 A 0 A_SetPitch(pitch+5)
			PK00 B 0 ACS_NamedExecuteWithResult("Painkiller_ProjectilePitch")
			PK00 B 0 A_SpawnItemEx("Painkiller_Smoke",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
			PK00 B 2 A_SpawnItemEx("Painkiller_GrenadeTrail",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH)
			Loop
		Launch:
			PK00 A 0 A_SetUserVar("user_pitch",-CallACS("Painkiller_ShooterPitch")/182)
			TNT1 A 2 A_CustomMissile("Painkiller_StakeProjectileLaunch",0,0,CallACS("Painkiller_ShooterAngle")/182,CMF_AIMDIRECTION|CMF_ABSOLUTEANGLE,user_pitch)
			Stop
		Death:
			PK00 B 0 A_PlaySound("Painkiller/Grenade/Explode",CHAN_5)
			PK00 B 0 A_RadiusGive("Painkiller_DamageCredit",96,RGF_MONSTERS|RGF_NOSIGHT,1)
			PK00 B 0 A_Explode(110,96)
			PK00 B 0 A_JumpIfInventory("Painkiller_WeaponModifier_Perma",1,"FireExplode",AAPTR_TARGET)
			PK00 B 0 A_JumpIfInventory("Painkiller_WeaponModifier_Token",1,"FireExplode",AAPTR_TARGET)
			Goto DeathEnd
		FireExplode:
			PK00 B 0 A_RadiusGive("Painkiller_RadiusRoaster",96,RGF_MONSTERS|RGF_CORPSES,1)
			PK00 BBB 0 A_SpawnItemEx("Painkiller_GasTankExplosionFire",0,0,16,frandom(-0.25,0.25),frandom(-0.25,0.25),frandom(0.25,0.5),0,SXF_NOCHECKPOSITION)
			PK00 BB 0 A_SpawnItemEx("Painkiller_FlamethrowerFireStarterMod",random(-16,16),random(-16,16),random(-16,16),0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS)
			Goto DeathEnd
		DeathEnd:
			TNT1 A 0 A_GiveInventory("Painkiller_DamageCredit",1,AAPTR_TRACER)
			TNT1 A 1 A_SpawnItemEx("Painkiller_GrenadeSmokeSpawner",0,0,0,0,0,0,0,SXF_CLIENTSIDE|SXF_NOCHECKPOSITION)
			Stop
	}
}

Actor Painkiller_StakeGrenadeCoop : Painkiller_StakeGrenade
{
	+MTHRUSPECIES
	States
	{
		Launch:
			PK00 A 0 A_SetUserVar("user_pitch",-CallACS("Painkiller_ShooterPitch")/182)
			TNT1 A 2 A_CustomMissile("Painkiller_StakeProjectileLaunchCoop",0,0,CallACS("Painkiller_ShooterAngle")/182,CMF_AIMDIRECTION|CMF_ABSOLUTEANGLE,user_pitch)
			Stop
	}
}

Actor Painkiller_GrenadeSmokeSpawner : Painkiller_VisualEffect
{
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 AAAAAAAAAAAAAAAA 0 A_SpawnItemEx("Painkiller_GrenadeSmoke",random(-16,16),random(-16,16),random(-16,16),random(-1,1),random(-1,1),random(4,5),0,SXF_CLIENTSIDE|SXF_NOCHECKPOSITION)
			TNT1 AAAAAAAAAAAAAAAA 0 A_SpawnItemEx("Painkiller_Smoke",random(-16,16),random(-16,16),random(-16,48),random(-1,1),random(-1,1),3,0,SXF_CLIENTSIDE|SXF_NOCHECKPOSITION)
			TNT1 A 0 A_SpawnItemEx("Painkiller_GrenadeWhiteSmoke",0,0,0,0,0,3,0,SXF_CLIENTSIDE|SXF_NOCHECKPOSITION)
			TNT1 A 0 A_SpawnItemEx("Painkiller_GrenadeSmokeDart",0,0,0,-12,0,12,0,SXF_CLIENTSIDE|SXF_NOCHECKPOSITION)
			TNT1 A 0 A_SpawnItemEx("Painkiller_GrenadeSmokeDart",0,0,0,-12,12,12,0,SXF_CLIENTSIDE|SXF_NOCHECKPOSITION)
			TNT1 A 0 A_SpawnItemEx("Painkiller_GrenadeSmokeDart",0,0,0,0,12,12,0,SXF_CLIENTSIDE|SXF_NOCHECKPOSITION)
			TNT1 A 0 A_SpawnItemEx("Painkiller_GrenadeSmokeDart",0,0,0,12,12,12,0,SXF_CLIENTSIDE|SXF_NOCHECKPOSITION)
			TNT1 A 0 A_SpawnItemEx("Painkiller_GrenadeSmokeDart",0,0,0,12,0,12,0,SXF_CLIENTSIDE|SXF_NOCHECKPOSITION)
			TNT1 A 3 A_SpawnItemEx("Painkiller_GrenadeSmokeDart",0,0,0,12,-12,12,0,SXF_CLIENTSIDE|SXF_NOCHECKPOSITION)
			TNT1 BCDEF 3
			Stop
	}
}

Actor Painkiller_GrenadeSmokeDart
{
	//Projectile
	Height 4
	Radius 2
	//Damage (0)
	Speed 20
	Gravity 0.75
	Scale 0.5
	+THRUACTORS
	+BLOODLESSIMPACT
	-NOGRAVITY
	+NOINTERACTION
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 A 0 A_SpawnItemEx("Painkiller_GrenadeFire",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)	TNT1 A 2 A_SpawnItemEx("Painkiller_Smoke",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERSCALE)
			//TNT1 A 0 A_ChangeVelocity(0,0,-8,CVF_RELATIVE)
			TNT1 A 0 A_SpawnItemEx("Painkiller_GrenadeFire",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)	TNT1 A 2 A_SpawnItemEx("Painkiller_Smoke",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERSCALE)
			TNT1 A 0 A_SpawnItemEx("Painkiller_GrenadeFire",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)	TNT1 A 2 A_SpawnItemEx("Painkiller_Smoke",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERSCALE)
			TNT1 A 0 A_SpawnItemEx("Painkiller_GrenadeFire",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)	TNT1 A 2 A_SpawnItemEx("Painkiller_Smoke",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERSCALE)
			TNT1 A 0 A_SpawnItemEx("Painkiller_GrenadeFire",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)	TNT1 A 2 A_SpawnItemEx("Painkiller_Smoke",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERSCALE)
			TNT1 A 0 A_SpawnItemEx("Painkiller_GrenadeFire",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)	TNT1 A 2 A_SpawnItemEx("Painkiller_Smoke",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERSCALE)
			TNT1 A 0 A_SpawnItemEx("Painkiller_GrenadeFire",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)	TNT1 A 2 A_SpawnItemEx("Painkiller_Smoke",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERSCALE)
			TNT1 A 0 A_SpawnItemEx("Painkiller_GrenadeFire",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)	TNT1 A 2 A_SpawnItemEx("Painkiller_Smoke",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERSCALE)
			Stop
	}
}

Actor Painkiller_GrenadeTrail : Painkiller_Trail {}

Actor Painkiller_GrenadeSmoke : Painkiller_Smoke
{
	Scale 0.4
	Renderstyle Add
	Alpha 1.0
	States
	{
		Spawn:
			TNT1 A 0
			PK00 MMMMMMMM 2 Bright A_Fadeout(0.04)
			PK00 LLLLLLLLL 2 Bright A_Fadeout(0.04)
			PK00 KKKKKKKK 2 Bright A_Fadeout(0.04)
			Stop
	}
}

Actor Painkiller_GrenadeFire : Painkiller_Smoke
{
	Scale 0.1
	States
	{
		Spawn:
			TNT1 A 0
			PK00 KKKKKKKKKKKKKKKK 3 Bright A_Fadeout(0.05)
			Stop
	}
}

Actor Painkiller_GrenadeWhiteSmoke : Painkiller_Smoke
{
	Scale 0.67
	States
	{
		Spawn:
			TNT1 A 0
			PK00 CCCCCCCCCCCCCCCC 3 A_Fadeout(0.05)
			Stop
	}
}

Actor Painkiller_GrenadeHitBox
{
	Height 8
	Radius 4
	Health 1
	Mass 50000
	+ISMONSTER
	-COUNTKILL
	//+THRUACTORS
	-SOLID
	+SHOOTABLE
	+NOBLOOD
	+NOGRAVITY
	DamageFactor "PainkillerStake", 1.0
	DamageFactor "Normal", 0.0
	Species "PainkillerGrenade"
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 A 0
		SpawnLoop:
			TNT1 A 0 ACS_NamedExecuteWithResult("Painkiller_HitBoxRemover")
			TNT1 A 2 A_Warp(AAPTR_TRACER,0,0,0,0,WARPF_NOCHECKPOSITION)
			Loop
		Death:
			TNT1 A 2
			Stop
		Death.PainkillerStake:
			TNT1 A 0 ACS_NamedExecuteWithResult("Painkiller_StakeGrenadeComboLaunch")
			TNT1 A 2
			Stop
	}
}

Actor Painkiller_StakeFire
{
	Height 16
	Radius 8
	Renderstyle Add
	Scale 0.33
	Alpha 0.75
	+NOINTERACTION
	+CLIENTSIDEONLY
	+FORCEXYBILLBOARD
	States
	{
		Spawn:
			PK02 A 0
			PK02 A 0 A_SpawnItemEx("Painkiller_FlamethrowerSmoke",0,0,0,frandom(-1.0,1.0),frandom(-1.0,1.0),frandom(0.5,1.0),0,SXF_NOCHECKPOSITION)
			PK02 ABCDE 1 Bright A_Fadeout(0.15)
			Stop
	}
}

Actor Painkiller_StakeHitWallEffect1 : Painkiller_Smoke
{
	Scale 0.1
	Renderstyle Translucent
	Alpha 0.1
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 A 0 A_SetScale(frandom(0.08,0.1))
			PK07 K 0 A_SetScale(scalex+0.02)	PK07 K 1 A_Fadeout(0.01)
			PK07 K 0 A_SetScale(scalex+0.02)	PK07 K 1 A_Fadeout(0.01)
			PK07 K 0 A_SetScale(scalex+0.02)	PK07 K 1 A_Fadeout(0.01)
			PK07 K 0 A_SetScale(scalex+0.02)	PK07 K 1 A_Fadeout(0.01)
			PK07 K 0 A_SetScale(scalex+0.02)	PK07 K 1 A_Fadeout(0.01)
			PK07 K 0 A_SetScale(scalex+0.02)	PK07 K 1 A_Fadeout(0.01)
			PK07 K 0 A_SetScale(scalex+0.02)	PK07 K 1 A_Fadeout(0.01)
			PK07 K 0 A_SetScale(scalex+0.02)	PK07 K 1 A_Fadeout(0.01)
			PK07 K 0 A_SetScale(scalex+0.02)	PK07 K 1 A_Fadeout(0.01)
			Stop
	}
}

Actor Painkiller_StakeHitWallEffect2 : Painkiller_Smoke
{
	Scale 0.1
	Renderstyle Translucent
	Alpha 0.3
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 A 0 A_SetScale(frandom(0.0,0.25))
			PK07 K 0 A_SetScale(scalex+0.05)	PK07 K 1 A_Fadeout(0.03)
			PK07 K 0 A_SetScale(scalex+0.05)	PK07 K 1 A_Fadeout(0.03)
			PK07 K 0 A_SetScale(scalex+0.05)	PK07 K 1 A_Fadeout(0.03)
			PK07 K 0 A_SetScale(scalex+0.05)	PK07 K 1 A_Fadeout(0.03)
			PK07 K 0 A_SetScale(scalex+0.05)	PK07 K 1 A_Fadeout(0.03)
			PK07 K 0 A_SetScale(scalex+0.05)	PK07 K 1 A_Fadeout(0.03)
			PK07 K 0 A_SetScale(scalex+0.05)	PK07 K 1 A_Fadeout(0.03)
			PK07 K 0 A_SetScale(scalex+0.05)	PK07 K 1 A_Fadeout(0.03)
			PK07 K 0 A_SetScale(scalex+0.05)	PK07 K 1 A_Fadeout(0.03)
			Stop
	}
}