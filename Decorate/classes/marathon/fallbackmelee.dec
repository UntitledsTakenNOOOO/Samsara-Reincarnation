actor UsingFists: Boolean {}
actor DOUBLEFISTING: Boolean {}
actor GibfistSide: Boolean {}

actor "Steel Knuckles" : SamsaraMarathonWeapon
{
    Weapon.SlotNumber 0
    Weapon.SelectionOrder 3700
    Weapon.KickBack 300
    Obituary "$SAMSARA_MARATHON_OB_FIST"
    Tag "Steel Knuckles"
    +MELEEWEAPON
    +WIMPY_WEAPON
    States
    {
      // Single
      Ready:
        TNT1 A 0 A_JumpIfInventory("DOUBLEFISTING",1,"ReadyDual")
        FIS2 A 1 A_WeaponReady
        loop

      Deselect:
        TNT1 A 0 A_JumpIfInventory("DOUBLEFISTING",1,"DeselectDual")
        TNT1 A 0 A_JumpIfHealthLower(1,"DeselectInstant")
        TNT1 A 0 A_Lower
        FIS2 A 1 A_Lower
        loop

      Select:
        //TNT1 A 0 A_TakeInventory("UsingFists")
        TNT1 A 0 A_TakeInventory("UsingPistols")
        TNT1 A 0 A_TakeInventory("UsingDualPistols")
        TNT1 A 0 A_TakeInventory("UsingSMG")
        TNT1 A 0 A_TakeInventory("UsingPacifier")
        TNT1 A 0 A_TakeInventory("UsingShotguns")
        TNT1 A 0 A_TakeInventory("UsingDualShotguns")
        TNT1 A 0 A_TakeInventory("UsingFusionPistol")
        TNT1 A 0 A_TakeInventory("UsingAssaultRifle")
        TNT1 A 0 A_TakeInventory("UsingSpanker")
        TNT1 A 0 A_TakeInventory("UsingToasty")
        TNT1 A 0 A_TakeInventory("UsingWMC")
        TNT1 A 0 A_TakeInventory("UsingAlienWeapon")
        TNT1 A 0 A_TakeInventory("UsingAlienWeapon2")
        TNT1 A 0 A_TakeInventory("UsingMarathonInstagib")

        TNT1 A 0 A_GiveInventory("UsingFists")
        TNT1 A 0 A_JumpIfInventory("DOUBLEFISTING",1,"SelectDual")
        TNT1 A 0 A_Raise
        FIS2 A 1 A_Raise
        loop

      Fire: // DoubleFiringSpeed parity with dual
        TNT1 A 0 A_JumpIfInventory("DOUBLEFISTING",1,"FireDual")
        FIS2 BBB 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        TNT1 A 0 A_GiveInventory("SamsaraMarathonFistsAttackHandler")
        FIS2 C 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        FIS2 C 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        FIS2 CBBB 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        FIS2 A 7 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        goto Ready

      // Dual Init
      AltFire:
        TNT1 A 0 A_JumpIfInventory("DOUBLEFISTING",1,"AltFireDual")
        TNT1 A 0 A_GiveInventory("DOUBLEFISTING")
        FIS3 KIGEDCBA 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        /*FIS3 LKJIHGFE 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        FIS3 DCBA 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)*/
        goto Ready

      // Dual
      ReadyDual:
        FIS3 A 1 A_WeaponReady
        goto Ready

      DeselectDual:
        TNT1 A 0 A_TakeInventory("DOUBLEFISTING")
        TNT1 A 0 A_JumpIfHealthLower(1,"DeselectInstant")
        TNT1 A 0 A_Lower
        FIS3 A 1 A_Lower
        loop

      SelectDual:
        TNT1 A 0 A_Raise
        FIS3 A 1 A_Raise
        goto Select

      // Left Fist
      FireDual:
        TNT1 A 0 A_JumpIfInventory("SamsaraSynthFireRight",1,"FireDualSideCheck")
        TNT1 A 0 A_JumpIfInventory("SamsaraReverseDualWieldFiring",1,"FireDualStart")
        goto AltFireDualStart

      FireDualSideCheck:
        TNT1 A 0 A_JumpIfInventory("GibfistSide",1,"AltFireDualStart")
        goto FireDualStart

      FireDualStart:
        TNT1 A 0 A_GiveInventory("GibfistSide")
        FIS4 ABC 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        TNT1 A 0 A_GiveInventory("SamsaraMarathonFistsAttackHandler")
        FIS4 D 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        FIS4 E 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        FIS4 DCBA 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        FIS3 A 7 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        goto Ready

      // Right Fist
      AltFireDual:
        TNT1 A 0 A_JumpIfInventory("SamsaraSynthFireLeft",1,"FireDualSideCheck")
        TNT1 A 0 A_JumpIfInventory("SamsaraReverseDualWieldFiring",1,"AltFireDualStart")
        goto FireDualStart

      AltFireDualStart:
        TNT1 A 0 A_TakeInventory("GibfistSide")
        FIS5 ABC 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        TNT1 A 0 A_GiveInventory("SamsaraMarathonFistsAttackHandler")
        FIS5 D 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        FIS5 E 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        FIS5 DCBA 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        FIS3 A 7 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        goto Ready
    }
}

actor SamsaraMarathonFistsAttackHandler : Trigger
{
    States
    {
      // Attack stuff
      Pickup:
        TNT1 A 0 A_JumpIf((velx * velx) + (vely * vely) >= 49, "PickupRunning")
      // Not running
      PickupNotRunning:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupNotRunningCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupNotRunningCoop")
      PickupNotRunningDM:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasSpread",1,"PickupNotRunningDMSpread")
        TNT1 A 0 A_CustomPunch(random(12,14),true,0,"BlankPuffMarathon",96)
        goto PickupFinish

      PickupNotRunningDMSpread:
        TNT1 A 0 A_CustomPunch(random(12,14)*3,true,0,"BlankPuffMarathon",96)
        goto PickupFinish

      PickupNotRunningCoop:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasSpread",1,"PickupNotRunningCoopSpread")
        TNT1 A 0 A_CustomPunch(random(12,14),true,0,"BlankPuffMarathonCoop",96)
        goto PickupFinish

      PickupNotRunningCoopSpread:
        TNT1 A 0 A_CustomPunch(random(12,14)*3,true,0,"BlankPuffMarathonCoop",96)
        goto PickupFinish

      // Running
      PickupRunning:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupRunningCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupRunningCoop")
      PickupRunningDM:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasSpread",1,"PickupRunningDMSpread")
        TNT1 A 0 A_CustomPunch(random(60,70),true,0,"BlankPuffMarathon",96) // Formerly 50-60
        goto PickupFinish

      PickupRunningDMSpread:
        TNT1 A 0 A_CustomPunch(random(60,70)*3,true,0,"BlankPuffMarathon",96)
        goto PickupFinish

      PickupRunningCoop:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasSpread",1,"PickupRunningCoopSpread")
        TNT1 A 0 A_CustomPunch(random(60,70),true,0,"BlankPuffMarathonCoop",96)
        goto PickupFinish

      PickupRunningCoopSpread:
        TNT1 A 0 A_CustomPunch(random(60,70)*3,true,0,"BlankPuffMarathonCoop",96)
        goto PickupFinish

      // Finishing stuff
      PickupFinish:
        TNT1 A 0 ACS_NamedExecuteWithResult("MarathonVidMaster")
        stop
    }
}

actor BlankPuffMarathon : BulletPuff
{
    DamageType "Pistol"
    //+BLOODLESSIMPACT
    +CLIENTSIDEONLY
    //+NOEXTREMEDEATH
    +PUFFONACTORS
    States
    {
      Melee:
        TNT1 A 1 A_PlaySound("marathon/fist/ding")
        stop

      XDeath:
      Crash:
        TNT1 A 1 A_PlaySound("marathon/fist/hit")
        stop
    }
}

actor BlankPuffMarathonCoop : BlankPuffMarathon { +MTHRUSPECIES }
