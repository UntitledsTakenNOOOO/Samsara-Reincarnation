actor Totenkopf_Sniper : InstantSwitchWeapon
{
	Tag "Kar98k Rifle (Scoped)"
	Weapon.SlotPriority 2
	Weapon.AmmoGive 0
    Weapon.SelectionOrder 800
    Weapon.SlotNumber 2
    Inventory.PickUpSound "Totenkopf/getweapon"
    Weapon.AmmoUse 1
    Weapon.AmmoType "Shell"
    +DONTBOB
    +INVENTORY.UNDROPPABLE
    Weapon.YAdjust 16
	Weapon.SlotPriority 3	
    Inventory.PickupMessage "Kar98k Sniper Rifle"
	Inventory.RestrictedTo "WolfenPlayer"	
	+WEAPON.NO_AUTO_SWITCH
	+WEAPON.NOALERT
    States
    {
      Spawn:
        TK12 M -1
        stop

      Ready:
        TNT1 A 0 A_ZoomFactor(4,ZOOM_INSTANT)
        TNT1 A 0 A_JumpIf(CallACS("Totenkopf_SniperScope")==1,1)
        TNT1 A 0 A_JumpIfInventory("Machine Gun",1,2)
        TNT1 A 1 A_WeaponReady(WRF_NOSECONDARY)
        loop
        TNT1 A 1 A_WeaponReady
        loop

      Deselect:
        TNT1 A 0 A_ZoomFactor(1,ZOOM_INSTANT)
        goto Super::Deselect

      Fire: // keep original durations for DoubleFiringSpeed consistency with unscoped version
        TNT1 A 3 A_AlertMonsters
        TNT1 A 3 A_GiveInventory("SamsaraTotenkopfSniperAttackHandler")
        TNT1 A 5
        TNT1 A 0 A_CheckReload
        TNT1 A 5 A_PlaySound("Totenkopf/kar98bolt")
        TNT1 A 5
        TNT1 AAA 2
        TNT1 AA 5
        TNT1 A 3
        TNT1 A 0 A_ReFire
        goto Ready

	  AltFire:
        TNT1 A 8
        TNT1 A 0 A_SelectWeapon("Machine Gun")
        goto Deselect
    }
}

actor SamsaraTotenkopfSniperAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Shell",1,1)
        stop
        TNT1 A 0 A_TakeInventory("Shell",1)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("Totenkopf/atkkar98",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireCustomMissile("Totenkopf_Tracer_Sniper",0,false)
        stop

      PickupAttackCoop:
        TNT1 A 0 A_FireCustomMissile("Totenkopf_Tracer_SniperCoop",0,false)
        stop
    }
}

actor Totenkopf_Tracer_Sniper : Totenkopf_Tracer
{
    Damage ((125+random(0,256)%25))
    +THRUGHOST
}

actor Totenkopf_Tracer_SniperCoop : Totenkopf_Tracer_Sniper { Species "Player" +THRUSPECIES }

/*Actor Totenkopf_SniperPickup : CustomInventory
{
	Inventory.PickupMessage "Scoped Kar98k Rifle"
	Inventory.PickUpSound "Totenkopf/getweapon"
	+INVENTORY.AUTOACTIVATE
	States
	{
		Spawn:
			TK12 M 4
			Loop
		Pickup:
			TNT1 A 0 A_JumpIfInventory("Shell",0,1)
			Goto Pickup+3
			TNT1 A 0 A_JumpIfInventory("Totenkopf_Sniper",1,1)
			Goto Pickup+3
			TNT1 A 0 A_JumpIfInventory("Totenkopf_Kar98K",1,"PickupFailure")
			TNT1 A 0 A_GiveInventory("Totenkopf_Sniper",1)
			TNT1 A 0 A_GiveInventory("Totenkopf_Kar98K",1)
			TNT1 A 0 A_GiveInventory("Shell",5)
			Stop
		PickupFailure:
			TNT1 A 0 A_Railwait
			fail
	}
}*/
