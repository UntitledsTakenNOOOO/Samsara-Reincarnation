actor VoodooDoll : CalebWeapon
{
	Obituary "%k \cutortured\c- %o."
	Inventory.PickupMessage "Voodoo Doll! Find some victims!"
	Inventory.PickupSound "Caleb/Weaponup2"
	Weapon.SelectionOrder 500
	Weapon.Kickback 0
	Weapon.AmmoType "Voodoo"
	Weapon.AmmoUse 0
	Weapon.AmmoUse2 0
	Weapon.AmmoGive 100
	Weapon.AmmoType2 "Voodoo"
	Weapon.SlotNumber 8
	Weapon.SlotPriority 8
	Tag "Voodoo Doll"
	+FRIGHTENED
	+WEAPON.NOALERT
	States
	{
	Ready:
		TNT1 A 0 A_TakeInventory("UsingPitchfork", 1)
		TNT1 A 0 A_JumpIfInventory("CalebReady",1,"IntroTaunt")
		TNT1 A 0 A_JumpIfInventory("CalebWaiting",60,"WaitingTaunt")
		TNT1 A 0 A_JumpIf(momx <= -6 || momx >= 6,"ReadyMoving")
		VOO3 A 1 A_WeaponReady
		Goto Ready+1
	ReadyMoving:
		VOO3 AAARRARRAAARRRRAAA 7 A_WeaponReady
		Goto Ready+1
	Deselect:
		VOO3 A 0 A_JumpIfInventory("Voodoo", 1, "DeselectLoop")
		VOO3 A 0 A_TakeInventory("VoodooDoll", 1)
		Goto Super::DeselectLoop
	Select:
		VOO3 A 0 A_JumpIfInventory("CalebLighterReadyDummy",1,"DeselectLighter")
		Goto Super::SelectLoop
	Fire:
		TNT1 A 0 A_JumpIfTargetInLOS(1)
		Goto Self
		TNT1 A 0 A_Jump(96,"Chest","Shoulder")
		TNT1 A 0 A_Jump(256,"Chest","Shoulder","Eye","Pelvis")
		Goto Chest
	Self:
		VOO3 BC 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		TNT1 A 0 A_PlayWeaponSound("Vodoo/Stab")
		TNT1 A 0 Thing_Damage(0,1,18)
		VDOE DEEFFGGHH 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		VOO3 CBA 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		TNT1 A 0 A_Refire
		Goto Ready
	Chest:
		VOO3 BC 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		TNT1 A 0 A_PlayWeaponSound("Vodoo/Stab")
		TNT1 A 0 A_JumpIfInventory("CoopModeOn", 1, 2)
		TNT1 A 0 A_FireBullets(1, 0, 1, 25, "VoodooPuff", FBF_NORANDOM|FBF_USEAMMO)
		Goto ChestEnd
		TNT1 A 0 A_FireBullets(1, 0, 1, 25, "VoodooPuffCoop", FBF_NORANDOM|FBF_USEAMMO)
		Goto ChestEnd
	ChestEnd:
		TNT1 A 0 A_TakeInventory("Voodoo", 1, TIF_NOTAKEINFINITE)
		VOO3 D 6 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		VOO3 CBA 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		Goto CheckVoodooAmmo
	Shoulder:
		VOO3 BC 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		TNT1 A 0 A_PlayWeaponSound("Vodoo/Stab")
		TNT1 A 0 A_JumpIfInventory("CoopModeOn", 1, 2)
		TNT1 A 0 A_FireBullets(1, 0, 1, 25, "VoodooPuff", FBF_NORANDOM|FBF_USEAMMO)
		Goto ShoulderEnd
		TNT1 A 0 A_FireBullets(1, 0, 1, 25, "VoodooPuffCoop", FBF_NORANDOM|FBF_USEAMMO)
		Goto ShoulderEnd
	ShoulderEnd:
		TNT1 A 0 A_TakeInventory("Voodoo", 1, TIF_NOTAKEINFINITE)
		VOO3 E 6 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		VOO3 CBA 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		Goto CheckVoodooAmmo
	Pelvis:
		VOO3 MN 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		TNT1 A 0 A_PlayWeaponSound("Vodoo/Stab")
		TNT1 A 0 A_PlaySound("Vodoo/Doll")
		TNT1 A 0 A_JumpIfInventory("CoopModeOn", 1, 2)
		TNT1 A 0 A_FireBullets(1, 0, 1, 25, "VoodooPuff", FBF_NORANDOM|FBF_USEAMMO)
		Goto PelvisEnd
		TNT1 A 0 A_FireBullets(1, 0, 1, 25, "VoodooPuffCoop", FBF_NORANDOM|FBF_USEAMMO)
		Goto PelvisEnd
	PelvisEnd:
		TNT1 A 0 A_TakeInventory("Voodoo", 3, TIF_NOTAKEINFINITE)
		VOO3 PQPQPQP 4 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		VOO3 NMA 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		Goto CheckVoodooAmmo
	Eye:
		VOO3 FG 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		TNT1 A 0 A_PlayWeaponSound("Vodoo/Stab")
		TNT1 A 0 A_JumpIfInventory("CoopModeOn", 1, 2)
		TNT1 A 0 A_FireBullets(1, 0, 1, 25, "VoodooPuff", FBF_NORANDOM|FBF_USEAMMO)
		Goto EyeEnd
		TNT1 A 0 A_FireBullets(1, 0, 1, 25, "VoodooPuffCoop", FBF_NORANDOM|FBF_USEAMMO)
		Goto EyeEnd
	EyeEnd:
		TNT1 A 0 A_TakeInventory("Voodoo", 1, TIF_NOTAKEINFINITE)
		VOO3 I 3 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		VOO3 JKLKJ 4 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		VOO3 GFA 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		Goto CheckVoodooAmmo
	CheckVoodooAmmo:
		TNT1 A 0 A_JumpIfInventory("Voodoo", 1, 1)
		Goto Drop
		TNT1 A 0
		Goto Ready
	AltFire:
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_PlaySound ("Vodoo/Altfire",CHAN_WEAPON)
		VOO3 C 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		VDOL ABCDEF 4 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		TNT1 A 0 A_JumpIfInventory("CoopModeOn", 1, 2)
		TNT1 A 0 A_FireCustomMissile("VoodooPuff2")
		Goto AltFireEnd
		TNT1 A 0 A_FireCustomMissile("VoodooPuff2Coop")
		Goto AltFireEnd
	AltFireEnd:
		VDOL G 19 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		Goto Drop
	Flash:
		TNT1 A 0
		Stop
	Spawn:
		VDLF A -1
		Stop
	Drop:
		TNT1 A 0 A_TakeInventory("VoodooDoll", 1)
		TNT1 A 0 A_SelectWeapon("Pitchfork")
		Goto Ready
	}
}

actor VoodooPuff
{
	RenderStyle None
	+INVISIBLE
	+NOBLOCKMAP
	+DONTSPLASH
	+NOGRAVITY
	+ALWAYSPUFF
	+FOILINVUL
	+FORCEPAIN
	DamageType "Vodoo"
	States
	{
	Spawn:
		VDLF A 5
		Stop
	}
}

Actor VoodooPuffCoop : VoodooPuff { +MTHRUSPECIES +THRUSPECIES Species "Player" }

actor VoodooPuff2 : BulletPuff // Uses tid of it as ammo, in the script the tid becomes half of the ammo amount.
{
	+NOBLOCKMAP
	+DONTSPLASH
	+NOGRAVITY
	+ALWAYSPUFF
	+FOILINVUL
	+FORCERADIUSDMG
	+PUFFGETSOWNER
	Radius 255
	Height 255
	speed 1000
	damage 10000
	PROJECTILE
	DamageType "Vodoo"
	States
	{
	Spawn:
		TNT1 A 1
		Goto Death
	Death:
		TNT1 A 1 ACS_NamedExecuteAlways("CalebUnique", 0)
		TNT1 A 0 A_BFGSpray("VoodooPuff",tid,15)
		Stop
	}
}

actor VoodooPuff2Coop : VoodooPuff2 { +MTHRUSPECIES +THRUSPECIES Species "Player" }

actor Voodoo : Ammo
{
	Inventory.MaxAmount 100
	Tag "Voodoo Doll Ammo"
	+IGNORESKILL
}
