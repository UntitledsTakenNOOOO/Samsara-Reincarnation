actor "    Missile Launcher    " : Weapon
{
	Weapon.SelectionOrder 2200
	Weapon.SlotNumber 5
	Weapon.BobStyle Smooth
	Weapon.BobRangeX 0.55
	Weapon.BobRangeY 0.55
	Weapon.BobSpeed 1.2  
	Weapon.AmmoType "RocketAmmo"
	Weapon.AmmoGive 6
	Weapon.AmmoUse 1
	+INVENTORY.UNDROPPABLE
	-WEAPON.NOAUTOFIRE
	+Weapon.NoAlert
	+WEAPON.EXPLOSIVE
	Inventory.PickupSound "EradAmmo/PUP"
	Inventory.PickupMessage "You got the Missile Launcher!"
	Inventory.RestrictedTo "Eleena"
	Tag "Missile Launcher"
	States
	{
		Ready:
			TNT1 A 0 A_JumpIfInventory("EleenaReady",1,"IntroTaunt")
			ELRM A 1 A_WeaponReady
			Loop
		Deselect:
			TNT1 A 0 A_PlaySound("Erad/wepswitch")
			TNT1 A 0 A_Lower
			ELRM A 1 A_Lower
			Goto Deselect+1
		Select:
			TNT1 A 0 A_Raise
			ELRM A 1 A_Raise
			Loop
		Fire:
			TNT1 A 0 A_AlertMonsters  
			TNT1 A 0 A_PlaySound("Eleena/RFire", CHAN_WEAPON)
			TNT1 A 0 A_GunFlash
			ELRM A 2 bright A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,2)	
			TNT1 A 0 A_FireCustomMissile("EradMissile",0,1,0,0)
			Goto A
			TNT1 A 0 A_FireCustomMissile("EradCoopMissile",0,1,0,0)
		A:	
			TNT1 A 0 A_Recoil(3)
			ELRM B 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			ELRM F 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			ELRM GH 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			ELRM IJ 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			ELRM K 3 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			ELRM L 3 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			ELRM MN 4 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			ELRM O 4 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			Goto Ready
		AltFire:
			TNT1 A 0 A_JumpIfInventory("SamsaraHasPermaWeaponLevel2",1,2)
			TNT1 A 0 A_JumpIfInventory("EradEnWP",1,1)
			Goto Ready
			TNT1 A 0 A_JumpIfInventory("RocketAmmo",1,1)
			Goto Ready	
			TNT1 A 0 A_AlertMonsters	
			TNT1 A 0 A_PlaySound("Eleena/RFire", CHAN_WEAPON)
			TNT1 A 0 A_GunFlash
			ELRM A 2 bright A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			TNT1 A 0 A_TakeInventory("RocketAmmo",1, TIF_NOTAKEINFINITE)	
			TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,2)	
			TNT1 A 0 A_FireCustomMissile("EradMissile_2",0,1,0,0)
			Goto B
			TNT1 A 0 A_FireCustomMissile("EradCoopMissile_2",0,1,0,0)	
		B:
			ELRM B 0 A_Recoil(3)  
			ELRM B 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			ELRM F 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			ELRM GH 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			ELRM IJ 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			ELRM K 3 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			ELRM L 3 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			ELRM MN 4 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			ELRM O 4 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			Goto Ready
		Flash:
			TNT1 A 0 A_Light2
			ELRL A 2 bright A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			TNT1 A 0 A_Light0
			stop
		Spawn:
			EMPU A -1
			Stop
		IntroTaunt:
			TNT1 A 0 A_GiveInventory("EleenaIntroTaunt")
			Goto Ready
  }
}

actor EradMissile : Rocket
{
  Decal Scorch
  Speed 50
  Damage (75)
  SeeSound ""
  DeathSound "EradMissile/Boom"
  +NOBLOOD
  +BLOODLESSIMPACT
  +WINDTHRUST
  -ROCKETTRAIL
  DamageType "EradExplosive"
  Obituary "%o rode %k's rocket."
  Scale 0.75
  States
  {
  Spawn:
    ERAM A 2 bright A_SpawnItemEx("EradSmokeTrail",0,0,0,0,0,0,1,128)
    Loop
  Death:
    TNT1 A 0 A_Explode(120,120)
    TNT1 A 0 A_SpawnITemEx("EradPlasmaExplosion1",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_CLIENTSIDE)
    TNT1 A 5 A_PlaySound("EradMissile/Boom")
    Stop
  }
}

Actor EradMissile_2 : EradMissile
{
  Speed 25
  Damage (100)
  +SEEKERMISSILE
  States
  {
  Spawn:
    ERAM A 1 bright A_SpawnItemEx("EradSmokeTrail",0,0,0,0,0,1.8,1,128)
	ERAM A 1 bright A_Seekermissile(90,90,SMF_LOOK,SMF_PRECISE,256)
    Loop
  Death:
    TNT1 A 0 A_Explode(140,120)
    TNT1 A 0 A_SpawnITemEx("EradPlasmaExplosion1",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_CLIENTSIDE)
    TNT1 A 5 A_PlaySound("EradMissile/Boom")
    Stop
  }
}

Actor EradCoopMissile_2 : EradMissile_2 {  Species "Player"  +THRUSPECIES  +DONTHARMCLASS  +DONTHARMSPECIES  +MTHRUSPECIES	}

actor EradSmokeTrail : RocketSmokeTrail
{
  +CLIENTSIDEONLY
  RenderStyle Translucent
  Alpha 0.5
  Scale 0.65
  States
  {
  Spawn:
    ERAS ABCDE 2 bright A_FadeOut
    stop
  }
}

Actor EradCoopMissile : EradMissile {  Species "Player"  +THRUSPECIES  +DONTHARMCLASS  +DONTHARMSPECIES  +MTHRUSPECIES	}