Actor "Hornetgun" : HLWeapon
{
	Weapon.Slotnumber 1
	Weapon.SelectionOrder 6750
	Inventory.Pickupmessage "You got the hornetgun!"
	Inventory.Pickupsound "halflife/weaponpickup"
	Weapon.AmmoType1 "HornetGunAmmo"
    Weapon.AmmoUse1 0
    Weapon.AmmoGive1 8
	Weapon.AmmoType2 "ShockRoachAmmo"
    Weapon.AmmoUse2 0
    Weapon.AmmoGive2 10
	-WEAPON.NOALERT
	Inventory.RestrictedTo "HalfLifePlayer"	
	states
	{
	Spawn:
		HW10 Z -1
		stop
	Select:
		TNT1 A 0 A_TakeInventory("freeman_fistselected",1)	
		TNT1 A 0 A_TakeInventory("freeman_pistolselected",1)
		TNT1 A 0 A_TakeInventory("freeman_slot2selected",1)		
		TNT1 A 0 A_TakeInventory("freeman_slot3selected",1)		
		TNT1 A 0 A_TakeInventory("freeman_slot4selected",1)		
		TNT1 A 0 A_TakeInventory("freeman_slot5selected",1)		
		TNT1 A 0 A_TakeInventory("freeman_slot6selected",1)
		TNT1 A 0 A_TakeInventory("freeman_slot7selected",1)			
		TNT1 A 0 A_GiveInventory("freeman_slot1selected",1)
	SelectLoop:	
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_Raise
		TNT1 A 1 A_Raise
		loop	
	Deselect:
		TNT1 A 0 A_StopSound(CHAN_6)
		TNT1 A 0 A_TakeInventory("freeman_slot1selected",1)
	DeselectLoop:	
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_Lower
		TNT1 A 1 A_Lower
		loop
	Ready:
		TNT1 A 1
		TNT1 A 0 ACS_NamedExecuteAlways("HalfLifeHiveHandRecharge")
		TNT1 A 0 A_JumpIfInventory("HalfLifeOpposingForce", 1, "ReadyOpp")
		TNT1 A 0 A_StopSound(CHAN_6)
		TNT1 A 0 A_PlayWeaponSound("halflife/weaponselect")
		HW10 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1 A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
		HW11 ABCD 1 A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
		goto WIdle
	ReadyOpp:
		TNT1 A 0 A_PlayWeaponSound("halflife/weaponselect")
		TNT1 A 0 A_PlaySound("HLShockRoach/Draw", CHAN_7)
		OP05 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1 A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
		OP06 ABC 1 A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
		goto WIdle
	WIdle:
		TNT1 A 0 A_TakeInventory("HLHiveHandFiring", 1)
		TNT1 A 0 A_JumpIfInventory("HalfLifeOpposingForce", 1, "WIdleOpp")
		TNT1 A 0 A_StopSound(CHAN_6)
		HW12 A 2 A_WeaponReady
		TNT1 A 0 A_Jump(4,"WIdle")
		TNT1 A 0 A_Jump(3,"WIdle")
		TNT1 A 0 A_Jump(2,"WIdle")				
		TNT1 A 0 A_Jump(1,"Idle1","Idle2","Idle3")
		loop
	Idle1:
		HW12 BCDEFGHIJKLMNOPQRSTUVWXYZ 1 A_WeaponReady
		HW21 ABCDEF 1 A_WeaponReady
		Goto WIdle
	Idle2:
		HW21 GHIJKLMNOPQRSTUVWXYZ 1 A_WeaponReady
		HW22 ABCDEFGHIJKLMNOPQRSTU 1 A_WeaponReady
		Goto WIdle
	Idle3:
		HW22 VWXYZ 1 A_WeaponReady
		HW23 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1 A_WeaponReady
		HW24 ABCDE 1 A_WeaponReady		
		Goto WIdle	
	WIdleOpp:
		TNT1 A 0 A_JumpIfInventory("ShockRoachAmmo", 0, "StopRechargeSound")
		Goto PlayRechargeSound
		TNT1 A 0 A_JumpIfInventory("HalfLifeOpposingForce", 1, 1)
		Goto WIdle+1
		OP00 A 2 A_WeaponReady
		TNT1 A 0 A_Jump(4,"WIdleOpp")
		TNT1 A 0 A_Jump(3,"WIdleOpp")
		TNT1 A 0 A_Jump(2,"WIdleOpp")				
		TNT1 A 0 A_Jump(1,"Idle1Opp","Idle2Opp")
		loop
	PlayRechargeSound:
		TNT1 A 0 A_PlaySound("HLShockRoach/Recharge", CHAN_6, 1.0, true)
		Goto WIdleOpp+1
	StopRechargeSound:
		TNT1 A 0 A_StopSound(CHAN_6)
		Goto WIdleOpp+1
	Idle1Opp:
		OP00 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1 A_WeaponReady
		OP01 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1 A_WeaponReady
		OP02 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1 A_WeaponReady
		OP03 ABCDEFGHIJKLMNOPQRSTUVW 1 A_WeaponReady
		Goto WIdle
	Idle2Opp:
		OP08 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1 A_WeaponReady
		OP09 ABCDEFGHIJKLMNOPQRSTUVWX 1 A_WeaponReady
		Goto WIdle
	Fire:
		TNT1 A 0 A_JumpIfInventory("HalfLifeOpposingForce", 1, "FireOpp")
		TNT1 A 0 A_JumpIfInventory("HornetGunAmmo",1,"FireAmmo")
		goto Nope
	FireAmmo:
		TNT1 A 0 A_GiveInventory("HLHiveHandFiring", 1)
		TNT1 A 0 A_JumpIfInventory("HalfLifeOpposingForce", 1, "FireAmmoOpp")
		TNT1 A 0 ACS_NamedExecuteAlways("HalfLifeRecoil", 0, 1)
		HW13 A 2 A_PlayWeaponSound("halflife/hornet_shoot")
		HW13 B 0 A_TakeInventory("HornetGunAmmo",1)
		HW13 B 0 A_JumpIfInventory("CoopModeOn",1,"FireCoop")
		HW13 B 2 A_FireCustomMissile("HLHornetSeeker",0,0,6,4)
		goto FireFinish
	FireCoop:
		HW13 B 2 A_FireCustomMissile("HLHornetSeekerCoop",0,0,6,4)
		goto FireFinish
	FireFinish:
		HW13 C 2
		HW13 DEF 2
		HW13 GHI 2 A_Refire
		TNT1 A 0 A_TakeInventory("HLHiveHandFiring", 1)
		goto WIdle
	FireOpp:
		TNT1 A 0 A_JumpIfInventory("ShockRoachAmmo",1,"FireAmmoOpp")
		goto NopeOpp
	FireAmmoOpp:
		TNT1 A 0 A_GiveInventory("HLHiveHandFiring", 1)
		OP04 A 1 A_PlayWeaponSound("HLShockRoach/Fire")
		OP04 B 0 A_TakeInventory("ShockRoachAmmo",1)
		OP04 B 0 A_JumpIfInventory("CoopModeOn",1,"FireCoopOpp")
		OP04 B 1 A_FireCustomMissile("HLShockProjectile",0,0,5,3)
		goto FireFinishOpp
	FireCoopOpp:
		OP04 B 1 A_FireCustomMissile("HLShockProjectile",0,0,5,3)
		goto FireFinishOpp
	FireFinishOpp:
		OP04 C 2
		OP04 DEF 1
		OP04 GHIJ 1 A_Refire
		TNT1 A 0 A_TakeInventory("HLHiveHandFiring", 1)
		goto WIdle
	AltFire:
		TNT1 A 0 A_JumpIfInventory("HalfLifeOpposingForce", 1, "Nope")
		TNT1 A 0 A_JumpIfInventory("HornetGunAmmo",1,"AltFireAmmo")
		goto Nope
	AltFireAmmo:
		TNT1 A 0 ACS_NamedExecuteAlways("HalfLifeRecoil", 0, 1)
		TNT1 A 0 A_GiveInventory("HLHiveHandFiring", 1)
		HW13 A 2 A_PlayWeaponSound("halflife/hornet_shoot")
		TNT1 A 0 A_TakeInventory("HornetGunAmmo",1)
		TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"AltFireCoop")
		HW13 B 2 A_FireCustomMissile("HLHornet",0,0,6,4)
		goto AltFireFinish
	AltFireCoop:
		HW13 B 2 A_FireCustomMissile("HLHornetCoop",0,0,6,4)
		goto AltFireFinish
	AltFireFinish:
		HW13 C 1
		HW13 DEFGHI 1 A_Refire
		TNT1 A 0 A_TakeInventory("HLHiveHandFiring", 1)
		goto WIdle
	Nope:
		TNT1 A 0 A_JumpIfInventory("HalfLifeOpposingForce", 1, "NopeOpp")
		HW12 A 6
		goto WIdle
	NopeOpp:
		OP00 A 6
		goto WIdle
	}
}

Actor HLHornet
{
	PROJECTILE
	+NOEXTREMEDEATH
	+NOBOUNCESOUND
	+BOUNCEONWALLS
	+BOUNCEONFLOORS
	+BOUNCEONCEILINGS
	+NOWALLBOUNCESND
	BounceCount 5
	Speed 35
	Radius 7
	Height 8
	Damagetype "HLHornet"
	Damage (random(4,6))
	Scale 0.08
	BounceFactor 1.0
	WallBounceFactor 1.0
	ActiveSound "halflife/hornet_buzz"
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_LoopActiveSound
		Goto SpawnLoop
	SpawnLoop:	
		HP11 A 1 A_SpawnItemEx("HLHornetTrail",0,0,0,0,0,0,0,SXF_CLIENTSIDE)
		HP11 B 1 A_SpawnItemEx("HLHornetTrail",0,0,0,0,0,0,0,SXF_CLIENTSIDE)
		HP11 C 1 A_SpawnItemEx("HLHornetTrail",0,0,0,0,0,0,0,SXF_CLIENTSIDE)
		HP11 D 1 A_SpawnItemEx("HLHornetTrail",0,0,0,0,0,0,0,SXF_CLIENTSIDE)
//		HP11 E 1 A_SpawnItemEx("HLHornetTrail",0,0,0,0,0,0,0,SXF_CLIENTSIDE)
		loop
	Death:
		TNT1 A 5 A_SpawnItemEx("HLHornetDeath",0,0,0,0,0,0,0,SXF_CLIENTSIDE)
		stop
	}
}

Actor HLHornetTrail
{
	+NOGRAVITY
	-SOLID
	+NOINTERACTION
	+CLIENTSIDEONLY
	height 8
	radius 3
	renderstyle add
	alpha 0.65
	scale 0.16
	states
	{
	Spawn:
		TNT1 A 0
		TNT1 A 1
	SpawnLoop:
		HW29 U 1 bright A_Fadeout(0.0275)
		loop
	}
}

Actor HLHornetDeath
{
	+CLIENTSIDEONLY
	+NOGRAVITY
	+NOINTERACTION
	-SOLID
	height 7
	radius 8
	renderstyle add
	alpha 0.9
	scale 0.6
	states
	{
	Spawn:
		TNT1 A 0
		HP11 B 1 bright A_PlaySound("halflife/hornet_hit",CHAN_BODY)
		HP11 CDEFG 1 bright
		stop
	}
}

Actor HLHornetSeeker
{
	PROJECTILE
	+NOEXTREMEDEATH
	+SEEKERMISSILE
	Speed 27
	Radius 7
	Height 8
	Damagetype "HLHornet"
	Damage (random(8,10))
	scale 0.08
	DeathSound ""
	ActiveSound "halflife/hornet_buzz"
	Obituary "%o died."
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_LoopActiveSound
		Goto SpawnLoop
	SpawnLoop:	
		HP10 A 0 A_SeekerMissile(45,45,SMF_LOOK|SMF_PRECISE,256,10)
		HP10 A 1 bright A_SpawnItemEx("HLHornetSeekerTrail",0,0,0,0,0,0,0,SXF_CLIENTSIDE)
		HP10 A 0 A_SeekerMissile(45,45,SMF_LOOK|SMF_PRECISE,256,10)
		HP10 B 1 bright A_SpawnItemEx("HLHornetSeekerTrail",0,0,0,0,0,0,0,SXF_CLIENTSIDE)
		HP10 A 0 A_SeekerMissile(45,45,SMF_LOOK|SMF_PRECISE,256,10)
		HP10 C 1 bright A_SpawnItemEx("HLHornetSeekerTrail",0,0,0,0,0,0,0,SXF_CLIENTSIDE)
		HP10 A 0 A_SeekerMissile(45,45,SMF_LOOK|SMF_PRECISE,256,10)
		HP10 D 1 bright A_SpawnItemEx("HLHornetSeekerTrail",0,0,0,0,0,0,0,SXF_CLIENTSIDE)
/*		HP10 A 0 A_SeekerMissile(45,45,SMF_LOOK|SMF_PRECISE,256,10)
		HP10 E 1 bright A_SpawnItemEx("HLHornetSeekerTrail",0,0,0,0,0,0,0,SXF_CLIENTSIDE)*/
		loop
	Death:
		TNT1 A 5 A_SpawnItemEx("HLHornetSeekerDeath",0,0,0,0,0,0,0,SXF_CLIENTSIDE)
		stop
	}
}

Actor HLHornetSeekerTrail
{
	+NOGRAVITY
	-SOLID
	+NOINTERACTION
	+CLIENTSIDEONLY
	height 8
	radius 3
	renderstyle add
	alpha 0.65
	scale 0.16
	states
	{
	Spawn:
		TNT1 A 0 
		TNT1 A 1
	SpawnLoop:
		HW29 T 1 bright A_Fadeout(0.0275)
		loop
	}
}

Actor HLHornetSeekerDeath
{
	+CLIENTSIDEONLY
	+NOGRAVITY
	+NOINTERACTION
	-SOLID
	height 7
	radius 8
	renderstyle add
	alpha 0.9
	scale 0.7
	states
	{
	Spawn:
		TNT1 A 0
		HP10 B 1 bright A_PlaySound("halflife/hornet_hit",CHAN_BODY)
		HP10 CDEFG 1 bright
		stop
	}
}

Actor HLHornetSeekerCoop : HLHornetSeeker { +THRUSPECIES Species "Player" +DONTHARMCLASS +DONTHARMSPECIES +MTHRUSPECIES }
Actor HLHornetCoop : HLHornet { +THRUSPECIES Species "Player" +DONTHARMCLASS +DONTHARMSPECIES +MTHRUSPECIES BounceCount 10 }

Actor freeman_slot1selected : Inventory
{
	Inventory.MaxAmount 1
	+INVENTORY.IGNORESKILL
	+INVENTORY.UNDROPPABLE
	-INVENTORY.INVBAR
}

Actor HornetGunAmmo : Ammo
{
	Inventory.amount 8
	inventory.maxamount 8
}

Actor ShockRoachAmmo : Ammo
{
	Inventory.amount 10
	inventory.maxamount 10
}

Actor HLShockProjectile : FastProjectile
{
	PROJECTILE
	+EXTREMEDEATH
	Speed 125
	Radius 7
	Height 8
	Damagetype "HLLaser"
	Damage (20)
	scale 0.7
	Renderstyle add
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_Jump(128, 2)
		TNT1 A 0 A_SetDamageType("HLEgon")
	SpawnLoop:
		TNT1 A 0
		HPI0 E 5 bright
		loop
	Death:
		TNT1 A 0 A_PlaySound("HLShockRoach/Impact",CHAN_BODY)
		HWX4 A 2 bright
		TNT1 A 0 A_Jump(128,2)
		TNT1 A 0 A_SpawnItemEx("HLPuffSpark",0,0,0,FRandom(1,4),0,Random(0,3),Random(-90,90))
		TNT1 A 0 A_Jump(128,2)
		TNT1 A 0 A_SpawnItemEx("HLPuffSpark",0,0,0,FRandom(1,4),0,Random(0,3),Random(-90,90))
		TNT1 A 0 A_SpawnItemEx("HLPuffSpark",0,0,0,FRandom(1,4),0,Random(0,3),Random(-90,90))
		stop
	}
}

Actor HLShockProjectileCoop : HLShockProjectile { +THRUSPECIES Species "Player" +DONTHARMCLASS +DONTHARMSPECIES +MTHRUSPECIES }

Actor HLHiveHandFiring : Boolean {}