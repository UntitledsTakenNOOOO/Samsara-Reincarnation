actor UsingSMG: Boolean {}
actor SMGClicked: Counter {}
actor SamsaraMarathonSMGAttackAmount : Counter { Inventory.MaxAmount 2 }

actor "KKV-7 SMG Flechette" : SamsaraMarathonWeapon
{
    Inventory.PickupMessage "You picked up a KKV-7 SMG Flechette!"
    Weapon.SlotNumber 1
    Weapon.SelectionOrder 2200
    Weapon.AmmoType1 "SmgBullet"
    Weapon.AmmoUse1 1
    Weapon.AmmoType2 "Clip"
    Weapon.AmmoGive2 32
    Tag "KKV-7 SMG Flechette"
    States
    {
      Spawn:
        FSMG A -1
        stop

      Ready:
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",0,"ReadyAllowReload")
      ReadyDisallowReload:
        HSMG A 1 A_WeaponReady
        goto ReadyCasingSound

      ReadyAllowReload:
        TNT1 A 0 A_JumpIfInventory("SmgBullet",0,"ReadyDisallowReload")
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("Clip",1,1)
        goto ReadyDisallowReload
        HSMG A 1 A_WeaponReady(WRF_ALLOWRELOAD)
        goto ReadyCasingSound

      ReadyCasingSound:
        TNT1 A 0 A_GiveInventory("SamsaraMarathonCasingSoundHandler")
      ReadyAmmoCheck:
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",2,1)
        goto Ready
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"Ready")
        TNT1 A 0 A_JumpIfInventory("SmgBullet",1,"Ready")
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"Reload")
        TNT1 A 0 A_JumpIfInventory("Clip",1,"Reload")
        goto Ready

      Deselect:
        TNT1 A 0 A_TakeInventory("SMGClicked")
        TNT1 A 0 A_TakeInventory("MarathonCasingSound")
        TNT1 A 0 A_JumpIfHealthLower(1,"DeselectInstant")
        TNT1 A 0 A_Lower
        HSMG A 1 A_Lower
        loop

      Select:
        TNT1 A 0 A_TakeInventory("UsingFists")
        TNT1 A 0 A_TakeInventory("UsingPistols")
        TNT1 A 0 A_TakeInventory("UsingDualPistols")
        //TNT1 A 0 A_TakeInventory("UsingSMG")
        TNT1 A 0 A_TakeInventory("UsingPacifier")
        TNT1 A 0 A_TakeInventory("UsingShotguns")
        TNT1 A 0 A_TakeInventory("UsingDualShotguns")
        TNT1 A 0 A_TakeInventory("UsingFusionPistol")
        TNT1 A 0 A_TakeInventory("UsingAssaultRifle")
        TNT1 A 0 A_TakeInventory("UsingSpanker")
        TNT1 A 0 A_TakeInventory("UsingToasty")
        TNT1 A 0 A_TakeInventory("UsingWMC")
        TNT1 A 0 A_TakeInventory("UsingAlienWeapon")
        TNT1 A 0 A_TakeInventory("UsingAlienWeapon2")
        TNT1 A 0 A_TakeInventory("UsingMarathonInstagib")

        TNT1 A 0 A_GiveInventory("UsingSMG")
        TNT1 A 0 A_TakeInventory("SMGClicked")
        TNT1 A 0 A_TakeInventory("MarathonCasingSound")
        TNT1 A 0 A_Raise
        HSMG A 1 A_Raise
        loop

      Fire:
      AltFire:
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",2,"FireReloadable")
      FireNonReloadable:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireStart")
        TNT1 A 0 A_JumpIfInventory("Clip",1,"FireStart")
        goto FireDry

      FireReloadable:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"FireStart")
        TNT1 A 0 A_JumpIfInventory("SmgBullet",1,"FireStart")
      FireReloadableDryCheck:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"Reload")
        TNT1 A 0 A_JumpIfInventory("Clip",1,"Reload")
        goto FireDry

      FireDry:
        TNT1 A 0 A_TakeInventory("MarathonCasingSound")
        TNT1 A 0 A_JumpIfInventory("SMGClicked",1,"Ready")
        TNT1 A 0 A_TakeInventory("SMGClicked") // reset
        TNT1 A 0 A_GiveInventory("SMGClicked",3)
        TNT1 A 0 A_PlaySound("marathon/rifle/dryfire")
        goto Ready

      FireStart:
        TNT1 A 0 A_AlertMonsters
        TNT1 A 0 A_GunFlash
        TNT1 A 0 A_GiveInventory("SamsaraMarathonSMGAttackHandler")
        HSMG B 1 Bright A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        TNT1 A 0 A_Light2
        HSMG B 1 Bright A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        TNT1 A 0 A_Light1
        HSMG C 1 Bright A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        TNT1 A 0 A_Light0
        TNT1 A 0 A_ReFire
        goto Ready

      Reload:
        HSMG D 11 A_TakeInventory("MarathonCasingSound")
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",2,"ReloadAmmo")
      ReloadFinish:
        HSMG EF 11
        goto Ready

      ReloadAmmo:
        TNT1 A 0 A_JumpIfInventory("SmgBullet",0,"ReloadFinish")
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("Clip",1,1)
        goto ReloadFinish
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_TakeInventory("Clip",1)
        TNT1 A 0 A_GiveInventory("SmgBullet")
        TNT1 A 0 A_PlaySound("marathon/smg/reload")
        loop
    }
}

actor SamsaraMarathonSMGAttackHandler : Trigger
{
    States
    {
      // Loop clearing/initializing stuff
      Pickup:
        TNT1 A 0 A_GiveInventory("SamsaraMarathonSMGCasingSpawnHandler") // netplay hack because like millions would spawn on the first shot, then spawn only one afterwards
        TNT1 A 0 A_TakeInventory("SamsaraMarathonSMGAttackAmount") // reset
      PickupInit:
        TNT1 A 0 A_GiveInventory("SamsaraMarathonSMGAttackAmount")
        goto PickupAmmoCheck

      // Ammo checking/taking stuff
      PickupAmmoCheck:
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",2,"PickupAmmoCheckReloadable")
      PickupAmmoCheckNonReloadable:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Clip",1,1)
        goto PickupFinish
        TNT1 A 0 A_TakeInventory("Clip",1)
        goto PickupAttack

      PickupAmmoCheckReloadable:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("SmgBullet",1,1)
        goto PickupFinish
        TNT1 A 0 A_TakeInventory("SmgBullet",1)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("marathon/smg/fire",CHAN_WEAPON)
        //TNT1 A 0 A_FireCustomMissile("FlechetteCasingSpawner",0,false) // yeah okay
        TNT1 A 0 A_JumpIfInventory("SamsaraMarathonSMGAttackAmount",2,2)
        TNT1 A 0 A_GiveInventory("MarathonCasingSound")
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireCustomMissile("MarathonSMGBullet",random(-10,10)/10.0,false,0,0,0,random(-10,10)/10.0)
        goto PickupFinish

      PickupAttackCoop:
        TNT1 A 0 A_JumpIfInventory("SamsaraCurrentWaterLevel",0,"PickupAttackCoopUnderwater")
        TNT1 A 0 A_FireCustomMissile("MarathonSMGBulletOverwater",random(-10,10)/10.0,false,0,0,0,random(-10,10)/10.0)
        goto PickupFinish

      PickupAttackCoopUnderwater:
        TNT1 A 0 A_FireCustomMissile("MarathonSMGBulletCoop",random(-10,10)/10.0,false,0,0,0,random(-10,10)/10.0)
        goto PickupFinish

      // Finishing stuff
      PickupFinish:
        TNT1 A 0 A_JumpIfInventory("SamsaraMarathonSMGAttackAmount",0,"PickupStop")
        goto PickupInit
    }
}

actor SamsaraMarathonSMGCasingSpawnHandler : Trigger
{
    States
    {
      Pickup:
      // Ammo checking stuff (first)
      Pickup1:
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",2,"Pickup1Reloadable")
      Pickup1NonReloadable:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"Pickup1Casing")
        TNT1 A 0 A_JumpIfInventory("Clip",1,"Pickup1Casing")
        goto Pickup2

      Pickup1Reloadable:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"Pickup1Casing")
        TNT1 A 0 A_JumpIfInventory("SmgBullet",1,"Pickup1Casing")
        goto Pickup2

      // Casing spawning stuff (first)
      Pickup1Casing:
        TNT1 A 0 A_FireCustomMissile("FlechetteCasingSpawner",0,false)
        goto Pickup2

      // Ammo checking stuff (second)
      Pickup2:
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",2,"Pickup2Reloadable")
      Pickup2NonReloadable:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"Pickup2Casing")
        TNT1 A 0 A_JumpIfInventory("Clip",2,"Pickup2Casing")
        stop

      Pickup2Reloadable:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"Pickup2Casing")
        TNT1 A 0 A_JumpIfInventory("SmgBullet",2,"Pickup2Casing")
        stop

      // Casing spawning stuff (second)
      Pickup2Casing:
        TNT1 A 0 A_FireCustomMissile("FlechetteCasingSpawner",0,false)
        stop
    }
}

actor SmgBullet : Ammo
{
    Inventory.MaxAmount 32
    Tag "10mm Flechette Magazine"
    +IGNORESKILL
}

actor MarathonSMGBullet : SamsaraMarathonBullet
{
    Damage (random(6,9))
    Obituary "$SAMSARA_MARATHON_OB_SLOT1"
    +THRUGHOST
}

actor MarathonSMGBulletCoop : MarathonSMGBullet
{
    Radius 1
    Height 1
    Speed 80
    Damage (random(9,15))
    Species "Player"
    +THRUSPECIES
}

actor MarathonSMGBulletOverwater : MarathonSMGBulletCoop { Damage (random(8,12)) }

actor FlechetteCasingSpawner : SamsaraMarathonCasingSpawner
{
    States
    {
      Death:
        TNT1 A 1 A_CustomMissile("FlechetteCasing",1,5,random(-110,-80),CMF_AIMDIRECTION,random(30,40))
        stop
    }
}

actor FlechetteCasing : SamsaraMarathonCasing
{ 
    Scale 0.25
    States
    {
      Spawn:
        FLEC ABCDEF 1
        loop
    }
}
