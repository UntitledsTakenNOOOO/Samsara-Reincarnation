actor SamsaraHexenHammerOfRetributionMeleePuffSpawned : Boolean {}

actor "Hammer of Retribution" : SamsaraHexenWeapon
{
    Inventory.PickupMessage "Frost Fang"
    Weapon.SlotNumber 5
    Weapon.SelectionOrder 900
    Weapon.YAdjust -10
    Weapon.AmmoType "Cell"
    Weapon.AmmoGive 25
    Weapon.AmmoUse 3
    Obituary "$SAMSARA_HEXEN_OB_SLOT5_1"
    Tag "Ice Fang"
    +AMMO_OPTIONAL
    +BLOODSPLATTER
    +MELEEWEAPON
    +NOALERT
    States
    {
      Spawn:
        H117 A -1
        stop

      // Parias
      Ready:
        TNT1 A 0 A_JumpIfInventory("HexenClassMode",2,"ReadyFighter")
        TNT1 A 0 A_JumpIfInventory("HexenClassMode",1,"ReadyMage")
      ReadyCleric:
        H117 B 1 A_WeaponReady
        goto Ready

      Deselect:
        TNT1 A 0 A_JumpIfInventory("HexenClassMode",2,"DeselectFighter")
        TNT1 A 0 A_JumpIfInventory("HexenClassMode",1,"DeselectMage")
      DeselectCleric:
        H117 B 1 A_Lower
        goto Deselect

      Select:
        TNT1 A 0 A_JumpIfInventory("HexenClassMode",2,"SelectFighter")
        TNT1 A 0 A_JumpIfInventory("HexenClassMode",1,"SelectMage")
      SelectCleric:
        H117 B 1 A_Raise
        goto Select

      Fire:
        TNT1 A 0 A_JumpIfInventory("HexenClassMode",2,"FireFighter")
        TNT1 A 0 A_JumpIfInventory("HexenClassMode",1,"FireMage")
      FireCleric:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("Cell",3,1)
        goto NoAmmoSwitch
        H117 B 3 A_AlertMonsters
        H117 C 4 Bright
        H117 D 5 Bright
        /*TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("Cell",3,1)
        goto FireClericContinue
        TNT1 AAAA 0 A_SpawnItemEx("FrostFangFog2",16,0,32,0.5,0,frandom(-0.5,0.5),random(-50,50),SXF_CLIENTSIDE)
        goto FireClericContinue*/

      FireClericContinue:
        H117 E 6 Bright A_GiveInventory("SamsaraHexenFrostFangAttackHandler")
        H117 D 1 Bright Offset(0,60)
        H117 D 1 Bright Offset(0,55)
        H117 D 1 Bright Offset(0,50)
        H117 D 1 Bright Offset(0,45)
        H117 D 1 Bright Offset(0,40)
        H117 D 1 Bright Offset(0,35)
        H117 C 5 Bright
        H117 B 4
        goto Ready

      // Daedolon (Viscerelagh)
      ReadyMage:
        H107 B 1 A_WeaponReady
        goto Ready

      DeselectMage:
        H107 B 1 A_Lower
        goto Deselect

      SelectMage:
        H107 B 1 A_Raise
        goto Select

      FireMage:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("Cell",3,1)
        goto NoAmmoSwitch
        TNT1 A 0 A_AlertMonsters
        H107 D 4 A_PlaySound("BloodScepter/Charge",CHAN_WEAPON)
        H107 EFG 4 Bright
        H107 H 6 Bright A_GiveInventory("SamsaraHexenViscerelaghAttackHandler")
        H107 G 4 Bright A_PlaySound("BloodScepter/CoolDown")
        H107 F 4 Bright
        H107 B 20
        goto Ready

      // Baratus (Hammer of Retribution)
      ReadyFighter:    
        FHMR A 1 A_WeaponReady
        goto Ready

      DeselectFighter:
        FHMR A 1 A_Lower
        goto Deselect

      SelectFighter:
        FHMR A 1 A_Raise
        goto Select

      FireFighter:
        H105 B 6 Offset(5,0) A_TakeInventory("SamsaraHexenHammerOfRetributionMeleePuffSpawned")
        H105 C 3 Offset(5,0) A_GiveInventory("SamsaraHexenHammerOfRetributionMeleeAttackHandler")
        H105 D 3 Offset(5,0)
        FHMR E 2 Offset(5,0)
        FHMR E 10 Offset(5,150) A_GiveInventory("SamsaraHexenHammerOfRetributionRangedAttackHandler")
        FHMR A 1 Offset(0,60)
        FHMR A 1 Offset(0,55)
        FHMR A 1 Offset(0,50)
        FHMR A 1 Offset(0,45)
        FHMR A 1 Offset(0,40)
        FHMR A 2 Offset(0,35)
        goto Ready

      // "No ammo" switch
      NoAmmoSwitch:
        TNT1 A 0 A_ClearReFire
        TNT1 A 0 ACS_NamedExecuteWithResult("Hexen_SwitchPriority")
        goto Deselect
    }
}

// Parias

actor SamsaraHexenFrostFangAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Cell",3,1)
        stop
        TNT1 A 0 A_TakeInventory("Cell",3)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("Hexen/FrostFang/Fire",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireCustomMissile("FrostFangBall",0,false,-4)
        TNT1 A 0 A_FireCustomMissile("FrostFangBall",0,false,4)
        stop

      PickupAttackCoop:
        TNT1 A 0 A_FireCustomMissile("FrostFangBallCoop",0,false,-4)
        TNT1 A 0 A_FireCustomMissile("FrostFangBallCoop",0,false,4)
        stop
    }
}

/*actor FrostFangFog2
{
    Radius 1
    Height 1
    RenderStyle "Add"
    Alpha 0.2
    XScale 0.6
    YScale 0.4
    +NOCLIP
    +NOGRAVITY
    States
    {
      Spawn:
        H118 A 4 A_FadeOut(0.03)
        loop
    }
}*/

actor FrostFangBall : IceGuyFX
{
    Speed 20
    Damage 5
    DamageType "HexenIce"
    DeathSound "Hexen/FrostFang/Hit"
    +ACTIVATEIMPACT
    +ACTIVATEPCROSS
    +BLOODSPLATTER
    States
    {
      Death:
        ICPR D 4 Bright A_Explode(80,128,0)
        TNT1 A 0 A_CustomMissile("FrostMissileDM",8,0,-135,CMF_TRACKOWNER)
        TNT1 A 0 A_CustomMissile("FrostMissileDM",8,0,-90,CMF_TRACKOWNER)
        TNT1 A 0 A_CustomMissile("FrostMissileDM",8,0,-45,CMF_TRACKOWNER)
        TNT1 A 0 A_CustomMissile("FrostMissileDM",8,0,0,CMF_TRACKOWNER)
        TNT1 A 0 A_CustomMissile("FrostMissileDM",8,0,45,CMF_TRACKOWNER)
        TNT1 A 0 A_CustomMissile("FrostMissileDM",8,0,90,CMF_TRACKOWNER)
        TNT1 A 0 A_CustomMissile("FrostMissileDM",8,0,135,CMF_TRACKOWNER)
        TNT1 A 0 A_CustomMissile("FrostMissileDM",8,0,180,CMF_TRACKOWNER)
      DeathFinish:
        ICPR EFG 4 Bright
        ICPR H 3 Bright
        stop
    }
}

actor FrostFangBallCoop : FrostFangBall
{
    Species "Player"
    +THRUSPECIES
    States
    {
      Death:
        ICPR D 4 Bright A_Explode(80,128,0)
        TNT1 A 0 A_CustomMissile("FrostMissile2_1",8,0,-135,CMF_TRACKOWNER)
        TNT1 A 0 A_CustomMissile("FrostMissile2_1",8,0,-90,CMF_TRACKOWNER)
        TNT1 A 0 A_CustomMissile("FrostMissile2_1",8,0,-45,CMF_TRACKOWNER)
        TNT1 A 0 A_CustomMissile("FrostMissile2_1",8,0,0,CMF_TRACKOWNER)
        TNT1 A 0 A_CustomMissile("FrostMissile2_1",8,0,45,CMF_TRACKOWNER)
        TNT1 A 0 A_CustomMissile("FrostMissile2_1",8,0,90,CMF_TRACKOWNER)
        TNT1 A 0 A_CustomMissile("FrostMissile2_1",8,0,135,CMF_TRACKOWNER)
        TNT1 A 0 A_CustomMissile("FrostMissile2_1",8,0,180,CMF_TRACKOWNER)
        goto DeathFinish
    }
}

// Daedolon

actor SamsaraHexenViscerelaghAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Cell",3,1)
        stop
        TNT1 A 0 A_TakeInventory("Cell",3)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireCustomMissile("BloodScepterBall",0,false)
        stop

      PickupAttackCoop:
        TNT1 A 0 A_FireCustomMissile("BloodScepterBallCoop",0,false)
        stop
    }
}

actor BloodScepterBall
{
    Projectile
    Radius 8
    Height 12
    Speed 30
    Damage 10
    DamageType "BloodScepter"
    Scale 1.5
    SeeSound "BloodScepter/PowerFire"
    DeathSound "BloodScepter/PowerHit"
    //Decal "BigBloodSmear"
    +BLOODSPLATTER
    States
    {
      Spawn:
        H108 ABCDEF 2 Bright //H108 AABBCCDDEEFF 1 Bright A_SpawnItemEx("BloodScepterTrail",random(-5,5),random(-6,6),random(-6,6),0,0,0,0,SXF_CLIENTSIDE)
        loop

      Death:
      Crash:
        TNT1 A 0 A_Explode(200,200,0)
        TNT1 A 1 A_SpawnItemEx("BloodScepterBallDeath",0,0,0,0,0,0,0,SXF_CLIENTSIDE)
        stop

      XDeath:
        TNT1 A 0 A_PlaySound("ClericCStaffHitThing")
        TNT1 A 0 A_GiveInventory("SamsaraHexenBloodScepterBallHealthDrain",1,AAPTR_TARGET)
        goto Crash
    }
}

actor SamsaraHexenBloodScepterBallHealthDrain : Trigger
{
    States
    {
      Pickup:
        TNT1 A 0 ACS_NamedExecuteWithResult("SamsaraPlayerHealer", random(2,4), 200)
        stop
    }
}

actor BloodScepterBallCoop : BloodScepterBall
{
    Species "Player"
    +THRUSPECIES
}

actor BloodScepterBallDeath
{
    Radius 1
    Height 1
    RenderStyle "Translucent"
    Alpha 0.8
    Scale 0.5
    +NOCLIP
    +NOGRAVITY
    States
    {
      Spawn:
        H108 GHIJKL 3 Bright
        H108 L 2 Bright A_FadeOut(0.05)
        wait
    }
}

// Baratus

// Melee

actor SamsaraHexenHammerOfRetributionMeleeAttackHandler : Trigger
{
    States
    {
      // Attack stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupCoop")
      PickupDM:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasSpread",1,"PickupDMSpread")
        TNT1 A 0 A_CustomPunch(random(60,113),true,0,"SamsaraHexenHammerPuff",96)
        stop

      PickupDMSpread:
        TNT1 A 0 A_CustomPunch(random(60,113)*3,true,0,"SamsaraHexenHammerPuff",96)
        stop

      PickupCoop:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasSpread",1,"PickupCoopSpread")
        TNT1 A 0 A_CustomPunch(random(60,113),true,0,"SamsaraHexenHammerPuffCoop",96)
        stop

      PickupCoopSpread:
        TNT1 A 0 A_CustomPunch(random(60,113)*3,true,0,"SamsaraHexenHammerPuffCoop",96)
        stop
    }
}

actor SamsaraHexenHammerPuff : HammerPuff
{
    +NOTIMEFREEZE
    +PUFFGETSOWNER
    States
    {
      Spawn:
        FHFX S 0 NoDelay A_GiveInventory("SamsaraHexenHammerOfRetributionMeleePuffSpawned",1,AAPTR_TARGET)
        TNT1 A 0 A_ChangeFlag("NOTIMEFREEZE",false)
        goto Super::Spawn
    }
}

actor SamsaraHexenHammerPuffCoop : SamsaraHexenHammerPuff { +MTHRUSPECIES }

// Ranged

actor SamsaraHexenHammerOfRetributionRangedAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHexenHammerOfRetributionMeleePuffSpawned",1,"PickupStop")
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Cell",3,1)
        stop
        TNT1 A 0 A_TakeInventory("Cell",3)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_AlertMonsters
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_JumpIfInventory("RJingModeOn",1,"PickupAttackDMRJ")
        TNT1 A 0 A_FireCustomMissile("HammerMissileDM",0,false)
        stop

      PickupAttackDMRJ:
        TNT1 A 0 A_FireCustomMissile("HammerMissileDMRJ",0,false)
        stop

      PickupAttackCoop:
        TNT1 A 0 A_JumpIfInventory("RJingModeOn",1,"PickupAttackCoopRJ")
        TNT1 A 0 A_FireCustomMissile("HammerMissile2",0,false)
        stop

      PickupAttackCoopRJ:
        TNT1 A 0 A_FireCustomMissile("HammerMissile2RJ",0,false)
        stop
    }
}

actor HammerMissileDM : HammerMissile
{
    DamageType "FireExplosive"
    Decal "Scorch"
    Obituary "$SAMSARA_HEXEN_OB_SLOT5_2"
    +BLOODSPLATTER
    +FORCERADIUSDMG
    +THRUGHOST
    States
    {
      Death:
        FHFX I 3 Bright A_SetTranslucent(1,1)
        FHFX J 3 Bright
        TNT1 A 0 A_Explode(128,128,0)
      DeathFinish:
        FHFX KLM 3 Bright
        FHFX N 3
        FHFX OPQR 3 Bright
        stop
    }
}

actor HammerMissileDMRJ : HammerMissileDM
{
    States
    {
      Death:
        FHFX I 3 Bright A_SetTranslucent(1,1)
        FHFX J 3 Bright
        TNT1 A 0 A_Explode(128,128)
        goto DeathFinish
    }
}

actor HammerMissile2 : HammerMissileDM
{
    Species "Player"
    +THRUSPECIES
}

actor HammerMissile2RJ : HammerMissileDMRJ
{
    Species "Player"
    +THRUSPECIES
}
