actor Goldeneye_Slapper : SamsaraGoldenEyeWeapon
{
    Weapon.SlotNumber 0
    Weapon.SelectionOrder 20
    Obituary "%o got slapped to death by %k."
    Tag "Unarmed"
    +MELEEWEAPON
    +WIMPY_WEAPON
    States
    {
      Spawn:
        PIST A -1
        stop

      // Unarmed (Hand)
      Ready:
        TNT1 A 0 A_JumpIfInventory("BondWatchQueue",1,"WatchRaise")
        TNT1 A 0 A_JumpIfInventory("BondWatchToken",1,"WatchReady")
      ReadyStart:
        //TNT1 A 0 A_JumpIfInventory("Goldeneye_SniperRifle",1,"ReadyStartSniper")
        TNT1 A 1 A_WeaponReady
        goto Ready

      Deselect:
        TNT1 A 0 A_TakeInventory("BondFireDelay")
        TNT1 A 0 A_TakeInventory("BondAlternateFire")
        TNT1 A 0 A_TakeInventory("BondDualWieldToken")
        TNT1 A 0 A_TakeInventory("BondAltToggle")
        TNT1 A 0 A_TakeInventory("BondZoomToken")
        TNT1 A 0 A_TakeInventory("BondZoomAmountModifyActive")
        TNT1 A 0 A_TakeInventory("BondDualWieldSpamProtector")
        TNT1 A 0 A_TakeInventory("BondDryFireSpamProtector1")
        TNT1 A 0 A_TakeInventory("BondDryFireSpamProtector2")
        TNT1 A 0 A_TakeInventory("BondAltActivationToken")
        //TNT1 A 0 A_JumpIfInventory("Goldeneye_SniperRifle",1,"DeselectSniper")
        TNT1 A 1 A_Lower
        loop

      Select:
        TNT1 A 0 A_GiveInventory("Goldeneye_UnarmedToken")
        TNT1 A 0 A_TakeInventory("Goldeneye_PP7Token")
        TNT1 A 0 A_TakeInventory("Goldeneye_KnifeToken")
        TNT1 A 0 A_TakeInventory("Goldeneye_KF7Token")
        TNT1 A 0 A_TakeInventory("Goldeneye_AutoShotgunToken")
        TNT1 A 0 A_TakeInventory("Goldeneye_AR33Token")
        TNT1 A 0 A_TakeInventory("Goldeneye_RocketLauncherToken")
        TNT1 A 0 A_TakeInventory("Goldeneye_RCP90Token")
        TNT1 A 0 A_TakeInventory("Goldeneye_LaserToken")
        TNT1 A 0 A_TakeInventory("Goldeneye_GoldenGunToken")
        TNT1 A 0 A_TakeInventory("Goldeneye_SniperToken")
        TNT1 A 0 A_TakeInventory("Goldeneye_D5KToken")
        //TNT1 A 0 A_JumpIfInventory("Goldeneye_SniperRifle",1,"SelectSniper")
        TNT1 A 1 A_Raise
        loop

      Fire:
        GE00 B 0 A_GiveInventory("BondFiringToken")
        GE00 B 0 A_GiveInventory("SamsaraGoldenEyeSlapperSwingSoundPlayer")
        //GE00 B 0 A_JumpIfInventory("Goldeneye_SniperRifle",1,"FireSniper")
        GE00 B 0 A_Jump(256,"Fire1","Fire2")
      Fire1:
        GE00 BCDEF 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        GE00 G 0 A_GiveInventory("SamsaraGoldenEyeSlapperAttackHandler")
        GE00 G 3 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        GE00 H 0
        TNT1 A 3 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        TNT1 A 0 A_TakeInventory("BondFiringToken")
        TNT1 A 5 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        goto FireFinish

      Fire2:
        GE00 IJKLMN 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        GE00 O 0 A_GiveInventory("SamsaraGoldenEyeSlapperAttackHandler")
        GE00 O 3 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        GE00 P 0
        TNT1 A 3 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        TNT1 A 0 A_TakeInventory("BondFiringToken")
        TNT1 A 5 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        goto FireFinish

      FireFinish:
        TNT1 A 0 A_TakeInventory("BondFiringToken")
        goto Ready

      // Unarmed (Sniper Rifle Stock) (not implemented yet; code is just here for future reference)
      /*ReadyStartSniper:
        TNT1 A 1 A_WeaponReady
        goto Ready

      DeselectSniper:
        TNT1 A 1 A_Lower
        goto Deselect

      SelectSniper:
        TNT1 A 1 A_Raise
        goto Select

      FireSniper:
        GE00 B 0 A_Jump(256,"Fire1Sniper","Fire2Sniper")
      Fire1Sniper:
        GE00 BCDEF 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        GE00 G 0 A_GiveInventory("SamsaraGoldenEyeSlapperAttackHandler")
        GE00 G 3 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        GE00 H 0
        TNT1 A 3 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        TNT1 A 0 A_TakeInventory("BondFiringToken")
        TNT1 A 5 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        goto FireFinish

      Fire2Sniper:
        GE00 IJKLMN 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        GE00 O 0 A_GiveInventory("SamsaraGoldenEyeSlapperAttackHandler")
        GE00 O 3 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        GE00 P 0
        TNT1 A 3 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        TNT1 A 0 A_TakeInventory("BondFiringToken")
        TNT1 A 5 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        goto FireFinish*/
    }
}

// Attack handler

actor SamsaraGoldenEyeSlapperAttackHandler : Trigger
{
    States
    {
      // Attack stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraAlternateSoundToken",1,"PickupSource")
      // GoldenEye 007 sounds
      PickupN64:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupN64Coop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupN64Coop")
      PickupN64DM:
        TNT1 A 0 A_FireCustomMissile("Goldeneye_SlapperTracer",0,false)
        stop

      PickupN64Coop:
        TNT1 A 0 A_FireCustomMissile("Goldeneye_SlapperTracerCoop",0,false)
        stop

      // GoldenEye: Source sounds
      PickupSource:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupSourceCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupSourceCoop")
      PickupSourceDM:
        TNT1 A 0 A_FireCustomMissile("Goldeneye_SlapperTracerSoundSource",0,false)
        stop

      PickupSourceCoop:
        TNT1 A 0 A_FireCustomMissile("Goldeneye_SlapperTracerCoopSoundSource",0,false)
        stop
    }
}

actor SamsaraGoldenEyeSlapperSwingSoundPlayer : Trigger
{
    States
    {
      // Sound playing stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraAlternateSoundToken",1,"PickupSource")
      // GoldenEye 007 sounds
      PickupN64:
        TNT1 A 0 A_PlaySound("Goldeneye/Slapper",CHAN_WEAPON)
        stop

      // GoldenEye: Source sounds
      PickupSource:
        TNT1 A 0 A_PlaySound("GoldeneyeSource/Slapper",CHAN_WEAPON)
        stop
    }
}

actor Goldeneye_SlapperTracer : FastProjectile
{
    var int user_damage;
    Radius 2
    Height 4
    Speed 24
    Damage (15)
    +DONTBLAST
    +FORCERADIUSDMG
    +HITTRACER
    +NODAMAGETHRUST
    +NOEXTREMEDEATH
    +NOTIMEFREEZE
    States
    {
      Spawn:
      Death:
        TNT1 A 1
        stop

      XDeath:
        TNT1 A 0 A_PlaySound("Goldeneye/Punch")
      XDeathFinish:
        TNT1 A 0 A_SetUserVar("user_damage",15)
        TNT1 A 0 A_Explode(CallACS("BondHitBox", user_damage),8,0,false,8)
        goto Death

      Crash:
        TNT1 A 0 A_PlaySound("Goldeneye/SlapperMetal")
        goto Death
    }
}

actor Goldeneye_SlapperTracerCoop : Goldeneye_SlapperTracer
{
    Species "Player"
    +THRUSPECIES
}

actor Goldeneye_SlapperTracerSoundSource : Goldeneye_SlapperTracer
{
    States
    {
      XDeath:
        TNT1 A 0 A_PlaySound("GoldeneyeSource/Punch")
        goto XDeathFinish

      Crash:
        TNT1 A 0 A_PlaySound("GoldeneyeSource/SlapperMetal")
        goto Death
    }
}

actor Goldeneye_SlapperTracerCoopSoundSource : Goldeneye_SlapperTracerCoop
{
    States
    {
      XDeath:
        TNT1 A 0 A_PlaySound("GoldeneyeSource/Punch")
        goto XDeathFinish

      Crash:
        TNT1 A 0 A_PlaySound("GoldeneyeSource/SlapperMetal")
        goto Death
    }
}
