actor "Rapid Zorcher" : Weapon
{
    Weapon.SelectionOrder 700
    Weapon.SlotNumber 4
    Inventory.PickUpSound "chex/weaponget"
    Weapon.AmmoUse 1
    Weapon.AmmoGive 20
    Weapon.AmmoType "Clip"
    +BLOODLESSIMPACT
    +PUFFONACTORS
    +THRUGHOST
    //+AMMO_OPTIONAL
    +INVENTORY.UNDROPPABLE
    DamageType "Zorch"
    Inventory.PickupMessage "You got the Rapid Zorcher!"
    Obituary "$SAMSARA_CHEX_OB_SLOT4"
	Inventory.RestrictedTo "ChexWarrior"
    Tag "Rapid Zorcher"
    States
    {
      Spawn:
        MGUC A -1
        stop

      Ready:
        CHGC A 1 A_WeaponReady
        loop

      Deselect:
        CHGC A 1 A_Lower
        loop

      Select:
        CHGC A 1 A_Raise
        loop

      Fire:
        TNT1 A 0 A_GunFlash
        CHGC A 4 A_GiveInventory("SamsaraChexRapidZorcherAttackHandler")
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("Clip",1,1)
        goto Fire2
        TNT1 A 0 A_GunFlash("Flash2")
        goto Fire2

      Fire2:
        CHGC B 4 A_GiveInventory("SamsaraChexRapidZorcherAttackHandler")
        TNT1 A 0 A_ReFire
        goto Ready

      Flash:
        CCHG A 5 Bright A_Light1
        goto LightDone

      Flash2:
        CCHG B 5 Bright A_Light2
        goto LightDone
    }
}

actor SamsaraChexRapidZorcherAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Clip",1,1)
        stop
        TNT1 A 0 A_TakeInventory("Clip",1)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("chex/chngun",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SamsaraImprovedDoomguyBalance",1,"PickupAttackImproved")
      // Vanilla
      PickupAttackVanilla:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackVanillaCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackVanillaCoop")
      PickupAttackVanillaDM:
        TNT1 A 0 A_FireBullets(5,0,1,5,"ChexPuffThruGhost",FBF_NOFLASH)
        stop

      PickupAttackVanillaCoop:
        TNT1 A 0 A_FireBullets(5,0,1,5,"ChexPuffThruGhostCoop",FBF_NOFLASH)
        stop

      // Improved
      PickupAttackImproved:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackImprovedCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackImprovedCoop")
      PickupAttackImprovedDM:
        TNT1 A 0 A_FireBullets(5,0,1,random(8,10)*2,"ChexPuffThruGhost",FBF_NORANDOM|FBF_NOFLASH)
        stop

      PickupAttackImprovedCoop:
        TNT1 A 0 A_FireBullets(5,0,1,random(8,10)*2,"ChexPuffThruGhostCoop",FBF_NORANDOM|FBF_NOFLASH)
        stop
    }
}
