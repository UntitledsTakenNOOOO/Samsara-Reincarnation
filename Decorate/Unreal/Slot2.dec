Actor UShockRifleFired : Boolean { }
Actor IsUShockBall : Boolean { }
Actor Unreal_ASMDShockRifle : UnrealWeapon 
{
	Weapon.SelectionOrder 1700
	Weapon.SlotPriority 1
	Weapon.SlotNumber 2
	Weapon.AmmoType1 "Shell"
	Weapon.AmmoUse1 1
	Weapon.AmmoType2 "Shell"
	Weapon.AmmoUse2 1
	Weapon.AmmoGive 20
	Weapon.UpSound "Unreal/ASMD/Select"
	Obituary "%k inflicted mortal damage upon %o with the ASMD."
	Inventory.PickupMessage "You got the ASMD"
	Tag "ASMD Shock Rifle"
	States
	{
		Spawn:
			UN00 A 10 BRIGHT
			UN00 A 10 
			Loop
		Select:
			TNT1 AAAAAAAAAAAAAAAAAAAA 0 A_Raise
			TNT1 A 1 A_Raise
			loop	
		Ready:
			UN01 A 0 A_GiveInventory("Unreal_ASMDSelected",1)
			UN01 ABCDEFGH 2 A_WeaponReady(WRF_NOFIRE)
			Goto WIdle
		WIdle:
			UN01 I 0 A_Jump(192, 2)
			UN01 I 0 A_JumpIfInventory("UShockRifleFired", 1, "Steam")
			UN01 I 4 A_WeaponReady UN01 I 0 A_Jump(12, "Sway")
			UN01 I 0 A_TakeInventory("UShockRifleFired", 1)
//			UN01 J 4 A_WeaponReady UN01 I 0 A_Jump(12, "Sway")
			Loop
		Sway:
			UN02 OPQQPOOPQ 4 A_WeaponReady
			Goto WIdle
		Steam:
			UN02 AA 6 A_WeaponReady UN02 B 0 A_PlaySound("Unreal/ASMD/Steam", CHAN_WEAPON)
			UN02 BB 6 A_WeaponReady UN02 C 0 A_TakeInventory("UShockRifleFired", 1)
			UN02 CCDDEE 6 A_WeaponReady
			Goto WIdle
		Deselect:
			UN01 A 0 A_TakeInventory("Unreal_ASMDSelected",1)
			UN01 KLMN 2 
		InstantDeselect: // I have to copy paste this everytime. Thanks, Zandronum.
			TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_Lower
			TNT1 A 1 A_Lower
			Wait			
		Fire:
			UN01 P 0 ACS_NamedExecuteWithResult("Unreal_Recoil")
			UN01 P 4 BRIGHT A_FireBullets(0.5,0.5,-1,35,"U1ASMDTracerPuff",FBF_NORANDOM|FBF_NORANDOMPUFFZ|FBF_USEAMMO)//A_FireCustomMissile("U1ASMDTracer",Random(-0.5,0.5),1,0,0,Random(-0.5,0.5))
			UN01 Q 0 BRIGHT A_PlaySound("Unreal/ASMD/Fire1", CHAN_WEAPON)
			UN01 Q 0 BRIGHT A_GiveInventory("UShockRifleFired", 1)
			UN01 Q 4 BRIGHT
			UN01 RS 4
			UN01 T 4 A_WeaponReady
			Goto WIdle
		AltFire:
			UN01 U 0 ACS_NamedExecuteWithResult("Unreal_Recoil")
			UN01 U 0 BRIGHT A_SetBlend("29 8f e8", 0.10, 10)
			UN01 U 4 BRIGHT A_FireCustomMissile("U1ASMDBall", 0,1,13,5)
			UN01 V 0 BRIGHT A_PlaySound("Unreal/ASMD/Fire2", CHAN_WEAPON)
			UN01 V 0 BRIGHT A_GiveInventory("UShockRifleFired", 1)
			UN01 V 4 BRIGHT 
			UN01 WX 4
			UN01 Y 4 A_WeaponReady
			Goto WIdle
	}
}

Actor U1ASMDTracerPuff
{
	DamageType "ASMDShockTrace"
	Height 16
	Radius 8
	+HITTRACER
	+PUFFGETSOWNER
	+PUFFONACTORS
	+ALWAYSPUFF
	+NOINTERACTION
	+CLIENTSIDEONLY
	+NOGRAVITY
	+SKYEXPLODE
	States
	{
		Spawn:
		Melee:
		Crash:
			TNT1 A 0 NoDelay ACS_NamedExecuteWithResult("Unreal_ASMDTrace")
			TNT1 A 0 A_RadiusThrust(700,20, !RTF_AFFECTSOURCE, 20)
			TNT1 A 0 A_GiveInventory("Unreal_ASMDZThrust", 1, AAPTR_TRACER)
			TNT1 A 0 A_JumpIf(z - floorz < 6,"HitPlane")
			TNT1 A 0 A_CheckCeiling("HitPlane")
			TNT1 A 0 A_JumpIfInventory("IsUShockBall",1,3,AAPTR_Tracer)
			TNT1 A 0 A_PlaySound("Unreal/ASMD/Explode", CHAN_BODY)
			TNT1 A 1 A_SpawnItemEX("U1ShockRippleB_Large")
			Stop
			TNT1 A 1
			Stop
		XDeath:	
			TNT1 A 0 ACS_NamedExecuteWithResult("Unreal_ASMDTrace")
			TNT1 A 0 A_JumpIf(CallACS("Unreal_BloodGenerator_Offsets")==1,1)
			TNT1 A 0 A_RadiusThrust(700,20, !RTF_AFFECTSOURCE, 20)
			TNT1 A 0 A_GiveInventory("Unreal_ASMDZThrust", 1, AAPTR_TRACER)
			TNT1 A 0 A_JumpIf(z - floorz < 6,"HitPlane")
			TNT1 A 0 A_CheckCeiling("HitPlane")
			TNT1 A 0 A_JumpIfInventory("IsUShockBall",1,2,AAPTR_Tracer)
			TNT1 A 0 A_PlaySound("Unreal/ASMD/Explode", CHAN_BODY)
			TNT1 A 1 A_SpawnItemEX("U1ShockRippleB_Large")
			TNT1 A 0 A_GiveInventory("UnrealBloodyPuffGenerator",1,AAPTR_TRACER)	
			Stop
		HitPlane:
			TNT1 A 0 A_PlaySound("Unreal/ASMD/Explode", CHAN_BODY)
			TNT1 A 1 A_SpawnItemEX("U1ShockRippleB_Plane")
			Stop			
	}
}

Actor Unreal_ASMDTracerIntermediate
{
	+ISMONSTER
	-COUNTKILL
	-SOLID
	+THRUACTORS
	-SHOOTABLE
	Speed 0
	Height 0
	Radius 0
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 A 0 A_Stop
//			TNT1 A 0 A_SetPitch(-CallACS("Painkiller_ShooterPitch")/182)
//			TNT1 A 0 A_CustomMissile("Painkiller_StakeProjectilePenetrate",0,0,0,CMF_AIMDIRECTION|CMF_TRACKOWNER,-pitch)
			TNT1 A 3
			Stop
	}
}

Actor U1ASMDBall 
{
	Speed 20
	Damage (0)
	Projectile
	+HITTRACER
	+FORCERADIUSDMG
	+THRUSPECIES
	Radius 12
	Height 24
	Health 1
	Scale 0.5
	RenderStyle Add
	Alpha 0.9
	DamageType "UnrealPlasma"
	Species "UnrealASMD"
	DamageFactor "ASMDShockTrace", 1.0
	DamageFactor "Normal", 0.0
	States
	{
		Spawn:
			UN00 A 0
			UN00 A 0 A_JumpIf(CallACS("Unreal_SpawnShockRifleHitbox", 3) == 1, 1)
			UN00 A 1
		SpawnLoop:
			UN00 A 2 BRIGHT
			Loop
		Explode:
			TNT1 A 2 A_Explode(215, 240, 1, 20)
			TNT1 A 0 A_PlaySound("Unreal/ASMD/SuperExplosion", CHAN_BODY)	
			TNT1 A 1 A_SpawnItemEX("U1ShockRippleP_Large")	
			TNT1 A 0 A_TakeFromTarget("Shell", 2, TIF_NOTAKEINFINITE)			
			TNT1 A 2
			Stop
		XDeath:	
			TNT1 A 0 A_JumpIf(CallACS("Unreal_BloodGenerator_Offsets")==1,1)
			TNT1 A 1 Bright
			TNT1 A 0 A_GiveInventory("UnrealBloodyPuffGenerator",1,AAPTR_TRACER)	
		Crash:				
		Death:
			TNT1 A 0 A_RadiusThrust(7000,20, !RTF_AFFECTSOURCE| RTF_NOIMPACTDAMAGE, 20)
			TNT1 A 0 A_Explode(55,70, 0, 1, 15)
			TNT1 A 0 A_KillChildren
			TNT1 A 0 A_GiveInventory("Unreal_ASMDZThrust", 1, AAPTR_TRACER)
			TNT1 A 0 A_PlaySound("Unreal/ASMD/Explode", CHAN_BODY)
			TNT1 A 1 A_SpawnItemEX("U1ShockRippleB_Large")
			Stop	
			
		}	
}	

Actor UASMDBallHitbox
{
	Height 64
	Radius 32
	Health 1
	Mass 50000
	+ISMONSTER
	-COUNTKILL
	-SOLID
	+SHOOTABLE
	+NOBLOOD
	+NOGRAVITY
	+FORCERADIUSDMG
	DamageFactor "ASMDShockTrace", 1.0
	DamageFactor "Normal", 0.0
	DamageType "UnrealPlasma"
	Species "UnrealASMD"
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 A 0 A_GiveInventory("IsUShockBall",1)
		SpawnLoop:
			TNT1 A 2 A_Warp(AAPTR_TRACER,0,0,0,0,WARPF_NOCHECKPOSITION|WARPF_COPYINTERPOLATION)
			Loop
		Death:
			TNT1 A 2 
			Stop
		Death.ASMDShockTrace:
			TNT1 A 0 ACS_NamedExecuteWithResult("Unreal_ShockRifleCombo")
			TNT1 A 5
			Stop	
	}
}			

Actor U1ShockRippleB_Large
{
	+CLIENTSIDEONLY
	+NOINTERACTION
	+NOGRAVITY
	Radius 4
	Height 4
	Scale 1.5
	RenderStyle Add
	Alpha 1.0
	States
	{
		Spawn:
			UN00 AAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx("Unreal_ASMDParticle",0,0,0,random(-6,6),random(-6,6),random(-6,6))
			UN00 ABCD 5 A_FadeOut(0.2)
			UN00 E 0
			Stop
	}
}	

Actor U1ShockRippleB_Plane : U1ShockRippleB_Large
{
	-NOINTERACTION
	+FLOORHUGGER
	States
	{
		Spawn:
			TNT1 A 0 NoDelay A_SetPitch(90)
			Goto Super::Spawn
	}
}

Actor U1ShockRippleB_Small : U1ShockRippleB_Large
{
	Scale 0.25
	States
	{
		Spawn:
			UN00 ABCD 5 A_FadeOut(0.2)
			UN00 E 0
			Stop
	}
}
Actor U1ShockRippleP_Large : U1ShockRippleB_Large 
{ 
	States
	{
		Spawn:
			UN00 AAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx("Unreal_ASMDParticleSuper",0,0,0,random(-6,6),random(-6,6),random(-6,6))
			UN00 A 0 A_SpawnItemEx("Unreal_ASMDSuperExplosion")
			UN00 ABCD 5 A_FadeOut(0.2)
			UN00 E 0
			Stop
	}
}

ACTOR UNreal_ASMDZThrust : CustomInventory
{
	-COUNTITEM
	+INVENTORY.ALWAYSPICKUP
	Inventory.PickupMessage ""
	Inventory.PickupSound ""
	States
	{
		Spawn:
			TNT1 A 0 
			TNT1 A 1
			Stop
		Pickup:
			TNT1 A 0 
			TNT1 A 0
			TNT1 A 1 ThrustThingZ(0,3500/mass,0,1)
			Stop
	}
}

Actor Unreal_ShockBallDissipate : Boolean { }

Actor Unreal_ASMDParticle
{
	Height 2
	Radius 1
	Scale 0.35
	RenderStyle Add
	+NOINTERACTION
	+CLIENTSIDEONLY
	States
	{
		Spawn:
			UN25 BBBBB 2 A_Fadeout(0.2)
			Stop
	}
}

Actor Unreal_ASMDParticleSuper : Unreal_ASMDParticle
{
	Scale 0.7
	States
	{
		Spawn:
			UN25 GGGGG 2 A_Fadeout(0.2)
			Stop
	}
}

Actor Unreal_ASMDSuperExplosion : Unreal_ASMDParticle
{
	Scale 2.8
	States
	{
		Spawn:
			UN25 ABCDEF 3
			Stop
	}
}