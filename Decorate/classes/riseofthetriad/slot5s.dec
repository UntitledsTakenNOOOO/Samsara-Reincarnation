actor SamsaraROTTDoomstickChargeAmount : Counter { Inventory.MaxAmount 35 }
actor SamsaraROTTDoomstickPrimaryAttackSilentSound : Boolean {}

actor Doomstick : SamsaraROTTWeapon
{
    Inventory.PickupMessage "You snatched a Doomstick!"
    Weapon.UpSound "gen/wepswitch"
    Weapon.SlotNumber 5
    Weapon.SlotPriority 1
    Weapon.SelectionOrder 1200
    Weapon.AmmoType "DMMissiles"
    Weapon.AmmoGive 5
    Weapon.AmmoUse 1
    Obituary "%o was shown %k's doomstick!"
    Tag "Doomstick"
    +AMMO_OPTIONAL
    +EXPLOSIVE
    +IGNORESKILL
    States
    {
      Spawn:
        PDSK A -1
        stop

      Ready:
        TNT1 A 0 A_TakeInventory("UsingFirearm")
        TNT1 A 0 A_TakeInventory("SamsaraROTTDoomstickChargeAmount")
        RTDS A 1 A_WeaponReady
        loop

      Deselect:
        TNT1 A 0 A_TakeInventory("SamsaraROTTDoomstickChargeAmount")
        TNT1 A 0 A_JumpIfHealthLower(1,"DeselectInstant")
        RTDS A 1 A_Lower
        loop

      Select:
        TNT1 A 0 A_TakeInventory("SamsaraROTTDoomstickChargeAmount")
        RTDS A 1 A_Raise
        loop

      Fire:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("DMMissiles",1,1)
        goto AltFire
        TNT1 A 0 A_PlaySound("dstick/charge",CHAN_WEAPON)
      FireCharge:
        RTDS A 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("DMMissiles",1,1)
        goto FireChargeStop
        TNT1 A 0 A_GiveInventory("SamsaraROTTDoomstickChargeAmount")
        TNT1 A 0 A_JumpIfInventory("SamsaraROTTDoomstickChargeAmount",0,"FireStart")
        TNT1 A 0 A_JumpIfInventory("PowerGodProtection",1,2)
        TNT1 A 0 A_ReFire("FireCharge")
        TNT1 A 0 A_ClearReFire
        goto FireChargeStop

      FireStart:
        TNT1 A 0 A_TakeInventory("SamsaraROTTDoomstickChargeAmount")
        TNT1 A 0 A_TakeInventory("SamsaraTakingAmmo")
        TNT1 A 0 A_TakeInventory("SamsaraROTTDoomstickPrimaryAttackSilentSound")
        TNT1 A 0 A_GiveInventory("SamsaraROTTDoomstickPrimaryAttackHandler1")
        RTDS B 1 Bright A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        TNT1 A 0 A_GiveInventory("SamsaraROTTDoomstickPrimaryAttackHandler1")
        RTDS C 1 Bright A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        TNT1 A 0 A_GiveInventory("SamsaraROTTDoomstickPrimaryAttackHandler1")
        RTDS D 1 Bright A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        TNT1 A 0 A_GiveInventory("SamsaraROTTDoomstickPrimaryAttackHandler1")
        RTDS E 1 Bright A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        TNT1 A 0 A_GiveInventory("SamsaraROTTDoomstickPrimaryAttackHandler1")
        RTDS F 1 Bright A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        TNT1 A 0 A_GiveInventory("SamsaraROTTDoomstickPrimaryAttackHandler1")
        RTDS G 1 Bright A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        TNT1 A 0 A_GiveInventory("SamsaraTakingAmmo")
        TNT1 A 0 A_GiveInventory("SamsaraROTTDoomstickPrimaryAttackHandler2")
        RTDS HIA 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        TNT1 A 0 A_JumpIfInventory("PowerGodProtection",1,2)
        TNT1 A 0 A_ReFire
        TNT1 A 0 A_ClearReFire
        goto Ready

      FireChargeStop:
        TNT1 A 0 A_TakeInventory("SamsaraROTTDoomstickChargeAmount")
        TNT1 A 0 A_StopSound(CHAN_WEAPON)
        goto AltFire

      AltFire:
        TNT1 A 0 A_GunFlash
        TNT1 A 0 A_GiveInventory("SamsaraROTTDoomstickSecondaryAttackHandler")
        RTDS A 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        RTDS BCDEFG 1 Bright A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        RTDS HIA 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        TNT1 A 0 A_JumpIfInventory("PowerGodProtection",1,2)
        TNT1 A 0 A_ReFire
        TNT1 A 0 A_ClearReFire
        goto Ready
    }
}

actor SamsaraROTTDoomstickPrimaryAttackHandler1 : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("DMMissiles",1,1)
        goto PickupFinish
        TNT1 A 0 A_JumpIfInventory("SamsaraTakingAmmo",1,1)
        goto PickupAttack
        TNT1 A 0 A_TakeInventory("DMMissiles",1)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_JumpIfInventory("SamsaraROTTDoomstickPrimaryAttackSilentSound",1,2)
        TNT1 A 0 A_PlaySound("dstick/fireball",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireCustomMissile("DoomStickFireBall",frandom(-7.5,7.5),false,0,-8,0,frandom(-5,5))
        goto PickupFinish

      PickupAttackCoop:
        TNT1 A 0 A_FireCustomMissile("DoomStickFireBallCoop",frandom(-7.5,7.5),false,0,-8,0,frandom(-5,5))
        goto PickupFinish

      // Finishing stuff
      PickupFinish:
        TNT1 A 0 A_GiveInventory("SamsaraROTTDoomstickPrimaryAttackSilentSound")
        stop
    }
}

actor SamsaraROTTDoomstickPrimaryAttackHandler2 : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("DMMissiles",1,1)
        goto PickupFinish
        TNT1 A 0 A_JumpIfInventory("SamsaraTakingAmmo",1,1)
        goto PickupAttack
        TNT1 A 0 A_TakeInventory("DMMissiles",1)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_JumpIfInventory("SamsaraROTTDoomstickPrimaryAttackSilentSound",1,2)
        TNT1 A 0 A_PlaySound("dstick/fireball",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 AA 0 A_FireCustomMissile("DoomStickFireBall",frandom(-7.5,7.5),false,0,-8,0,frandom(-5,5))
        goto PickupFinish

      PickupAttackCoop:
        TNT1 AA 0 A_FireCustomMissile("DoomStickFireBallCoop",frandom(-7.5,7.5),false,0,-8,0,frandom(-5,5))
        goto PickupFinish

      // Finishing stuff
      PickupFinish:
        TNT1 A 0 A_GiveInventory("SamsaraROTTDoomstickPrimaryAttackSilentSound")
        stop
    }
}

actor SamsaraROTTDoomstickSecondaryAttackHandler : Trigger
{
    States
    {
      // Attack stuff
      Pickup:
        TNT1 A 0 A_PlaySound("dstick/fire",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupCoop")
      PickupDM:
        TNT1 A 0 A_FireBullets(6.2,5.5,8,10,"RottDoomStickPuff",FBF_NORANDOM|FBF_NOFLASH)
        stop

      PickupCoop:
        TNT1 A 0 A_FireBullets(6.2,5.5,8,15,"RottDoomStickPuffCoop",FBF_NORANDOM|FBF_NOFLASH)
        stop
    }
}

actor DMMissiles : Ammo
{
    Inventory.MaxAmount 25
    Ammo.BackpackAmount 1 // skills with different AmmoFactor values and sv_doubleammo can still give extra regardless of +IGNORESKILL
    Ammo.BackpackMaxAmount 50
    Inventory.Icon "AMSMS0"
    Tag "Doomstick Missiles"
    +IGNORESKILL
}

actor DoomStickFireBall
{
    Projectile
    Radius 11
    Height 8
    Speed 40
    Damage (20)
    DamageType "RottExplosive"
    Scale 0.5
    //SeeSound "NME/Fire3"
    DeathSound "god/bang"
    Decal "Scorch"
    +BLOODLESSIMPACT
    +EXTREMEDEATH
    +FORCERADIUSDMG
    +WINDTHRUST
    States
    {
      Spawn:
        RNME JKLM 3 Bright Light("ROTTROCKET")
        loop

      Death:
        EXPL A 2 Bright Light("ROTTROCKETEXP") A_Explode(15,48,XF_HURTSOURCE,false,24)
        EXPL B 1 Bright Light("ROTTROCKETEXP")
        EXPL B 1 Bright Light("ROTTROCKETEXP") A_RadiusThrust(200,96)
        EXPL C 2 Bright Light("ROTTROCKETEXP")
        EXPL D 2 Bright Light("ROTTROCKETEXP") A_Explode(15,48,XF_HURTSOURCE,false,24)
        EXPL EFGHIJKLMNOPQRST 2 Bright Light("ROTTROCKETEXP")
        stop

      Crash:
        TNT1 A 0 A_Jump(250,"Death")
        TNT1 A 0 A_PlaySound("rmissile/cook",CHAN_5)
        goto Death
    }
}

actor DoomStickFireBallCoop : DoomStickFireBall
{
    Species "Player"
    +THRUSPECIES
}

actor RottDoomStickPuff : RottPuff
{
    +HITTRACER
    +PUFFGETSOWNER
    +PUFFONACTORS
    States
    {
      XDeath:
        TNT1 A 1 ACS_NamedExecuteWithResult("RottDoomStickThrust")
        stop
    }
}

actor RottDoomStickPuffCoop : RottDoomStickPuff { +MTHRUSPECIES }
