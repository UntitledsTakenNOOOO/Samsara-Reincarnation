actor "Phasing Zorcher" : Weapon
{
    Weapon.SelectionOrder 100
    Weapon.SlotNumber 6
    Inventory.PickUpSound "chex/weaponget"
    Weapon.AmmoUse 1
    Weapon.AmmoGive 40
    Weapon.AmmoType "Cell"
    +INVENTORY.UNDROPPABLE
    Inventory.PickupMessage "You got the Phasing Zorcher!"
	Inventory.RestrictedTo "ChexWarrior"
    Tag "Phasing Zorcher"
    States
    {
      Spawn:
        PLAC A -1
        stop

      Ready:
        PHSC A 1 A_WeaponReady
        loop

      Deselect:
        PHSC A 1 A_Lower
        loop

      Select:
        PHSC A 1 A_Raise
        loop

      Fire:
        TNT1 A 0 A_GunFlash("Flash",GFF_NOEXTCHANGE)
        TNT1 A 0 A_GiveInventory("SamsaraChexPhasingZorcherAttackHandler")
        TNT1 A 0 A_Jump(256,1,2)
        CPLS A 0
        goto FireContinue
        CPLS B 0
        goto FireContinue

      FireContinue:
        "####" "#" 2 Bright
        PHSC A 1
        PHSC B 20 A_ReFire
        goto Ready

      Flash:
        TNT1 A 3 A_Light1
        goto LightDone

      /*Flash: // old flash system
        TNT1 A 0 A_Jump(256,1,2)
        CPLS A 0
        goto FlashStart
        CPLS B 0
        goto FlashStart

      FlashStart:
        "####" "#" 4 Bright A_Light1
        goto LightDone*/
    }
}

actor SamsaraChexPhasingZorcherAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Cell",1,1)
        stop
        TNT1 A 0 A_TakeInventory("Cell",1)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireCustomMissile("ChexPlasma",0,false)
        stop

      PickupAttackCoop:
        TNT1 A 0 A_FireCustomMissile("ChexPlasma2",0,false)
        stop
    }
}

actor ChexPlasma
{
    Radius 13
    Height 8
    Speed 25
    Damage (random(1,6)*5)
    Projectile
    +RANDOMIZE
    +BLOODLESSIMPACT
    +BRIGHT
    RenderStyle Add
    Alpha 0.75
    SeeSound "chex/plasmaf"
    DeathSound "chex/plasmax"
    Obituary "$SAMSARA_CHEX_OB_SLOT6"
    DamageType "PhasingZorch"
    Decal ChexPlasmaScorch
    States
    {
      Spawn:
        CLSS AB 6 bright
        loop

      Death:
        CLSE ABCDE 4 bright
        stop
    }
}

actor ChexPlasma2 : ChexPlasma { +THRUSPECIES Species "Player" }
