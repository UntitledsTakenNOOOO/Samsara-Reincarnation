actor SamsaraDoomguyStrPistolLoaded : Boolean {}

actor " Pistol " : Weapon
{
    Weapon.SelectionOrder 1900
    Weapon.SlotNumber 1  
    Weapon.SlotPriority 1
    Weapon.AmmoUse 0
    Weapon.AmmoGive 0
    Weapon.AmmoType ""
    +WEAPON.WIMPY_WEAPON
    +INVENTORY.UNDROPPABLE
    +NOALERT
    DamageType Pistol
	Tag "Pistol"
    Inventory.Pickupmessage "i eat you"
    Obituary "$SAMSARA_DOOMGUY_OB_PISTOL"
	Inventory.RestrictedTo "DoomguyPlayer"
    States
    {
      Spawn:
        WPIS A -1
        stop

      // Original
      Ready:
        TNT1 A 0 A_JumpIfInventory("DoomClassMode",2,"ReadyStr")
		TNT1 A 0 A_JumpIfInventory("DoomClassMode",1,"Ready64")
        TNT1 A 0 A_GiveInventory("SamsaraDoomguyStrPistolLoaded")
        DPIS A 1 A_WeaponReady(WRF_NOSECONDARY)
        loop

      Deselect:
        TNT1 A 0 A_JumpIfInventory("DoomClassMode",2,"DeselectStr")
		TNT1 A 0 A_JumpIfInventory("DoomClassMode",1,"Deselect64")
        DPIS A 1 A_Lower
        loop

      Select:
        TNT1 A 0 A_JumpIfInventory("DoomClassMode",2,"SelectStr")
		TNT1 A 0 A_JumpIfInventory("DoomClassMode",1,"Select64")
        DPIS A 1 A_Raise
        loop

      Fire:
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto FireStart
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireStart")
        TNT1 A 0 A_JumpIfInventory("Clip",1,"FireStart")
        goto Nothing

      FireStart:
        TNT1 A 0 A_AlertMonsters
        TNT1 A 0 A_JumpIfInventory("DoomClassMode",2,"FireStr")
        DPIS A 4 A_JumpIfInventory("DoomClassMode",1,"Fire64")
		TNT1 A 0 A_JumpIfInventory("SamsaraUsingSmoothAnims",1,"FireSmooth")
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto FireVanillaAttack
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireVanillaAttack")
        TNT1 A 0 A_JumpIfInventory("Clip",1,"FireVanillaAttack")
        goto FireVanillaContinue

      FireVanillaAttack:
        TNT1 A 0 A_GunFlash
      FireVanillaContinue:
        DPIS F 6 Bright A_GiveInventory("SamsaraDoomguyPistolAttackHandler")
        DPIS G 4
      FireVanillaFinish:
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto FireVanillaFinishReFire
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireVanillaFinishReFire")
        TNT1 A 0 A_JumpIfInventory("Clip",1,"FireVanillaFinishReFire")
        goto Nothing

      FireVanillaFinishReFire:
        DPIS H 5 A_ReFire
        goto Ready

      Flash:
        TNT1 A 3 Bright A_Light1
      FlashFinish:
        TNT1 A 2 A_Light1
        goto LightDone

      FireSmooth:
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto FireSmoothAttack
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireSmoothAttack")
        TNT1 A 0 A_JumpIfInventory("Clip",1,"FireSmoothAttack")
        goto FireSmoothContinue

      FireSmoothAttack:
        TNT1 A 0 A_GunFlash("FlashSmooth")
      FireSmoothContinue:
        TNT1 A 0 A_GiveInventory("SamsaraDoomguyPistolAttackHandler")
        DPIS B 2 Bright A_JumpIfInventory("SamsaraHasDoubleFiringSpeed",1,"FireSmoothContinueRage")
        DPIS C 1 Bright
        DPIS C 1
        DPIS EDB 2
      FireSmoothFinish:
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto FireSmoothFinishReFire
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireSmoothFinishReFire")
        TNT1 A 0 A_JumpIfInventory("Clip",1,"FireSmoothFinishReFire")
        goto Nothing

      FireSmoothFinishReFire:
        DPIS A 5 A_ReFire
        goto Ready

      FireSmoothContinueRage:
        DPIS BC 1 Bright
        DPIS EDB 1
      FireSmoothFinishRage:
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto FireSmoothFinishReFireRage
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireSmoothFinishReFireRage")
        TNT1 A 0 A_JumpIfInventory("Clip",1,"FireSmoothFinishReFireRage")
        goto Nothing

      FireSmoothFinishReFireRage:
        DPIS A 3 A_ReFire
        goto Ready

      FlashSmooth:
        DPSF A 2 Bright A_Light1
        goto FlashFinish

      // Doom 64 (fires slightly faster)
      Ready64:
        TNT1 A 0 A_GiveInventory("SamsaraDoomguyStrPistolLoaded")
        64PS A 1 A_WeaponReady(WRF_NOSECONDARY)
        goto Ready

      Deselect64:
        64PS A 1 A_Lower
        goto Deselect

      Select64:
        64PS A 1 A_Raise
        goto Select

      Fire64:
        TNT1 A 0 A_GunFlash("Flash64")
        TNT1 A 0 A_GiveInventory("SamsaraDoomguy64PistolAttackHandler")
        TNT1 A 0 A_JumpIfInventory("SamsaraUsingSmoothAnims",1,"Fire64Smooth")
	  Fire64Vanilla:
        64PS B 4 Bright
        64PS C 9
      Fire64VanillaFinish:
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto Fire64VanillaFinishReFire
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"Fire64VanillaFinishReFire")
        TNT1 A 0 A_JumpIfInventory("Clip",1,"Fire64VanillaFinishReFire")
        goto Nothing

      Fire64VanillaFinishReFire:
        64PS B 3 A_ReFire
        goto Ready

      Fire64Smooth:
        64PS B 4 Bright
        64PS CDE 3
      Fire64SmoothFinish:
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto Fire64SmoothFinishReFire
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"Fire64SmoothFinishReFire")
        TNT1 A 0 A_JumpIfInventory("Clip",1,"Fire64SmoothFinishReFire")
        goto Nothing

      Fire64SmoothFinishReFire:
        64PS F 2 A_ReFire
		64PS B 1
        goto Ready

      Flash64:
        64PM A 4 Bright A_Light1
        goto LightDone

      // Stronghold
      // Unpowered
      DeselectStr:
        TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrWeaponPower",1,"DeselectStrPowered")
        TNT1 A 0 A_JumpIfInventory("SamsaraHasPermaWeaponLevel2",1,"DeselectStrPowered")
      DeselectStrContinue:
        TNT1 A 0 A_Lower
        DPIS I 1 A_Lower
        goto Deselect

      SelectStr:
        TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrWeaponPower",1,"SelectStrPowered")
        TNT1 A 0 A_JumpIfInventory("SamsaraHasPermaWeaponLevel2",1,"SelectStrPowered")
      SelectStrContinue:
        TNT1 A 0 A_Raise
        DPIS I 1 A_Raise
        goto Select

      ReadyStr:
        TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrPistolLoaded",1,"ReadyStrLoaded")
      ReloadAnim:
        TNT1 A 0 A_GiveInventory("SamsaraDoomguyStrPistolLoaded")
        DPSR A 5 A_PlaySound("doomguy/stronghold/clipou")
        DPSR BCDEFGH 4
        DPSR I 4 A_PlaySound("doomguy/stronghold/clipin")
        DPSR JKL 4
        DPSR M 4 A_PlaySound("doomguy/stronghold/clipre")
        DPSR NOP 4
        goto Ready

      ReadyStrLoaded:
        TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrWeaponPower",1,"ReadyStrLoadedPowered")
        TNT1 A 0 A_JumpIfInventory("SamsaraHasPermaWeaponLevel2",1,"ReadyStrLoadedPowered")
        DPIS I 1 A_WeaponReady(WRF_NOSECONDARY)
        goto Ready

      FireStr:
        TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrWeaponPower",1,"FireStrPowered")
        TNT1 A 0 A_JumpIfInventory("SamsaraHasPermaWeaponLevel2",1,"FireStrPowered")
        TNT1 A 0 A_JumpIfInventory("SamsaraImprovedDoomguyBalance",1,"FireStrImproved")
      FireStrVanilla:
        DPIS I 4
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto FireStrAttackVanilla
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireStrAttackVanilla")
        TNT1 A 0 A_JumpIfInventory("Clip",1,"FireStrAttackVanilla")
        goto FireStrContinueVanilla

      FireStrAttackVanilla:
        TNT1 A 0 A_GunFlash("FlashStrVanilla")
      FireStrContinueVanilla:
        DPIS L 6 Bright A_GiveInventory("SamsaraDoomguyStrPistolAttackHandler")
        DPIS K 4
      FireStrFinishVanilla:
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto FireStrFinishReFireVanilla
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireStrFinishReFireVanilla")
        TNT1 A 0 A_JumpIfInventory("Clip",1,"FireStrFinishReFireVanilla")
        goto Nothing

      FireStrFinishReFireVanilla:
        DPIS J 5 A_ReFire
        goto Ready

      FlashStrVanilla:
        TNT1 A 7 Bright A_Light1
        goto LightDone

      FireStrImproved:
        DPIS I 3
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto FireStrAttackImproved
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireStrAttackImproved")
        TNT1 A 0 A_JumpIfInventory("Clip",1,"FireStrAttackImproved")
        goto FireStrContinueImproved

      FireStrAttackImproved:
        TNT1 A 0 A_GunFlash("FlashStrImproved")
      FireStrContinueImproved:
        DPIS L 3 Bright A_GiveInventory("SamsaraDoomguyStrPistolAttackHandler")
        DPIS K 3
      FireStrFinishImproved:
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto FireStrFinishReFireImproved
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireStrFinishReFireImproved")
        TNT1 A 0 A_JumpIfInventory("Clip",1,"FireStrFinishReFireImproved")
        goto Nothing

      FireStrFinishReFireImproved:
        DPIS J 3 A_ReFire
        goto Ready

      FlashStrImproved:
        TNT1 A 4 Bright A_Light1
        goto LightDone

      // Powered
      DeselectStrPowered:
        TNT1 A 0 A_Lower
        goto DeselectStrContinue

      SelectStrPowered:
        TNT1 A 0 A_Raise
        goto SelectStrContinue

      ReadyStrLoadedPowered:
        DPIS I 1 A_WeaponReady
        goto Ready

      FireStrPowered:
        TNT1 A 0 A_GunFlash("FlashStrPowered")
        DPIS J 3 A_GiveInventory("SamsaraDoomguyStrPistolPoweredPrimaryAttackHandler")
      FireStrPoweredFinish1:
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto FireStrPoweredFinish1ReFire
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireStrPoweredFinish1ReFire")
        TNT1 A 0 A_JumpIfInventory("Clip",1,"FireStrPoweredFinish1ReFire")
        goto Nothing

      FireStrPoweredFinish1ReFire:
        DPIS K 3 A_ReFire
      FireStrPoweredFinish2:
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto FireStrPoweredFinish2ReFire
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireStrPoweredFinish2ReFire")
        TNT1 A 0 A_JumpIfInventory("Clip",1,"FireStrPoweredFinish2ReFire")
        goto Nothing

      FireStrPoweredFinish2ReFire:
        DPIS J 3 A_ReFire
      FireStrPoweredFinish3:
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto FireStrPoweredFinish3ReFire
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireStrPoweredFinish3ReFire")
        TNT1 A 0 A_JumpIfInventory("Clip",1,"FireStrPoweredFinish3ReFire")
        goto Nothing

      FireStrPoweredFinish3ReFire:
        DPIS I 3 A_ReFire
        goto Ready

      FlashStrPowered:
        DPIS L 2 Bright A_Light1
        goto LightDone

      AltFire:
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto AltFireStart
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"AltFireStart")
        TNT1 A 0 A_JumpIfInventory("Clip",8,"AltFireStart")
        goto Nothing

      AltFireStart:
        DPIS I 4 A_AlertMonsters
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto AltFireAttack
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"AltFireAttack")
        TNT1 A 0 A_JumpIfInventory("Clip",8,"AltFireAttack")
        goto AltFireContinue

      AltFireAttack:
        TNT1 A 0 A_GunFlash
      AltFireContinue:
        DPIS L 4 Bright A_GiveInventory("SamsaraDoomguyStrPistolPoweredSecondaryAttackHandler")
        DPIS KJ 4
        TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrWeaponPower",1,"AltFireFinish")
        TNT1 A 0 A_JumpIfInventory("SamsaraHasPermaWeaponLevel2",1,"AltFireFinish")
      AltFireFinishAmmoCheck:
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto AltFireFinishReFireDone
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"AltFireFinishReFireDone")
        TNT1 A 0 A_JumpIfInventory("Clip",8,"AltFireFinishReFireDone")
        goto Nothing

      AltFireFinish:
        TNT1 A 0 A_JumpIfInventory("DoomClassMode",2,1)
        goto AltFireFinishAmmoCheck
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto AltFireFinishReFire
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"AltFireFinishReFire")
        TNT1 A 0 A_JumpIfInventory("Clip",8,"AltFireFinishReFire")
        goto Nothing

      AltFireFinishReFire:
        TNT1 A 0 A_ReFire
      AltFireFinishReFireDone:
        DPIS I 4
        goto Ready

      AltFlash:
        TNT1 A 5 Bright A_Light1
        goto LightDone

      Nothing:
        TNT1 A 0 A_ClearReFire
        TNT1 A 0 A_SelectWeapon(" Fist ")
        goto Deselect
    }
}

actor SamsaraDoomguyPistolAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto PickupAttack
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Clip",1,1)
        stop
        TNT1 A 0 A_TakeInventory("Clip",1)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("doomguy/pistol",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SamsaraImprovedDoomguyBalance",1,"PickupAttackImproved")
      // Vanilla
      PickupAttackVanilla:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackVanillaCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackVanillaCoop")
      PickupAttackVanillaDM:
        TNT1 A 0 A_FireBullets(5.6,0,1,5,"DoomBulletPuff2",FBF_NOFLASH)
        stop

      PickupAttackVanillaCoop:
        TNT1 A 0 A_FireBullets(5.6,0,1,5,"DoomBulletPuff2_Coop",FBF_NOFLASH)
        stop

      // Improved
      PickupAttackImproved:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackImprovedCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackImprovedCoop")
      PickupAttackImprovedDM:
        TNT1 A 0 A_FireBullets(5.6,0,1,random(6,8)*2,"DoomBulletPuff2",FBF_NORANDOM|FBF_NOFLASH)
        stop

      PickupAttackImprovedCoop:
        TNT1 A 0 A_FireBullets(5.6,0,1,random(6,8)*2,"DoomBulletPuff2_Coop",FBF_NORANDOM|FBF_NOFLASH)
        stop
    }
}

actor DoomBulletPuff2 : DoomBulletPuff { DamageType "DoomDamageType" }
actor DoomBulletPuff2_Coop : DoomBulletPuff2 { +MTHRUSPECIES }

// use these for other weapons
actor DoomBulletPuff2ThruGhost : DoomBulletPuff2 { +THRUGHOST }
actor DoomBulletPuff2ThruGhost_Coop : DoomBulletPuff2ThruGhost { +MTHRUSPECIES }

actor SamsaraDoomguy64PistolAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto PickupAttack
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Clip",1,1)
        stop
        TNT1 A 0 A_TakeInventory("Clip",1)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("doom64guy/pistol",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SamsaraImprovedDoomguyBalance",1,"PickupAttackImproved")
      // Vanilla
      PickupAttackVanilla:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackVanillaCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackVanillaCoop")
      PickupAttackVanillaDM:
        TNT1 A 0 A_FireBullets(5.6,0,1,random(1,4)*4,"Doom64BulletPuff2",FBF_NORANDOM|FBF_NOFLASH)
        stop

      PickupAttackVanillaCoop:
        TNT1 A 0 A_FireBullets(5.6,0,1,random(1,4)*4,"Doom64BulletPuff2_Coop",FBF_NORANDOM|FBF_NOFLASH)
        stop

      // Improved
      PickupAttackImproved:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackImprovedCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackImprovedCoop")
      PickupAttackImprovedDM:
        TNT1 A 0 A_FireBullets(5.6,0,1,random(7,11)*2,"Doom64BulletPuff2",FBF_NORANDOM|FBF_NOFLASH) // Doom64 pistol is based on a Desert Eagle, so more damage than PC Doom // hold state used random(5,7)*2. hm...
        stop

      PickupAttackImprovedCoop:
        TNT1 A 0 A_FireBullets(5.6,0,1,random(7,11)*2,"Doom64BulletPuff2_Coop",FBF_NORANDOM|FBF_NOFLASH)
        stop
    }
}

actor Doom64BulletPuff2 : Doom64BulletPuff { DamageType "Pistol" }
actor Doom64BulletPuff2_Coop : Doom64BulletPuff2 { +MTHRUSPECIES }

// Unpowered attack handler

actor SamsaraDoomguyStrPistolAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto PickupAttack
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Clip",1,1)
        stop
        TNT1 A 0 A_TakeInventory("Clip",1)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("doomguy/stronghold/pistol",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireBullets(5.6,0,1,5,"SamsaraDoomguyStrBulletPuffBallistic",FBF_NOFLASH)
        stop

      PickupAttackCoop:
        TNT1 A 0 A_FireBullets(5.6,0,1,5,"SamsaraDoomguyStrBulletPuffBallisticCoop",FBF_NOFLASH)
        stop
    }
}

// Powered attack handlers

actor SamsaraDoomguyStrPistolPoweredPrimaryAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto PickupAttack
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Clip",1,1)
        stop
        TNT1 A 0 A_TakeInventory("Clip",1)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("doomguy/stronghold/pistol",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireBullets(4,2.5,-1,12,"SamsaraDoomguyStrElectricalPuff",FBF_NOFLASH)
        stop

      PickupAttackCoop:
        TNT1 A 0 A_FireBullets(4,2.5,-1,12,"SamsaraDoomguyStrElectricalPuffCoop",FBF_NOFLASH)
        stop
    }
}

actor SamsaraDoomguyStrPistolPoweredSecondaryAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto PickupAttack
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Clip",8,1)
        stop
        TNT1 A 0 A_TakeInventory("Clip",8)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("doomguy/stronghold/pistol",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireBullets(10,7,8,12,"SamsaraDoomguyStrElectricalPuff",FBF_NOFLASH)
        stop

      PickupAttackCoop:
        TNT1 A 0 A_FireBullets(10,7,8,12,"SamsaraDoomguyStrElectricalPuffCoop",FBF_NOFLASH)
        stop
    }
}
