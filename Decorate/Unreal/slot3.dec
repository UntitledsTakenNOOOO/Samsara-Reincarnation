Actor Unreal_FlakCannon : UnrealWeapon 
{
	Weapon.SelectionOrder 1600
	Weapon.SlotNumber 3
	Weapon.AmmoType1 "Shell"
	Weapon.AmmoUse1 2
	Weapon.AmmoType2 "Shell"
	Weapon.AmmoUse2 2
	Weapon.AmmoGive 20
	Weapon.UpSound "Unreal/Flak/Down"
	Obituary "%o was ripped to shreds by %k's Flak Cannon."
	Inventory.PickupMessage "You got the Flak Cannon"
	Tag "Flak Cannon"
	States
	{
		Spawn:
			UN00 A 10 BRIGHT
			UN00 A 10
			Loop	
		Select:
			TNT1 AAAAAAAAAAAAAAAAAAAA 0 A_Raise
			TNT1 A 1 A_Raise
			loop				
		Ready:
			UN01 A 0 A_GiveInventory("Unreal_FlakSelected",1)
			UN01 ABCDEF 1
			UN01 GH 2
			UN01 I 0 A_PlaySound("Unreal/Flak/Select", CHAN_WEAPON)
			UN01 JKLMNO 2
			UN02 ABCDEFGH 2
			Goto WIdle
		WIdle:
			UN03 A 2 A_WeaponReady 	UN03 A 0 A_Jump(24, "Sway")
			Loop
		Sway:
			UN07 ABABA 12 A_WeaponReady
			Goto WIdle
		Deselect:
			UN06 A 0 A_TakeInventory("Unreal_FlakSelected",1)
			UN06 ABCDEFG 2
			Goto InstantDeselect
		InstantDeselect:
			TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_Lower
			TNT1 A 1 A_Lower
			Wait		
		Fire:
			UN03 B 0 ACS_NamedExecuteWithResult("Unreal_Recoil")
			UN03 B 3 BRIGHT A_FireCustomMissile("Unreal_FlakChunk",0,1,20,0,0)
			UN03 C 0 BRIGHT A_FireCustomMissile("Unreal_FlakChunk2",random(-10,10),0,16,4,0,Random(-6,6))
			UN03 C 0 BRIGHT A_FireCustomMissile("Unreal_FlakChunk3",random(-10,10),0,16,2,0,Random(-10,10))
			UN03 C 0 BRIGHT A_FireCustomMissile("Unreal_FlakChunk4",random(-10,10),0,16,4,0, Random(-6,6))
			UN03 C 0 BRIGHT A_FireCustomMissile("Unreal_FlakChunk",0,0,14,0,0)
			UN03 C 0 BRIGHT A_FireCustomMissile("Unreal_FlakChunk2",random(-10,10),0,16,-4,0, Random(-6,6))
			UN03 C 0 BRIGHT A_FireCustomMissile("Unreal_FlakChunk3",random(-10,0),0,16,-2,0,Random(-10,10))
			UN03 C 0 BRIGHT A_FireCustomMissile("Unreal_FlakChunk4",random(-10,10),0,16,-4,0,Random(-6,6))
			UN03 C 0 BRIGHT A_PlaySound("Unreal/Flak/Fire1", CHAN_WEAPON)
			UN03 CDE 1 BRIGHT
			UN03 FGHIJK 1 
			UN05 A 0 A_PlaySound("Unreal/Flak/Barrel", CHAN_BODY)
			UN05 ABCDEFGHIJ 1
			UN04 A 0 A_PlaySound("Unreal/Flak/Load", CHAN_WEAPON)
			UN02 ABCDEFGH 2
			Goto WIdle
		Altfire:
			UN04 A 0 ACS_NamedExecuteWithResult("Unreal_Recoil")
			UN04 A 3 BRIGHT A_FireCustomMissile("Unreal_FlakShell",0,1,13,0)
			UN04 B 0 BRIGHT A_PlaySound("Unreal/Flak/Explode1", CHAN_WEAPON)
			UN04 CDE 2 BRIGHT
			UN04 FGHIJK 2 BRIGHT
			UN04 A 0 A_PlaySound("Unreal/Flak/Load", CHAN_WEAPON)
			UN02 ABCDEFGH 2
			Goto WIdle
	}
}		

Actor Unreal_FlakChunk 
{
	Projectile
	Gravity 0.70
	+ALLOWBOUNCEONACTORS
	+BOUNCEONFLOORS
	+BOUNCEONCEILINGS
	+BOUNCEONWALLS
	+USEBOUNCESTATE
	+CANBOUNCEWATER
	+NOGRAVITY
	+HITTRACER
	BounceCount 5
	BounceSound "Unreal/FlakChunk/Bounce"
	BounceType Grenade
	Speed 50
	Damage (20)
	Radius 3
	Height 5
	BounceFactor 0.8
	DamageType "UnrealRazor"
	Var Int user_pitch;
	States
	{
		Spawn:
			UN00 A 0 A_SetUserVar(user_pitch, random(0,360))
			UN00 A 0 A_SetAngle(random(0,360))
			UN00 A 0 A_Jump(96, 1)
			Goto SpawnLoop
			UN00 A 0
			UN00 A 0 A_SetDamageType("UnrealShredded")			
		SpawnLoop:
			UN00 A 0 A_SetAngle(angle-5)
			UN00 A 0 A_SetPitch(user_pitch+5)
			UN00 A 2 A_JumpIf(velx <= 0 && vely <= 0 && velz <= 0, "Remove")
			Loop
		Remove:
			UN00 AAAAAAAAAAA 2
			UN00 A 2 A_FadeOut(0.5)
			Wait
		Bounce:
			UN00 A 0 A_Gravity
			Goto SpawnLoop
		Death:
		Crash:
			TNT1 A 1
			Stop
		XDeath:
			TNT1 A 0 A_JumpIf(CallACS("Unreal_BloodGenerator_Offsets")==1,1)
			TNT1 A 1
			TNT1 A 0 A_GiveInventory("UnrealBloodyPuffGenerator",1,AAPTR_TRACER)	
			TNT1 A 1 A_PlaySound("Unreal/FlakChunk/HitMeat", CHAN_BODY)
			Stop
			}
}		

Actor Unreal_FlakChunk2 : Unreal_FlakChunk { }
Actor Unreal_FlakChunk3 : Unreal_FlakChunk { }
Actor Unreal_FlakChunk4 : Unreal_FlakChunk { }

Actor Unreal_FlakShell
{
	PROJECTILE
	-NOGRAVITY
	Speed 24
	Damage (70)
	DeathSound "Unreal/Flak/Explode2"
	Radius 12
	Height 12
	Gravity 0.70
	DamageType "UnrealExplosive"
	States
	{
		Spawn:
			UN00 A 0 
			UN00 A 0 ThrustThingZ(0,20,0,1)
		SpawnLoop:
			UN00 A 2 A_SpawnItemEx("UnrealSmoke", 0,0,0,0,0,0,0, SXF_NOCHECKPOSITION)
			Loop
		Death:
			TNT1 A 0 A_Explode(70, 150)
			TNT1 A 0 A_SpawnItemEX("Unreal_FlareExplosiveEffect", 0,0,0,0,0,0,0, SXF_NOCHECKPOSITION)
			TNT1 AA 0 A_CustomMissile("Unreal_FlakChunk", 0,0,Random(0,360), CMF_TRACKOWNER)
			TNT1 AA 0 A_CustomMissile("Unreal_FlakChunk2", 0,0,Random(0,360), CMF_TRACKOWNER)
			TNT1 A 0 A_CustomMissile("Unreal_FlakChunk3", 0,0,Random(0,360), CMF_TRACKOWNER)
			TNT1 A 5 A_CustomMissile("Unreal_FlakChunk4", 0,0,Random(0,360), CMF_TRACKOWNER)
			Stop
	}
}			
		