actor DGHasMinigun : Boolean {}
actor SamsaraDoomguyStrMinigunFiringSoundCheck : Boolean {}
actor SamsaraDoomguyStrMinigunPoweredAttackAmount : Counter { Inventory.MaxAmount 2 }

ACTOR " Minigun " : Weapon
{
	Radius 20
	Height 16
	Weapon.AmmoType "Clip"
	Weapon.AmmoGive 0
	Weapon.AmmoUse 1
	Weapon.Selectionorder 700
	Weapon.Kickback 100
	Weapon.SlotNumber 4 // This line isn't in skulltag.pk3, which instead defines the slot directly in DoomPlayer
	Inventory.PickupMessage "You got the minigun!"
	Inventory.RestrictedTo "DoomguyPlayer"
	Obituary "$SAMSARA_DOOMGUY_OB_SLOT4S"
	Tag "Minigun"
	+ALT_USES_BOTH
	+UNDROPPABLE
	States
	{
	Spawn:
		WMNG A -1
		loop

	// Original
	Ready:
		TNT1 A 0 A_TakeInventory("SamsaraDoomguyStrMinigunFiringSoundCheck")
		TNT1 A 0 A_JumpIfInventory("DoomClassMode",2,"ReadyStr")
		TNT1 A 0 A_JumpIfInventory("DoomClassMode",1,"Ready64")
		TNT1 A 0 A_JumpIfInventory("SamsaraUsingSmoothAnims",1,"ReadySmooth")
	ReadyVanilla:
		DMNG A 1 A_WeaponReady(WRF_NOSECONDARY)
		goto Ready
	ReadySmooth:
		PKMG A 1 A_WeaponReady(WRF_NOSECONDARY)
		goto Ready

	Deselect:
		TNT1 A 0 A_GiveInventory("SamsaraDoomguyStrMinigunFiringSoundStopper")
		TNT1 A 0 A_JumpIfInventory("DoomClassMode",2,"DeselectStr")
		TNT1 A 0 A_JumpIfInventory("DoomClassMode",1,"Deselect64")
		TNT1 A 0 A_JumpIfInventory("SamsaraUsingSmoothAnims",1,"DeselectSmooth")
	DeselectVanilla:
		DMNG A 1 A_Lower
		goto Deselect
	DeselectSmooth:
		PKMG A 1 A_Lower
		goto Deselect

	Select:
		TNT1 A 0 A_TakeInventory("SamsaraDoomguyStrMinigunFiringSoundCheck")
		TNT1 A 0 A_JumpIfInventory("DoomClassMode",2,"SelectStr")
		TNT1 A 0 A_JumpIfInventory("DoomClassMode",1,"Select64")
		TNT1 A 0 A_JumpIfInventory("SamsaraUsingSmoothAnims",1,"SelectSmooth")
	SelectVanilla:
		DMNG A 1 A_Raise
		loop
	SelectSmooth:
		PKMG A 1 A_Raise
		loop

	Fire:
		TNT1 A 0 A_JumpIfInventory("DoomClassMode",2,"FireStr")
		TNT1 A 0 A_GiveInventory("SamsaraDoomguyStrMinigunFiringSoundStopper")
		TNT1 A 0 A_JumpIfInventory("DoomClassMode",1,"Fire64")
		TNT1 A 0 A_JumpIfInventory("SamsaraUsingSmoothAnims",1,"FireSmooth")
	FireVanilla:
		TNT1 A 0 A_GunFlash
		DMNG A 2 A_GiveInventory("SamsaraDoomguyMinigunAttackHandler")
		TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireVanilla2Attack")
		TNT1 A 0 A_JumpIfInventory("Clip",1,"FireVanilla2Attack")
		goto FireVanilla2

	FireVanilla2Attack:
		TNT1 A 0 A_GunFlash("Flash2")
	FireVanilla2:
		DMNG B 2 A_GiveInventory("SamsaraDoomguyMinigunAttackHandler")
	FireVanillaFinish:
		DMNG A 2 A_ReFire
		DMNG B 2
		DMNG AB 4
		DMNG AB 8
		goto Ready
		
	FireSmooth:
		TNT1 A 0 A_JumpIfInventory("SamsaraHasDoubleFiringSpeed",1,"FireSmoothRage")
		TNT1 A 0 A_GunFlash("FlashSmooth")
		PKMG A 1 A_GiveInventory("SamsaraDoomguyMinigunAttackHandler")
		PKMG B 1
		TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireSmooth2Attack")
		TNT1 A 0 A_JumpIfInventory("Clip",1,"FireSmooth2Attack")
		goto FireSmooth2

	FireSmoothRage:
		TNT1 A 0 A_GunFlash("FlashSmoothRage")
		PKMG A 1 A_GiveInventory("SamsaraDoomguyMinigunAttackHandler")
		TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireSmooth2Attack")
		TNT1 A 0 A_JumpIfInventory("Clip",1,"FireSmooth2Attack")
		goto FireSmoothRage2

	FireSmooth2Attack:
		TNT1 A 0 A_JumpIfInventory("SamsaraHasDoubleFiringSpeed",1,"FireSmoothRage2Attack")
		TNT1 A 0 A_GunFlash("FlashSmooth2")
	FireSmooth2:
		PKMG C 1 A_GiveInventory("SamsaraDoomguyMinigunAttackHandler")
		PKMG D 1
	FireFinishSmooth:
		PKMG A 1 A_ReFire
		PKMG B 1
		PKMG CD 1
		PKMG ABCD 2
		PKMG ABCD 4
		goto Ready

	FireSmoothRage2Attack:
		TNT1 A 0 A_GunFlash("FlashSmoothRage2")
	FireSmoothRage2:
		PKMG C 1 A_GiveInventory("SamsaraDoomguyMinigunAttackHandler")
	FireFinishSmoothRage:
		PKMG A 1 A_ReFire
		PKMG C 1
		PKMG ABCD 2
		PKMG ABCD 4
		goto Ready

	Flash:
		DMNF A 2 Bright A_Light1
		goto LightDone

	Flash2:
		DMNF B 2 Bright A_Light2
		goto LightDone

	FlashSmooth:
		PKMF A 1 Bright A_Light2
		PKMF B 1 Bright A_Light1
		goto LightDone

	FlashSmoothRage:
		PKMF A 1 Bright A_Light2
		PKMF B 0 Bright A_Light1
		goto LightDone

	FlashSmooth2:
		PKMF C 1 Bright A_Light2
		PKMF D 1 Bright A_Light1
		goto LightDone

	FlashSmoothRage2:
		PKMF C 1 Bright A_Light2
		PKMF D 0 Bright A_Light1
		goto LightDone

	// Doom 64 (no changes)
	Ready64:
		64MN A 1 A_WeaponReady(WRF_NOSECONDARY)
		goto Ready

	Deselect64:
		64MN A 1 A_Lower
		goto Deselect

	Select64:
		64MN A 1 A_Raise
		goto Select

	Fire64:
		TNT1 A 0 A_JumpIfInventory("SamsaraUsingSmoothAnims",1,"Fire64Smooth")
	Fire64Vanilla:
		TNT1 A 0 A_GunFlash("Flash64")
		64MN A 2 A_GiveInventory("SamsaraDoomguy64MinigunAttackHandler")
		TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"Fire64Vanilla2Attack")
		TNT1 A 0 A_JumpIfInventory("Clip",1,"Fire64Vanilla2Attack")
		goto Fire64Vanilla2

	Fire64Vanilla2Attack:
		TNT1 A 0 A_GunFlash("Flash64_2")
	Fire64Vanilla2:
		64MN B 2 A_GiveInventory("SamsaraDoomguy64MinigunAttackHandler")
	Fire64Finish:
		64MN A 2 A_ReFire
		64MN B 2
		64MN AB 4
		64MN AB 8
		goto Ready
		
	Fire64Smooth:
		TNT1 A 0 A_GunFlash("Flash64Smooth")
		64MN A 1 A_GiveInventory("SamsaraDoomguy64MinigunAttackHandler")
		64MN E 1
		TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"Fire64Smooth2Attack")
		TNT1 A 0 A_JumpIfInventory("Clip",1,"Fire64Smooth2Attack")
		goto Fire64Smooth2

	Fire64Smooth2Attack:
		TNT1 A 0 A_GunFlash("Flash64_2Smooth")
	Fire64Smooth2:
		64MN B 1 A_GiveInventory("SamsaraDoomguy64MinigunAttackHandler")
		64MN F 1
	FireFinish64Smooth:
		64MN A 1 A_ReFire
		64MN EBF 1
		64MN AEBF 2
		64MN AEBF 4
		goto Ready

	Flash64:
		64NM A 2 Bright A_Light1
		goto LightDone

	Flash64_2:
		64NM B 2 Bright A_Light2
		goto LightDone
		
	Flash64Smooth:
		64NM A 1 Bright A_Light2
		64NM D 1 Bright A_Light1
		goto LightDone

	Flash64_2Smooth:
		64NM B 1 Bright A_Light2
		64NM F 1 Bright A_Light1
		goto LightDone

	// Stronghold
	// Unpowered
	ReadyStr:
		TNT1 A 0 A_JumpIfInventory("PowerSamsaraDoomguyStrWeaponPower",1,"ReadyStrPowered")
		TNT1 A 0 A_JumpIfInventory("SamsaraHasPermaWeaponLevel2",1,"ReadyStrPowered")
		DMNG C 1 A_WeaponReady(WRF_NOSECONDARY)
		goto Ready

	DeselectStr:
		TNT1 A 0 A_JumpIfInventory("PowerSamsaraDoomguyStrWeaponPower",1,"DeselectStrPowered")
		TNT1 A 0 A_JumpIfInventory("SamsaraHasPermaWeaponLevel2",1,"DeselectStrPowered")
	DeselectStrContinue:
		TNT1 A 0 A_Lower
		DMNG C 1 A_Lower
		goto Deselect

	SelectStr:
		TNT1 A 0 A_JumpIfInventory("PowerSamsaraDoomguyStrWeaponPower",1,"SelectStrPowered")
		TNT1 A 0 A_JumpIfInventory("SamsaraHasPermaWeaponLevel2",1,"SelectStrPowered")
	SelectStrContinue:
		TNT1 A 0 A_Raise
		DMNG C 1 A_Raise
		goto Select

	FireStr:
		TNT1 A 0 A_JumpIfInventory("SamsaraUsingSmoothAnims",1,"FireStrSmooth")
		TNT1 A 0 A_JumpIfInventory("PowerSamsaraDoomguyStrWeaponPower",1,"FireStrPowered")
		TNT1 A 0 A_JumpIfInventory("SamsaraHasPermaWeaponLevel2",1,"FireStrPowered")
		TNT1 A 0 A_GunFlash("FlashStr")
		DMNG C 2 A_GiveInventory("SamsaraDoomguyStrMinigunAttackHandler")
		TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireStr2Attack")
		TNT1 A 0 A_JumpIfInventory("Clip",1,"FireStr2Attack")
		goto FireStr2

	FireStr2Attack:
		TNT1 A 0 A_GunFlash("FlashStr2")
	FireStr2:
		DMNG D 2 A_GiveInventory("SamsaraDoomguyStrMinigunAttackHandler")
	FireStrFinish:
		TNT1 A 0 A_ReFire
	FireStrFinishAnim:
		TNT1 A 0 A_JumpIfInventory("SamsaraUsingSmoothAnims",1,"FireStrFinishAnimSmooth")
		DMNG C 2 A_GiveInventory("SamsaraDoomguyStrMinigunFiringEndSoundPlayer")
		DMNG D 2
		DMNG CD 4
		DMNG CD 8
		//DMNG CD 12
		goto Ready
		
	FireStrSmooth:
		TNT1 A 0 A_JumpIfInventory("PowerSamsaraDoomguyStrWeaponPower",1,"FireStrPowered")
		TNT1 A 0 A_JumpIfInventory("SamsaraHasPermaWeaponLevel2",1,"FireStrPowered")
		TNT1 A 0 A_GunFlash("FlashStrSmooth")
		DMNG C 1 A_GiveInventory("SamsaraDoomguyStrMinigunAttackHandler")
		DMNG E 1
		TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireStr2AttackSmooth")
		TNT1 A 0 A_JumpIfInventory("Clip",1,"FireStr2AttackSmooth")
		goto FireStr2

	FireStr2AttackSmooth:
		TNT1 A 0 A_GunFlash("FlashStr2Smooth")
	FireStr2Smooth:
		DMNG D 1 A_GiveInventory("SamsaraDoomguyStrMinigunAttackHandler")
		DMNG F 1
	FireStrFinishSmooth:
		TNT1 A 0 A_ReFire
	FireStrFinishAnimSmooth:
		DMNG C 1 A_GiveInventory("SamsaraDoomguyStrMinigunFiringEndSoundPlayer")
		DMNG E 1
		DMNG DF 1
		DMNG CEDF 2
		DMNG CEDF 4
		//DMNG CEDF 6
		goto Ready

	FlashStr:
		DMNF C 2 Bright A_Light1
		goto LightDone

	FlashStr2:
		DMNF G 2 Bright A_Light2
		goto LightDone
		
	FlashStrSmooth:
		DMNF C 1 Bright A_Light2
		DMNF E 1 Bright A_Light1
		goto LightDone

	FlashStr2Smooth:
		DMNF D 1 Bright A_Light2
		DMNF F 1 Bright A_Light1
		goto LightDone

	// Powered
	ReadyStrPowered:
		DMNG C 1 A_WeaponReady
		goto Ready

	DeselectStrPowered:
		TNT1 A 0 A_Lower
		goto DeselectStrContinue

	SelectStrPowered:
		TNT1 A 0 A_Raise
		goto SelectStrContinue

	FireStrPowered:
		TNT1 A 0 A_TakeInventory("SamsaraDoomguyStrMinigunPoweredAttackAmount") // reset
	FireStrPoweredLoop:
		TNT1 A 0 A_GiveInventory("SamsaraDoomguyStrMinigunPoweredAttackAmount")
		TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireStrPoweredLoop1Attack")
		TNT1 A 0 A_JumpIfInventory("Clip",1,"FireStrPoweredLoop1Attack")
		goto FireStrPoweredLoop1

	FireStrPoweredLoop1Attack:
		TNT1 A 0 A_GunFlash("FlashStrPowered")
	FireStrPoweredLoop1:
		TNT1 A 0 A_TakeInventory("SamsaraTakingAmmo")
		DMNG C 1 A_GiveInventory("SamsaraDoomguyStrMinigunPoweredPrimaryAttackHandler")
		TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireStrPoweredLoop2Attack")
		TNT1 A 0 A_JumpIfInventory("Clip",1,"FireStrPoweredLoop2Attack")
		goto FireStrPoweredLoop2

	FireStrPoweredLoop2Attack:
		TNT1 A 0 A_GunFlash("FlashStrPowered2")
	FireStrPoweredLoop2:
		TNT1 A 0 A_GiveInventory("SamsaraTakingAmmo")
		DMNG D 1 A_GiveInventory("SamsaraDoomguyStrMinigunPoweredPrimaryAttackHandler")
		TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrMinigunPoweredAttackAmount",0,"FireStrPoweredFinish")
		goto FireStrPoweredLoop

	FireStrPoweredFinish:
		TNT1 A 0 A_ReFire
	FireStrPoweredFinishAnim:
		TNT1 A 0 A_JumpIfInventory("SamsaraUsingSmoothAnims",1,"FireStrPoweredFinishAnimSmooth")
		DMNG C 2 A_GiveInventory("SamsaraDoomguyStrMinigunFiringPoweredEndSoundPlayer")
		DMNG D 2
		DMNG CD 4 //Extremely shortened refire delay
		goto Ready
	FireStrPoweredFinishAnimSmooth: //no smooth powered fire because it fires really fast
		DMNG C 1 A_GiveInventory("SamsaraDoomguyStrMinigunFiringPoweredEndSoundPlayer")
		DMNG E 1
		DMNG DF 1
		DMNG CEDF 2 //Extremely shortened refire delay
		goto Ready

	AltFire:
		TNT1 A 0 A_TakeInventory("SamsaraDoomguyStrMinigunPoweredAttackAmount") // reset
	AltFireLoop:
		TNT1 A 0 A_GiveInventory("SamsaraDoomguyStrMinigunPoweredAttackAmount")
		TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"AltFireLoop1Attack")
		TNT1 A 0 A_JumpIfInventory("Clip",1,"AltFireLoop1Attack")
		goto AltFireLoop1

	AltFireLoop1Attack:
		TNT1 A 0 A_GunFlash("FlashStrPowered")
	AltFireLoop1:
		DMNG C 1 A_GiveInventory("SamsaraDoomguyStrMinigunPoweredSecondaryAttackHandler")
		TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"AltFireLoop2Attack")
		TNT1 A 0 A_JumpIfInventory("Clip",1,"AltFireLoop2Attack")
		goto AltFireLoop2

	AltFireLoop2Attack:
		TNT1 A 0 A_GunFlash("FlashStrPowered2")
	AltFireLoop2:
		DMNG D 1 A_GiveInventory("SamsaraDoomguyStrMinigunPoweredSecondaryAttackHandler")
		TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrMinigunPoweredAttackAmount",0,"AltFireFinish")
		goto AltFireLoop

	AltFireFinish:
		TNT1 A 0 A_JumpIfInventory("PowerSamsaraDoomguyStrWeaponPower",1,"AltFireFinishReFire")
		TNT1 A 0 A_JumpIfInventory("SamsaraHasPermaWeaponLevel2",1,"AltFireFinishReFire")
		TNT1 A 0 A_CheckReload
		goto FireStrFinishAnim

	AltFireFinishReFire:
		TNT1 A 0 A_JumpIfInventory("SamsaraUsingSmoothAnims",1,"AltFireFinishReFireSmooth")
		TNT1 A 0 A_JumpIfInventory("DoomClassMode",2,"FireStrFinish")
		TNT1 A 0 A_CheckReload
		goto FireStrFinishAnim

	FlashStrPowered:
		DMNF C 1 Bright A_Light1
		//DMNF C 1 Bright
		goto LightDone

	FlashStrPowered2:
		DMNF G 1 Bright A_Light2
		//DMNF D 1 Bright
		goto LightDone
	}
}

actor SamsaraDoomguyMinigunAttackHandler : Trigger
{
	States
	{
	// Ammo checking/taking stuff
	Pickup:
		TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupSound")
		TNT1 A 0 A_JumpIfInventory("Clip",1,1)
		stop
		TNT1 A 0 A_TakeInventory("Clip",1)
		goto PickupSound

	// Sound stuff
	PickupSound:
		TNT1 A 0 A_JumpIfInventory("SamsaraAlternateSoundToken",2,"PickupSoundPerkristan")
		TNT1 A 0 A_JumpIfInventory("SamsaraAlternateSoundToken",1,"PickupSoundPSX")
		TNT1 A 0 A_PlaySound("doomguy/minigun",CHAN_WEAPON)
		goto PickupAttack
	PickupSoundPSX:
		TNT1 A 0 A_PlaySound("doom64guy/minigun",CHAN_WEAPON)
		goto PickupAttack
	PickupSoundPerkristan:
		TNT1 A 0 A_PlaySound("doomguy/minigun",CHAN_WEAPON)
		goto PickupAttack

	// Attack stuff
	PickupAttack:
		TNT1 A 0 A_JumpIfInventory("SamsaraImprovedDoomguyBalance",1,"PickupAttackImproved")
	// Vanilla
	PickupAttackVanilla:
		TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackVanillaCoop")
		TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackVanillaCoop")
	PickupAttackVanillaDM:
		TNT1 A 0 A_FireBullets(5.6,0,1,5,"DoomBulletPuff2ThruGhost",FBF_NOFLASH)
		stop

	PickupAttackVanillaCoop:
		TNT1 A 0 A_FireBullets(5.6,0,1,5,"DoomBulletPuff2ThruGhost_Coop",FBF_NOFLASH)
		stop

	// Improved
	PickupAttackImproved:
		TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackImprovedCoop")
		TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackImprovedCoop")
	PickupAttackImprovedDM:
		TNT1 A 0 A_FireBullets(5.6,0,1,random(8,10)*2,"DoomBulletPuff2ThruGhost",FBF_NORANDOM|FBF_NOFLASH)
		stop

	PickupAttackImprovedCoop:
		TNT1 A 0 A_FireBullets(5.6,0,1,random(8,10)*2,"DoomBulletPuff2ThruGhost_Coop",FBF_NORANDOM|FBF_NOFLASH)
		stop
	}
}

actor SamsaraDoomguy64MinigunAttackHandler : Trigger
{
	States
	{
	// Ammo checking/taking stuff
	Pickup:
		TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
		TNT1 A 0 A_JumpIfInventory("Clip",1,1)
		stop
		TNT1 A 0 A_TakeInventory("Clip",1)
		goto PickupAttack

	// Attack stuff
	PickupAttack:
		TNT1 A 0 A_PlaySound("doom64guy/minigun",CHAN_WEAPON)
		TNT1 A 0 A_JumpIfInventory("SamsaraImprovedDoomguyBalance",1,"PickupAttackImproved")
	// Vanilla
	PickupAttackVanilla:
		TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackVanillaCoop")
		TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackVanillaCoop")
	PickupAttackVanillaDM:
		TNT1 A 0 A_FireBullets(5.6,0,1,random(1,4)*4,"Doom64BulletPuff2ThruGhost",FBF_NORANDOM|FBF_NOFLASH)
		stop

	PickupAttackVanillaCoop:
		TNT1 A 0 A_FireBullets(5.6,0,1,random(1,4)*4,"Doom64BulletPuff2ThruGhost_Coop",FBF_NORANDOM|FBF_NOFLASH)
		stop

	// Improved
	PickupAttackImproved:
		TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackImprovedCoop")
		TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackImprovedCoop")
	PickupAttackImprovedDM:
		TNT1 A 0 A_FireBullets(5.6,0,1,random(6,9)*2,"Doom64BulletPuff2ThruGhost",FBF_NORANDOM|FBF_NOFLASH)
		stop

	PickupAttackImprovedCoop:
		TNT1 A 0 A_FireBullets(5.6,0,1,random(6,9)*2,"Doom64BulletPuff2ThruGhost_Coop",FBF_NORANDOM|FBF_NOFLASH)
		stop
	}
}

// Unpowered attack handler

actor SamsaraDoomguyStrMinigunAttackHandler : Trigger
{
	States
	{
	// Ammo checking/taking stuff
	Pickup:
		TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupSound")
		TNT1 A 0 A_JumpIfInventory("Clip",1,1)
		stop
		TNT1 A 0 A_TakeInventory("Clip",1)
		goto PickupSound

	// Sound stuff
	PickupSound:
		TNT1 A 0 A_JumpIfInventory("SamsaraAlternateSoundToken",1,"PickupSoundDE")
		TNT1 A 0 A_PlaySound("doomguy/stronghold/minigun",CHAN_WEAPON)
		TNT1 A 0 A_PlaySound("doomguy/stronghold/minigunloop",CHAN_6,1,true)
		TNT1 A 0 A_GiveInventory("SamsaraDoomguyStrMinigunFiringSoundCheck")
		goto PickupAttack
	PickupSoundDE:
		TNT1 A 0 A_GiveInventory("SamsaraDoomguyStrMinigunFiringSoundStopper")
		TNT1 A 0 A_PlaySound("doomguy/demoneclipse/minigun",CHAN_WEAPON)
		goto PickupAttack

	// Attack stuff
	PickupAttack:
		TNT1 A 0 A_JumpIfInventory("SamsaraImprovedDoomguyBalance",1,"PickupAttackImproved")
	// Vanilla
	PickupAttackVanilla:
		TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackVanillaCoop")
		TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackVanillaCoop")
	PickupAttackVanillaDM:
		TNT1 A 0 A_FireBullets(4,2.3,1,5,"SamsaraDoomguyStrBulletPuffBallisticThruGhost",FBF_NOFLASH)
		stop

	PickupAttackVanillaCoop:
		TNT1 A 0 A_FireBullets(4,2.3,1,5,"SamsaraDoomguyStrBulletPuffBallisticThruGhostCoop",FBF_NOFLASH)
		stop

	// Improved
	PickupAttackImproved:
		TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackImprovedCoop")
		TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackImprovedCoop")
	PickupAttackImprovedDM:
		TNT1 A 0 A_FireBullets(4,2.3,1,random(8,10)*2,"SamsaraDoomguyStrBulletPuffBallisticThruGhost",FBF_NORANDOM|FBF_NOFLASH)
		stop

	PickupAttackImprovedCoop:
		TNT1 A 0 A_FireBullets(4,2.3,1,random(8,10)*2,"SamsaraDoomguyStrBulletPuffBallisticThruGhostCoop",FBF_NORANDOM|FBF_NOFLASH)
		stop
	}
}
//SamsaraDoomguyStrMinigunFiringPoweredEndSoundPlayer
actor SamsaraDoomguyStrMinigunFiringEndSoundPlayer : Trigger
{
	States
	{
	// Sound playing stuff
	Pickup:
		TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrMinigunFiringSoundCheck",1,1)
		stop
		TNT1 A 0 A_PlaySound("doomguy/stronghold/minigundown",CHAN_6)
		TNT1 A 0 A_TakeInventory("SamsaraDoomguyStrMinigunFiringSoundCheck")
		stop
	}
}

actor SamsaraDoomguyStrMinigunFiringPoweredEndSoundPlayer : Trigger
{
	States
	{
	// Sound playing stuff
	Pickup:
		TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrMinigunFiringSoundCheck",1,1)
		stop
		TNT1 A 0 A_PlaySound("doomguy/stronghold/minigundown2",CHAN_6)
		TNT1 A 0 A_TakeInventory("SamsaraDoomguyStrMinigunFiringSoundCheck")
		stop
	}
}


actor SamsaraDoomguyStrMinigunFiringSoundStopper : Trigger
{
	States
	{
	// Sound stopping stuff
	Pickup:
		TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrMinigunFiringSoundCheck",1,1)
		stop
		TNT1 A 0 A_PlaySound("silence",CHAN_6)
		TNT1 A 0 A_StopSound(CHAN_6)
		TNT1 A 0 A_TakeInventory("SamsaraDoomguyStrMinigunFiringSoundCheck")
		stop
	}
}

// Powered attack handlers

actor SamsaraDoomguyStrMinigunPoweredPrimaryAttackHandler : Trigger
{
	States
	{
	// Ammo checking/taking stuff
	Pickup:
		TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
		TNT1 A 0 A_JumpIfInventory("Clip",1,1)
		stop
		TNT1 A 0 A_JumpIfInventory("SamsaraTakingAmmo",1,1)
		goto PickupAttack
		TNT1 A 0 A_TakeInventory("Clip",1)
		goto PickupAttack

	// Attack stuff
	PickupAttack:
		TNT1 A 0 A_JumpIfInventory("SamsaraAlternateSoundToken",1,"PickupAttackDE")
	PickupAttackStr:
		TNT1 A 0 A_PlaySound("doomguy/stronghold/minigun",CHAN_WEAPON)
		TNT1 A 0 A_PlaySound("doomguy/stronghold/minigunloop",CHAN_6,1,true)
		TNT1 A 0 A_GiveInventory("SamsaraDoomguyStrMinigunFiringSoundCheck")
		TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackStrCoop")
		TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackStrCoop")
	PickupAttackStrDM:
		TNT1 A 0 A_FireBullets(4,2.3,2,10,"SamsaraDoomguyStrElectricalPuff",FBF_NOFLASH)
		stop

	PickupAttackStrCoop:
		TNT1 A 0 A_FireBullets(4,2.3,2,10,"SamsaraDoomguyStrElectricalPuffCoop",FBF_NOFLASH)
		stop

	PickupAttackDE:
		TNT1 A 0 A_GiveInventory("SamsaraDoomguyStrMinigunFiringSoundStopper")
		TNT1 A 0 A_PlaySound("doomguy/demoneclipse/minigun",CHAN_WEAPON)
		TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackDECoop")
		TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackDECoop")
	PickupAttackDEDM:
		TNT1 A 0 A_FireBullets(4,2.3,2,10,"SamsaraDoomguyStrElectricalPuffSoundDE",FBF_NOFLASH)
		stop

	PickupAttackDECoop:
		TNT1 A 0 A_FireBullets(4,2.3,2,10,"SamsaraDoomguyStrElectricalPuffCoopSoundDE",FBF_NOFLASH)
		stop
	}
}

actor SamsaraDoomguyStrMinigunPoweredSecondaryAttackHandler : Trigger
{
	States
	{
	// Ammo checking/taking stuff
	Pickup:
		TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
		TNT1 A 0 A_JumpIfInventory("Clip",1,1)
		stop
		TNT1 A 0 A_TakeInventory("Clip",1)
		goto PickupAttack

	// Attack stuff
	PickupAttack:
		TNT1 A 0 A_JumpIfInventory("SamsaraAlternateSoundToken",1,"PickupAttackDE")
	PickupAttackStr:
		TNT1 A 0 A_PlaySound("doomguy/stronghold/minigun",CHAN_WEAPON)
		TNT1 A 0 A_PlaySound("doomguy/stronghold/minigunloop",CHAN_6,1,true)
		TNT1 A 0 A_GiveInventory("SamsaraDoomguyStrMinigunFiringSoundCheck")
		TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackStrCoop")
		TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackStrCoop")
	PickupAttackStrDM:
		TNT1 A 0 A_FireBullets(8,4.6,4,10,"SamsaraDoomguyStrElectricalPuff",FBF_NOFLASH)
		stop

	PickupAttackStrCoop:
		TNT1 A 0 A_FireBullets(8,4.6,4,10,"SamsaraDoomguyStrElectricalPuffCoop",FBF_NOFLASH)
		stop

	PickupAttackDE:
		TNT1 A 0 A_GiveInventory("SamsaraDoomguyStrMinigunFiringSoundStopper")
		TNT1 A 0 A_PlaySound("doomguy/demoneclipse/minigun",CHAN_WEAPON)
		TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackDECoop")
		TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackDECoop")
	PickupAttackDEDM:
		TNT1 A 0 A_FireBullets(8,4.6,4,10,"SamsaraDoomguyStrElectricalPuffSoundDE",FBF_NOFLASH)
		stop

	PickupAttackDECoop:
		TNT1 A 0 A_FireBullets(8,4.6,4,10,"SamsaraDoomguyStrElectricalPuffCoopSoundDE",FBF_NOFLASH)
		stop
	}
}
