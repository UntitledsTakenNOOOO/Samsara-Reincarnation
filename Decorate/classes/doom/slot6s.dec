actor DGHasRailGun : Boolean {}
actor SamsaraDoomguyStrRailgunScope : Boolean {}
actor SamsaraDoomguyStrRailgunPoweredHeldCheck : Boolean {}

ACTOR " RailGun " : Weapon
{
	Radius 20
	Height 16
	Weapon.Selectionorder 100
	Weapon.AmmoType1 "DoomguyRailgunClip"
	Weapon.AmmoUse1 1
	Weapon.AmmoType2 "Cell"
	//Weapon.AmmoGive2 40
	Inventory.Pickupmessage "You got the railgun!"
	Inventory.RestrictedTo "DoomguyPlayer"
	Decal "RailScorch"
	Obituary "$SAMSARA_DOOMGUY_OB_SLOT6S"
	Tag "Railgun"
	+AMMO_OPTIONAL
	+NOALERT
	+UNDROPPABLE
	States
	{
	Spawn:
		WRLG A -1
		stop

	// Original
	Ready:
		TNT1 A 0 A_JumpIfInventory("DoomClassMode",2,"ReadyStr")
		TNT1 A 0 A_JumpIfInventory("DoomClassMode",1,"Ready64")
		TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrRailgunScope",1,"ReadyZoomOut")
		goto ReadyContinue

	ReadyZoomOut:
		TNT1 A 0 A_ZoomFactor(1)
		TNT1 A 0 A_TakeInventory("SamsaraDoomguyStrRailgunScope")
		goto ReadyContinue

	ReadyContinue:
		TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",0,"ReadyModernReloadable")
	ReadyNonReloadable:
		DRLG A 1 A_WeaponReady(WRF_NOSECONDARY)
		goto Ready

	ReadyModernReloadable:
		TNT1 A 0 A_JumpIfInventory("DoomguyRailgunClip",0,"ReadyNonReloadable")
		TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
		TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
		TNT1 A 0 A_JumpIfInventory("Cell",10,1)
		goto ReadyNonReloadable
		DRLG A 1 A_WeaponReady(WRF_NOSECONDARY|WRF_ALLOWRELOAD)
		goto Ready

	Deselect:
		TNT1 A 0 A_JumpIfInventory("DoomClassMode",2,"DeselectStr")
		TNT1 A 0 A_JumpIfInventory("DoomClassMode",1,"Deselect64")
		TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrRailgunScope",1,"DeselectZoomOut")
		goto DeselectContinue

	DeselectZoomOut:
		TNT1 A 0 A_ZoomFactor(1)
		TNT1 A 0 A_TakeInventory("SamsaraDoomguyStrRailgunScope")
		goto DeselectContinue

	DeselectContinue:
		DRLG A 1 A_Lower
		goto Deselect

	Select:
		TNT1 A 0 A_JumpIfInventory("DoomClassMode",2,"SelectStr")
		TNT1 A 0 A_JumpIfInventory("DoomClassMode",1,"Select64")
		TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrRailgunScope",1,"SelectZoomOut")
		goto SelectContinue

	SelectZoomOut:
		TNT1 A 0 A_ZoomFactor(1,ZOOM_INSTANT) // map change
		TNT1 A 0 A_TakeInventory("SamsaraDoomguyStrRailgunScope")
		goto SelectContinue

	SelectContinue:
		DRLG A 1 A_Raise
		goto Select

	Fire:
		TNT1 A 0 A_JumpIfInventory("DoomClassMode",2,"FireStr")
		TNT1 A 0 A_JumpIfInventory("DoomClassMode",1,"Fire64")
		TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",2,"FireReloadable")
	FireNonReloadable:
		TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireStart")
		TNT1 A 0 A_JumpIfInventory("Cell",10,"FireStart")
		goto Nothing

	FireReloadable:
		TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"FireStart")
		TNT1 A 0 A_JumpIfInventory("DoomguyRailgunClip",1,"FireStart")
	FireReloadableDryCheck:
		TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"ReloadStart")
		TNT1 A 0 A_JumpIfInventory("Cell",10,"ReloadStart")
		goto Nothing

	FireStart:
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_GunFlash("Flash",GFF_NOEXTCHANGE)
		DRLG E 12 A_GiveInventory("SamsaraDoomguyRailgunAttackHandler")
		TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",2,"FireFinishReloadable")
		goto FireFinish

	FireFinishReloadable:
		TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"FireFinish")
		TNT1 A 0 A_JumpIfInventory("DoomguyRailgunClip",1,"FireFinish")
		TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"ReloadAfterFire")
		TNT1 A 0 A_JumpIfInventory("Cell",10,"ReloadAfterFire")
		goto FireFinish

	FireFinish:
		DRLG A 6
		goto FireReFireCheck

	FireReFireCheck:
		TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",2,"FireReFireReloadable")
		goto FireReFireNonReloadable

	FireReFireNonReloadable:
		TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireReFire")
		TNT1 A 0 A_JumpIfInventory("Cell",10,"FireReFire")
		goto Nothing

	FireReFireReloadable:
		TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"FireReFire")
		TNT1 A 0 A_JumpIfInventory("DoomguyRailgunClip",1,"FireReFire")
		TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireReFire")
		TNT1 A 0 A_JumpIfInventory("Cell",10,"FireReFire")
		goto Nothing

	FireReFire:
		TNT1 A 0 A_ReFire
		goto Ready

	Flash:
		TNT1 A 5 Bright A_Light1
		TNT1 A 5 Bright A_Light2
		goto LightDone

	Reload:
		TNT1 A 0 A_JumpIfInventory("DoomClassMode",1,"Reload64")
	ReloadStart:
		DRLG E 6
	ReloadAfterFire:
		DRLG F 6 A_GiveInventory("SamsaraDoomguyRailgunUnloadSoundPlayer")
		DRLG GHIJ 6
		TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",2,"ReloadAmmo")
	ReloadFinish:
		DRLG K 6 A_GiveInventory("SamsaraDoomguyRailgunLoadSoundPlayer")
		DRLG L 6
		goto FireFinish

	ReloadAmmo:
		TNT1 A 0 A_JumpIfInventory("DoomguyRailgunClip",0,"ReloadFinish")
		TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
		TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
		TNT1 A 0 A_JumpIfInventory("Cell",10,1)
		goto ReloadFinish
		TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
		TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
		TNT1 A 0 A_TakeInventory("Cell",10)
		TNT1 A 0 A_GiveInventory("DoomguyRailgunClip")
		loop

	// Doom 64 (reloads slightly slower)
	Ready64:
		TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrRailgunScope",1,"Ready64ZoomOut")
		goto Ready64Continue

	Ready64ZoomOut:
		TNT1 A 0 A_ZoomFactor(1)
		TNT1 A 0 A_TakeInventory("SamsaraDoomguyStrRailgunScope")
		goto Ready64Continue

	Ready64Continue:
		TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",0,"Ready64ModernReloadable")
	Ready64NonReloadable:
		64RA A 1 A_WeaponReady(WRF_NOSECONDARY)
		goto Ready

	Ready64ModernReloadable:
		TNT1 A 0 A_JumpIfInventory("DoomguyRailgunClip",0,"Ready64NonReloadable")
		TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
		TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
		TNT1 A 0 A_JumpIfInventory("Cell",10,1)
		goto Ready64NonReloadable
		64RA A 1 A_WeaponReady(WRF_NOSECONDARY|WRF_ALLOWRELOAD)
		goto Ready

	Deselect64:
		TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrRailgunScope",1,"Deselect64ZoomOut")
		goto Deselect64Continue

	Deselect64ZoomOut:
		TNT1 A 0 A_ZoomFactor(1)
		TNT1 A 0 A_TakeInventory("SamsaraDoomguyStrRailgunScope")
		goto Deselect64Continue

	Deselect64Continue:
		64RA A 1 A_Lower
		goto Deselect

	Select64:
		TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrRailgunScope",1,"Select64ZoomOut")
		goto Select64Continue

	Select64ZoomOut:
		TNT1 A 0 A_ZoomFactor(1,ZOOM_INSTANT) // map change
		TNT1 A 0 A_TakeInventory("SamsaraDoomguyStrRailgunScope")
		goto Select64Continue

	Select64Continue:
		64RA A 1 A_Raise
		goto Select

	Fire64:
		TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",2,"Fire64Reloadable")
	Fire64NonReloadable:
		TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"Fire64Start")
		TNT1 A 0 A_JumpIfInventory("Cell",10,"Fire64Start")
		goto Nothing

	Fire64Reloadable:
		TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"Fire64Start")
		TNT1 A 0 A_JumpIfInventory("DoomguyRailgunClip",1,"Fire64Start")
	Fire64ReloadableDryCheck:
		TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"Reload64")
		TNT1 A 0 A_JumpIfInventory("Cell",10,"Reload64")
		goto Nothing

	Fire64Start:
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_GunFlash("Flash",GFF_NOEXTCHANGE)
		64RA B 12 Bright A_GiveInventory("SamsaraDoomguy64RailgunAttackHandler")
		TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",2,"Fire64FinishReloadable")
		goto Fire64Finish

	Fire64FinishReloadable:
		TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"Fire64Finish")
		TNT1 A 0 A_JumpIfInventory("DoomguyRailgunClip",1,"Fire64Finish")
		TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"Reload64")
		TNT1 A 0 A_JumpIfInventory("Cell",10,"Reload64")
		goto Fire64Finish

	Fire64Finish:
		64RA A 6
		goto FireReFireCheck

	Reload64:
		64RA CD 4
		64RA EF 6
		64RA G 18 A_PlaySound("doom64guy/railgu")
		64RA F 6
		TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",2,"Reload64Ammo")
	Reload64Finish:
		64RA E 6 A_PlaySound("doom64guy/railgl")
		64RA DC 4
		goto Fire64Finish

	Reload64Ammo:
		TNT1 A 0 A_JumpIfInventory("DoomguyRailgunClip",0,"Reload64Finish")
		TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
		TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
		TNT1 A 0 A_JumpIfInventory("Cell",10,1)
		goto Reload64Finish
		TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
		TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
		TNT1 A 0 A_TakeInventory("Cell",10)
		TNT1 A 0 A_GiveInventory("DoomguyRailgunClip")
		loop

	// Stronghold (its own version)
	ReadyStr:
		TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrRailgunScope",1,"ReadyStrZoom")
		RAIG A 1 A_WeaponReady(WRF_ALLOWZOOM)
		goto Ready

	ReadyStrZoom:
		RSCP A 1 Bright A_WeaponReady(WRF_NOBOB|WRF_ALLOWZOOM)
		goto Ready

	// Unpowered
	DeselectStr:
		TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrRailgunScope",1,"DeselectStrZoomOut")
		goto DeselectStrStart

	DeselectStrZoomOut:
		TNT1 A 0 A_GiveInventory("SamsaraDoomguyStrRailgunZoomOutSoundPlayer")
		TNT1 A 0 A_ZoomFactor(1)
		TNT1 A 0 A_TakeInventory("SamsaraDoomguyStrRailgunScope")
		goto DeselectStrStart

	DeselectStrStart:
		TNT1 A 0 A_JumpIfInventory("PowerSamsaraDoomguyStrWeaponPower",1,"DeselectStrStartPowered")
		TNT1 A 0 A_JumpIfInventory("SamsaraHasPermaWeaponLevel2",1,"DeselectStrStartPowered")
	DeselectStrContinue:
		TNT1 A 0 A_Lower
		RAIG A 1 A_Lower
		goto Deselect

	SelectStr:
		TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrRailgunScope",1,"SelectStrZoomOut")
		goto SelectStrStart

	SelectStrZoomOut:
		// no need for a sound
		TNT1 A 0 A_ZoomFactor(1,ZOOM_INSTANT) // map change
		TNT1 A 0 A_TakeInventory("SamsaraDoomguyStrRailgunScope")
		goto SelectStrStart

	SelectStrStart:
		TNT1 A 0 A_JumpIfInventory("PowerSamsaraDoomguyStrWeaponPower",1,"SelectStrStartPowered")
		TNT1 A 0 A_JumpIfInventory("SamsaraHasPermaWeaponLevel2",1,"SelectStrStartPowered")
	SelectStrContinue:
		TNT1 A 0 A_Raise
		RAIG A 1 A_Raise
		goto Select

	AltFire:
	Zoom:
		TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrRailgunScope",1,"Unzoom")
		TNT1 A 0 A_GiveInventory("SamsaraDoomguyStrRailgunZoomInSoundPlayer")
		TNT1 A 0 A_ZoomFactor(3)
		RSCP A 8 Bright A_GiveInventory("SamsaraDoomguyStrRailgunScope")
		goto Ready

	Unzoom:
		TNT1 A 0 A_GiveInventory("SamsaraDoomguyStrRailgunZoomOutSoundPlayer")
		TNT1 A 0 A_ZoomFactor(1)
		RAIG A 8 A_TakeInventory("SamsaraDoomguyStrRailgunScope")
		goto Ready

	FireStr:
		TNT1 A 0 A_JumpIfInventory("PowerSamsaraDoomguyStrWeaponPower",1,"FireStrPowered")
		TNT1 A 0 A_JumpIfInventory("SamsaraHasPermaWeaponLevel2",1,"FireStrPowered")
		TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireStrCharge")
		TNT1 A 0 A_JumpIfInventory("Cell",5,"FireStrCharge")
		goto Nothing

	FireStrCharge:
		TNT1 A 0 A_PlaySound("doomguy/stronghold/railch",CHAN_WEAPON)
	FireStrCharge0:
		RAIG B 12 Bright A_JumpIfInventory("SamsaraDoomguyStrRailgunScope",1,"FireStrCharge0Zoomed")
		TNT1 A 0 A_WeaponReady(WRF_NOBOB|WRF_NOSWITCH|WRF_NOFIRE)
		TNT1 A 0 A_ReFire("FireStrCharge0Check")
		RAIG E 4 Bright A_GunFlash("FlashStr")
		TNT1 A 0 A_AlertMonsters
		RAIG F 6 Bright A_GiveInventory("SamsaraDoomguyStrRailgunCharge0AttackHandler")
	FireStrChargeFinish:
		RAIG A 4
		RAIG K 15
		goto Ready

	FireStrCharge0Zoomed:
		RSCP A 12 Bright
		TNT1 A 0 A_WeaponReady(WRF_NOBOB|WRF_NOSWITCH|WRF_NOFIRE)
		TNT1 A 0 A_ReFire("FireStrCharge0Check")
		RSCP A 4 Bright A_GunFlash("FlashStr")
		TNT1 A 0 A_AlertMonsters
		RSCP A 6 Bright A_GiveInventory("SamsaraDoomguyStrRailgunCharge0AttackHandler")
	FireStrChargeFinishZoomed:
		RSCP A 4 Bright
		RSCP A 15 Bright
		goto Ready

	FireStrCharge0Check:
		TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireStrCharge1")
		TNT1 A 0 A_JumpIfInventory("Cell",10,"FireStrCharge1")
		goto FireStrCharge0

	FireStrCharge1:
		RAIG C 12 Bright A_JumpIfInventory("SamsaraDoomguyStrRailgunScope",1,"FireStrCharge1Zoomed")
		TNT1 A 0 A_WeaponReady(WRF_NOBOB|WRF_NOSWITCH|WRF_NOFIRE)
		TNT1 A 0 A_ReFire("FireStrCharge1Check")
		RAIG G 4 Bright A_GunFlash("FlashStr")
		TNT1 A 0 A_AlertMonsters
		RAIG H 6 Bright A_GiveInventory("SamsaraDoomguyStrRailgunCharge1AttackHandler")
		goto FireStrChargeFinish

	FireStrCharge1Zoomed:
		RSCP A 12 Bright
		TNT1 A 0 A_WeaponReady(WRF_NOBOB|WRF_NOSWITCH|WRF_NOFIRE)
		TNT1 A 0 A_ReFire("FireStrCharge1Check")
		RSCP A 4 Bright A_GunFlash("FlashStr")
		TNT1 A 0 A_AlertMonsters
		RSCP A 6 Bright A_GiveInventory("SamsaraDoomguyStrRailgunCharge1AttackHandler")
		goto FireStrChargeFinishZoomed

	FireStrCharge1Check:
		TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireStrCharge2")
		TNT1 A 0 A_JumpIfInventory("Cell",15,"FireStrCharge2")
		goto FireStrCharge1

	FireStrCharge2:
		RAIG D 12 Bright A_JumpIfInventory("SamsaraDoomguyStrRailgunScope",1,"FireStrCharge2Zoomed")
		TNT1 A 0 A_WeaponReady(WRF_NOBOB|WRF_NOSWITCH|WRF_NOFIRE)
		TNT1 A 0 A_ReFire("FireStrCharge2")
		RAIG I 4 Bright A_GunFlash("FlashStr")
		TNT1 A 0 A_AlertMonsters
		RAIG J 6 Bright A_GiveInventory("SamsaraDoomguyStrRailgunCharge2AttackHandler")
		goto FireStrChargeFinish

	FireStrCharge2Zoomed:
		RSCP A 12 Bright
		TNT1 A 0 A_WeaponReady(WRF_NOBOB|WRF_NOSWITCH|WRF_NOFIRE)
		TNT1 A 0 A_ReFire("FireStrCharge2")
		RSCP A 4 Bright A_GunFlash("FlashStr")
		TNT1 A 0 A_AlertMonsters
		RSCP A 6 Bright A_GiveInventory("SamsaraDoomguyStrRailgunCharge2AttackHandler")
		goto FireStrChargeFinishZoomed

	FlashStr:
		TNT1 A 4 A_Light1
		TNT1 A 6 A_Light2
		goto LightDone

	// Powered
	DeselectStrStartPowered:
		TNT1 A 0 A_Lower
		goto DeselectStrContinue

	SelectStrStartPowered:
		TNT1 A 0 A_Raise
		goto SelectStrContinue

	FireStrPowered:
		TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrRailgunScope",1,"FireStrPoweredZoomed")
		TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireStrPoweredStart")
		TNT1 A 0 A_JumpIfInventory("Cell",2,"FireStrPoweredStart")
		goto Nothing

	FireStrPoweredStart:
		RAIG A 1
		TNT1 A 0 A_PlaySound("doomguy/stronghold/railch",CHAN_WEAPON)
		RAIG BCD 1 Bright
		RAIG I 1 Bright A_GunFlash("FlashStrPowered")
		TNT1 A 0 A_TakeInventory("SamsaraDoomguyStrRailgunPoweredHeldCheck")
	FireStrPoweredContinue:
		TNT1 A 0 A_AlertMonsters
		RAIG J 1 Bright A_GiveInventory("SamsaraDoomguyStrRailgunPoweredAttackHandler")
		TNT1 A 0 A_JumpIfInventory("PowerSamsaraDoomguyStrWeaponPower",1,"FireStrPoweredFinishReFire")
		TNT1 A 0 A_JumpIfInventory("SamsaraHasPermaWeaponLevel2",1,"FireStrPoweredFinishReFire")
	FireStrPoweredFinish:
		RAIG K 10
		goto Ready

	FireStrPoweredFinishReFire:
		TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireStrPoweredFinishReFireStart")
		TNT1 A 0 A_JumpIfInventory("Cell",2,"FireStrPoweredFinishReFireStart")
		goto Nothing

	FireStrPoweredFinishReFireStart:
		TNT1 A 0 A_JumpIfInventory("DoomClassMode",2,1)
		goto FireStrPoweredFinish
		TNT1 A 0 A_ReFire(1)
		goto FireStrPoweredFinish
		RAIG A 1
		RAIG BCD 1 Bright
		RAIG I 1 Bright A_GunFlash("FlashStrPowered")
		TNT1 A 0 A_GiveInventory("SamsaraDoomguyStrRailgunPoweredHeldCheck")
		goto FireStrPoweredContinue

	FireStrPoweredZoomed:
		TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireStrPoweredStartZoomed")
		TNT1 A 0 A_JumpIfInventory("Cell",4,"FireStrPoweredStartZoomed")
		goto Nothing

	FireStrPoweredStartZoomed:
		RSCP A 1 Bright
		RSCP A 7 Bright A_PlaySound("doomguy/stronghold/railch",CHAN_WEAPON)
		RSCP A 1 Bright A_GunFlash("FlashStrPowered")
		TNT1 A 0 A_TakeInventory("SamsaraDoomguyStrRailgunPoweredHeldCheck")
	FireStrPoweredContinueZoomed:
		TNT1 A 0 A_AlertMonsters
		RSCP A 1 Bright A_GiveInventory("SamsaraDoomguyStrRailgunPoweredAttackHandler")
		TNT1 A 0 A_JumpIfInventory("PowerSamsaraDoomguyStrWeaponPower",1,"FireStrPoweredFinishReFireZoomed")
		TNT1 A 0 A_JumpIfInventory("SamsaraHasPermaWeaponLevel2",1,"FireStrPoweredFinishReFireZoomed")
	FireStrPoweredFinishZoomed:
		RSCP A 10 Bright
		goto Ready

	FireStrPoweredFinishReFireZoomed:
		TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireStrPoweredFinishReFireStartZoomed")
		TNT1 A 0 A_JumpIfInventory("Cell",4,"FireStrPoweredFinishReFireStartZoomed")
		goto Nothing

	FireStrPoweredFinishReFireStartZoomed:
		TNT1 A 0 A_JumpIfInventory("DoomClassMode",2,1)
		goto FireStrPoweredFinishZoomed
		TNT1 A 0 A_ReFire(1)
		goto FireStrPoweredFinishZoomed
		RSCP A 1 Bright
		RSCP A 7 Bright
		RSCP A 1 Bright A_GunFlash("FlashStrPowered")
		TNT1 A 0 A_GiveInventory("SamsaraDoomguyStrRailgunPoweredHeldCheck")
		goto FireStrPoweredContinueZoomed

	FlashStrPowered:
		TNT1 A 1 A_Light1
		TNT1 A 1 A_Light2
		goto LightDone

	Nothing:
		TNT1 A 0 A_ClearReFire
		TNT1 A 0 A_SelectWeapon(" Pistol ")
		goto Deselect
	}
}

actor SamsaraDoomguyRailgunAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",2,"PickupReloadable")
      PickupNonReloadable:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupSound")
        TNT1 A 0 A_JumpIfInventory("Cell",10,1)
        stop
        TNT1 A 0 A_TakeInventory("Cell",10)
        goto PickupSound

      PickupReloadable:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"PickupSound")
        TNT1 A 0 A_JumpIfInventory("DoomguyRailgunClip",1,1)
        stop
        TNT1 A 0 A_TakeInventory("DoomguyRailgunClip",1)
        goto PickupSound

      // Sound stuff
      PickupSound:
        TNT1 A 0 A_JumpIfInventory("SamsaraAlternateSoundToken",2,"PickupSoundPerkristan")
        TNT1 A 0 A_JumpIfInventory("SamsaraAlternateSoundToken",1,"PickupSoundPSX")
        TNT1 A 0 A_PlaySound("doomguy/railgf",CHAN_WEAPON)
        goto PickupAttack

      PickupSoundPSX:
        TNT1 A 0 A_PlaySound("doom64guy/railgf",CHAN_WEAPON)
        goto PickupAttack

      PickupSoundPerkristan:
        TNT1 A 0 A_PlaySound("doomguy/railgf",CHAN_WEAPON)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_RailAttack(75,0,false,"","",RGF_SILENT|RGF_FULLBRIGHT,0,"DoomRailPuff")
        stop

      PickupAttackCoop:
        TNT1 A 0 A_RailAttack(200,0,false,"","",RGF_SILENT|RGF_FULLBRIGHT,0,"DoomRailPuff_Coop")
        stop
    }
}

actor SamsaraDoomguy64RailgunAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",2,"PickupReloadable")
      PickupNonReloadable:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Cell",10,1)
        stop
        TNT1 A 0 A_TakeInventory("Cell",10)
        goto PickupAttack

      PickupReloadable:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("DoomguyRailgunClip",1,1)
        stop
        TNT1 A 0 A_TakeInventory("DoomguyRailgunClip",1)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("doom64guy/railgf",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_RailAttack(75,0,false,"","",RGF_SILENT|RGF_FULLBRIGHT,0,"Doom64RailPuff")
        stop

      PickupAttackCoop:
        TNT1 A 0 A_RailAttack(200,0,false,"","",RGF_SILENT|RGF_FULLBRIGHT,0,"Doom64RailPuff_Coop")
        stop
    }
}

actor SamsaraDoomguyRailgunUnloadSoundPlayer : Trigger
{
    States
    {
      // Sound playing stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraAlternateSoundToken",2,"PickupPerkristan")
        TNT1 A 0 A_JumpIfInventory("SamsaraAlternateSoundToken",1,"PickupPSX")
      PickupOriginal:
        TNT1 A 0 A_PlaySound("doomguy/railgu")
        stop

      PickupPSX:
        TNT1 A 0 A_PlaySound("doom64guy/railgu")
        stop

      PickupPerkristan:
        TNT1 A 0 A_PlaySound("doomguy/perkristan/railgu")
        stop
    }
}

actor SamsaraDoomguyRailgunLoadSoundPlayer : Trigger
{
    States
    {
      // Sound playing stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraAlternateSoundToken",2,"PickupPerkristan")
        TNT1 A 0 A_JumpIfInventory("SamsaraAlternateSoundToken",1,"PickupPSX")
      PickupOriginal:
        TNT1 A 0 A_PlaySound("doomguy/railgl")
        stop

      PickupPSX:
        TNT1 A 0 A_PlaySound("doom64guy/railgl")
        stop

      PickupPerkristan:
        TNT1 A 0 A_PlaySound("doomguy/perkristan/railgl")
        stop
    }
}

actor SamsaraDoomguyRailgunSpawnReloader : Trigger // used for (re)spawn
{
    States
    {
      Pickup:
        TNT1 A 0 A_JumpIfInventory("DoomClassMode",2,"PickupStop")
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",2,1)
        stop
        TNT1 A 0 A_JumpIfInventory("DoomguyRailgunClip",0,"PickupStop")
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("Cell",10,1)
        stop
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_TakeInventory("Cell",10)
        TNT1 A 0 A_GiveInventory("DoomguyRailgunClip")
        loop
    }
}

actor DoomguyRailgunClip : Ammo
{
	Inventory.MaxAmount 4
	Tag "Railgun Magazine"
	+IGNORESKILL
	+UNTOSSABLE
}

actor DoomRailPuff : DoomBulletPuff2
{
	Decal ""
	+PIERCEARMOR
}

actor DoomRailPuff_Coop : DoomRailPuff
{
	//+ALLOWTHRUFLAGS // doesn't exist in zandronum yet
	+MTHRUSPECIES
}

actor Doom64RailPuff : Doom64BulletPuff2
{
	Decal ""
	+PIERCEARMOR
}

actor Doom64RailPuff_Coop : Doom64RailPuff
{
	//+ALLOWTHRUFLAGS // doesn't exist in zandronum yet
	+MTHRUSPECIES
}

// Unpowered attack handlers

actor SamsaraDoomguyStrRailgunCharge0AttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Cell",5,1)
        stop
        TNT1 A 0 A_TakeInventory("Cell",5)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("doomguy/stronghold/railf1",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrRailgunScope",1,"PickupAttackZoomed")
      // Unzoomed
      PickupAttackUnzoomed:
        TNT1 A 0 A_JumpIfInventory("SamsaraImprovedDoomguyBalance",1,"PickupAttackUnzoomedImproved")
      // Vanilla
      PickupAttackUnzoomedVanilla:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackUnzoomedVanillaCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackUnzoomedVanillaCoop")
      PickupAttackUnzoomedVanillaDM:
        TNT1 A 0 A_RailAttack(random(50,100),0,false,"FF 00 00","FF FF FF",RGF_SILENT|RGF_FULLBRIGHT,0,"SamsaraDoomguyStrRailgunPuff")
        stop

      PickupAttackUnzoomedVanillaCoop:
        TNT1 A 0 A_RailAttack(random(50,100),0,false,"FF 00 00","FF FF FF",RGF_SILENT|RGF_FULLBRIGHT,0,"SamsaraDoomguyStrRailgunPuffCoop")
        stop

      // Improved
      PickupAttackUnzoomedImproved:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackUnzoomedImprovedCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackUnzoomedImprovedCoop")
      PickupAttackUnzoomedImprovedDM:
        TNT1 A 0 A_RailAttack(100,0,false,"FF 00 00","FF FF FF",RGF_SILENT|RGF_FULLBRIGHT,0,"SamsaraDoomguyStrRailgunPuff")
        stop

      PickupAttackUnzoomedImprovedCoop:
        TNT1 A 0 A_RailAttack(100,0,false,"FF 00 00","FF FF FF",RGF_SILENT|RGF_FULLBRIGHT,0,"SamsaraDoomguyStrRailgunPuffCoop")
        stop

      // Zoomed
      PickupAttackZoomed:
        TNT1 A 0 A_JumpIfInventory("SamsaraImprovedDoomguyBalance",1,"PickupAttackZoomedImproved")
      // Vanilla
      PickupAttackZoomedVanilla:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackZoomedVanillaCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackZoomedVanillaCoop")
      PickupAttackZoomedVanillaDM:
        TNT1 A 0 A_RailAttack(random(75,125),0,false,"FF 00 00","FF FF FF",RGF_SILENT|RGF_FULLBRIGHT,0,"SamsaraDoomguyStrRailgunPuff")
        stop

      PickupAttackZoomedVanillaCoop:
        TNT1 A 0 A_RailAttack(random(75,125),0,false,"FF 00 00","FF FF FF",RGF_SILENT|RGF_FULLBRIGHT,0,"SamsaraDoomguyStrRailgunPuffCoop")
        stop

      // Improved
      PickupAttackZoomedImproved:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackZoomedImprovedCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackZoomedImprovedCoop")
      PickupAttackZoomedImprovedDM:
        TNT1 A 0 A_RailAttack(125,0,false,"FF 00 00","FF FF FF",RGF_SILENT|RGF_FULLBRIGHT,0,"SamsaraDoomguyStrRailgunPuff")
        stop

      PickupAttackZoomedImprovedCoop:
        TNT1 A 0 A_RailAttack(125,0,false,"FF 00 00","FF FF FF",RGF_SILENT|RGF_FULLBRIGHT,0,"SamsaraDoomguyStrRailgunPuffCoop")
        stop
    }
}

actor SamsaraDoomguyStrRailgunCharge1AttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Cell",10,1)
        stop
        TNT1 A 0 A_TakeInventory("Cell",10)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("doomguy/stronghold/railf2",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrRailgunScope",1,"PickupAttackZoomed")
      // Unzoomed
      PickupAttackUnzoomed:
        TNT1 A 0 A_JumpIfInventory("SamsaraImprovedDoomguyBalance",1,"PickupAttackUnzoomedImproved")
      // Vanilla
      PickupAttackUnzoomedVanilla:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackUnzoomedVanillaCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackUnzoomedVanillaCoop")
      PickupAttackUnzoomedVanillaDM:
        TNT1 A 0 A_RailAttack(random(100,150),0,false,"FF FF 00","FF 45 00",RGF_SILENT|RGF_FULLBRIGHT,0,"SamsaraDoomguyStrRailgunPuff")
        stop

      PickupAttackUnzoomedVanillaCoop:
        TNT1 A 0 A_RailAttack(random(100,150),0,false,"FF FF 00","FF 45 00",RGF_SILENT|RGF_FULLBRIGHT,0,"SamsaraDoomguyStrRailgunPuffCoop")
        stop

      // Improved
      PickupAttackUnzoomedImproved:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackUnzoomedImprovedCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackUnzoomedImprovedCoop")
      PickupAttackUnzoomedImprovedDM:
        TNT1 A 0 A_RailAttack(150,0,false,"FF FF 00","FF 45 00",RGF_SILENT|RGF_FULLBRIGHT,0,"SamsaraDoomguyStrRailgunPuff")
        stop

      PickupAttackUnzoomedImprovedCoop:
        TNT1 A 0 A_RailAttack(150,0,false,"FF FF 00","FF 45 00",RGF_SILENT|RGF_FULLBRIGHT,0,"SamsaraDoomguyStrRailgunPuffCoop")
        stop

      // Zoomed
      PickupAttackZoomed:
        TNT1 A 0 A_JumpIfInventory("SamsaraImprovedDoomguyBalance",1,"PickupAttackZoomedImproved")
      // Vanilla
      PickupAttackZoomedVanilla:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackZoomedVanillaCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackZoomedVanillaCoop")
      PickupAttackZoomedVanillaDM:
        TNT1 A 0 A_RailAttack(random(125,175),0,false,"FF FF 00","FF 45 00",RGF_SILENT|RGF_FULLBRIGHT,0,"SamsaraDoomguyStrRailgunPuff")
        stop

      PickupAttackZoomedVanillaCoop:
        TNT1 A 0 A_RailAttack(random(125,175),0,false,"FF FF 00","FF 45 00",RGF_SILENT|RGF_FULLBRIGHT,0,"SamsaraDoomguyStrRailgunPuffCoop")
        stop

      // Improved
      PickupAttackZoomedImproved:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackZoomedImprovedCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackZoomedImprovedCoop")
      PickupAttackZoomedImprovedDM:
        TNT1 A 0 A_RailAttack(175,0,false,"FF FF 00","FF 45 00",RGF_SILENT|RGF_FULLBRIGHT,0,"SamsaraDoomguyStrRailgunPuff")
        stop

      PickupAttackZoomedImprovedCoop:
        TNT1 A 0 A_RailAttack(175,0,false,"FF FF 00","FF 45 00",RGF_SILENT|RGF_FULLBRIGHT,0,"SamsaraDoomguyStrRailgunPuffCoop")
        stop
    }
}

actor SamsaraDoomguyStrRailgunCharge2AttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Cell",15,1)
        stop
        TNT1 A 0 A_TakeInventory("Cell",15)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("doomguy/stronghold/railf3",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrRailgunScope",1,"PickupAttackZoomed")
      // Unzoomed
      PickupAttackUnzoomed:
        TNT1 A 0 A_JumpIfInventory("SamsaraImprovedDoomguyBalance",1,"PickupAttackUnzoomedImproved")
      // Vanilla
      PickupAttackUnzoomedVanilla:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackUnzoomedVanillaCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackUnzoomedVanillaCoop")
      PickupAttackUnzoomedVanillaDM:
        TNT1 A 0 A_RailAttack(random(150,200),0,false,"00 00 FF","00 FF 00",RGF_SILENT|RGF_FULLBRIGHT,0,"SamsaraDoomguyStrRailgunPuff")
        stop

      PickupAttackUnzoomedVanillaCoop:
        TNT1 A 0 A_RailAttack(random(150,200),0,false,"00 00 FF","00 FF 00",RGF_SILENT|RGF_FULLBRIGHT,0,"SamsaraDoomguyStrRailgunPuffCoop")
        stop

      // Improved
      PickupAttackUnzoomedImproved:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackUnzoomedImprovedCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackUnzoomedImprovedCoop")
      PickupAttackUnzoomedImprovedDM:
        TNT1 A 0 A_RailAttack(200,0,false,"00 00 FF","00 FF 00",RGF_SILENT|RGF_FULLBRIGHT,0,"SamsaraDoomguyStrRailgunPuff")
        stop

      PickupAttackUnzoomedImprovedCoop:
        TNT1 A 0 A_RailAttack(200,0,false,"00 00 FF","00 FF 00",RGF_SILENT|RGF_FULLBRIGHT,0,"SamsaraDoomguyStrRailgunPuffCoop")
        stop

      // Zoomed
      PickupAttackZoomed:
        TNT1 A 0 A_JumpIfInventory("SamsaraImprovedDoomguyBalance",1,"PickupAttackZoomedImproved")
      // Vanilla
      PickupAttackZoomedVanilla:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackZoomedVanillaCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackZoomedVanillaCoop")
      PickupAttackZoomedVanillaDM:
        TNT1 A 0 A_RailAttack(random(175,225),0,false,"00 00 FF","00 FF 00",RGF_SILENT|RGF_FULLBRIGHT,0,"SamsaraDoomguyStrRailgunPuff")
        stop

      PickupAttackZoomedVanillaCoop:
        TNT1 A 0 A_RailAttack(random(175,225),0,false,"00 00 FF","00 FF 00",RGF_SILENT|RGF_FULLBRIGHT,0,"SamsaraDoomguyStrRailgunPuffCoop")
        stop

      // Improved
      PickupAttackZoomedImproved:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackZoomedImprovedCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackZoomedImprovedCoop")
      PickupAttackZoomedImprovedDM:
        TNT1 A 0 A_RailAttack(225,0,false,"00 00 FF","00 FF 00",RGF_SILENT|RGF_FULLBRIGHT,0,"SamsaraDoomguyStrRailgunPuff")
        stop

      PickupAttackZoomedImprovedCoop:
        TNT1 A 0 A_RailAttack(225,0,false,"00 00 FF","00 FF 00",RGF_SILENT|RGF_FULLBRIGHT,0,"SamsaraDoomguyStrRailgunPuffCoop")
        stop
    }
}

// Powered attack handler

actor SamsaraDoomguyStrRailgunPoweredAttackHandler : Trigger
{
    States
    {
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrRailgunScope",1,"PickupZoomed")
      // Unzoomed
      // Ammo checking/taking stuff (unzoomed)
      PickupUnzoomed:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupUnzoomedPlaySound")
        TNT1 A 0 A_JumpIfInventory("Cell",2,1)
        stop
        TNT1 A 0 A_TakeInventory("Cell",2)
        goto PickupUnzoomedPlaySound

      // Sound stuff (unzoomed)
      PickupUnzoomedPlaySound:
        TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrRailgunPoweredHeldCheck",1,"PickupUnzoomedPlaySoundHold")
      PickupUnzoomedPlaySoundFire:
        TNT1 A 0 A_PlaySound("doomguy/stronghold/railf2",CHAN_WEAPON)
        goto PickupUnzoomedAttack

      PickupUnzoomedPlaySoundHold:
        TNT1 A 0 A_PlaySound("doomguy/stronghold/railf1",CHAN_WEAPON)
        goto PickupUnzoomedAttack

      // Attack stuff (unzoomed)
      PickupUnzoomedAttack:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupUnzoomedAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupUnzoomedAttackCoop")
      PickupUnzoomedAttackDM:
        TNT1 A 0 A_RailAttack(45,0,false,"00 00 FF","FF FF FF",RGF_SILENT|RGF_FULLBRIGHT,0,"SamsaraDoomguyStrRailgunPuff")
        TNT1 A 0 A_RailAttack(45,0,false,"00 FF 00","FF FF FF",RGF_SILENT|RGF_FULLBRIGHT,0,"SamsaraDoomguyStrRailgunPuff")
        stop

      PickupUnzoomedAttackCoop:
        TNT1 A 0 A_RailAttack(45,0,false,"00 00 FF","FF FF FF",RGF_SILENT|RGF_FULLBRIGHT,0,"SamsaraDoomguyStrRailgunPuffCoop")
        TNT1 A 0 A_RailAttack(45,0,false,"00 FF 00","FF FF FF",RGF_SILENT|RGF_FULLBRIGHT,0,"SamsaraDoomguyStrRailgunPuffCoop")
        stop

      // Zoomed
      // Ammo checking/taking stuff (zoomed)
      PickupZoomed:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupZoomedPlaySound")
        TNT1 A 0 A_JumpIfInventory("Cell",4,1)
        stop
        TNT1 A 0 A_TakeInventory("Cell",4)
        goto PickupZoomedPlaySound

      // Sound stuff (zoomed)
      PickupZoomedPlaySound:
        TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrRailgunPoweredHeldCheck",1,"PickupZoomedPlaySoundHold")
      PickupZoomedPlaySoundFire:
        TNT1 A 0 A_PlaySound("doomguy/stronghold/railf2",CHAN_WEAPON)
        goto PickupZoomedAttack

      PickupZoomedPlaySoundHold:
        TNT1 A 0 A_PlaySound("doomguy/stronghold/railf3",CHAN_WEAPON)
        goto PickupZoomedAttack

      // Attack stuff (zoomed)
      PickupZoomedAttack:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupZoomedAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupZoomedAttackCoop")
      PickupZoomedAttackDM:
        TNT1 AAAA 0 A_RailAttack(30,0,false,"00 FF FF","FF FF FF",RGF_SILENT|RGF_FULLBRIGHT,0,"SamsaraDoomguyStrRailgunPuff") // total damage: 120
        stop

      PickupZoomedAttackCoop:
        TNT1 AAAA 0 A_RailAttack(30,0,false,"00 FF FF","FF FF FF",RGF_SILENT|RGF_FULLBRIGHT,0,"SamsaraDoomguyStrRailgunPuffCoop")
        stop
    }
}

actor SamsaraDoomguyStrRailgunZoomInSoundPlayer : Trigger
{
    States
    {
      // Sound playing stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraAlternateSoundToken",1,"PickupDE")
      PickupStr:
        TNT1 A 0 A_PlaySound("doomguy/stronghold/railzoomin")
        stop

      PickupDE:
        TNT1 A 0 A_PlaySound("doomguy/demoneclipse/railzoomin")
        stop
    }
}

actor SamsaraDoomguyStrRailgunZoomOutSoundPlayer : Trigger
{
    States
    {
      // Sound playing stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraAlternateSoundToken",1,"PickupDE")
      PickupStr:
        TNT1 A 0 A_PlaySound("doomguy/stronghold/railzoomout")
        stop

      PickupDE:
        TNT1 A 0 A_PlaySound("doomguy/demoneclipse/railzoomout")
        stop
    }
}

// Bullet puff (railgun)

actor SamsaraDoomguyStrRailgunPuff : SamsaraDoomguyStrBulletPuff { +PIERCEARMOR }

actor SamsaraDoomguyStrRailgunPuffCoop : SamsaraDoomguyStrRailgunPuff
{
	//+ALLOWTHRUFLAGS // doesn't exist in zandronum yet
	+MTHRUSPECIES
}
