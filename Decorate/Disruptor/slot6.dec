actor SamsaraDisruptorAMCycloneAttackAmount : Counter { Inventory.MaxAmount 5 }

actor " AM Cyclone " : SamsaraDisruptorWeapon
{
    Inventory.PickupMessage "Picked up AM Cyclone."
    Weapon.SlotNumber 6
    Weapon.SlotPriority 0
    Weapon.SelectionOrder 1735
    Weapon.AmmoType "Shell"
    Weapon.AmmoGive 25
    Weapon.AmmoUse 5 // originally took this total amount at once on fire, used here as a requirement to fire
    Obituary "%o ate %k's big fresh mint snowballs."
    Tag "AM Cyclone"
    States
    {
      Spawn:
        SQ72 T -1
        stop

      Ready:
        SQ71 QQQQRRRRSSSSTTTTUUUUVVVV 1 A_WeaponReady
        loop

      Deselect:
        TNT1 A 0 A_Lower
        SQ71 Q 1 A_Lower
        loop

      Select:
        TNT1 A 0 A_Raise
        SQ71 Q 1 A_Raise
        loop

      Fire:
        TNT1 A 0 A_TakeInventory("SamsaraDisruptorAMCycloneAttackAmount") // reset
      FireLoop:
        TNT1 A 0 A_GiveInventory("SamsaraDisruptorAMCycloneAttackAmount")
        TNT1 A 0 A_GiveInventory("SamsaraDisruptorAMCycloneAttackHandler")
        SQ71 W 2 Bright A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        SQ71 X 3 Bright A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        TNT1 A 0 A_JumpIfInventory("SamsaraDisruptorAMCycloneAttackAmount",0,"FireFinish")
        loop

      FireFinish:
        SQ71 YZ 3 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        SQ72 A 3 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        TNT1 A 0 A_CheckReload
        SQ71 Q 3 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        goto Ready
    }
}

actor SamsaraDisruptorAMCycloneAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Shell",1,1)
        stop
        TNT1 A 0 A_TakeInventory("Shell",1)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("Disruptor/Cannon2",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireCustomMissile("DisruptorSnowBall",0,false)
        stop

      PickupAttackCoop:
        TNT1 A 0 A_FireCustomMissile("DisruptorSnowBall_1",0,false)
        stop
    }
}

actor DisruptorSnowBall
{
    Projectile
    Radius 8
    Height 8
    Speed 25
    Damage (random(25,35))
    RenderStyle "Add"
    Scale 0.6
    ActiveSound "Disruptor/SnowTrail"
    DeathSound "Disruptor/SnowBoom"
    Obituary "%o enjoyed %k's fresh mint snow-balls."
    +BLOODLESSIMPACT
    +FORCERADIUSDMG
    +FORCEXYBILLBOARD
    +THRUGHOST
    +WINDTHRUST
    States
    {
      Spawn:
        SQ94 V 0 Bright
        SQ94 V 10 Bright A_LoopActiveSound
      SpawnLoop:
        TNT1 A 0 A_LoopActiveSound
        SQ94 VV 1 Bright A_Weave(2,2,2,1.5)
        loop

      Death:
        TNT1 A 0 A_SetScale(1.2)
        TNT1 A 0 A_Explode(random(32,45),40)
      DeathFinish:
        SQ94 HIJKLMNOPQRS 3 Bright
        SQ94 TU 2 Bright
        stop
    }
}

actor DisruptorSnowBall_1 : DisruptorSnowBall
{
    Speed 35
    Species "Player"
    +THRUSPECIES
    States
    {
      Death:
        TNT1 A 0 A_SetScale(1.2)
        TNT1 A 0 A_Explode(14,44)
        goto DeathFinish
    }
}
