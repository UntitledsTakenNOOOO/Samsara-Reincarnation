Actor DescentCounter : TemporaryCounter
{
	Inventory.MaxAmount 131072 //signed 16 bit
}

Actor DescentPrimaryCounter : Counter { Inventory.MaxAmount 9 }
Actor DescentSecondaryCounter : Counter { Inventory.MaxAmount 9 }
Actor DescentPrimaryTrigger : TemporaryBoolean {}
Actor DescentSecondaryKeySwitcher : TemporaryBoolean {}
Actor DescentSecondaryTrigger : TemporaryBoolean {}
Actor DescentSecondaryFiring : TemporaryBoolean {}
Actor DescentRecoil : DescentCounter {}
Actor DescentThirdPerson : TemporaryBoolean {}
Actor DescentRollToken : TemporaryBoolean {}
Actor DescentWeaponSwitching : TemporaryBoolean {}
Actor DescentBotTrigger : TemporaryBoolean {}

Actor DescentWeapon : Weapon
{
	Weapon.SlotNumber 0
	Weapon.SlotPriority 1
	Inventory.PickupSound "Descent/Item/Pickup"
	Inventory.RestrictedTo "DescentPyroGX"
	Tag "Pyro-GX Weapons"
	+ALT_AMMO_OPTIONAL
	+AMMO_OPTIONAL
	+NOALERT
	States
	{
		Spawn:
			PIST A -1
			stop

		Select:
		SelectLoop:
			TNT1 AAAAAAAAAAAAAAAAAAAA 0 A_Raise
			TNT1 A 1 A_Raise
			loop

		Deselect:
		DeselectLoop:
			TNT1 AAAAAAAAAAAAAAAAAAAA 0 A_Lower
			TNT1 A 1 A_Lower
			loop

		Ready:
			TNT1 A 0 A_JumpIfInventory("DescentSecondaryFiring",1,2)
			TNT1 A 0 A_GunFlash("ReadySecondary",GFF_NOEXTCHANGE)
			TNT1 A 0 A_JumpIfInventory("DescentPrimaryTrigger",1,"FirePrimary")
			TNT1 A 0 A_JumpIfInventory("DescentBotTrigger",1,"ReadyBot")
			TNT1 A 1 A_WeaponReady(WRF_NOFIRE)
			loop

		ReadyBot:
			TNT1 A 1 A_WeaponReady
			goto Ready

		ReadySecondary:
			TNT1 A 1 A_JumpIfInventory("DescentPrimaryTrigger",1,1)
			stop
			TNT1 A 1 A_JumpIfInventory("DescentSecondaryTrigger",1,"FireSecondary")
			stop

		Fire:
			TNT1 A 1 A_JumpIfInventory("DescentBotTrigger",1,"FirePrimary")
			goto Ready

		// Primary
		FirePrimary:
			TNT1 A 0 A_JumpIfInventory("DescentSecondaryTrigger",1,"Fire")
			TNT1 A 0 A_JumpIfInventory("DescentPrimaryCounter",9,"FirePrimaryPhoenix")
			TNT1 A 0 A_JumpIfInventory("DescentPrimaryCounter",8,"FirePrimaryPlasma")
			TNT1 A 0 A_JumpIfInventory("DescentPrimaryCounter",7,"FirePrimaryOmega")
			TNT1 A 0 A_JumpIfInventory("DescentPrimaryCounter",6,"FirePrimaryHelix")
			TNT1 A 0 A_JumpIfInventory("DescentPrimaryCounter",5,"FirePrimaryFusion")
			TNT1 A 0 A_JumpIfInventory("DescentPrimaryCounter",4,"FirePrimarySpreadFire")
			TNT1 A 0 A_JumpIfInventory("DescentPrimaryCounter",3,"FirePrimaryGauss")
			TNT1 A 0 A_JumpIfInventory("DescentPrimaryCounter",2,"FirePrimaryVulcan")
			// 1 redirects to Laser due to being upgraded
		// Laser
		FirePrimaryLaser:
			TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
			goto FirePrimaryLaserStart
			TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FirePrimaryLaserStart")
			TNT1 A 0 A_JumpIfInventory("Clip",1,"FirePrimaryLaserStart")
			goto NoAmmoAlertPrimaryClip

		FirePrimaryLaserStart:
			TNT1 A 0 A_AlertMonsters
			TNT1 A 10 A_GiveInventory("SamsaraDescentLaserAttackHandler")
			goto Ready

		// Vulcan
		FirePrimaryVulcan:
			TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FirePrimaryVulcanStart")
			TNT1 A 0 A_JumpIfInventory("Clip",1,"FirePrimaryVulcanStart")
			goto NoAmmoAlertPrimaryClip

		FirePrimaryVulcanStart:
			TNT1 A 0 A_AlertMonsters
			TNT1 A 0 A_TakeInventory("SamsaraDescentVulcanAttackSilentSound")
			TNT1 A 1 A_GiveInventory("SamsaraDescentVulcanAttackHandler")
			TNT1 A 0 A_GiveInventory("SamsaraDescentVulcanAttackSilentSound")
			TNT1 A 1 A_GiveInventory("SamsaraDescentVulcanAttackHandler")
			goto Ready

		// Gauss
		FirePrimaryGauss:
			TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FirePrimaryGaussStart")
			TNT1 A 0 A_JumpIfInventory("Clip",1,"FirePrimaryGaussStart")
			goto NoAmmoAlertPrimaryClip

		FirePrimaryGaussStart:
			TNT1 A 0 A_AlertMonsters
			TNT1 A 4 A_GiveInventory("SamsaraDescentGaussAttackHandler")
			goto Ready

		// Spreadfire
		FirePrimarySpreadFire:
			TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FirePrimarySpreadFireStart")
			TNT1 A 0 A_JumpIfInventory("Clip",2,"FirePrimarySpreadFireStart")
			goto NoAmmoAlertPrimaryClip

		FirePrimarySpreadFireStart:
			TNT1 A 0 A_AlertMonsters
			TNT1 A 6 A_GiveInventory("SamsaraDescentSpreadFireAttackHandler")
			goto Ready

		// Fusion
		FirePrimaryFusion:
			TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FirePrimaryFusionChargeInit")
			TNT1 A 0 A_JumpIfInventory("Cell",5,"FirePrimaryFusionChargeInit")
			goto NoAmmoAlertPrimaryCell

		FirePrimaryFusionChargeInit:
			TNT1 A 0 A_TakeInventory("SamsaraDescentFusionChargeAmount")
		FirePrimaryFusionCharge:
			TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
			TNT1 A 0 A_JumpIfInventory("Cell",1,1)
			goto FirePrimaryFusionStart
			TNT1 A 7
			TNT1 A 0 A_JumpIfInventory("DescentPrimaryTrigger",1,1)
			goto FirePrimaryFusionStart
			TNT1 A 0 A_GiveInventory("SamsaraDescentFusionChargeGiver")
			loop

		FirePrimaryFusionStart:
			TNT1 A 0 A_AlertMonsters
			TNT1 A 10 A_GiveInventory("SamsaraDescentFusionAttackHandler")
			goto Ready

		// Helix
		FirePrimaryHelix:
			TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FirePrimaryHelixStart")
			TNT1 A 0 A_JumpIfInventory("Clip",4,"FirePrimaryHelixStart")
			goto NoAmmoAlertPrimaryClip

		FirePrimaryHelixStart:
			TNT1 A 0 A_AlertMonsters
			TNT1 A 6 A_GiveInventory("SamsaraDescentHelixAttackHandler")
			goto Ready

		// Omega
		FirePrimaryOmega:
			TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FirePrimaryOmegaStart")
			TNT1 A 0 A_JumpIfInventory("Cell",2,"FirePrimaryOmegaStart")
			goto NoAmmoAlertPrimaryCell

		FirePrimaryOmegaStart:
			TNT1 A 0 A_AlertMonsters
			TNT1 A 4 A_GiveInventory("SamsaraDescentOmegaAttackHandler")
			goto Ready

		// Plasma
		FirePrimaryPlasma:
			TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FirePrimaryPlasmaStart")
			TNT1 A 0 A_JumpIfInventory("Clip",1,"FirePrimaryPlasmaStart")
			goto NoAmmoAlertPrimaryClip

		FirePrimaryPlasmaStart:
			TNT1 A 0 A_AlertMonsters
			TNT1 A 5 A_GiveInventory("SamsaraDescentPlasmaAttackHandler")
			goto Ready

		// Phoenix
		FirePrimaryPhoenix:
			TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FirePrimaryPhoenixStart")
			TNT1 A 0 A_JumpIfInventory("Cell",1,"FirePrimaryPhoenixStart")
			goto NoAmmoAlertPrimaryCell

		FirePrimaryPhoenixStart:
			TNT1 A 0 A_AlertMonsters
			TNT1 A 4 A_GiveInventory("SamsaraDescentPhoenixAttackHandler")
			goto Ready

		// Secondary
		FireSecondary:
			TNT1 A 0 A_GiveInventory("DescentSecondaryFiring")
			TNT1 A 0 A_JumpIfInventory("DescentSecondaryCounter",9,"FireSecondarySmartMine")
			TNT1 A 0 A_JumpIfInventory("DescentSecondaryCounter",8,"FireSecondaryProximity")
			TNT1 A 0 A_JumpIfInventory("DescentSecondaryCounter",7,"FireSecondaryShaker")
			TNT1 A 0 A_JumpIfInventory("DescentSecondaryCounter",6,"FireSecondaryMercury")
			TNT1 A 0 A_JumpIfInventory("DescentSecondaryCounter",5,"FireSecondaryMega")
			TNT1 A 0 A_JumpIfInventory("DescentSecondaryCounter",4,"FireSecondaryGuided")
			TNT1 A 0 A_JumpIfInventory("DescentSecondaryCounter",3,"FireSecondaryHoming")
			TNT1 A 0 A_JumpIfInventory("DescentSecondaryCounter",2,"FireSecondaryConcussion")
			TNT1 A 0 A_JumpIfInventory("DescentSecondaryCounter",1,"FireSecondarySmart")
		// Flash Missiles
		FireSecondaryFlash:
			TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
			goto FireSecondaryFlashStart
			TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireSecondaryFlashStart")
			TNT1 A 0 A_JumpIfInventory("Shell",1,"FireSecondaryFlashStart")
			goto NoAmmoAlertSecondaryShell

		FireSecondaryFlashStart:
			TNT1 A 0 A_AlertMonsters
			TNT1 A 10 A_GiveInventory("SamsaraDescentFlashMissileAttackHandler")
			goto FireSecondaryFinish

		// Smart Missiles
		FireSecondarySmart:
			TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireSecondarySmartStart")
			TNT1 A 0 A_JumpIfInventory("Shell",2,"FireSecondarySmartStart")
			goto NoAmmoAlertSecondaryShell

		FireSecondarySmartStart:
			TNT1 A 0 A_AlertMonsters
			TNT1 A 10 A_GiveInventory("SamsaraDescentSmartMissileAttackHandler")
			goto FireSecondaryFinish

		// Concussion Missiles
		FireSecondaryConcussion:
			TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireSecondaryConcussionStart")
			TNT1 A 0 A_JumpIfInventory("Shell",1,"FireSecondaryConcussionStart")
			goto NoAmmoAlertSecondaryShell

		FireSecondaryConcussionStart:
			TNT1 A 0 A_AlertMonsters
			TNT1 A 10 A_GiveInventory("SamsaraDescentConcussionMissileAttackHandler")
			goto FireSecondaryFinish

		// Homing Missiles
		FireSecondaryHoming:
			TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireSecondaryHomingStart")
			TNT1 A 0 A_JumpIfInventory("Shell",1,"FireSecondaryHomingStart")
			goto NoAmmoAlertSecondaryShell

		FireSecondaryHomingStart:
			TNT1 A 0 A_AlertMonsters
			TNT1 A 10 A_GiveInventory("SamsaraDescentHomingMissileAttackHandler")
			goto FireSecondaryFinish

		// Guided Missiles
		FireSecondaryGuided:
			TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireSecondaryGuidedStart")
			TNT1 A 0 A_JumpIfInventory("Shell",1,"FireSecondaryGuidedStart")
			goto NoAmmoAlertSecondaryShell

		FireSecondaryGuidedStart:
			TNT1 A 0 A_AlertMonsters
			TNT1 A 10 A_GiveInventory("SamsaraDescentGuidedMissileAttackHandler")
			goto FireSecondaryFinish

		// Mega Missiles
		FireSecondaryMega:
			TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireSecondaryMegaStart")
			TNT1 A 0 A_JumpIfInventory("RocketAmmo",1,"FireSecondaryMegaStart")
			goto NoAmmoAlertSecondaryRocketAmmo

		FireSecondaryMegaStart:
			TNT1 A 0 A_AlertMonsters
			TNT1 A 30 A_GiveInventory("SamsaraDescentMegaMissileAttackHandler")
			goto FireSecondaryFinish

		// Mercury Missiles
		FireSecondaryMercury:
			TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireSecondaryMercuryStart")
			TNT1 A 0 A_JumpIfInventory("Shell",2,"FireSecondaryMercuryStart")
			goto NoAmmoAlertSecondaryShell

		FireSecondaryMercuryStart:
			TNT1 A 0 A_AlertMonsters
			TNT1 A 10 A_GiveInventory("SamsaraDescentMercuryMissileAttackHandler")
			goto FireSecondaryFinish

		// Earthshaker Missiles
		FireSecondaryShaker:
			TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireSecondaryShakerStart")
			TNT1 A 0 A_JumpIfInventory("RocketAmmo",5,"FireSecondaryShakerStart")
			goto NoAmmoAlertSecondaryRocketAmmo

		FireSecondaryShakerStart:
			TNT1 A 0 A_AlertMonsters
			TNT1 A 30 A_GiveInventory("SamsaraDescentShakerMissileAttackHandler")
			goto FireSecondaryFinish

		// Proximity Bombs
		FireSecondaryProximity:
			TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireSecondaryProximityStart")
			TNT1 A 0 A_JumpIfInventory("Shell",1,"FireSecondaryProximityStart")
			goto NoAmmoAlertSecondaryShell

		FireSecondaryProximityStart:
			TNT1 A 30 A_GiveInventory("SamsaraDescentProximityBombAttackHandler")
			goto FireSecondaryFinish

		// Smart Mines
		FireSecondarySmartMine:
			TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireSecondarySmartMineStart")
			TNT1 A 0 A_JumpIfInventory("Shell",1,"FireSecondarySmartMineStart")
			goto NoAmmoAlertSecondaryShell

		FireSecondarySmartMineStart:
			TNT1 A 30 A_GiveInventory("SamsaraDescentSmartMIneAttackHandler")
			goto FireSecondaryFinish

		// Secondary finish sequence
		FireSecondaryFinish:
			TNT1 A 0 A_TakeInventory("DescentSecondaryFiring")
			stop

		// "No ammo" alerts
		NoAmmoAlertPrimaryClip:
			TNT1 A 0 A_Print("$PYROGX_NOCLIP") // ha ha
			goto NoAmmoAlertPrimary

		NoAmmoAlertPrimaryCell:
			TNT1 A 0 A_Print("$PYROGX_NOCELL")
			goto NoAmmoAlertPrimary

		NoAmmoAlertPrimary:
			TNT1 A 10 A_PlaySound("Descent/NoAmmo")
			goto Ready

		NoAmmoAlertSecondaryShell:
			TNT1 A 0 A_Print("$PYROGX_NOSHELL")
			goto NoAmmoAlertSecondary

		NoAmmoAlertSecondaryRocketAmmo:
			TNT1 A 0 A_Print("$PYROGX_NOROCKETAMMO")
			goto NoAmmoAlertSecondary

		NoAmmoAlertSecondary:
			TNT1 A 10 A_PlaySound("Descent/NoAmmo")
			goto FireSecondaryFinish

		// "No target" attack prevention
		/*NoTargetPrimary:
			TNT1 A 4
			goto Ready*/
	}
}

Actor DescentWeaponPrimer : Weapon
{
	Inventory.PickupSound "Descent/Item/Pickup"
	Tag "Pyro-GX Weapons"
	+AMMO_OPTIONAL
	+CHEATNOTWEAPON
	+NOALERT
	States
	{
		Spawn:
			PIST A -1
			stop

		Select:
		SelectLoop:
			TNT1 AAAAAAAAAAAAAAAAAAAA 0 A_Raise
			TNT1 A 1 A_Raise
			loop

		Deselect:
		DeselectLoop:
			TNT1 AAAAAAAAAAAAAAAAAAAA 0 A_Lower
			TNT1 A 1 A_Lower
			loop

		Ready:
			TNT1 A 10 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			TNT1 A 0 A_TakeInventory("DescentSecondaryFiring")
			TNT1 A 0 A_JumpIfInventory("DescentSecondaryKeySwitcher",1,"SetSecondary")
			TNT1 A 0 A_JumpIfInventory("DescentSecondaryTrigger",1,"SetSecondary")
			TNT1 A 0 A_Jump(256,"SetPrimary")
			goto Deselect

		SetPrimary:
			TNT1 A 3 A_SelectWeapon("DescentWeapon")
			goto Deselect

		SetSecondary:
			TNT1 A 3 A_SelectWeapon("DescentWeapon")
			goto Deselect

		Fire:
			TNT1 A 1
			goto Ready
	}
}

Actor DescentWeaponPadUp : Weapon
{
	Weapon.SlotNumber 0
	Weapon.SlotPriority 2
	Inventory.RestrictedTo "DescentPyroGX"
	Tag "Pyro-GX Weapons"
	+NOALERT
	States
	{
		Select:
		SelectLoop:
			TNT1 AAAAAAAAAAAAAAAAAAAA 0 A_Raise
			TNT1 A 1 A_Raise
			loop

		Deselect:
		DeselectLoop:
			TNT1 AAAAAAAAAAAAAAAAAAAA 0 A_Lower
			TNT1 A 1 A_Lower
			loop

		Ready:
			TNT1 A 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			TNT1 A 0 A_TakeInventory("DescentSecondaryFiring")
			TNT1 A 0 A_JumpIfInventory("DescentSecondaryTrigger",1,"SetSecondary")
			TNT1 A 0 A_Jump(256,"SetPrimary")
			goto Deselect

		SetPrimary:
			TNT1 A 1 ACS_NamedExecuteWithResult("DescentSelectWeapon", 0, 0)
			goto Deselect

		SetSecondary:
			TNT1 A 1 ACS_NamedExecuteWithResult("DescentSelectWeapon", 1, 0)
			goto Deselect

		Fire:
			TNT1 A 1
			goto Ready
	}
}

Actor DescentWeaponPadDown : Weapon
{
	Weapon.SlotNumber 0
	Weapon.SlotPriority 0
	Inventory.RestrictedTo "DescentPyroGX"
	Tag "Pyro-GX Weapons"
	+NOALERT
	States
	{
		Select:
		SelectLoop:
			TNT1 AAAAAAAAAAAAAAAAAAAA 0 A_Raise
			TNT1 A 1 A_Raise
			loop

		Deselect:
		DeselectLoop:
			TNT1 AAAAAAAAAAAAAAAAAAAA 0 A_Lower
			TNT1 A 1 A_Lower
			loop

		Ready:
			TNT1 A 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			TNT1 A 0 A_TakeInventory("DescentSecondaryFiring")
			TNT1 A 0 A_JumpIfInventory("DescentSecondaryTrigger",1,"SetSecondary")
			TNT1 A 0 A_Jump(256,"SetPrimary")
			goto Deselect

		SetPrimary:
			TNT1 A 1 ACS_NamedExecuteWithResult("DescentSelectWeapon", 0, 1)
			goto Deselect

		SetSecondary:
			TNT1 A 1 ACS_NamedExecuteWithResult("DescentSelectWeapon", 1, 1)
			goto Deselect

		Fire:
			TNT1 A 1
			goto Ready
	}
}

Actor DescentLaserProjectile
{
	Damage (5)
	Speed 20
	Projectile
	RenderStyle Add
	DamageType "DescentLaser"
	+FORCEXYBILLBOARD
	+BRIGHT
	States
	{
		Spawn:
			DS00 A -1
			stop

		Death:
			TNT1 A 0 A_PlaySound("Descent/Laser/Explode",CHAN_VOICE,0.8)
			TNT1 A 1 A_SpawnItemEx("DescentVulcanPuff",0,0,0,0,0,0,0,SXF_CLIENTSIDE)
			stop
	}
}

Actor DescentPrimarySwitcher : Boolean {}
Actor DescentMissileCamera : DescentCounter { Inventory.MaxAmount 256 }
Actor DescentMissileSwitcher : Boolean {}

Actor DescentMissile
{
	Damage (0)
	Speed 20
	Radius 4
	Height 8
	Projectile
	+FORCEXYBILLBOARD
	+FORCERADIUSDMG
	+SKYEXPLODE
	+BRIGHT
	States
	{
		Spawn:
			DS00 A 0
			DS00 A 0 A_GiveToTarget("DescentMissileCamera",1)
			DS00 A 0 ACS_NamedExecuteWithResult("DescentMissileCamera")
		SpawnLoop:
			DS00 A -1
			stop

		Death:
			TNT1 A 0 A_RemoveMaster
			TNT1 A 0 A_Explode(50,50,0)
			TNT1 A 0 A_GiveToTarget("DescentMissileStaticGiver")
			TNT1 A 0 A_TakeFromTarget("DescentMissileCamera",1)
			TNT1 A 0 A_PlaySound("Descent/Laser/Explode",CHAN_VOICE)
			TNT1 A 1 A_SpawnItemEx("DescentExplosion",0,0,0,0,0,0,0,SXF_CLIENTSIDE)
			stop
	}
}

Actor DescentPuff
{
	+NOINTERACTION
	+NOGRAVITY
	+FORCEXYBILLBOARD
	+PUFFONACTORS
	+BRIGHT
}

Actor DescentHelixPuff : DescentPuff
{
	States
	{
		Spawn:
			DS09 ABCDE 2
			Stop
	}
}

Actor DescentOmegaPuff : DescentPuff
{
	States
	{
		Spawn:
			DS11 ABCDE 2
			Stop
	}
}

Actor DescentPlasmaPuff : DescentPuff
{
	States
	{
		Spawn:
			DS13 ABCDE 2
			Stop
	}
}

Actor DescentLaser2Puff : DescentPuff
{
	States
	{
		Spawn:
			DS59 ABCDE 2
			Stop
	}
}

Actor DescentLaser3Puff : DescentPuff
{
	States
	{
		Spawn:
			DS60 ABCDE 2
			Stop
	}
}

Actor DescentSmallExplosion
{
	+NOINTERACTION
	+NOGRAVITY
	+FORCEXYBILLBOARD
	+BRIGHT
	Scale 0.5
	States
	{
		Spawn:
			DS18 A 0
			DS18 A 2 A_PlaySound("Descent/Laser/Explode",CHAN_VOICE)
			DS18 BCDEFGH 2
			Stop
	}
}

Actor DescentExplosion
{
	+NOINTERACTION
	+NOGRAVITY
	+FORCEXYBILLBOARD
	+BRIGHT
	States
	{
		Spawn:
			DS02 ABCDEFGHIJKL 2
			Stop
	}
}

Actor DescentMegaExplosion : DescentExplosion
{
	Scale 2.0
	States
	{
		Spawn:
			DS04 ABCDEFGHIJKLM 2
			Stop
	}
}

Actor DescentShakerExplosion : DescentMegaExplosion
{
	States
	{
		Spawn:
			DS12 ABCDEFGHIJKLM 2
			Stop
	}
}

Actor DescentBigExplosion1 : DescentExplosion
{
	States
	{
		Spawn:
			DS61 A 0
			DS61 A 2 A_PlaySound("Descent/Laser/Explode",CHAN_VOICE)
			DS61 BCDEFGHIJKLMNOPQ 2
			Stop
	}
}

Actor DescentBigExplosion2 : DescentBigExplosion1
{
	States
	{
		Spawn:
			DS62 A 0
			DS62 A 2 A_PlaySound("Descent/Laser/Explode",CHAN_VOICE)
			DS62 BCDEFGHIJKLMNOPQ 2
			Stop
	}
}

Actor DescentBigExplosion3 : DescentBigExplosion1
{
	States
	{
		Spawn:
			DS63 A 0
			DS63 A 2 A_PlaySound("Descent/Laser/Explode",CHAN_VOICE)
			DS63 BCDEFGHIJKLMNOPQ 2
			Stop
	}
}

Actor DescentBigExplosion4 : DescentBigExplosion1
{
	States
	{
		Spawn:
			DS64 A 0
			DS64 A 2 A_PlaySound("Descent/Laser/Explode",CHAN_VOICE)
			DS64 BCDEFGHIJK 2
			Stop
	}
}

Actor DescentMissileStaticGiver : Trigger
{
	States
	{
		Pickup:
			TNT1 A 0 ACS_NamedExecuteWithResult("DescentStaticHUD")
			stop
	}
}

Actor DescentMissileStaticToken : TemporaryBoolean {}

Actor DescentDeathCamera
{
	+NOGRAVITY
	-COUNTKILL
	States
	{
		Spawn:
			TNT1 A 1 A_FaceTarget(0,0)
			Loop
	}
}

Actor DescentBackpackPower : BackpackItem {}

Actor DescentLaserUpgrade : CustomInventory
{
	Inventory.PickupSound "Descent/Item/Pickup"
	+FLOATBOB
	States
	{
		Spawn:
			DS53 ABCDEFGHIJKLMNO 2
			loop

		Pickup:
			TNT1 A 0 A_JumpIfInventory("DescentLaserLevel",4,"UpgradeMax")
			TNT1 A 0 A_JumpIfInventory("DescentLaserLevel",3,2)
			TNT1 A 0 A_GiveInventory("DescentLaserLevel")
			TNT1 A 0 A_GiveInventory("DescentBackpackPower")
			stop

		UpgradeMax:
			TNT1 A 0 A_GiveInventory("DescentLaserLevel")
			stop
	}
}

Actor DescentSuperLaserUpgrade : CustomInventory
{
	Inventory.PickupSound "Descent/Item/Pickup"
	-INVBAR
	States
	{
		Spawn:
			DS60 ABCDEFGHIJKLMNO 2
			loop

		Pickup:
			TNT1 A 0 A_GiveInventory("DescentSuperLaser")
			TNT1 A 0 A_GiveInventory("DescentLaserLevel",5)
			TNT1 A 0 A_JumpIfInventory("DescentPrimaryCounter",1,1)
			goto SwapLaser
			TNT1 A 0
			stop

		SwapLaser:
			TNT1 A 0 A_GiveInventory("DescentPrimaryCounter")
			stop

		Use:
			TNT1 A 0 A_RailWait
			fail
	}
}

Actor DescentGravityGiver : Trigger
{
	States
	{
		Pickup:
			TNT1 A 0 A_ChangeFlag("NOGRAVITY",false)
			stop
	}
}

Actor DescentGravityTaker : Trigger
{
	States
	{
		Pickup:
			TNT1 A 0 A_ChangeFlag("NOGRAVITY",true)
			stop
	}
}
