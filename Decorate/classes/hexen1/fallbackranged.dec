// Parias (Reincarnation)

actor "Jade Wand" : SamsaraHexenWeapon
{
    Inventory.PickupMessage "Jade Wand"
    Weapon.SlotNumber 1
    Weapon.SelectionOrder 3600
    Weapon.Kickback 0
    Weapon.YAdjust 9
    Tag "Jade Wand"
    +WIMPY_WEAPON
    States
    {
      Ready:
        H116 A 1 A_WeaponReady
        loop

      Deselect:
        H116 A 1 A_Lower
        loop

      Select:
        H116 A 1 A_Raise
        loop

      Fire:
        H116 A 2
        H116 B 6 Bright Offset(0,48) A_GiveInventory("SamsaraHexenJadeWandAttackHandler")
        H116 A 5 Offset(0,40)
        TNT1 A 0 Offset(0,36) A_ReFire
        goto Ready
    }
}

actor SamsaraHexenJadeWandAttackHandler : Trigger
{
    States
    {
      // Attack stuff
      Pickup:
        TNT1 A 0 A_PlaySound("hexen/jadefire",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupCoop")
      PickupDM:
        TNT1 A 0 A_FireBullets(4,4,4,3,"JadePuff",FBF_NOFLASH)
        stop

      PickupCoop:
        TNT1 A 0 A_FireBullets(4,4,4,3,"JadePuffCoop",FBF_NOFLASH)
        stop
    }
}

actor JadePuff
{
    DamageType "Electric"
    RenderStyle "Add"
    Alpha 0.5
    Scale 0.5
    +NOBLOCKMAP
    +NOGRAVITY
    +PAINLESS
    +PUFFONACTORS
    States
    {
      Spawn:
        H116 IJKLMNO 2 Bright
        stop
    }
}

actor JadePuffCoop : JadePuff { +MTHRUSPECIES }

// Parias (Classic)/Daedolon

actor SamsaraHexenHasLMSPistolDisabled : Boolean {} // hack

actor "Sapphire Wand" : SamsaraHexenWeapon
{
    Inventory.PickupMessage "Sapphire Wand"
    Weapon.SlotNumber 1
    Weapon.SelectionOrder 3600
    Weapon.Kickback 0
    Weapon.YAdjust 9
    Tag "Sapphire Wand"
    +NOALERT
    +WIMPY_WEAPON
    States
    {
      Ready:
        MWND A 1 A_WeaponReady
        loop

      Deselect:
        MWND A 1 A_Lower
        loop

      Select:
        MWND A 1 A_Raise
        loop

      Fire:
        TNT1 A 0 A_JumpIfInventory("HexenClassMode",2,2) // no fallback melee for mage, so don't alert enemies
        TNT1 A 0 A_AlertMonsters
        TNT1 A 0 A_JumpIfInventory("SamsaraUsingSmoothAnims",1,"FireSmooth")
      FireVanilla:
        MWND A 6
        MWND B 6 Bright Offset(0,48) A_GiveInventory("SamsaraHexenSapphireWandAttackHandler")
        MWND A 3 Offset(0,40)
        MWND A 3 Offset(0,36) A_ReFire
        goto Ready

      FireSmooth:
        MWND AJ 2
        MWND K 2 Bright
        MWND B 4 Bright Offset(0,48) A_GiveInventory("SamsaraHexenSapphireWandAttackHandler")
        MWND J 3 Offset(0,40)
        MWND A 2 Offset(0,36)
        MWND A 3 Offset(0,32) A_ReFire
        goto Ready
    }
}

actor SamsaraHexenSapphireWandAttackHandler : Trigger
{
    States
    {
      // Attack stuff
      Pickup:
        TNT1 A 0 A_PlaySound("MageWandFire",CHAN_WEAPON)
        TNT1 A 0 A_FireBullets(0,0,1,0,"SamsaraBlankPuff",FBF_NORANDOM|FBF_NOFLASH)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupCoop")
      PickupDM:
        TNT1 A 0 A_FireCustomMissile("MageWandMissile2",0,false)
        stop

      PickupCoop:
        TNT1 A 0 A_FireCustomMissile("MageWandMissile3",0,false)
        stop
    }
}

actor MageWandMissile2 : MageWandMissile
{
    Radius 3
    Height 2
    Damage (random(1,4)*2)
    DamageType "Pistol"
    SeeSound "" // currently handled in attack handler due to +SPAWNSOUNDSOURCE not working properly in netplay
    Decal "RailScorchLower"
    Obituary "$SAMSARA_HEXEN_OB_FALLBACKRANGED"
    +BLOODSPLATTER
    +NOBOSSRIP
    -SPAWNSOUNDSOURCE
}

actor MageWandMissile3 : MageWandMissile2
{
    Species "Player"
    +THRUSPECIES
}

// Baratus

actor "Impaler Crossbow" : SamsaraHexenWeapon
{
    Inventory.PickupMessage "Impaler Crossbow"
    Weapon.SlotNumber 1
    Weapon.SelectionOrder 3600
    Weapon.Kickback 0
    Weapon.YAdjust 9
    Tag "Impaler Crossbow"
    +NOALERT
    +WIMPY_WEAPON
    States
    {
      Spawn:
        H100 G -1
        stop

      Ready:
        H100 EF 5
      ReadyLoop:
        H100 A 1 A_WeaponReady
        loop

      Deselect:
        H100 E 1 A_Lower
        loop

      Select:
        H100 E 1 A_Raise
        loop

      Fire:
        TNT1 A 0 A_JumpIfCloser(64,"FireMelee")
        goto FireRanged

      FireMelee:
        H100 A 2 Offset(0,24)
        H100 A 2 Offset(0,14)
        H100 A 2 Offset(0,12) A_GiveInventory("SamsaraHexenImpalerCrossbowMeleeAttackHandler")
        H100 A 2 Offset(0,14)
        H100 A 2 Offset(0,24)
        H100 A 10 Offset(0,32)
        goto ReadyLoop

      FireRanged:
        TNT1 A 0 A_AlertMonsters
        H100 B 6 A_GiveInventory("SamsaraHexenImpalerCrossbowRangedAttackHandler")
        H100 C 8 A_PlaySound("Hexen/Weapon/XBowLoad",CHAN_WEAPON)
        H100 DEF 5
        H100 A 12
        goto ReadyLoop
    }
}

// Melee

actor SamsaraHexenImpalerCrossbowMeleeAttackHandler : Trigger
{
    States
    {
      // Attack stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupCoop")
      PickupDM:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasSpread",1,"PickupDMSpread")
        TNT1 A 0 A_CustomPunch(random(10,30),true,0,"HexenImpalerPuff")
        stop

      PickupDMSpread:
        TNT1 A 0 A_CustomPunch(random(10,30)*3,true,0,"HexenImpalerPuff")
        stop

      PickupCoop:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasSpread",1,"PickupCoopSpread")
        TNT1 A 0 A_CustomPunch(random(10,30),true,0,"HexenImpalerPuffCoop")
        stop

      PickupCoopSpread:
        TNT1 A 0 A_CustomPunch(random(10,30)*3,true,0,"HexenImpalerPuffCoop")
        stop
    }
}

actor HexenImpalerPuff : BulletPuff
{
    Radius 4
    Height 8
    VSpeed 0
    Mass 100
    RenderStyle "Normal"
    ActiveSound "FighterPunchMiss"
    -ALLOWPARTICLES
    +NOINTERACTION
    +PUFFONACTORS
    -RANDOMIZE
    States
    {
      Spawn:
        H100 IJKLMN 3
        stop

      Melee:
      Crash:
        TNT1 A 0 A_PlaySound("Hexen/Weapon/XBowHit")
        goto Spawn

      XDeath:
        TNT1 A 1 A_PlaySound("Hexen/Weapon/XBowSplatter")
        stop
    }
}

actor HexenImpalerPuffCoop : HexenImpalerPuff { +MTHRUSPECIES }

// Ranged

actor SamsaraHexenImpalerCrossbowRangedAttackHandler : Trigger
{
    States
    {
      // Attack stuff
      Pickup:
        TNT1 A 0 A_FireBullets(0,0,1,0,"SamsaraBlankPuff",FBF_NORANDOM|FBF_NOFLASH)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupCoop")
      PickupDM:
        TNT1 A 0 A_FireCustomMissile("HexenImpalerMissile",0,false)
        goto PickupFinish

      PickupCoop:
        TNT1 A 0 A_FireCustomMissile("HexenImpalerMissileCoop",0,false)
        goto PickupFinish

      // Finishing stuff
      PickupFinish:
        TNT1 A 0 A_Recoil(1)
        stop
    }
}

actor HexenImpalerMissile : FastProjectile
{
    Radius 4
    Height 8
    Speed 80
    Damage (random(1,4)*10)
    SeeSound "Hexen/Weapon/XBowFire"
    +BLOODSPLATTER
    States
    {
      Spawn:
        H100 H -1
        stop

      Death:
      Crash:
        H100 I 3 A_PlaySound("Hexen/Weapon/XBowHit",CHAN_VOICE)
        H100 JKLMN 3
        stop

      XDeath:
        TNT1 A 1 A_PlaySound("Hexen/Weapon/XBowSplatter",CHAN_VOICE)
        stop
    }
}

actor HexenImpalerMissileCoop : HexenImpalerMissile
{
    Species "Player"
    +THRUSPECIES
}
