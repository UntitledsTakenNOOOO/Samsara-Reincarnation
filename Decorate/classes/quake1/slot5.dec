// Normal rockets

actor "  Rocket Launcher  " : SamsaraQuakeWeapon
{
    Inventory.PickupMessage "You got the Rocket Launcher."
    Weapon.SlotPriority 0
    Weapon.SelectionOrder 2
    Weapon.AmmoType "RocketAmmo"
    Weapon.AmmoGive 5
    Weapon.AmmoUse 1
    Tag "Rocket Launcher (Normal Rockets)"
    +EXPLOSIVE
    +NOAUTOFIRE
    States
    {
      Spawn:
        QRKL Z -1
        stop

      Ready:
        QRKL A 0 A_JumpIfInventory("  Rocket Launcher DOE  ",1,2)
        QRKL A 1 A_WeaponReady(WRF_NOSECONDARY)
        loop
        QRKL A 1 A_WeaponReady
        loop

      Select:
        TNT1 A 0 A_TakeInventory("Ranger_AxeSelected")
        TNT1 A 0 A_TakeInventory("Ranger_GunSelected")
        TNT1 A 0 A_GiveInventory("Ranger_ExplosiveSelected")
        TNT1 A 0 A_TakeInventory("Ranger_NailSelected")
        TNT1 A 0 A_TakeInventory("Ranger_PlasmaSelected")
        goto Super::Select

      // 27 tics
      Fire:
        QRKL B 0 A_GiveInventory("SamsaraQuakeWeaponAlert")
        QRKL B 0 A_GiveInventory("SamsaraQuakeRocketLauncherNormalAttackHandler")
        QRKL BC 2 Bright A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        QRKL D 3 Bright A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        QRKL E 4 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        QRKL FG 5 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        QRKL H 6 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        QRKL A 0 A_ReFire
        goto Ready

	  AltFire:
        QRKL A 8 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        TNT1 A 0 A_SelectWeapon("  Rocket Launcher DOE  ")
        goto Deselect
    }
}

actor SamsaraQuakeRocketLauncherNormalAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupPlaySound")
        TNT1 A 0 A_JumpIfInventory("RocketAmmo",1,1)
        stop
        TNT1 A 0 A_TakeInventory("RocketAmmo",1)
        goto PickupPlaySound

      // Sound stuff
      PickupPlaySound:
        TNT1 A 0 A_JumpIfInventory("SamsaraAlternateSoundToken",1,"PickupPlaySoundRerelease")
      // Quake I (Original) sounds
      PickupPlaySoundOriginal:
        TNT1 A 0 A_PlaySound("quakeweps/rlfire",CHAN_WEAPON)
        goto PickupAttack

      // Quake I (Rerelease) sounds
      PickupPlaySoundRerelease:
        TNT1 A 0 A_PlaySound("quakeweps/rerelease/rlfire",CHAN_WEAPON)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_JumpIfInventory("QuadDamagePower",1,"PickupAttackQuad")
      // Normal
      PickupAttackNormal:
        TNT1 A 0 A_JumpIfInventory("SamsaraAlternateSoundToken",1,"PickupAttackNormalRerelease")
      // Quake I (Original) sounds
      PickupAttackNormalOriginal:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackNormalOriginalCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackNormalOriginalCoop")
      PickupAttackNormalOriginalDM:
        TNT1 A 0 A_FireCustomMissile("QuakeRocket",0,false)
        goto PickupFinish

      PickupAttackNormalOriginalCoop:
        TNT1 A 0 A_FireCustomMissile("QuakeRocketCoop",0,false)
        goto PickupFinish

      // Quake I (Rerelease) sounds
      PickupAttackNormalRerelease:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackNormalRereleaseCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackNormalRereleaseCoop")
      PickupAttackNormalRereleaseDM:
        TNT1 A 0 A_FireCustomMissile("QuakeRocketSoundRerelease",0,false)
        goto PickupFinish

      PickupAttackNormalRereleaseCoop:
        TNT1 A 0 A_FireCustomMissile("QuakeRocketCoopSoundRerelease",0,false)
        goto PickupFinish

      // Quad
      PickupAttackQuad:
        TNT1 A 0 A_JumpIfInventory("SamsaraAlternateSoundToken",1,"PickupAttackQuadRerelease")
      // Quake I (Original) sounds
      PickupAttackQuadOriginal:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackQuadOriginalCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackQuadOriginalCoop")
      PickupAttackQuadOriginalDM:
        TNT1 A 0 A_FireCustomMissile("QuakeRocketQuad",0,false)
        goto PickupFinish

      PickupAttackQuadOriginalCoop:
        TNT1 A 0 A_FireCustomMissile("QuakeRocketCoopQuad",0,false)
        goto PickupFinish

      // Quake I (Rerelease) sounds
      PickupAttackQuadRerelease:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackQuadRereleaseCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackQuadRereleaseCoop")
      PickupAttackQuadRereleaseDM:
        TNT1 A 0 A_FireCustomMissile("QuakeRocketQuadSoundRerelease",0,false)
        goto PickupFinish

      PickupAttackQuadRereleaseCoop:
        TNT1 A 0 A_FireCustomMissile("QuakeRocketCoopQuadSoundRerelease",0,false)
        goto PickupFinish

      // Finishing stuff
      PickupFinish:
        TNT1 A 0 ACS_NamedExecuteAlways("SamsaraRecoil", 0, 5, 4, 15)
        stop
    }
}

actor QuakeRocket
{
    Radius 2
    Height 2
    Speed 35 //50
    DamageType "QuakeExplosive"
    Projectile
    +RANDOMIZE
    +DEHEXPLOSION
    //+ROCKETTRAIL
    +EXPLODEONWATER
    Obituary "$SAMSARA_RANGER_OB_SLOT5_1"
    //SelfObituary "%o becomes bored with life." // had randomized obituary in SOA, use this one for differentiation
    States
    {
      Spawn:
        QRKT A 0
		QRKT A 0 A_Jump(32,2)
		QRKT A 0 A_ChangeFlag(EXTREMEDEATH, true)
      SpawnLoop:
		QRKT AAAA 0 A_SpawnItemEx(QuakeSmokeTrail2,Random2(5),0,Random2(5),0,0,FRandom(0.1,0.5),Random(1,360),0,128)
        QRKT A 1 A_SpawnItemEx(QuakeSmokeTrail2,Random2(5),0,Random2(5),0,0,FRandom(0.1,0.5),Random(1,360),0,128)
        loop
        
      Death:
		TNT1 A 0 A_Explode(64,128,0)
        TNT1 A 0 A_ChangeFlag("FORCERADIUSDMG",1)
        TNT1 A 0 A_Explode(64,128)
        TNT1 A 1 A_SpawnItemEx("QuakeExplosion")
        stop
    }
}

actor QuakeRocketQuad: QuakeRocket
{
    Obituary "$SAMSARA_RANGER_OB_SLOT5_3"
    DamageType "NotQuadded"
    States
    {
      Death:
        TNT1 A 0 A_Explode(256,128,0)
        TNT1 A 0 A_ChangeFlag("FORCERADIUSDMG",1)
        TNT1 A 0 A_Explode(256,128)
        TNT1 A 1 A_SpawnItemEx("QuakeExplosion")
        stop
    }
}

actor QuakeRocketCoop: QuakeRocket
{
    +THRUSPECIES
    Species "Player"
	Damage (30) // [ijon] used to be 25, but demons were surviving with *one health*
}

actor QuakeRocketCoopQuad: QuakeRocketQuad
{
    +THRUSPECIES
    Species "Player"
	Damage (120)
}

actor QuakeRocketSoundRerelease : QuakeRocket
{
    States
    {
      Death:
		TNT1 A 0 A_Explode(64,128,0)
        TNT1 A 0 A_ChangeFlag("FORCERADIUSDMG",1)
        TNT1 A 0 A_Explode(64,128)
        TNT1 A 1 A_SpawnItemEx("QuakeExplosionSoundRerelease")
        stop
    }
}

actor QuakeRocketQuadSoundRerelease : QuakeRocketSoundRerelease
{
    Obituary "$SAMSARA_RANGER_OB_SLOT5_3"
    DamageType "NotQuadded"
    States
    {
      Death:
        TNT1 A 0 A_Explode(256,128,0)
        TNT1 A 0 A_ChangeFlag("FORCERADIUSDMG",1)
        TNT1 A 0 A_Explode(256,128)
        TNT1 A 1 A_SpawnItemEx("QuakeExplosionSoundRerelease")
        stop
    }
}

actor QuakeRocketCoopSoundRerelease : QuakeRocketSoundRerelease
{
    +THRUSPECIES
    Species "Player"
	Damage (30) // [ijon] used to be 25, but demons were surviving with *one health*
}

actor QuakeRocketCoopQuadSoundRerelease : QuakeRocketQuadSoundRerelease
{
    +THRUSPECIES
    Species "Player"
	Damage (120)
}

// Multi rockets

actor "  Rocket Launcher DOE  " : "  Rocket Launcher  "
{
    Weapon.SlotPriority 1
    Weapon.AmmoType "MultiRocketAmmo"
    Weapon.AmmoGive 0
    Tag "Rocket Launcher (Multi Rockets)"
    States
    {
      Ready:
        QMRL A 0 A_JumpIfInventory("  Rocket Launcher  ",1,2)
        QMRL A 1 A_WeaponReady(WRF_NOSECONDARY)
        loop
        QMRL A 1 A_WeaponReady
        loop

      // 27 tics
      Fire:
        QMRL B 0 A_GiveInventory("SamsaraQuakeWeaponAlert")
        QMRL B 0 A_GiveInventory("SamsaraQuakeRocketLauncherMultiAttackHandler")
        QMRL BC 2 Bright A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        QMRL D 3 Bright A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        QMRL E 4 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        QMRL FG 5 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        QMRL H 6 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        QMRL A 0 A_ReFire
        goto Ready

	  AltFire:
        QMRL A 8 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        TNT1 A 0 A_SelectWeapon("  Rocket Launcher  ")
        goto Deselect
    }
}

actor SamsaraQuakeRocketLauncherMultiAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupPlaySound")
        TNT1 A 0 A_JumpIfInventory("MultiRocketAmmo",1,1)
        stop
        TNT1 A 0 A_TakeInventory("MultiRocketAmmo",1)
        goto PickupPlaySound

      // Sound stuff
      PickupPlaySound:
        TNT1 A 0 A_JumpIfInventory("SamsaraAlternateSoundToken",1,"PickupPlaySoundRerelease")
      // Quake I (Original) sounds
      PickupPlaySoundOriginal:
        TNT1 A 0 A_PlaySound("quakeweps/rlfire",CHAN_WEAPON)
        goto PickupAttack

      // Quake I (Rerelease) sounds
      PickupPlaySoundRerelease:
        TNT1 A 0 A_PlaySound("quakeweps/rerelease/rlfire",CHAN_WEAPON)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_JumpIfInventory("QuadDamagePower",1,"PickupAttackQuad")
      // Normal
      PickupAttackNormal:
        TNT1 A 0 A_JumpIfInventory("SamsaraAlternateSoundToken",1,"PickupAttackNormalRerelease")
      // Quake I (Original) sounds
      PickupAttackNormalOriginal:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackNormalOriginalCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackNormalOriginalCoop")
      PickupAttackNormalOriginalDM:
        TNT1 A 0 A_FireCustomMissile("QuakeClusterRocket",-4,false)
        TNT1 A 0 A_FireCustomMissile("QuakeClusterRocket",-2,false)
        TNT1 A 0 A_FireCustomMissile("QuakeClusterRocket",2,false)
        TNT1 A 0 A_FireCustomMissile("QuakeClusterRocket",4,false)
        goto PickupFinish

      PickupAttackNormalOriginalCoop:
        TNT1 A 0 A_FireCustomMissile("QuakeClusterRkCoop",-4,false)
        TNT1 A 0 A_FireCustomMissile("QuakeClusterRkCoop",-2,false)
        TNT1 A 0 A_FireCustomMissile("QuakeClusterRkCoop",2,false)
        TNT1 A 0 A_FireCustomMissile("QuakeClusterRkCoop",4,false)
        goto PickupFinish

      // Quake I (Rerelease) sounds
      PickupAttackNormalRerelease:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackNormalRereleaseCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackNormalRereleaseCoop")
      PickupAttackNormalRereleaseDM:
        TNT1 A 0 A_FireCustomMissile("QuakeClusterRocketSoundRerelease",-4,false)
        TNT1 A 0 A_FireCustomMissile("QuakeClusterRocketSoundRerelease",-2,false)
        TNT1 A 0 A_FireCustomMissile("QuakeClusterRocketSoundRerelease",2,false)
        TNT1 A 0 A_FireCustomMissile("QuakeClusterRocketSoundRerelease",4,false)
        goto PickupFinish

      PickupAttackNormalRereleaseCoop:
        TNT1 A 0 A_FireCustomMissile("QuakeClusterRkCoopSoundRerelease",-4,false)
        TNT1 A 0 A_FireCustomMissile("QuakeClusterRkCoopSoundRerelease",-2,false)
        TNT1 A 0 A_FireCustomMissile("QuakeClusterRkCoopSoundRerelease",2,false)
        TNT1 A 0 A_FireCustomMissile("QuakeClusterRkCoopSoundRerelease",4,false)
        goto PickupFinish

      // Quad
      PickupAttackQuad:
        TNT1 A 0 A_JumpIfInventory("SamsaraAlternateSoundToken",1,"PickupAttackQuadRerelease")
      // Quake I (Original) sounds
      PickupAttackQuadOriginal:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackQuadOriginalCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackQuadOriginalCoop")
      PickupAttackQuadOriginalDM:
        TNT1 A 0 A_FireCustomMissile("QuadClusterRocket",-4,false)
        TNT1 A 0 A_FireCustomMissile("QuadClusterRocket",-2,false)
        TNT1 A 0 A_FireCustomMissile("QuadClusterRocket",2,false)
        TNT1 A 0 A_FireCustomMissile("QuadClusterRocket",4,false)
        goto PickupFinish

      PickupAttackQuadOriginalCoop:
        TNT1 A 0 A_FireCustomMissile("QuadClusterRkCoop",-4,false)
        TNT1 A 0 A_FireCustomMissile("QuadClusterRkCoop",-2,false)
        TNT1 A 0 A_FireCustomMissile("QuadClusterRkCoop",2,false)
        TNT1 A 0 A_FireCustomMissile("QuadClusterRkCoop",4,false)
        goto PickupFinish

      // Quake I (Rerelease) sounds
      PickupAttackQuadRerelease:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackQuadRereleaseCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackQuadRereleaseCoop")
      PickupAttackQuadRereleaseDM:
        TNT1 A 0 A_FireCustomMissile("QuadClusterRocketSoundRerelease",-4,false)
        TNT1 A 0 A_FireCustomMissile("QuadClusterRocketSoundRerelease",-2,false)
        TNT1 A 0 A_FireCustomMissile("QuadClusterRocketSoundRerelease",2,false)
        TNT1 A 0 A_FireCustomMissile("QuadClusterRocketSoundRerelease",4,false)
        goto PickupFinish

      PickupAttackQuadRereleaseCoop:
        TNT1 A 0 A_FireCustomMissile("QuadClusterRkCoopSoundRerelease",-4,false)
        TNT1 A 0 A_FireCustomMissile("QuadClusterRkCoopSoundRerelease",-2,false)
        TNT1 A 0 A_FireCustomMissile("QuadClusterRkCoopSoundRerelease",2,false)
        TNT1 A 0 A_FireCustomMissile("QuadClusterRkCoopSoundRerelease",4,false)
        goto PickupFinish

      // Finishing stuff
      PickupFinish:
        TNT1 A 0 ACS_NamedExecuteAlways("SamsaraRecoil", 0, 7, 4, 15)
        stop
    }
}

actor QuakeClusterRocket: QuakeRocket
{
    Obituary "$SAMSARA_RANGER_OB_SLOT5_2"
    States
    {
      Death:
        TNT1 A 0 A_Explode(50, 91, 0)
        TNT1 A 0 A_ChangeFlag("FORCERADIUSDMG",1)
        TNT1 A 0 A_Explode(50, 91)
        TNT1 A 1 A_SpawnItemEx("QuakeTinyExplosion")
        stop
    }
}

actor QuakeClusterRkCoop: QuakeClusterRocket
{
    +THRUSPECIES
    Species "Player"
    Damage (5)
}

actor QuadClusterRocket: QuakeClusterRocket
{
    DamageType "NotQuadded"
    Obituary "$SAMSARA_RANGER_OB_SLOT5_4"
    States
    {
      Death:
        TNT1 A 0 A_Explode(200, 91, 0)
        TNT1 A 0 A_ChangeFlag("FORCERADIUSDMG",1)
        TNT1 A 0 A_Explode(200, 91)
        TNT1 A 1 A_SpawnItemEx("QuakeTinyExplosion")
        stop
    }
}

actor QuadClusterRkCoop: QuadClusterRocket
{
    +THRUSPECIES
    Species "Player"
    Damage (20)
}

actor QuakeClusterRocketSoundRerelease : QuakeClusterRocket
{
    States
    {
      Death:
        TNT1 A 0 A_Explode(50, 91, 0)
        TNT1 A 0 A_ChangeFlag("FORCERADIUSDMG",1)
        TNT1 A 0 A_Explode(50, 91)
        TNT1 A 1 A_SpawnItemEx("QuakeTinyExplosionSoundRerelease")
        stop
    }
}

actor QuakeClusterRkCoopSoundRerelease : QuakeClusterRocketSoundRerelease
{
    +THRUSPECIES
    Species "Player"
    Damage (5)
}

actor QuadClusterRocketSoundRerelease : QuakeClusterRocketSoundRerelease
{
    DamageType "NotQuadded"
    Obituary "$SAMSARA_RANGER_OB_SLOT5_4"
    States
    {
      Death:
        TNT1 A 0 A_Explode(200, 91, 0)
        TNT1 A 0 A_ChangeFlag("FORCERADIUSDMG",1)
        TNT1 A 0 A_Explode(200, 91)
        TNT1 A 1 A_SpawnItemEx("QuakeTinyExplosionSoundRerelease")
        stop
    }
}

actor QuadClusterRkCoopSoundRerelease : QuadClusterRocketSoundRerelease
{
    +THRUSPECIES
    Species "Player"
    Damage (20)
}
