actor "Frost Shards" : MWeapFrost
{
	Weapon.SelectionOrder 1700
	Weapon.SlotNumber 2
	Weapon.AmmoUse1 0
	Weapon.AmmoGive1 25
	Weapon.KickBack 150
	Weapon.YAdjust 20
	DamageType "HexenIce"
	Weapon.AmmoType1 "Clip"
	Inventory.PickupMessage "You got the Frost Shards!"
	Obituary "$SAMSARA_HEXEN_OB_SLOT2"
	Inventory.PickupSound "hexen/weaponget"
	Inventory.RestrictedTo "HexenPlayer"
	+BLOODSPLATTER
	-ICEDAMAGE
	+INVENTORY.UNDROPPABLE
	Tag "Crusader's Longbow"
	States
	{
	  Spawn:
		H114 A 8 Bright
		Loop

	  Select:
		TNT1 A 0 A_JumpIfInventory("HexenClassMode", 2, "SelectFighter")
		TNT1 A 0 A_JumpIfInventory("HexenClassMode", 1, "SelectMage")
		H114 B 1 A_Raise
		Loop

	  SelectMage:
		CONE H 1 A_Raise
		Loop

	  SelectFighter:
		H101 A 0 A_JumpIfInventory("Clip", 3, "SelectFighterGlow")
		H101 A 1 A_Raise
		Loop
	  SelectFighterGlow:
		H101 B 1 Bright A_Raise
		Goto Select

	  Deselect:
		TNT1 A 0 A_JumpIfInventory("HexenClassMode", 2, "DeselectFighter")
		TNT1 A 0 A_JumpIfInventory("HexenClassMode", 1, "DeselectMage")
		H114 B 1 A_Lower
		Loop

	  DeselectMage:
		CONE H 1 A_Lower
		Loop

	  DeselectFighter:
		H101 A 0 A_JumpIfInventory("Clip", 3, "DeselectFighterGlow")
		H101 A 1 A_Lower
		Loop
	  DeselectFighterGlow:
		H101 B 1 Bright A_Lower
		Goto Deselect

	  Ready:
		TNT1 A 0 A_JumpIfInventory("HexenClassMode", 2, "ReadyFighter")
		TNT1 A 0 A_JumpIfInventory("HexenClassMode", 1, "ReadyMage")
		H114 BBBFFF 1 A_WeaponReady
		Loop

	  ReadyMage:
		CONE H 1 A_WeaponReady
		Goto Ready

	  ReadyFighter:
		H101 A 0 A_JumpIfInventory("Clip", 3, "ReadyFighterGlow")
		H101 A 1 A_WeaponReady
		Goto Ready
	  ReadyFighterGlow:
		H101 BCDC 5 Bright A_WeaponReady
		Goto Ready

	  Fire:
		TNT1 A 0 A_JumpIfInventory("HexenClassMode", 2, "FireFighter")
		TNT1 A 0 A_JumpIfInventory("HexenClassMode", 1, "FireMage")
		TNT1 A 0 A_JumpIfInventory("Clip",3,1)
		Goto SwitchWeapon
		H114 C 3
		H114 D 3 A_PlayWeaponSound("Hexen/BowString")

	  Hold:
		TNT1 A 0 A_JumpIfInventory("Clip",3,1)
		Goto SwitchWeapon
		H114 EG 3
		TNT1 A 0 A_Refire
		TNT1 A 0 A_TakeInventory("Clip",3,TIF_NOTAKEINFINITE)
		TNT1 A 0 A_PlayWeaponSound("Hexen/ElectricArrow")
		TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"CoopHold")
		H114 H 3 A_FireCustomMissile("HexenShockArrow2",0,1,5,0)
		Goto HoldEnd
	  CoopHold:
		H114 H 3 A_FireCustomMissile("HexenShockArrow2",0,1,5,0)
		Goto HoldEnd
	  HoldEnd:
	  	H114 ICB 3
		Goto Ready

	  FireMage:
		TNT1 A 0 A_JumpIfInventory("Clip",2,1)
		Goto SwitchWeapon
		CONE I 3
		CONE J 4
		Goto HoldMage
	  HoldMage:
		TNT1 A 0 A_JumpIfInventory("Clip",2,1)
		Goto SwitchWeapon
		CONE K 3
		CONE L 5 BRIGHT
		TNT1 A 0 A_PlayWeaponSound("MageShardsFire")
		TNT1 A 0 A_TakeInventory("Clip", 2, TIF_NOTAKEINFINITE)
		TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"CoopFireMage")
		TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"CoopFireMage")
		TNT1 A 0 A_JumpIfCloser(92,"FrostMeleeMage")
		// THIS HERE AREA WAS A DICK
		TNT1 A 0 A_FireCustomMissile("FrostMissileDM",14,0,6,1)
		TNT1 A 0 A_FireCustomMissile("FrostMissileDM",9,0,4,1)
		TNT1 A 0 A_FireCustomMissile("FrostMissileDM",5,0,2,1)
		TNT1 A 0 A_FireCustomMissile("FrostMissileDM",-14,0,-6,1)
		TNT1 A 0 A_FireCustomMissile("FrostMissileDM",-9,0,-4,1)
		TNT1 A 0 A_FireCustomMissile("FrostMissileDM",-5,0,-2,1)

		TNT1 A 0 A_FireCustomMissile("FrostMissileDM2",0,0,0,8,0,0)
		TNT1 A 0 A_FireCustomMissile("FrostMissileDM3",0,0,0,16,0,0)
		TNT1 A 0 A_FireCustomMissile("FrostMissileDM4",0,0,0,24,0,0)
		TNT1 A 0 A_FireCustomMissile("FrostMissileDM2",0,0,0,-2,0,0)
		TNT1 A 0 A_FireCustomMissile("FrostMissileDM3",0,0,0,-3,0,0)
		TNT1 A 0 A_FireCustomMissile("FrostMissileDM4",0,0,0,-4,0,0)

		TNT1 A 0 A_FireCustomMissile("FrostMissileDM2",4,0,1,16,0,0)
		TNT1 A 0 A_FireCustomMissile("FrostMissileDM2",4,0,1,-2,0,0)
		TNT1 A 0 A_FireCustomMissile("FrostMissileDM2",-4,0,-1,16,0,0)
		TNT1 A 0 A_FireCustomMissile("FrostMissileDM2",-4,0,-1,-2,0,0)
		//Y'ALL MOTHERFUCKERS BETTER APPRECIATE IT
		TNT1 A 0 A_FireCustomMissile("FrostMissileDM",0,0)
		Goto HoldMageEnd
	  FrostMeleeMage:
	    TNT1 A 0 A_FireCustomMissile("FrostMissileMelee2",0,0)
		Goto HoldMageEnd
	  CoopFireMage:
		TNT1 A 0 A_JumpIfCloser(92,"FrostMeleeMageCoop")

		// THIS HERE AREA WAS A DICK
		TNT1 A 0 A_FireCustomMissile("FrostMissile2",14,0,6,1)
		TNT1 A 0 A_FireCustomMissile("FrostMissile2",9,0,4,1)
		TNT1 A 0 A_FireCustomMissile("FrostMissile2",5,0,2,1)
		TNT1 A 0 A_FireCustomMissile("FrostMissile2",-14,0,-6,1)
		TNT1 A 0 A_FireCustomMissile("FrostMissile2",-9,0,-4,1)
		TNT1 A 0 A_FireCustomMissile("FrostMissile2",-5,0,-2,1)

		TNT1 A 0 A_FireCustomMissile("FrostMissile2_2",0,0,0,8,0,0)
		TNT1 A 0 A_FireCustomMissile("FrostMissile2_3",0,0,0,16,0,0)
		TNT1 A 0 A_FireCustomMissile("FrostMissile2_4",0,0,0,24,0,0)
		TNT1 A 0 A_FireCustomMissile("FrostMissile2_2",0,0,0,-2,0,0)
		TNT1 A 0 A_FireCustomMissile("FrostMissile2_3",0,0,0,-3,0,0)
		TNT1 A 0 A_FireCustomMissile("FrostMissile2_4",0,0,0,-4,0,0)

		TNT1 A 0 A_FireCustomMissile("FrostMissile2_2",4,0,1,16,0,0)
		TNT1 A 0 A_FireCustomMissile("FrostMissile2_2",4,0,1,-2,0,0)
		TNT1 A 0 A_FireCustomMissile("FrostMissile2_2",-4,0,-1,16,0,0)
		TNT1 A 0 A_FireCustomMissile("FrostMissile2_2",-4,0,-1,-2,0,0)
		//Y'ALL MOTHERFUCKERS BETTER APPRECIATE IT
		TNT1 A 0 A_FireCustomMissile("FrostMissile2",0,0)
		Goto HoldMageEnd
	  FrostMeleeMageCoop:
		TNT1 A 0 A_FireCustomMissile("FrostMissileMelee2Coop",0,0)
		Goto HoldMageEnd
	  HoldMageEnd:
		CONE M 4 BRIGHT
		CONE N 4
		CONE H 5
		TNT1 A 0 A_JumpIfInventory("Clip",2,1)
		Goto SwitchWeapon
		CONE H 7 A_ReFire("HoldMage")
		Goto Ready

	  FireFighter:
		TNT1 A 0 A_JumpIfCloser(128,"FighterMelee")
		H101 A 0 A_JumpIfInventory("Clip", 3, "FireFighterGlow")
		H101 EF 4
		H101 G 3
		H101 H 2 A_PlayWeaponSound("FighterHammerMiss")
		TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"FireFighterCoop")
		TNT1 A 0 A_FireCustomMissile("Hexen_FSpearFX1",0,0,8,16)
		Goto FireFighterFinish
	  FireFighterCoop:
		TNT1 A 0 A_FireCustomMissile("Hexen_FSpearFX1Coop",0,0,8,16)
		Goto FireFighterFinish

	  FireFighterGlow:
		H101 NO 4
		H101 Q 2 A_PlayWeaponSound("FighterHammerMiss")
		TNT1 A 0 A_TakeInventory("Clip",3,TIF_NOTAKEINFINITE)
		TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"FireFighterGlowCoop")
		TNT1 A 0 A_FireCustomMissile("Hexen_FSpearFX2",0,0,8,16)
		Goto FireFighterFinish
	  FireFighterGlowCoop:
		TNT1 A 0 A_FireCustomMissile("Hexen_FSpearFX2Coop",0,0,8,16)
		Goto FireFighterFinish

	  FireFighterFinish:
		H101 IJKLM 2
		TNT1 A 16
	    H101 A 0 A_JumpIfInventory("Clip", 3, "FireFighterGlowFinish")
		H101 A 3 Offset(0,92)
		H101 A 3 Offset(0,72)
		H101 A 3 Offset(0,52)
		Goto Ready
	  FireFighterGlowFinish:
		H101 A 3 Offset(0,92)
		H101 D 3 Offset(0,72)
		H101 C 3 Offset(0,52)
		Goto Ready

	  FighterMelee:
		H101 A 0 A_JumpIfInventory("Clip", 3, "FighterMeleeGlow")
		H101 A 2 Offset(-32,34)
		H101 A 2 Offset(-64,38)
		H101 A 0 A_JumpIfInventory("CoopModeOn",1,2)
		H101 A 0 A_CustomPunch(random(24,48),1,1,"AxePuff2",128)
		Goto FighterMeleeEnd
		H101 A 0 A_CustomPunch(random(24,48),1,1,"AxePuff2Coop",128)
		Goto FighterMeleeEnd
	  FighterMeleeEnd:
		H101 A 2 Offset(-96,42)
		H101 A 2 Offset(-64,38)
		H101 A 2 Offset(-32,34)
		H101 A 8 Offset(0,32)
		Goto Ready

	  FighterMeleeGlow:
		H101 B 2 Bright Offset(-32,34)
		H101 B 2 Bright Offset(-64,38)
		H101 B 0 A_JumpIfInventory("CoopModeOn",1,2)
		H101 B 0 A_CustomPunch(random(48,96),1,1,"SpearPuffGlow",128)
		Goto FighterMeleeGlowEnd
		H101 B 0 A_CustomPunch(random(48,96),1,1,"SpearPuffGlowCoop",128)
		Goto FighterMeleeGlowEnd
	  FighterMeleeGlowEnd:
		H101 B 2 Bright Offset(-96,42)
		H101 B 2 Bright Offset(-64,38)
		H101 B 2 Bright Offset(-32,34)
		H101 B 8 Bright Offset(0,32)
		Goto Ready

	  SwitchWeapon:
		TNT1 A 0 ACS_NamedExecuteWithResult("Hexen_SwitchPriority")
		TNT1 A 0
		Goto Ready
	}
}

ACTOR Hexen_FSpearFX1
{
	Speed 50
	Radius 6
	Height 6
	Damage 10
	Projectile
	-NOGRAVITY
	+BLOODSPLATTER
	Gravity 0.33
	DeathSound "Hexen/weapons/spearhit"
	Obituary "%o was impaled by %k's throwing spears."
	States
	{
		Spawn:
			H101 R 1 A_SpawnItemEx("Hexen_FSpearFX3",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
			Loop
		Death:
			TNT1 A 1
			Stop
	}
}

actor Hexen_FSpearFX1Coop : Hexen_FSpearFX1
{
	Species "Player"
	+THRUSPECIES
}

ACTOR Hexen_FSpearFX2 : Hexen_FSpearFX1
{
	Speed 90
	Radius 4
	Height 6
	Damage 20
	States
	{
		Spawn:
			H101 S 1 Bright A_SpawnItemEx("Hexen_FSpearFX4",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
			Loop
		Death:
			TNT1 A 1 A_SpawnItemEx("Hexen_FSpearFX5Spawner",0,0,0,0,0,0,0,SXF_CLIENTSIDE)
			Stop
	}
}

actor Hexen_FSpearFX2Coop : Hexen_FSpearFX2
{
	Species "Player"
	+THRUSPECIES
}

ACTOR Hexen_FSpearFX3
{
	Radius 2
	Height 2
	+NOINTERACTION
	+CLIENTSIDEONLY
	States
	{
		Spawn:
			H101 R 2
			H101 RRRR 2 A_FadeOut(0.2)
			Stop
	}
}

ACTOR Hexen_FSpearFX4
{
	Radius 2
	Height 2
	+NOINTERACTION
	+CLIENTSIDEONLY
	States
	{
		Spawn:
			H101 S 2 Bright
			H101 SSSS 2 Bright A_FadeOut(0.2)
			Stop
	}
}

ACTOR Hexen_FSpearFX5Spawner
{
	+CLIENTSIDEONLY
	+NOINTERACTION
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 AAAAAAAAA 0 A_SpawnItemEx("Hexen_FSpearFX5",0,0,2,frandom(-4,4),frandom(-4,4),random(-2,6),0,SXF_CLIENTSIDE|SXF_NOCHECKPOSITION)
			TNT1 ABCD 2
			Stop
	}
}

ACTOR Hexen_FSpearFX5
{
	Radius 2
	Height 2
	+CLIENTSIDEONLY
	+DONTSPLASH
	RenderStyle Add
	Alpha 0.85
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 A 0 A_Jump(64,"Spawn2")
			Goto Spawn1
		Spawn1:
			H123 A 1 Bright
			H123 B 2 Bright
			H123 C 1 Bright
			H123 D 2 Bright
			H123 E 1 Bright
			H123 F 2 Bright
			H123 G 1 Bright
			Stop
		Spawn2:
			H123 H 4 Bright
			H123 I 3 Bright
			H123 J 2 Bright
			TNT1 A 0 A_Jump(128,"Spawn1")
			Stop
	}
}

actor FrostMissileMelee2 : FastProjectile
{
	Damage (random(70,95)) // Previously 90-115
	Radius 6
	Height 6
	Speed 20
	DamageType "HexenIce"
	Projectile
	-BLOODSPLATTER
	States
	{
	  Spawn:
		TNT1 A 1
		TNT1 A 1
		Stop

	  Death:
		TNT1 A 1 A_Stop
		Stop
	}
}

Actor FrostMissileMelee2Coop : FrostMissileMelee2 { +THRUSPECIES Species "Player" }

actor FrostMissileMelee : FastProjectile
{
	Damage 6
	Radius 1
	Height 1
	Speed 20
	DamageType "HexenIce"
	Projectile
	+STRIFEDAMAGE
	-BLOODSPLATTER
	States
	{
	  Spawn:
		TNT1 A 1
		TNT1 A 1
		Stop

	  Death:
		TNT1 A 1 A_Stop
		Stop
	}
}

actor FrostMissileDM : FrostMissile
{
	Damage (random(1,4))
	Obituary "$SAMSARA_HEXEN_OB_SLOT2"
	-ICEDAMAGE
	DamageType "HexenIce"
	+THRUGHOST
	+BLOODSPLATTER
}

actor FrostMissileDM2 : FrostMissileDM
{
	Speed 23
}

actor FrostMissileDM3 : FrostMissileDM
{
	Speed 21
}

actor FrostMissileDM4 : FrostMissileDM
{
	Speed 19
}

actor FrostMissile2 : FrostMissile replaces FrostMissile
{
	Obituary "$SAMSARA_HEXEN_OB_SLOT2"
	-ICEDAMAGE
	DamageType "HexenIce"
	+THRUGHOST
	+THRUSPECIES
	+BLOODSPLATTER
	Species "Player"
}

actor FrostMissile2_1 : FrostMissileDM { +THRUSPECIES Species "Player" }
Actor FrostMissile2_2 : FrostMissileDM2 { +THRUSPECIES Species "Player" }
Actor FrostMissile2_3 : FrostMissileDM3 { +THRUSPECIES Species "Player" }
Actor FrostMissile2_4 : FrostMissileDM4 { +THRUSPECIES Species "Player" }

actor BlankPuff3 : BlankPuff
{
	-ICEDAMAGE
	DamageType "HexenIce"
}

actor SpearPuffGlow : AxePuffGlow
{
	DamageType "TimonsAxe"
	+PUFFGETSOWNER
	States
	{
	Spawn:
	Crash:
		 H101 TUVWXYZ 4 Bright
		 Stop
	Melee:
		 TNT1 A 0
		 TNT1 A 0 A_TakeFromTarget("Clip",3,TIF_NOTAKEINFINITE)
		 Goto Spawn
	}
}

actor SpearPuffGlowCoop : SpearPuffGlow { Species "Player" +THRUSPECIES +MTHRUSPECIES }

Actor HexenShockArrow 
{
    +BLOODSPLATTER
	+DONTREFLECT
	Damage 21
	Radius 5
	Height 5
	PROJECTILE
	Renderstyle Add
	Obituary "%o was shocked by one of %k's thunder arrows."
	DamageType "Electric"
	DeathSound "Hexen/ArrowHit"
	Speed 50
	States
	{
		Spawn:
			H115 A 1 bright A_SpawnItem("HexenElectroTrail",0,0,0)
			Loop
		Death:
			TNT1 A 1 A_AlertMonsters
			FAXE RSTUVWX 4 Bright
			Stop
	}
}

Actor HexenShockArrow2 : HexenShockArrow { Species "Player" +THRUSPECIES }

Actor HexenElectroTrail
{
	Renderstyle Add
	+CLIENTSIDEONLY
	+NOGRAVITY
	Scale 0.2
	States
	{
		Spawn:
			FAXE UVWX 2 Bright
			Stop
	}
}