actor Luger: InstantSwitchWeapon
{
    Weapon.SelectionOrder 1900
    Weapon.SlotNumber 1
    Inventory.PickUpSound "wolfen/knifeget"
    Obituary "$SAMSARA_WOLFEN_OB_PISTOL"
    Inventory.PickupMessage "You got the Luger Pistol!"
    DamageType "Pistol"
    Weapon.AmmoUse 0
    Weapon.AmmoGive 0
    +WEAPON.WIMPY_WEAPON
    +DONTBOB
    +INVENTORY.UNDROPPABLE
    Weapon.YAdjust 16
	Inventory.RestrictedTo "WolfenPlayer"
    States
    {
      Spawn:
        WPSP A -1
        Loop

      Ready:
		TNT1 A 0 A_JumpIfInventory("WolfenLostMode",1,2)
        WPST F 1 A_WeaponReady
        loop
		WPST K 1 A_WeaponReady
        loop

      Fire:
        TNT1 A 0 A_JumpIfInventory("WolfenLostMode",1,"FireLost")
		TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,"CheckForAmmo")
      Fire2:
        WPST B 3
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,"TakePistolAmmo")
      Fire3:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"SPFire")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"CoopFire") //"CoopFire")
        WPST F 0 A_PlayWeaponSound("wolfen/pistol")
        WPST C 0 A_FireBullets(0,0,1,2,"BlankPuff2Bloodless",0,120)
        WPST C 3 BRIGHT A_FireBullets(0,0,1,7,"BlankPuff2")
        WPST DE 4
        Goto Ready
	  FireLost:
		TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,"CheckForAmmo")
      Fire2Lost:
        WPST H 3
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,"TakePistolAmmo")
      Fire3Lost:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"SPFireLost")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"CoopFireLost") //"CoopFire")
        WPST K 0 A_PlayWeaponSound("wolfen/pistol2")
        WPST H 0 A_FireBullets(0,0,1,2,"BlankPuff2Bloodless",0,120)
        WPST H 3 BRIGHT A_FireBullets(0,0,1,7,"BlankPuff2")
        WPST IJ 4
        Goto Ready
        
      SPFire:
        WPST F 0 A_PlayWeaponSound("wolfen/pistol")
        WPST C 0 A_FireBullets(0,0,1,2,"BlankPuff2Bloodless",0,256)
        WPST C 3 BRIGHT A_FireBullets(0,0,1,7,"BlankPuff2")
        WPST DE 4
        Goto Ready

      CoopFire:
        WPST F 0 A_PlayWeaponSound("wolfen/pistol")
        WPST C 0 A_FireCustomMissile("WolfenBulletSupplement",0,0,0,0)
        WPST C 3 BRIGHT A_FireCustomMissile("WolfenBullet2",0,0,0,1)
        WPST DE 4
        Goto Ready
		
	  SPFireLost:
        WPST K 0 A_PlayWeaponSound("wolfen/pistol2")
        WPST H 0 A_FireBullets(0,0,1,2,"BlankPuff2Bloodless",0,256)
        WPST H 3 BRIGHT A_FireBullets(0,0,1,7,"BlankPuff2")
        WPST IJ 4
        Goto Ready

      CoopFireLost:
        WPST K 0 A_PlayWeaponSound("wolfen/pistol2")
        WPST H 0 A_FireCustomMissile("WolfenBulletSupplement",0,0,0,0)
        WPST H 3 BRIGHT A_FireCustomMissile("WolfenBullet2",0,0,0,1)
        WPST IJ 4
        Goto Ready
        
      CheckForAmmo:
		TNT1 A 0 A_JumpIfInventory("WolfenLostMode",1, 3)
        TNT1 A 0 A_JumpIfInventory("Clip",1,"Fire2")
        TNT1 A 0 A_SelectWeapon("Knife")
        goto Deselect
		TNT1 A 0 A_JumpIfInventory("Clip",1,"Fire2Lost")
		TNT1 A 0 A_SelectWeapon("Knife")
        goto Deselect
		
      TakePistolAmmo:
        TNT1 A 0 A_TakeInventory("Clip",1)
		TNT1 A 0 A_JumpIfInventory("WolfenLostMode",1,"Fire3Lost")
        goto Fire3
    }
}

actor BlankPuff2: BlankPuff { DamageType "Pistol" Species "Player" }

actor BlankPuff2Bloodless : BlankPuff2 { -BLOODSPLATTER +BLOODLESSIMPACT }

actor WolfenBullet2: FastProjectile
{
    Damage (Random(1,3)*7)
    Radius 1
    Height 1
    Speed 320
    Projectile
    Species "Player"
    DamageType "Pistol"
    +BLOODSPLATTER
    +THRUSPECIES
    +NOTIMEFREEZE
    States
    {
      Spawn:
        TNT1 A 1
        loop

      Death:
        TNT1 A 1
        stop
    }
}

actor WolfenBulletSupplement//: WolfenBullet2
{
    Speed 256
    Damage (Random(1,3)*2)
    Radius 1
    Height 1
    Projectile
    Species "Player"
    DamageType "Pistol"
    -BLOODSPLATTER
    +BLOODLESSIMPACT
    +THRUSPECIES
    +NOTIMEFREEZE
    States
    {
      Spawn:
        TNT1 A 0
        TNT1 A 1
        TNT1 A 0 A_Stop
        TNT1 A 0 A_Die
        stop

      Death:
        TNT1 A 1
        stop
    }
}

actor WolfenBulletSupplement2
{
    Speed 256
    Damage (Random(1,3)*2)
    Radius 1
    Height 1
    Projectile
    Species "Player"
    -BLOODSPLATTER
    +BLOODLESSIMPACT
    +THRUSPECIES
    +NOTIMEFREEZE
    States
    {
      Spawn:
        TNT1 A 0
        TNT1 A 1
        TNT1 A 0 A_Stop
        TNT1 A 0 A_Die
        stop

      Death:
        TNT1 A 1
        stop
    }
}