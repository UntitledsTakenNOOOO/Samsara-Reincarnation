Actor DeusExFrameCounter : Counter { Inventory.MaxAmount 64 }
Actor DeusExAnimClip : Counter { Inventory.MaxAmount 64 }

Actor DeusExWeapon : Weapon
{
	Weapon.BobStyle Smooth
	Weapon.BobSpeed 1.5
	Weapon.BobRangeX 0.1
	Weapon.BobRangeY 0.4
	+INVENTORY.UNDROPPABLE
	+WEAPON.AMMO_OPTIONAL
	+WEAPON.ALT_AMMO_OPTIONAL
	+WEAPON.NOALERT
	Inventory.PickupSound "DeusEx/WeaponPickup"
	Tag "Deus Ex Weapon"
	Inventory.RestrictedTo "JCDenton"
	States
	{
		Ready:
			TNT1 A 0 A_TakeInventory("DeusExWeapon")
			TNT1 A 1 A_WeaponReady
			loop

		Deselect:
		DeselectInstant:
			TNT1 AAAAAAAAAAAAAAAAAAAA 0 A_Lower
			TNT1 A 1 A_Lower
			loop

		Select:
			TNT1 AAAAAAAAAAAAAAAAAAAA 0 A_Raise
			TNT1 A 1 A_Raise
			loop

		Fire:
			TNT1 A 1
			goto Ready
	}
}

actor DeusEx_PointBlank : Boolean {}
actor DeusEx_Zoomed : Boolean {}
actor DeusEx_ZoomedMemory : Boolean {}
actor DeusEx_MovementPenaltyEnabled : Boolean { Inventory.RestrictedTo "JCDenton" }
actor SamsaraDeusExSwappedAmmo : Boolean {}

// Player Controller
Actor DeusEx_Crouching : Boolean {}
Actor DeusEx_Running : Boolean {}
Actor DeusEx_StrafeLeft : Boolean {}
Actor DeusEx_StrafeRight : Boolean {}
Actor DeusEx_Jumping : Boolean {}
Actor DeusEx_Reloading : Boolean {}
Actor DeusEx_Firing : Boolean {}

// Weapon Selection Tokens
// One-Handed
Actor DeusEx_KnifeEquipped : Boolean {}
Actor DeusEx_GlockEquipped : Boolean {}
Actor DeusEx_MiniXBowEquipped : Boolean {}
Actor DeusEx_StealthEquipped : Boolean {}
Actor DeusEx_ShotgunEquipped : Boolean {}
Actor DeusEx_LAMEquipped : Boolean {}
Actor DeusEx_ProdEquipped : Boolean {}
// Two-Handed
Actor DeusEx_AssShotgunEquipped : Boolean {}
Actor DeusEx_AssGunEquipped : Boolean {}
Actor DeusEx_SniperEquipped : Boolean {}
Actor DeusEx_GEPGunEquipped : Boolean {}
Actor DeusEx_FlameThrowerEquipped : Boolean {}
Actor DeusEX_PREquipped : Boolean {}
Actor DeusEx_LAWEquipped : Boolean {}
Actor DeusEx_NanoSwordEquipped : Boolean {}

Actor DeusExStepAlerter : Trigger
{
	States
	{
		Pickup:
			TNT1 A 0 A_AlertMonsters(CallACS("DeusEx_Decorate",9))
			Stop
	}
}

Actor DeusExBullet : FastProjectile
{
	Speed 150
	Damage (0)
	Radius 1
	Height 2
	+NOTIMEFREEZE
	+DONTBLAST
	+NOEXTREMEDEATH
	+CANNOTPUSH
	+HITTRACER
	+FORCERADIUSDMG
	+NODAMAGETHRUST
	+BLOODLESSIMPACT
	+BRIGHT
	DamageType "DeusExShot"
	RenderStyle Shaded
	StencilColor "af af 61"
	Decal "DeusEx_BulletChip"
	Var Int user_damage;
	states
	{
	Spawn:
		TNT1 A 0
		TNT1 A 1
		Goto Spawn2
	Spawn2:
		DX00 A 1 //A_TakeInventory("DeusEx_PointBlank", 99999)
	SpawnLoop:
		DX00 A 1
		loop
	Death:
		//TNT1 A 0 A_PlaySound("DeusEx/Bullet/HitStone", CHAN_BODY)
		TNT1 A 0 A_SpawnItemEx("DeusEx_RockChip", Random(-1,1), -2, 0, frandom(0.0,2.0), 0, frandom(0.5,2.0), random(0,360))
		//TNT1 A 0 A_CustomMissile("DeusEx_RockChip", 0, 0, Angle-180, CMF_TRACKOWNER)
		TNT1 A 1 A_SpawnItemEx("DeusEx_BulletPuff")
		stop
	Crash:
		TNT1 A 0 A_PlaySound("DeusEx/Bullet/HitRobot", CHAN_BODY)
		TNT1 A 1 A_Explode(CallACS("DeusEx_DamageModifiers",2),8,!XF_HURTSOURCE,0,8)
		stop
	XDeath:
		//TNT1 A 0 A_PlaySound("DeusEx/Bullet/HitFlesh", CHAN_BODY)
		TNT1 A 0 A_Explode(CallACS("DeusEx_DamageModifiers",2),8,!XF_HURTSOURCE,0,8)
		TNT1 A 0 ACS_NamedExecuteWithResult("SamsaraBloodGeneratorOffsetsWithBleedAngle")
		TNT1 A 1 A_GiveInventory("DeusEx_BloodGenerator",1,AAPTR_TRACER)
		stop
	}
}

Actor DeusEx_BloodGenerator : Trigger
{
	States
	{
		Pickup:
			TNT1 A 0 A_CheckFlag("NOBLOOD","PickupStop")
			TNT1 A 0 A_SpawnItemEx("Server_FlyingBloodSpawner",(radius-16)*cos(CallACS("SamsaraBloodGeneratorReturn",1)),(radius-16)*sin(CallACS("SamsaraBloodGeneratorReturn",1)),CallACS("SamsaraBloodGeneratorReturn",2),0,0,0,0,SXF_NOCHECKPOSITION|SXF_USEBLOODCOLOR|SXF_ABSOLUTEPOSITION)
			TNT1 A 0 A_SpawnItemEx("DeusEx_BloodSpurt",(radius-16)*cos(CallACS("SamsaraBloodGeneratorReturn",1)),(radius-16)*sin(CallACS("SamsaraBloodGeneratorReturn",1)),CallACS("SamsaraBloodGeneratorReturn",2),frandom(0.25,0.5)*sin(CallACS("SamsaraBloodGeneratorReturn",3)),frandom(-0.5,0.5)*cos(CallACS("SamsaraBloodGeneratorReturn",3)),frandom(0.05,0.25),CallACS("SamsaraBloodGeneratorReturn",3),SXF_NOCHECKPOSITION|SXF_USEBLOODCOLOR|SXF_ABSOLUTEPOSITION|SXF_ABSOLUTEVELOCITY|SXF_ABSOLUTEANGLE)
			stop
	}
}

Actor DeusEx_BulletPuff
{
	+CLIENTSIDEONLY
	+NOEXTREMEDEATH
	+FORCEXYBILLBOARD
	+NOINTERACTION
	+BRIGHT
	RenderStyle Add
	Scale 0.025
	VSpeed 0
	Mass 0
	DamageType "DeusExShot"
	States
	{
		Spawn:
			DX05 ABCDEFG 2
			Stop
	}
}

Actor DeusEx_ExplosionMid
{
	+CLIENTSIDEONLY
	+NOINTERACTION
	+FORCEXYBILLBOARD
	+BRIGHT
	RenderStyle Add
	Alpha 0.7
	States
	{
		Spawn:
			DX01 ABCDEFGH 2
			Stop
	}
}

Actor DeusEx_ExplosionSmall : DeusEx_ExplosionMid
{
	Scale 0.25
	States
	{
		Spawn:
			DX00 BCDEFG 2
			Stop
	}
}

Actor DeusEx_ExplosionLarge : DeusEx_ExplosionMid { Scale 4.0 }

Actor DeusEx_BloodPool
{
	Radius 12
	Height 2
	Mass 1
	RenderStyle Translucent
	Alpha 0.75
	Scale 0.05
	+NOINTERACTION
	+FLOORHUGGER
	-NOGRAVITY
	+THRUACTORS
	-SOLID
	Var Int user_scalex;
	Var Int user_scaley;
	States
	{
		Spawn:
			TNT1 A 0 //A_SetUserVar(user_scalex, 0.05)
			TNT1 A 0 //A_SetUserVar(user_scaley, 0.05)
			TNT1 A 0 A_CheckFloor(1)
			stop
			DX00 AAAA 3
			DX00 A 3 A_SetScale(scalex+0.05, scaley+0.05)
			DX00 AAAA 3
			DX00 A 3 A_SetScale(scalex+0.05, scaley+0.05)
			DX00 AAAA 3
			DX00 A 3 A_SetScale(scalex+0.05, scaley+0.05)
			DX00 AAAA 3
			DX00 A 3 A_SetScale(scalex+0.05, scaley+0.05)
			DX00 AAAA 3
			DX00 A 3 A_SetScale(scalex+0.05, scaley+0.05)
			DX00 AAAA 3
			DX00 A 3 A_SetScale(scalex+0.05, scaley+0.05)
			DX00 AAAA 3
			DX00 A 3 A_SetScale(scalex+0.05, scaley+0.05)
			DX00 AAAA 3
			DX00 A 3 A_SetScale(scalex+0.05, scaley+0.05)
			DX00 AAAA 3
			DX00 A 3 A_SetScale(scalex+0.05, scaley+0.05)
			DX00 AAAA 3
			DX00 A 3 A_SetScale(scalex+0.05, scaley+0.05)
			DX00 AAAA 3
			DX00 A 3 A_SetScale(scalex+0.05, scaley+0.05)
			DX00 AAAA 3
			DX00 A 3 A_SetScale(scalex+0.05, scaley+0.05)
			DX00 AAAA 3
			DX00 A 3 A_SetScale(scalex+0.05, scaley+0.05)
			DX00 AAAA 3
			DX00 A 3 A_SetScale(scalex+0.05, scaley+0.05)
			DX00 AAAA 3
			DX00 A 3 A_SetScale(scalex+0.05, scaley+0.05)
			DX00 AAAA 3
			DX00 A 3 A_SetScale(scalex+0.05, scaley+0.05)
			DX00 AAAA 3
			DX00 A 3 A_SetScale(scalex+0.05, scaley+0.05)
			DX00 AAAA 3
			DX00 A 3 A_SetScale(scalex+0.05, scaley+0.05)
			DX00 AAAA 3
			DX00 A 3 A_SetScale(scalex+0.05, scaley+0.05)
		Puddle:
			DX00 A 2100
			DX00 A 0 A_JumpIf(ACS_NamedExecuteWithResult("SamsaraClientDecorate", 13), "Puddle")
			DX00 AAAAAAAAAA 1 A_FadeOut(0.1)
			stop
	}
}

Actor DeusExGore
{
	Projectile
	BounceType "Doom"
	Height 4
	Radius 2
	Damage (0)
	Gravity 0.5
	BounceFactor 0.25
	BounceCount 3
	+BLOODLESSIMPACT
	-NOGRAVITY
	States
	{
		Spawn:
			DX00 A 0
			DX00 A 0 A_SetPitch(pitch+5)
			DX00 A 0 A_SetAngle(angle+5)
			DX00 A 2 A_SpawnItemEx("DeusEx_BloodDrop",0,0,0,0,frandom(-2.0,2.0),frandom(-2.0,-0.5),frandom(-90,90),SXF_TRANSFERTRANSLATION)
			DX00 A 0 A_SetPitch(pitch+5)
			DX00 A 0 A_SetAngle(angle+5)
			DX00 A 2
			Loop
		Death:
			DX00 A 350
			DX00 A 0 A_JumpIf(ACS_NamedExecuteWithResult("SamsaraClientDecorate", 13), "Death")
			DX00 A 1 A_FadeOut(0.025)
			Wait
	}
}

Actor DeusEx_FleshFragment1 : DeusExGore {}
Actor DeusEx_FleshFragment2 : DeusExGore {}
Actor DeusEx_FleshFragment3 : DeusExGore {}
Actor DeusEx_FleshFragment4 : DeusExGore {}

Actor DeusEx_TossChunk : Trigger
{
	States
	{
		Pickup:
			TNT1 AA 0 A_SpawnItemEx("DeusEx_BloodDrop",0,0,0,frandom(-16.0,16.0),0,frandom(1.0,4.0),frandom(0,360),SXF_USEBLOODCOLOR)
			TNT1 A 0 A_Jump(256, "Chunk1", "Chunk2", "Chunk3", "Chunk4")
		Chunk1:
			TNT1 A 1 A_SpawnItemEx("DeusEx_FleshFragment1",0,0,random(0,CallACS("DeusEx_GibHeightCheck")),frandom(1,6),0,frandom(2,4),frandom(0.0,360.0),SXF_NOCHECKPOSITION|SXF_USEBLOODCOLOR)
			stop
		Chunk2:
			TNT1 A 1 A_SpawnItemEx("DeusEx_FleshFragment2",0,0,random(0,CallACS("DeusEx_GibHeightCheck")),frandom(1,6),0,frandom(2,4),frandom(0.0,360.0),SXF_NOCHECKPOSITION|SXF_USEBLOODCOLOR)
			stop
		Chunk3:
			TNT1 A 1 A_SpawnItemEx("DeusEx_FleshFragment3",0,0,random(0,CallACS("DeusEx_GibHeightCheck")),frandom(1,6),0,frandom(2,4),frandom(0.0,360.0),SXF_NOCHECKPOSITION|SXF_USEBLOODCOLOR)
			stop
		Chunk4:
			TNT1 A 1 A_SpawnItemEx("DeusEx_FleshFragment4",0,0,random(0,CallACS("DeusEx_GibHeightCheck")),frandom(1,6),0,frandom(2,4),frandom(0.0,360.0),SXF_NOCHECKPOSITION|SXF_USEBLOODCOLOR)
			stop
	}
}

Actor DeusEx_BloodDrop
{
	Gravity 1.0
	+THRUACTORS
	+CLIENTSIDEONLY
	States
	{
		Spawn:
			DX00 A 1 A_CheckFloor("Floor")
			Loop
		Floor:
			TNT1 A 0 A_Jump(256, "Type1", "Type2", "Type3", "Type4")
		Type1:
			TNT1 A 1 A_SpawnItemEx("DeusEx_BloodSpot1",0,0,0,0,0,0,0,SXF_TRANSFERTRANSLATION)
			Stop
		Type2:
			TNT1 A 1 A_SpawnItemEx("DeusEx_BloodSpot2",0,0,0,0,0,0,0,SXF_TRANSFERTRANSLATION)
			Stop
		Type3:
			TNT1 A 1 A_SpawnItemEx("DeusEx_BloodSpot3",0,0,0,0,0,0,0,SXF_TRANSFERTRANSLATION)
			Stop
		Type4:
			TNT1 A 1 A_SpawnItemEx("DeusEx_BloodSpot4",0,0,0,0,0,0,0,SXF_TRANSFERTRANSLATION)
			Stop
	}
}

Actor DeusEx_BloodSpurt
{
	Height 2
	Radius 1
	Damage (0)
	Gravity 0.25
	+THRUACTORS
	+BLOODLESSIMPACT
	+NOTRIGGER
	-NOGRAVITY
	States
	{
		Spawn:
			DX00 ABCDEFGHIJKLM 2 A_CheckFloor("Spot")
			Stop
		Spot:
			TNT1 A 0 A_Jump(256, "Type1", "Type2", "Type3", "Type4")
		Type1:
			TNT1 A 1 A_SpawnItemEx("DeusEx_BloodSpot1",frandom(-32.0,-28.0),0,0,0,0,0,frandom(-4.0,4.0),SXF_TRANSFERTRANSLATION)
			Stop
		Type2:
			TNT1 A 1 A_SpawnItemEx("DeusEx_BloodSpot2",frandom(-32.0,-28.0),0,0,0,0,0,frandom(-4.0,4.0),SXF_TRANSFERTRANSLATION)
			Stop
		Type3:
			TNT1 A 1 A_SpawnItemEx("DeusEx_BloodSpot3",frandom(-32.0,-28.0),0,0,0,0,0,frandom(-4.0,4.0),SXF_TRANSFERTRANSLATION)
			Stop
		Type4:
			TNT1 A 1 A_SpawnItemEx("DeusEx_BloodSpot4",frandom(-32.0,-28.0),0,0,0,0,0,frandom(-4.0,4.0),SXF_TRANSFERTRANSLATION)
			Stop
	}
}

Actor DeusEx_BloodSpot
{
	Radius 12
	Height 2
	Mass 1
	Scale 0.15
	RenderStyle Translucent
	Alpha 0.9
	+NOINTERACTION
	+FLOORHUGGER
	-NOGRAVITY
	+THRUACTORS
	-SOLID
	+CLIENTSIDEONLY
	+MOVEWITHSECTOR
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 A 0 A_CheckFloor("Spawn2")
			stop
		Spawn2:
			DX00 A 350
			DX00 A 0 A_JumpIf(ACS_NamedExecuteWithResult("SamsaraClientDecorate", 13), "Spawn2")
			DX00 AAAAAAAAAA 1 A_FadeOut(0.1)
			stop
	}
}

Actor DeusEx_BloodSpot1 : DeusEx_BloodSpot { }
Actor DeusEx_BloodSpot2 : DeusEx_BloodSpot { }
Actor DeusEx_BloodSpot3 : DeusEx_BloodSpot { }
Actor DeusEx_BloodSpot4 : DeusEx_BloodSpot { }

Actor DeusEx_Rockchip
{
	PROJECTILE
	-NOGRAVITY
	+NOBLOCKMAP
	+MOVEWITHSECTOR
	+THRUACTORS
	+NOTELEPORT
	-SOLID
	-TELESTOMP
	+BOUNCEAUTOOFF
	+CLIENTSIDEONLY
	+BOUNCEONWALLS
	+BOUNCEONFLOORS
	+BOUNCEONCEILINGS
	Scale 0.5
	BounceCount 2
	BounceFactor 0.75
	Gravity 0.66
	Radius 3
	Height 4
	Speed 6
	BounceSound "DeusEx/HitStone"
	Var Int User_Angle;
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 A 0 A_Jump(128,2)
			TNT1 A 0 A_SetUserVar(User_Angle,Random(6,20))
			Goto Decide
			TNT1 A 0 A_SetUserVar(User_Angle,Random(-20,-6))
			Goto Decide
		Decide:
			DX00 A 0 A_Jump(256, "Spawn1", "Spawn2", "Spawn3")
			Goto Spawn1
		Spawn1:
			DX00 A 1 A_Jump(64,"Flying")
			Loop
		Spawn2:
			DX01 A 1 A_Jump(64,"Flying")
			Loop
		Spawn3:
			DX02 A 1 A_Jump(64,"Flying")
			Loop
		Flying:
			"----" A 0 A_SetPitch(Pitch+User_Angle)
			"----" A 1 A_SetAngle(Angle+User_Angle)
			Loop
		Death:
			"----" A 500
			"----" A 35 A_FadeOut(0.5)
			Wait
	}
}

Actor DeusEx_ShellCasing1
{
	Speed 2
	Mass 8
	Radius 2
	Height 2
	+MISSILE
	+CLIENTSIDEONLY
	+THRUACTORS
	+DROPOFF
	+NOTELEPORT
	+FLOORCLIP
	-SOLID
	States
	{
		Spawn:
			DX00 A 0 A_SetPitch(Random(-40,25))
			DX00 A 0 A_SetAngle(Random(-50,50))
			Goto SpawnLoop
		SpawnLoop:
			DX00 A 0 A_SetPitch(pitch-15)
			DX00 A 1 A_SetAngle(angle-15)
			loop
		Death:
			DX00 A 0 A_JumpIf(waterlevel != 0,"Done")
			DX00 A 0 A_PlaySound("DeusEx/ShellCasing",CHAN_BODY)
			DX00 A 0 ThrustThingZ(0,4,0,0)
			goto Done
		Done:
			DX00 A 500
			DX00 A 1 A_FadeOut
			Goto Done+1
	}
}

Actor DeusEx_LAWDrop : DeusEx_ShellCasing1
{
	Speed 3
	Mass 30 // it's heavy
	Radius 8
	Height 4
	Scale 1.0
	Gravity 1.4
	States
	{
		Spawn:
			DX03 A 0 A_SetPitch(Random(-40,25))
			DX03 A 0 A_SetAngle(Random(-50,50))
			Goto SpawnLoop
		SpawnLoop:
			DX03 A 0 A_SetPitch(pitch-15)
			DX03 A 1 A_SetAngle(angle-15)
			loop
		Death:
			DX03 A 0 A_JumpIf(waterlevel != 0,"Done")
			DX03 A 0 A_PlaySound("DeusEx/LAW/Drop",CHAN_BODY)
			DX03 A 0 ThrustThingZ(0,4,0,0)
			goto Done
		Done:
			DX03 A 500
			DX03 A 1 A_FadeOut
			Goto Done+1
	}
}

Actor DeusEx_ShellCasing2 : DeusEx_ShellCasing1 { }
Actor DeusEx_ShellCasing3 : DeusEx_ShellCasing1 { }

Actor DeusEx_Smoke
{
	Height 4
	Radius 2
	Renderstyle Add
	+NOGRAVITY
	+NOINTERACTION
	+FORCEXYBILLBOARD
	States
	{
		Spawn:
			DX10 ABCDEFGH 2
			DX10 ABCDEFGH 2 A_FadeOut(0.0416666667)
			DX10 ABCDEFGH 2 A_FadeOut(0.0416666667)
			DX10 ABCDEFGH 2 A_FadeOut(0.0416666667)
			Stop
	}
}

Actor DeusEx_GrenadeSmoke : DeusEx_Smoke { Scale 0.33 }
Actor DeusEx_RocketSmoke : DeusEx_Smoke { Scale 0.5 }
Actor DeusEx_LAMSmoke : DeusEx_Smoke { Scale 2.0 }

Actor DeusEx_SmokeTrail
{
	Height 4
	Radius 2
	Renderstyle Add
	Scale 0.0
	+NOGRAVITY
	+NOINTERACTION
	+FORCEXYBILLBOARD
	States
	{
		Spawn:
			DX10 ABCDEFGH 2 A_SetScale(scaleX+0.125)
			DX10 ABCDEFGH 2 A_FadeOut(0.0416666667)
			DX10 ABCDEFGH 2 A_FadeOut(0.0416666667)
			DX10 ABCDEFGH 2 A_FadeOut(0.0416666667)
			Stop
	}
}


Actor DeusEx_PlasmaSmoke
{
	+CLIENTSIDEONLY
	+NOEXTREMEDEATH
	+NOINTERACTION
	+FORCEXYBILLBOARD
	+BRIGHT
	RenderStyle Add
	Scale 0.5
	Mass 5
	States
	{
		Spawn:
			DX04 IIIIJJJJKKKKLLLLMMMMNNNN 1 A_SetScale(Scalex+0.05, scaley+0.05)
			DX04 OOOOPPPP 1 A_FadeOut(0.125)
			Stop
	}
}

Actor DeusEx_PlasmaSmoke2 : DeusEx_PlasmaSmoke
{
	States
	{
		Spawn:
			DX04 IIIIJJJJKKKKLLLLMMMMNNNN 2 A_SetScale(Scalex+0.05, scaley+0.05)
			DX04 OOOOPPPP 2 A_FadeOut(0.125)
			Stop
	}
}

Actor DeusEx_PlasmaParticle
{
	+CLIENTSIDEONLY
	+NOEXTREMEDEATH
	+NOINTERACTION
	+FORCEXYBILLBOARD
	+BRIGHT
	RenderStyle Add
	Scale 0.125
	States
	{
		Spawn:
			DX11 A 2 A_FadeOut(0.2)
			Wait
	}
}

Actor DeusEx_ShockWaveSpawner
{
	+CLIENTSIDEONLY
	+NOINTERACTION
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 A 0 A_SpawnItemEx("DeusEx_ShockWave",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_CLIENTSIDE)
			Goto SpawnEnd
		SpawnEnd:
			TNT1 ABCDEFGHI 2
			TNT1 J 1
			Stop
	}
}

Actor DeusEx_ShockWaveBigSpawner : DeusEx_ShockWaveSpawner
{
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 A 0 A_SpawnItemEx("DeusEx_ShockWaveBig",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_CLIENTSIDE)
			Goto Super::SpawnEnd
	}
}

Actor DeusEx_ShockWaveHugeSpawner : DeusEx_ShockWaveSpawner
{
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 A 0 A_SpawnItemEx("DeusEx_ShockWaveHuge",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_CLIENTSIDE)
			Goto Super::SpawnEnd
	}
}

Actor DeusEx_GreenShockWaveHugeSpawner : DeusEx_ShockWaveSpawner
{
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 A 0 A_SpawnItemEx("DeusEx_GreenShockWaveHuge",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_CLIENTSIDE)
			Goto Super::SpawnEnd
	}
}

Actor DeusEx_ShockWave
{
	+CLIENTSIDEONLY
	+NOINTERACTION
	+BRIGHT
	RenderStyle Add
	Alpha 0.5
	Mass 5
	States
	{
		Spawn:
			DX00 AAAAAAAAAAAAAAAAAAAA 1 A_SetScale(Scalex+0.05)
			Stop
	}
}

Actor DeusEx_ShockWaveBig : DeusEx_ShockWave
{
	Scale 1.5
	States
	{
		Spawn:
			DX00 AAAAAAAAAAAAAAAAAAAA 1 A_SetScale(Scalex+0.10)
			Stop
	}
}
Actor DeusEx_ShockWaveHuge : DeusEx_ShockWave
{
	Scale 2.5
	States
	{
		Spawn:
			DX00 AAAAAAAAAAAAAAAAAAAA 1 A_SetScale(Scalex+0.25)
			Stop
	}
}

Actor DeusEx_GreenShockWaveHuge : DeusEx_ShockWaveHuge { }

Actor DeusEx_FireComet
{
	PROJECTILE
	+THRUACTORS
	-NOGRAVITY
	+NOBLOCKMAP
	+BRIGHT
	Renderstyle Add
	Radius 5
	Height 5
	Speed 20
	Gravity 0.5
	Damage (0)
	Decal "DeusExCometDecal"
	States
	{
		Spawn:
			DX00 A 1
			Loop
		Death:
			TNT1 A 0 A_CheckCeiling(1)
			Goto Death+2
			TNT1 A 0 A_SpawnItemEx("DeusEx_CometBurnCeiling")
			TNT1 A 0 A_CheckFloor(1)
			Goto Death+4
			TNT1 A 0 A_SpawnItemEx("DeusEx_CometBurnFloor")
			TNT1 A 1
			Stop
	}
}

Actor DeusEx_FireCometLAM : DeusEx_FireComet
{
	States
	{
		Death:
			TNT1 A 0 A_CheckCeiling(1)
			Goto Death+2
			TNT1 A 0 A_SpawnItemEx("DeusEx_CometBurnCeiling")
			TNT1 A 0 A_CheckFloor(1)
			Goto Death+4
			TNT1 A 0 A_SpawnItemEx("DeusEx_CometBurnFloor")
			TNT1 A 1 A_SpawnItemEx("DeusEx_LAMSmoke")
			Stop
	}
}

Actor DeusEx_ExplosionCraterFloor
{
	Radius 12
	Height 2
	Mass 1
	Renderstyle Translucent
	+NOINTERACTION
	+FLOORHUGGER
	-NOGRAVITY
	+THRUACTORS
	States
	{
		Spawn:
			DX00 A 150
			DX00 A 1 A_FadeOut(0.05)
			Wait
		Remove:
			TNT1 A 1
			Stop
	}
}

Actor DeusEx_ExplosionCraterCeiling : DeusEx_ExplosionCraterFloor { +SPAWNCEILING }
Actor DeusEx_CometBurnFloor : DeusEx_ExplosionCraterFloor { Scale 0.33 }
Actor DeusEx_CometBurnCeiling : DeusEx_CometBurnFloor {}