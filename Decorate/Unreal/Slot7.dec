/*Actor Unreal_BFGReplacer replaces BFG9000
{
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 A 0 A_SpawnItemEx("Unreal_Redeemer",0,0,0,frandom(-0.25,0.25),frandom(-0.25,0.25))
			TNT1 A 0 A_SpawnItemEx("Unreal_QuadShot",0,0,0,frandom(-0.25,0.25),frandom(-0.25,0.25))
			Stop
	}
}
*/

Actor Unreal_RedeemerLoad : Boolean { }
Actor Unreal_Redeemer : UnrealWeapon 
{
	Tag "'Redeemer' Missile Launcher"
	Weapon.SlotNumber 7
	Weapon.AmmoType1 "Cell"
	Weapon.AmmoUse1 6
	Weapon.AmmoGive1 60
	Weapon.AmmoType2 "Cell"
	Weapon.AmmoUse2 60
	Weapon.AmmoGive2 60
	Weapon.UpSound "Unreal/RocketL/Select"
	Inventory.PickupMessage "You got the Redeemer."	
	Obituary "%o was vaporized by %k's Redeemer!!"
	States
	{
		Spawn:
			UN00 A 10 BRIGHT
			UN00 A 10
			Loop
		Ready:
			UN05 A 0 A_GiveInventory("Unreal_RedeemerSelected",1)
			UN05 ABCDEFG 2 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
			Goto WIdle
		WIdle:
			//UN01 O 0 A_GunFlash("ReadyFlash",GFF_NOEXTCHANGE)
			UN01 O 0 A_Jump(6, "PigeonThrust", "Sway")
			UN01 O 1 A_WeaponReady
			Loop
		ReadyFlash:
			TNT1 A 1 A_FireBullets(1,1,1,0, "Unreal_RazorBlankPuff", FBF_NORANDOM|FBF_NOFLASH|FBF_EXPLICITANGLE)
			Stop
		Deselect:
			UN02 A 0 A_TakeInventory("Unreal_RedeemerSelected",1)
			UN02 ABCDEFGHIJ 1
			Goto InstantDeselect
		InstantDeselect: // I have to copy paste this everytime. Thanks, Zandronum.
			TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_Lower
			TNT1 A 1 A_Lower
			Wait	
		PigeonThrust:
			//UN06 A 0 A_GunFlash("PigeonFlash",GFF_NOEXTCHANGE)
			UN06 ABCABC 12 A_WeaponReady
			Goto WIdle
		PigeonFlash:
			TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 2 A_FireBullets(1,1,1,0, "Unreal_RazorBlankPuff", FBF_NORANDOM|FBF_NOFLASH|FBF_EXPLICITANGLE)
			Stop
		Sway:
			//UN07 A 0 A_GunFlash("PigeonFlash",GFF_NOEXTCHANGE)
			UN07 ABABAB 12 A_WeaponReady
			Goto WIdle
		Fire:
			UN03 A 0 ACS_NamedExecuteWithResult("Unreal_Recoil")
			UN03 A 5 BRIGHT A_FireCustomMissile("Unreal_UMSRocket", 0, 1, 13)
			UN03 A 0 BRIGHT A_PlaySound("Unreal/RocketL/Fire", CHAN_WEAPON)
			UN03 B 5 BRIGHT 
			UN03 CDEFGHIJKLM 2 BRIGHT
			Goto WIdle
		AltFire:
			UN03 A 0 ACS_NamedExecuteWithResult("Unreal_Recoil")
			UN03 A 5 BRIGHT A_FireCustomMissile("Unreal_UMSRocket2", 0, 1, 13)
			UN03 A 0 BRIGHT A_PlaySound("Unreal/RocketL/Fire", CHAN_WEAPON)
			UN03 B 5 BRIGHT //A_FireBullets(1,1,1,0, "Unreal_RazorBlankPuff", FBF_NORANDOM|FBF_NOFLASH|FBF_EXPLICITANGLE)
			UN03 CDEFGHIJKLM 2 BRIGHT //A_FireBullets(1,1,1,0, "Unreal_RazorBlankPuff", FBF_NORANDOM|FBF_NOFLASH|FBF_EXPLICITANGLE)
		AltFireLoop:
			UN03 A 2 A_JumpIfInventory("Unreal_RedeemerLoad",1,"Load")
			Loop
		Load:
			UN01 A 0 A_TakeInventory("Unreal_RedeemerLoad",1)
			UN01 ABCDEFGHIJKLMN 2 //A_FireBullets(1,1,1,0, "Unreal_RazorBlankPuff", FBF_NORANDOM|FBF_NOFLASH|FBF_EXPLICITANGLE)
			Goto WIdle
	}
}	

Actor Unreal_UMSRocket
{
	Projectile
	Speed 55
	Radius 5
	Height 5
	Scale 2.0
	Damage (300)
	+FORCERADIUSDMG
	DamageType "UnrealExplosive"
	DeathSound "Unreal/Flak/Explode2"
	States
	{
		Spawn:
			UN00 A 1 A_PlaySound("Unreal/RocketL/Loop1", CHAN_BODY, 1.0, True)
			UN00 A 0 ACS_NamedExecuteWithResult("Unreal_RedeemerPitch")
			UN00 BC 1 
		SpawnLoop:	
			UN00 D 0 A_SpawnItemEx("UnrealSmoke",-32*cos(pitch),0,-32*sin(-pitch),0,0,0)
			UN00 D 1 A_SpawnItemEx("UnrealSmoke",-64*cos(pitch),0,-64*sin(-pitch),0,0,0)
			Loop
		Death:
			TNT1 A 0 A_StopSound(CHAN_BODY)
			TNT1 A 0 A_Explode(300, 180)
			TNT1 AAA 0 A_SpawnItemEX("Unreal_RedeemerExplosionEffect", Random(0,4),Random(0,4),Random(0,4),0,0,0,0, SXF_NOCHECKPOSITION)
			TNT1 A 0 A_SpawnItemEX("Unreal_RedeemerExplosionEffect2",0,0,0,0,0,0,0, SXF_NOCHECKPOSITION)
			TNT1 A 5 A_SpawnItemEx("Unreal_YellowShockwave", 0,0,0,0,0,0,0, SXF_NOCHECKPOSITION)
			Stop
	}
}	

Actor Unreal_UMSRocket2 : Unreal_UMSRocket
{
	Speed 16
	Damage (1500)
	States
	{
		Spawn:
			UN00 A 0 NoDelay ACS_NamedExecuteWithResult("Unreal_RedeemerRocket")
			UN00 A 0 ACS_NamedExecuteWithResult("Unreal_RedeemerPitch")
			UN00 A 1 A_PlaySound("Unreal/RocketL/Loop1", CHAN_BODY, 1.0, True)
			//UN00 A 0 ACS_NamedExecuteAlways("rocketFollow",0,0,0,0)
			UN00 BC 1 
		SpawnLoop:	
			UN00 D 0 A_SpawnItemEx("UnrealSmoke",-32*cos(pitch),0,-32*sin(-pitch),0,0,0)
			UN00 D 1 A_SpawnItemEx("UnrealSmoke",-64*cos(pitch),0,-64*sin(-pitch),0,0,0)
			Loop
		Death:
			TNT1 A 0 A_StopSound(CHAN_BODY)
			TNT1 A 0 A_Explode(1500, 360)
			TNT1 AAA 0 A_SpawnItemEX("Unreal_EightballExplosiveEffect", Random(0,4),Random(0,4),Random(0,4),0,0,0,0, SXF_NOCHECKPOSITION)
			TNT1 A 0 A_SpawnItemEX("Unreal_RedeemerExplosionEffect2",0,0,0,0,0,0,0, SXF_NOCHECKPOSITION)
			TNT1 A 5 A_SpawnItemEx("Unreal_YellowShockwave2", 0,0,0,0,0,0,0, SXF_NOCHECKPOSITION)
			Stop
	}
}	

Actor Unreal_RedeemerExplosionEffect : Unreal_EightballExplosiveEffect
{
	States
	{
		Spawn:
			UN18 ABCDEFG 2 BRIGHT A_SetScale(scalex+0.5)
			Stop
		}
} 

Actor Unreal_RedeemerExplosionEffect2 : Unreal_RedeemerExplosionEffect
{
	Scale 1.0
	States
	{
		Spawn:
			UN27 AAABBBCCCDDDEEEFFFGGGHHH 1 BRIGHT A_SetScale(scalex+0.333)
			Stop
		}
} 
	
Actor Unreal_YellowShockwave
{
	+CLIENTSIDEONLY
	+NOGRAVITY
	RenderStyle Add
	Alpha 0.5
	Scale 3.5
	States
	{
		Spawn:
			UN00 ABCDEF 1 BRIGHT
			Stop
	}
}	

Actor Unreal_YellowShockwave2 : Unreal_YellowShockwave
{ 
	Scale 7.0 
	States
	{
		Spawn:
			UN00 ABCDEF 1 BRIGHT
			Stop
	}
}	

Actor Unreal_QuadShotLoaded : Inventory { Inventory.MaxAmount 3	Inventory.InterHubAmount 0 +Inventory.UNDROPPABLE  -INVBAR }

Actor Unreal_QuadShot : UnrealWeapon
{
	Tag "QuadShot"
	Weapon.SlotNumber 8
	Weapon.AmmoType "Shell"
	Weapon.AmmoUse1 3
	Weapon.AmmoGive 24
	Weapon.AmmoType2 "Shell"
	Weapon.AmmoUse2 2
	Weapon.UpSound "Unreal/QuadShot/Reload"
	Inventory.PickupMessage "You got the QuadShot."	
	Obituary "%o was turned into ground beef by %k's QuadShot!"
	States
	{
		Spawn:
			UN00 A 10 BRIGHT
			UN00 A 10
			Loop	
		Select:
			TNT1 AAAAAAAAAAAAAAAAAAAA 0 A_Raise
			TNT1 A 1 A_Raise
			loop			
		Ready:
			UN02 ABCDEFGHIJ 2
			Goto Widle
		WIdle:
			UN01 A 1 A_WeaponReady
			Loop
		Deselect:
			UN03 ABCD 2 
			Goto InstantDeselect
		InstantDeselect: // I have to copy paste this everytime. Thanks, Zandronum.
			TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_Lower
			TNT1 A 1 A_Lower
			Wait	
		Fire:
			UN01 B 3 BRIGHT A_FireBullets(6,6, 7, 25, "UnrealPuff", FBF_NORANDOM|FBF_USEAMMO)
			UN01 C 0 BRIGHT A_PlaySound("Unreal/QuadShot/Fire", CHAN_WEAPON)
			UN01 CDE 3 BRIGHT
			Goto ShotgunPump
		ShotgunPump:
			UN02 ABCD 2
			UN02 E 2 A_PlaySound("Unreal/QuadShot/Reload", CHAN_WEAPON)
			UN02 FGHIJ 2
			Goto WIdle
		AltFire:
			UN02 AB 2
			Goto AltHold
		AltHold:
			UN02 C 0 A_JumpIfInventory("Shell", 3, 1)
			Goto Release
			UN02 C 0 A_JumpIfInventory("Unreal_QuadShotLoaded", 3, "Release")
			UN02 CD 2
			UN02 E 2 A_PlaySound("Unreal/QuadShot/Reload", CHAN_WEAPON)
			UN02 F 2 A_GiveInventory("Unreal_QuadShotLoaded", 1)
			UN02 G 2 A_TakeInventory("Shell", 3, TIF_NOTAKEINFINITE)
			UN02 HI 2
			UN02 J 2 A_Refire
			Goto Release
		Release:
			UN01 A 0 A_JumpIfInventory("Unreal_QuadShotLoaded", 3, "FourBarrels")
			UN01 A 0 A_JumpIfInventory("Unreal_QuadShotLoaded", 2, "ThreeBarrels")
			UN01 A 0 A_JumpIfInventory("Unreal_QuadShotLoaded", 1, "TwoBarrels")
			Goto OneBarrel
		OneBarrel:
			UN01 B 3 BRIGHT A_FireBullets(4,4, 7, 25, "UnrealPuff", FBF_NORANDOM|FBF_USEAMMO)
			UN01 C 0 BRIGHT A_PlaySound("Unreal/QuadShot/Fire", CHAN_WEAPON)
			UN01 C 0 BRIGHT A_TakeInventory("Unreal_QuadShotLoaded", 4)
			UN01 CDE 3 BRIGHT
			Goto ShotgunPump		
		TwoBarrels:
			UN01 B 3 BRIGHT A_FireBullets(6,6, 14, 25, "UnrealPuff", FBF_NORANDOM|FBF_USEAMMO)
			UN01 C 0 BRIGHT A_PlaySound("Unreal/QuadShot/Fire", CHAN_WEAPON)
			UN01 C 0 BRIGHT A_TakeInventory("Unreal_QuadShotLoaded", 4)
			UN01 CDE 3 BRIGHT
			Goto ShotgunPump			
		ThreeBarrels:
			UN01 B 3 BRIGHT A_FireBullets(8,8, 21, 25, "UnrealExtremePuff", FBF_NORANDOM|FBF_USEAMMO)
			UN01 C 0 BRIGHT A_PlaySound("Unreal/QuadShot/Fire", CHAN_WEAPON)
			UN01 C 0 BRIGHT A_TakeInventory("Unreal_QuadShotLoaded", 4)
			UN01 CDE 3 BRIGHT
			Goto ShotgunPump			
		FourBarrels:
			UN01 B 3 BRIGHT A_FireBullets(10,10, 28, 25, "UnrealExtremePuff", FBF_NORANDOM|FBF_USEAMMO)
			UN01 C 0 BRIGHT A_PlaySound("Unreal/QuadShot/Fire", CHAN_WEAPON)
			UN01 C 0 BRIGHT A_TakeInventory("Unreal_QuadShotLoaded", 4)
			UN01 CDE 3 BRIGHT
			Goto ShotgunPump					
		}
}		