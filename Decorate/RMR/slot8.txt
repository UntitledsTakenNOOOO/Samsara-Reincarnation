ACTOR "Subestron Arm" : Weapon
{
  Weapon.SelectionOrder 600
  Weapon.SlotNumber 8
  Weapon.BobStyle Inverse
  Weapon.BobRangeX 0.85
  Weapon.BobSpeed 1.1  
  Weapon.AmmoUse 8
  Weapon.AmmoGive 10
  Weapon.AmmoType "Cell"
  Inventory.PickupSound "RMR/pickup" 	  
  Inventory.PickupMessage "You got a Mechanical Weapon Arm!"
  Inventory.RestrictedTo "RebelMoonCommando"  
  States
  {
  Ready:
    RM2X E 1 A_WeaponReady
    Loop
  Deselect:
	TNT1 A 0 A_Lower
    RM2X E 1 A_Lower
	Loop
  Select:
	TNT1 A 0 A_Raise  
    RM2X E 1 A_Raise
    Loop
  Fire:   
    TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"CoopFire")  
    TNT1 A 0 A_PlaySound("RMR/RPlasma")
	RM2X F 10 bright A_FireCustomMissile("RMR-RedPlasma",0,1,0,0)
	RM2X G 10 bright
 	RM2X G 1 bright A_Refire
    Goto Ready
  CoopFire:
    TNT1 A 0 A_PlaySound("RMR/RPlasma")
	RM2X F 10 bright A_FireCustomMissile("RMR-RedPlasmaCoop",0,1,0,0)
	RM2X G 10 bright 	
 	RM2X G 1 bright A_Refire	
	Goto Ready
  Spawn:
	RM2X H 0 Bright
	TNT1 A 1 Bright A_SpawnItemEx("SamsaraSlotSevenPickup")
	Stop
  }
}

Actor RMR-RedPlasma : FastProjectile
{
    Damage (180)
    Radius 5
    Height 5
    Speed 115
    Projectile
	+WINDTHRUST
	+THRUACTORS
//	+NOBLOOD
	-SKYEXPLODE
	+FORCERADIUSDMG
//	+BLOODLESSIMPACT	
	DeathSound "RMR/RPlasmaB"
    DamageType "RMR-BigPlasma"		
	Obituary "%k killed %o with spicy doritos."
	Scale 0.50
    States
    {
      Spawn:
		TNT1 A 0
		TNT1 A 0 Bright A_JumpIf(CallACS("Samsara_ProjectilePredictor",3)==1,1)
        RMZ3 GH 1 bright ACS_NamedExecuteAlways("Samsara_ProjectilePredictor",0,3,0,0)
        loop
      Death:
        RMZ3 IJK 4 bright
		RMZ3 L 3 bright A_FadeOut
        stop
	  XDeath:
		TNT1 A 0 A_Explode(400,60,!XF_HURTSOURCE,1,60)
        RMZ3 IJK 4 bright
		RMZ3 L 3 bright A_FadeOut
        stop		
    }
}

Actor RMR-RedPlasma_Explosion
{
	Projectile
	Height 3
	Radius 3
	Speed 0
	DamageType "RMR-BigPlasma"	
	Obituary "%k killed %o with spicy doritos."
	+NOINTERACTION
	+FORCERADIUSDMG
	+NODAMAGETHRUST
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 A 0 A_Explode(Random(5,9),5,!XF_HURTSOURCE,0,5)
			TNT1 A 1
			Stop
	}
}

Actor RMR-RedPlasmaCoop : RMR-RedPlasma { Species "Player" +THRUSPECIES }