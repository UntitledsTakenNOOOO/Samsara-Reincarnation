actor "Elven Wand" : Weapon
{
    +BLOODSPLATTER
    +PUFFONACTORS
    +WEAPON.WIMPY_WEAPON
    +INVENTORY.UNDROPPABLE
    +NOALERT
    Inventory.PickUpSound "heretic/weaponget"
    Weapon.SelectionOrder 2000
    Weapon.SlotNumber 1
    Weapon.SlotPriority 1
    Inventory.PickupMessage "You got the Elven Wand!"
    Weapon.AmmoUse 0
    Weapon.AmmoGive 0
    Decal RailScorchLower
    Obituary "$SAMSARA_CORVUS_OB_FALLBACKRANGED"
    Weapon.YAdjust 5
    Inventory.RestrictedTo "CorvusPlayer"
    Tag "Elven Wand"
    States
    {
      Ready:
		TNT1 A 0 A_TakeInventory("CorvusHoldingSlot")
		GWND A 1 A_WeaponReady
        loop

      Deselect:
        GWND A 1 A_Lower
        loop

      Select:
        GWND A 1 A_Raise
        loop

      // Unpowered
      Fire:
        TNT1 A 0 A_JumpIfInventory("PowerHereticTome",1,"FirePowered")
		TNT1 A 0 A_JumpIfInventory("SamsaraHasPermaWeaponLevel2",1,"FirePowered")
      FireUnpowered:
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto FireStartUnpowered
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireStartUnpowered")
        TNT1 A 0 A_JumpIfInventory("Clip",1,"FireStartUnpowered")
        goto NoAmmoSwitch

      FireStartUnpowered:
        TNT1 A 0 A_AlertMonsters
        GWND B 3 Bright A_GiveInventory("CorvusShootCloneTrigger")
        TNT1 A 0 A_GiveInventory("SamsaraHereticGoldWandAttackHandler")
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"FireContinueUnpoweredCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"FireContinueUnpoweredCoop")
	  FireContinueUnpoweredDM:
        GWND C 4 Bright
        GWND D 2
        goto FireFinishUnpowered

	  FireContinueUnpoweredCoop:
        GWND C 5 Bright
        GWND D 3
        goto FireFinishUnpowered

      FireFinishUnpowered:
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto FireFinishReFire
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireFinishReFire")
        TNT1 A 0 A_JumpIfInventory("Clip",1,"FireFinishReFire")
        goto NoAmmoSwitch

      FireFinishReFire:
        TNT1 A 0 A_ReFire
        goto Ready

      // Powered
      FirePowered:
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto FireStartPowered
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireStartPowered")
        TNT1 A 0 A_JumpIfInventory("Clip",3,"FireStartPowered")
        goto NoAmmoSwitch

      FireStartPowered:
        TNT1 A 0 A_AlertMonsters
        GWND B 3 Bright A_GiveInventory("CorvusShootCloneTrigger")
        GWND C 4 Bright A_GiveInventory("SamsaraHereticGoldWandPoweredAttackHandler")
        GWND D 3
      FireFinishPowered:
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto FireFinishReFire
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireFinishReFire")
        TNT1 A 0 A_JumpIfInventory("Clip",3,"FireFinishReFire")
        goto NoAmmoSwitch

      // "No ammo" switch
      NoAmmoSwitch:
        TNT1 A 0 A_ClearReFire
        TNT1 A 0 A_SelectWeapon(" Staff ")
        goto Deselect
    }
}

// Unpowered

actor SamsaraHereticGoldWandAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto PickupAttack
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Clip",1,1)
        stop
        TNT1 A 0 A_TakeInventory("Clip",1)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("heretic/wand",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireBullets(5.6,0,1,random(1,6)+12,"GoldWandPuff3",FBF_NORANDOM|FBF_NOFLASH) // Previously random(1,8)+6
        stop

      PickupAttackCoop:
        TNT1 A 0 A_FireBullets(5.6,0,1,random(1,8)+6,"GoldWandPuff3Coop",FBF_NORANDOM|FBF_NOFLASH)
        stop
    }
}

actor GoldWandPuff3 : GoldWandPuff1
{
    DamageType "Pistol"
    +CLIENTSIDEONLY
    States
    {
      Spawn:
        PUF2 A 3 Bright Light("WANDPUFF1")
        PUF2 B 3 Bright Light("WANDPUFF2")
        PUF2 C 3 Bright Light("WANDPUFF3")
        PUF2 D 3 Bright Light("WANDPUFF4")
        PUF2 E 3 Bright
        stop
    }
}

actor GoldWandPuff3Coop : GoldWandPuff3 { +MTHRUSPECIES }

// Powered

actor SamsaraHereticGoldWandPoweredAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto PickupAttack
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Clip",3,1)
        stop
        TNT1 A 0 A_TakeInventory("Clip",3)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("heretic/wand",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireCustomMissile("GoldWandFX3",-5.6,false,0,1)
        TNT1 A 0 A_FireCustomMissile("GoldWandFX3",5.6,false,0,1)
        TNT1 A 0 A_FireBullets(-5.625,0,-1,random(2,12),"GoldWandPuff4",FBF_NORANDOM|FBF_EXPLICITANGLE|FBF_NOFLASH)
        TNT1 A 0 A_FireBullets(-2.8125,0,-1,random(2,12),"GoldWandPuff4",FBF_NORANDOM|FBF_EXPLICITANGLE|FBF_NOFLASH)
        TNT1 A 0 A_FireBullets(0,0,-1,random(2,12),"GoldWandPuff4",FBF_NORANDOM|FBF_EXPLICITANGLE|FBF_NOFLASH)
        TNT1 A 0 A_FireBullets(2.8125,0,-1,random(2,12),"GoldWandPuff4",FBF_NORANDOM|FBF_EXPLICITANGLE|FBF_NOFLASH)
        TNT1 A 0 A_FireBullets(5.625,0,-1,random(2,12),"GoldWandPuff4",FBF_NORANDOM|FBF_EXPLICITANGLE|FBF_NOFLASH)
        stop

      PickupAttackCoop:
        TNT1 A 0 A_FireCustomMissile("GoldWandFX32",-5.6,false,0,1)
        TNT1 A 0 A_FireCustomMissile("GoldWandFX32",5.6,false,0,1)
        TNT1 A 0 A_FireBullets(-5.625,0,-1,random(2,12),"GoldWandPuff4Coop",FBF_NORANDOM|FBF_EXPLICITANGLE|FBF_NOFLASH)
        TNT1 A 0 A_FireBullets(-2.8125,0,-1,random(2,12),"GoldWandPuff4Coop",FBF_NORANDOM|FBF_EXPLICITANGLE|FBF_NOFLASH)
        TNT1 A 0 A_FireBullets(0,0,-1,random(2,12),"GoldWandPuff4Coop",FBF_NORANDOM|FBF_EXPLICITANGLE|FBF_NOFLASH)
        TNT1 A 0 A_FireBullets(2.8125,0,-1,random(2,12),"GoldWandPuff4Coop",FBF_NORANDOM|FBF_EXPLICITANGLE|FBF_NOFLASH)
        TNT1 A 0 A_FireBullets(5.625,0,-1,random(2,12),"GoldWandPuff4Coop",FBF_NORANDOM|FBF_EXPLICITANGLE|FBF_NOFLASH)
        stop
    }
}

actor GoldWandFX3 : GoldWandFX2
{
    Decal HImpScorch
    DamageType "Pistol"
    Obituary "$SAMSARA_CORVUS_OB_FALLBACKRANGED"
    +BLOODSPLATTER
    States
    {
      Spawn:
        FX01 CD 6 Bright Light("WANDBALL")
        loop

      Death:
        FX01 E 3 Bright Light("WANDBALL_X1")
        FX01 F 3 Bright Light("WANDBALL_X2")
        FX01 G 3 Bright Light("WANDBALL_X3")
        FX01 H 3 Bright Light("WANDBALL_X4")
        stop
    }
}

actor GoldWandFX32 : GoldWandFX3
{
    Species "Player"
    +THRUSPECIES
}

actor GoldWandPuff4 : GoldWandPuff2
{
    DamageType "Pistol"
    +CLIENTSIDEONLY
    States
    {
      Spawn:
        FX01 E 3 Bright Light("WANDBALL_X1")
        FX01 F 3 Bright Light("WANDBALL_X2")
        FX01 G 3 Bright Light("WANDBALL_X3")
        FX01 H 3 Bright Light("WANDBALL_X4")
        stop
    }
}

actor GoldWandPuff4Coop : GoldWandPuff4 { +MTHRUSPECIES }
