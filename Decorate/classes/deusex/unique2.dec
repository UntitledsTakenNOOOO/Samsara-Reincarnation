Actor DeusEx_SniperRifle : DeusExWeapon
{
	Weapon.SelectionOrder 1600
	Weapon.SlotNumber 2
	Weapon.SlotPriority 2
	Weapon.AmmoType1 "DeusEx_SniperMagazine"
	Weapon.AmmoType2 "Shell"
	Weapon.AmmoUse1 1
	Weapon.AmmoGive2 8
	Tag "Sniper Rifle"
	Inventory.PickupMessage "You found a Sniper Rifle."
	Obituary "%o was sniped by %k."
	States
	{
		Spawn:
			DX99 A -1
			stop

		ReadyActual:
			DX00 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"ReadyActualReloadable")
		ReadyActualNonReloadable:
			DX00 A 0 A_JumpIfInventory("DeusEx_Zoomed",1,2)
			DX00 A 1 A_WeaponReady(WRF_ALLOWZOOM)
			goto ReadyActualJump
			TNT1 A 1 A_WeaponReady(WRF_ALLOWZOOM)
			goto ReadyActualJump

		ReadyActualReloadable:
			DX00 A 0 A_JumpIfInventory("DeusEx_Zoomed",1,2)
			DX00 A 1 A_WeaponReady(WRF_ALLOWRELOAD|WRF_ALLOWZOOM)
			goto ReadyActualJump
			TNT1 A 1 A_WeaponReady(WRF_ALLOWRELOAD|WRF_ALLOWZOOM)
			goto ReadyActualJump

		ReadyActualJump:
			DX00 A 0 A_JumpIfInventory("DeusEx_Zoomed",1,"ReadyActualAmmoCheck")
			DX00 A 0 A_Jump(3,"ReadyActualIdle1","ReadyActualIdle2","ReadyActualIdle3")
			goto ReadyActualAmmoCheck

		ReadyActualIdle1:
			DX01 H 0 A_JumpIfInventory("SamsaraReloadMode",1,"ReadyActualIdle1Reloadable")
		ReadyActualIdle1NonReloadable:
			DX01 HIJKLMNOPQ 6 A_WeaponReady(WRF_ALLOWZOOM)
			goto ReadyActual

		ReadyActualIdle1Reloadable:
			DX01 HIJKLMNOPQ 6 A_WeaponReady(WRF_ALLOWRELOAD|WRF_ALLOWZOOM)
			goto ReadyActual

		ReadyActualIdle2:
			DX00 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"ReadyActualIdle2Reloadable")
		ReadyActualIdle2NonReloadable:
			DX00 A 6 A_WeaponReady(WRF_ALLOWZOOM)
			DX01 "RSTUVWXYZ[" 6 A_WeaponReady(WRF_ALLOWZOOM)
			goto ReadyActual

		ReadyActualIdle2Reloadable:
			DX00 A 6 A_WeaponReady(WRF_ALLOWRELOAD|WRF_ALLOWZOOM)
			DX01 "RSTUVWXYZ[" 6 A_WeaponReady(WRF_ALLOWRELOAD|WRF_ALLOWZOOM)
			goto ReadyActual

		ReadyActualIdle3:
			DX00 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"ReadyActualIdle3Reloadable")
		ReadyActualIdle3NonReloadable:
			DX00 A 6 A_WeaponReady(WRF_ALLOWZOOM)
			DX01 "\]" 6 A_WeaponReady(WRF_ALLOWZOOM)
			DX02 ABCDEFGH 6 A_WeaponReady(WRF_ALLOWZOOM)
			goto ReadyActual

		ReadyActualIdle3Reloadable:
			DX00 A 6 A_WeaponReady(WRF_ALLOWRELOAD|WRF_ALLOWZOOM)
			DX01 "\]" 6 A_WeaponReady(WRF_ALLOWRELOAD|WRF_ALLOWZOOM)
			DX02 ABCDEFGH 6 A_WeaponReady(WRF_ALLOWRELOAD|WRF_ALLOWZOOM)
			goto ReadyActual

		ReadyActualAmmoCheck:
			DX00 A 0 A_JumpIfInventory("SamsaraReloadMode",1,1)
			goto ReadyActual
			DX00 A 0 A_JumpIfInventory("SamsaraBottomlessMagazines",1,"ReadyActual")
			DX00 A 0 A_JumpIfInventory("DeusEx_SniperMagazine",1,"ReadyActual")
			goto ReloadCheck

		Deselect:
			DX00 R 0 A_GiveInventory("DeusEx_Reloading")
			DX00 R 0 A_JumpIfHealthLower(1,"DeathDeselect")
			DX00 R 0 A_JumpIfInventory("DeusEx_Zoomed",1,1)
			goto DeselectStart
			DX00 R 0 A_TakeInventory("DeusEx_Zoomed")
			DX00 R 0 A_ZoomFactor(1)
			goto DeselectStart

		DeselectStart:
			DX00 R 4
			DX00 S 0
			TNT1 A 4
			goto DeselectFinish

		DeathDeselect:
			TNT1 A 0 A_JumpIfInventory("DeusEx_Zoomed",1,1)
			goto DeathDeselectStart
			TNT1 A 0 A_TakeInventory("DeusEx_Zoomed")
			TNT1 A 0 A_ZoomFactor(1,ZOOM_INSTANT)
			goto DeathDeselectStart

		DeathDeselectStart:
			TNT1 AA 4
			goto DeselectFinish

		DeselectFinish:
			TNT1 A 0 A_TakeInventory("DeusEx_SniperEquipped")
			goto DeselectInstant

		Ready: // our "Select" state
			DX00 B 0 A_PlaySound("DeusEx/Sniper/Select")
			DX00 B 0 A_GiveInventory("DeusEx_SniperEquipped")
			DX00 B 0 A_JumpIfInventory("DeusEx_Zoomed",1,"ReadyZoomOut1") // map change hack
		ReadyStart1:
			DX00 B 1 A_JumpIfInventory("SamsaraHasDoubleFiringSpeed",1,1)
			DX00 B 0 A_JumpIfInventory("DeusEx_Zoomed",1,"ReadyZoomOut2") // map change hack
		ReadyStart2:
			DX00 B 1
			DX00 CDEFGHIJK 2
			DX00 A 0 A_TakeInventory("DeusEx_Reloading")
			goto ReadyActual

		ReadyZoomOut1:
			DX00 B 0 A_TakeInventory("DeusEx_Zoomed")
			DX00 B 0 A_ZoomFactor(1,ZOOM_INSTANT)
			goto ReadyStart1

		ReadyZoomOut2:
			DX00 B 0 A_TakeInventory("DeusEx_Zoomed")
			DX00 B 0 A_ZoomFactor(1,ZOOM_INSTANT)
			goto ReadyStart2

		Fire:
			DX00 L 0 A_JumpIfInventory("SamsaraReloadMode",1,"FireReloadable")
		FireNonReloadable:
			DX00 L 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireWaterCheck")
			DX00 L 0 A_JumpIfInventory("Shell",1,"FireWaterCheck")
			goto FireDry

		FireReloadable:
			DX00 L 0 A_JumpIfInventory("SamsaraBottomlessMagazines",1,"FireWaterCheck")
			DX00 L 0 A_JumpIfInventory("DeusEx_SniperMagazine",1,"FireWaterCheck")
		FireReloadableDryCheck:
			DX00 L 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"ReloadScopeCheck")
			DX00 L 0 A_JumpIfInventory("Shell",1,"ReloadScopeCheck")
			goto FireDry

		FireWaterCheck:
			DX00 L 0 A_JumpIfInventory("SamsaraCurrentWaterLevel",0,"FireDry")
			goto FireStart

		FireDry:
			DX00 A 0 A_PlaySound("DeusEx/DryFire",CHAN_6)
			DX00 A 0 A_ClearReFire
			DX00 A 0 A_JumpIfInventory("DeusEx_Zoomed",1,2)
			DX00 A 4 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			goto ReadyActualAmmoCheck
			TNT1 A 4 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			goto ReadyActualAmmoCheck

		FireStart:
			DX00 L 0 A_GiveInventory("DeusEx_Firing")
			DX00 L 0 A_AlertMonsters(3500)
			DX00 L 0 A_SpawnItemEx("DeusEx_ShellCasing2",8,-16,39,random(1,3),0,3,-90,SXF_NOCHECKPOSITION|SXF_MULTIPLYSPEED|SXF_TRANSFERPITCH)
			DX00 L 0 A_JumpIfInventory("DeusEx_Zoomed",1,"FireScoped")
			DX00 L 0 A_GiveInventory("SamsaraDeusExSniperRifleAttackHandler")
			DX00 LMNOPQA 5 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			goto ReadyActual

		FireScoped:
			TNT1 A 0 A_GiveInventory("SamsaraDeusExSniperRifleAttackHandler")
			TNT1 AAAAAAA 5 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			goto ReadyActual

		AltFire:
		Zoom:
			DX02 A 0 A_JumpIfInventory("DeusEx_Zoomed",1,"ZoomUnscope")
		ZoomScope:
			DX02 IJ 3 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			TNT1 A 0 A_GiveInventory("DeusEx_Zoomed")
			TNT1 A 0 A_ZoomFactor(5)
			TNT1 A 0 ACS_NamedExecuteWithResult("DeusEx_ScopeZoom")
			TNT1 A 3 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			goto ReadyActual

		ZoomUnscope:
			DX02 K 0 A_TakeInventory("DeusEx_Zoomed")
			DX02 K 0 A_ZoomFactor(1)
			DX02 KJI 3 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			goto ReadyActual

		Reload:
		ReloadCheck:
			DX00 A 0 A_JumpIfInventory("DeusEx_SniperMagazine",0,"ReadyActual")
			DX00 A 0 A_JumpIfInventory("SamsaraBottomlessMagazines",1,2)
			DX00 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"ReloadScopeCheck")
			DX00 A 0 A_JumpIfInventory("Shell",1,"ReloadScopeCheck")
			goto ReadyActual

		ReloadScopeCheck:
			DX00 T 0 A_JumpIfInventory("DeusEx_Zoomed",1,2)
			DX00 T 0 A_TakeInventory("DeusEx_ZoomedMemory")
			goto ReloadStart
			DX00 T 0 A_GiveInventory("DeusEx_ZoomedMemory")
			DX00 T 0 A_TakeInventory("DeusEx_Zoomed")
			DX00 T 0 A_ZoomFactor(1)
			goto ReloadStart

		ReloadStart:
			DX00 T 0 A_GiveInventory("DeusEx_Reloading")
			DX00 T 0 A_PlaySound("DeusEx/Sniper/Reload1")
			DX00 "TUVWXYZ[" 3 A_WeaponReady(WRF_NOFIRE)
			DX00 "\" 0 A_JumpIfInventory("SPModeOn",1,2)
			DX00 "\" 0 A_JumpIfInventory("CoopModeOn",1,1)
			goto ReloadContinueAdvanced
			DX00 "\" 0 A_JumpIfInventory("DeusEx_RifleSkill",0,"ReloadContinueMaster")
			DX00 "\" 0 A_JumpIfInventory("DeusEx_RifleSkill",3,"ReloadContinueAdvanced")
			DX00 "\" 0 A_JumpIfInventory("DeusEx_RifleSkill",2,"ReloadContinueProficient")
			DX00 "\" 0 A_JumpIfInventory("DeusEx_RifleSkill",1,"ReloadContinueTrained")
		ReloadContinueUntrained:
			DX00 "\]" 3 A_WeaponReady(WRF_NOFIRE)
			DX01 ABC 3 A_WeaponReady(WRF_NOFIRE)
		ReloadContinueTrained:
			DX00 "\]" 3 A_WeaponReady(WRF_NOFIRE)
			DX01 ABC 3 A_WeaponReady(WRF_NOFIRE)
		ReloadContinueProficient:
			DX00 "\]" 3 A_WeaponReady(WRF_NOFIRE)
			DX01 ABC 3 A_WeaponReady(WRF_NOFIRE)
		ReloadContinueAdvanced:
			DX00 "\]" 3 A_WeaponReady(WRF_NOFIRE)
			DX01 ABC 3 A_WeaponReady(WRF_NOFIRE)
		ReloadContinueMaster:
			DX01 D 0 A_TakeInventory("DeusEx_Reloading")
			DX01 D 0 A_PlaySound("DeuxEx/Sniper/Reload2")
			DX01 DEFG 3 A_WeaponReady(WRF_NOFIRE)
			DX00 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"ReloadAmmo")
			goto ReloadDone

		ReloadDone:
			DX00 A 0 A_JumpIfInventory("DeusEx_ZoomedMemory",1,"ZoomScope")
			goto ReadyActual

		ReloadAmmo:
			DX00 A 0 A_JumpIfInventory("DeusEx_SniperMagazine",0,"ReloadDone")
			DX00 A 0 A_JumpIfInventory("SamsaraBottomlessMagazines",1,2)
			DX00 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
			DX00 A 0 A_JumpIfInventory("Shell",1,1)
			goto ReloadDone
			DX00 A 0 A_JumpIfInventory("SamsaraBottomlessMagazines",1,2)
			DX00 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
			DX00 A 0 A_TakeInventory("Shell",1)
			DX00 A 0 A_GiveInventory("DeusEx_SniperMagazine")
			loop
	}
}

actor SamsaraDeusExSniperRifleAttackHandler : Trigger
{
	States
	{
	// Ammo checking/taking stuff
	Pickup:
		TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"PickupReloadable")
	PickupNonReloadable:
		TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
		TNT1 A 0 A_JumpIfInventory("Shell",1,1)
		stop
		TNT1 A 0 A_TakeInventory("Shell",1)
		goto PickupAttack
	PickupReloadable:
		TNT1 A 0 A_JumpIfInventory("SamsaraBottomlessMagazines",1,"PickupAttack")
		TNT1 A 0 A_JumpIfInventory("DeusEx_SniperMagazine",1,1)
		stop
		TNT1 A 0 A_TakeInventory("DeusEx_SniperMagazine",1)
		goto PickupAttack

	// Attack stuff
	PickupAttack:
		TNT1 A 0 A_PlaySound("DeusEx/Sniper/Fire",CHAN_WEAPON)
		TNT1 A 0 A_JumpIfInventory("DeusEx_Zoomed",1,"PickupAttackScoped")
	// Unscoped
	PickupAttackUnscoped:
		TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackUnscopedCoop")
		TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackUnscopedCoop")
	PickupAttackUnscopedDM:
		TNT1 A 0 A_FireCustomMissile("DeusEx_SniperBullet",frandom(-2.2,2.2),false,5,16)
		goto PickupFinish
	PickupAttackUnscopedCoop:
		TNT1 A 0 A_FireCustomMissile("DeusEx_SniperBulletCoop",frandom(-2.2,2.2),false,5,16)
		goto PickupFinish

	// Scoped
	PickupAttackScoped:
		TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackScopedCoop")
		TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackScopedCoop")
	PickupAttackScopedDM:
		TNT1 A 0 A_FireCustomMissile("DeusEx_SniperBullet",0,false,0,16)
		goto PickupFinish
	PickupAttackScopedCoop:
		TNT1 A 0 A_FireCustomMissile("DeusEx_SniperBulletCoop",0,false,0,16)
		goto PickupFinish

	// Finishing stuff
	PickupFinish:
		TNT1 A 0 ACS_NamedExecuteWithResult("DeusExRecoil", 18, 6, 1)
		stop
	}
}

Actor DeusEx_SniperMagazine : Ammo
{
	Inventory.MaxAmount 6
	Tag "Sniper Rifle Magazine"
	+IGNORESKILL
}

Actor DeusEx_SniperBullet : DeusExBullet
{
	Damage (CallACS("DeusEx_WeaponSkillDamage", 3, 30)) // was 25. buffed for rarity and to keep it viable alongside other sniper weapons
	DamageType "DeusExRifleShot"
	States
	{
		Crash:
			TNT1 A 0 A_PlaySound("DeusEx/Bullet/HitRobot", CHAN_BODY)
			TNT1 A 1 A_Explode(CallACS("DeusEx_DamageModifiers",CallACS("DeusEx_WeaponSkillDamage", 3, 30)),8,!XF_HURTSOURCE,0,8)
			stop
		XDeath:
			//TNT1 A 0 A_PlaySound("DeusEx/Bullet/HitFlesh", CHAN_BODY)
			TNT1 A 0 A_Explode(CallACS("DeusEx_DamageModifiers",CallACS("DeusEx_WeaponSkillDamage", 3, 30)),8,!XF_HURTSOURCE,0,8)
			TNT1 A 0 ACS_NamedExecuteWithResult("DeusEx_BloodGenerator_Offsets")
			TNT1 A 1 A_GiveInventory("DeusEx_BloodGenerator",1,AAPTR_TRACER)
			stop
	}
}

Actor DeusEx_SniperBulletCoop : DeusEx_SniperBullet { +THRUSPECIES Species "Player" }

Actor DeusEx_SniperPuff : DeusEx_BulletPuff
{
	DamageType "DeusExRifleShot"
	+ALWAYSPUFF
	+PUFFGETSOWNER
	+PUFFONACTORS
	+FORCERADIUSDMG
	+HITTRACER
	+NODAMAGETHRUST
	States
	{
		Crash:
		Melee:
		XDeath:
			TNT1 A 1 A_Explode(CallACS("DeusEx_DamageModifiers",CallACS("DeusEx_WeaponSkillDamage", 3, 30)),8,!XF_HURTSOURCE,0,8)
			Goto Super::Spawn
	}
}

Actor DeusEx_SniperPuffCoop : DeusEx_SniperPuff { +THRUSPECIES +MTHRUSPECIES Species "Player" }
