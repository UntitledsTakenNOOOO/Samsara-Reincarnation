Actor Pitchfork : Weapon
{
  Obituary "%k \cupoked\c- %o"
  Weapon.selectionorder 1800
  weapon.bobstyle Smooth
  weapon.bobrangeX 0.2
  weapon.bobrangeY 0.4
  weapon.bobspeed 0.5 
  +WEAPON.NOALERT
  +WEAPON.MELEEWEAPON
  +WEAPON.WIMPY_WEAPON
  Weapon.SlotNumber 0
  Weapon.SlotPriority 0
  Decal ZBloodBulletChip1
  Inventory.RestrictedTo "Caleb"
  Tag "Pitchfork"
  States
  {
  Spawn:
	TNT1 A 1
	Stop
  Ready:
	TNT1 A 0 A_JumpIfInventory("RemoveVodoo", 1, "TakeDoll")
	TNT1 A 0 A_GiveInventory("UsingPitchfork", 1)
	TNT1 A 0 A_JumpIfInventory("CalebReady",1,"IntroTaunt")
	TNT1 A 0 A_JumpIfInventory("CalebWaiting",60,"WaitingTaunt")
	PITC A 1 A_WeaponReady
	Goto Ready+2
  TakeDoll:
	TNT1 A 0 A_TakeInventory("VoodooDoll", 1)
	TNT1 A 0 A_TakeInventory("RemoveVodoo", 1)
	Goto Ready 
  Deselect:
	PITC AA 0 A_Lower
	PITC A 1 A_Lower
	Loop
  Select: 
	PITC AA 0 A_Raise
	PITC A 1 A_Raise
	Loop
  Fire:
	TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"Coopfire")
	PITC BBB 1 A_WeaponReady(14)
	PITC CC 1 A_WeaponReady(14)
	TNT1 A 0 A_FireCustomMissile("PitchforkStuff_DM", -12, 0)
	TNT1 A 0 A_FireCustomMissile("PitchforkStuff_DM", -4, 0)
	TNT1 A 0 A_FireCustomMissile("PitchforkStuff_DM", 4, 0)
	TNT1 A 0 A_FireCustomMissile("PitchforkStuff_DM", 12, 0)
	PITC DEFGFEDCB 1 A_WeaponReady(14)
	Goto Ready
  Altfire:
	Goto Fire
  Coopfire:
	PITC BBB 1 A_WeaponReady(14)
	PITC CC 1 A_WeaponReady(14)
	TNT1 A 0 A_FireCustomMissile("PitchforkStuff", -12, 0)
	TNT1 A 0 A_FireCustomMissile("PitchforkStuff", -4, 0)
	TNT1 A 0 A_FireCustomMissile("PitchforkStuff", 4, 0)
	TNT1 A 0 A_FireCustomMissile("PitchforkStuff", 12, 0)
	PITC DEFGFEDCB 1 A_WeaponReady(14)
	Goto Ready
  IntroTaunt:
	TNT1 A 0 A_JumpIfInventory("CalebBallgag",1,2)
	TNT1 A 0 A_PlaySoundEx("Caleb/Enter", "SoundSlot7", 0, 0)
	TNT1 A 0 A_TakeInventory("CalebReady", 1)
	TNT1 A 0 A_GiveInventory("CalebTauntCooldown",5)
	TNT1 A 0 ACS_NamedExecuteAlways("CalebTauntCooldown", 0)
	Goto Ready
  WaitingTaunt:
	TNT1 A 0 A_JumpIfInventory("CalebIdleBallgag",1,"IdleBallgagStuff")
	TNT1 A 0 A_JumpIfInventory("CalebBallgag",1,2)
	TNT1 A 0 A_PlaySoundEx("Caleb/Bored", "SoundSlot7", 0, 0)
	TNT1 A 0 A_TakeInventory("CalebWaiting", 525)
	TNT1 A 0 A_GiveInventory("CalebTauntCooldown",5)
	TNT1 A 0 ACS_NamedExecuteAlways("CalebTauntCooldown", 0)
	Goto Ready	
  IdleBallgagStuff:
	TNT1 A 0 A_TakeInventory("CalebWaiting",525)
	goto Ready
  }
}

actor CalebBullet : FastProjectile
{
	Damage (9)
	Radius 1
	Height 1
	Speed 300
	Projectile
	Renderstyle "Normal"
	Species "Player"
	DamageType "CalebBullet"
	+BLOODSPLATTER
	+THRUSPECIES
	+NOTIMEFREEZE
	+THRUGHOST
	Alpha 1.0
	Decal ZBloodBulletChip1	
	States
	{
	  Spawn:
		TNT1 A 1
		Loop
	  Death:
	  Crash:	  
		TNT1 A 0 A_PlaySound("Shoot/BloodBullet")	  
		TNT1 A 1 A_SpawnItemEx("ZBBulletPuff",0,0,0,0,0,1,0,0)
		stop
	  XDeath:
		TNT1 A 1
		Stop
	}
}

Actor PitchforkStuff : CalebBullet
{
	Damage (9)
	Speed 56
	-THRUGHOST
	Decal ZBloodBulletChip1
	States
	{
	  Spawn:
		TNT1 A 1
		Stop	
	  Death:
		TNT1 A 0 A_PlaySound("PITCHOW2")
		TNT1 A 1 A_SpawnItemEx("PitchforkPuff",0,0,0,0,0,1,0,0)
		stop
	  XDeath:
		TNT1 A 1 A_PlaySound("Pitchfork/Flesh")
		Stop
	  Crash:
		TNT1 A 0 A_PlaySound("PITCHWO2")
		TNT1 A 1 A_SpawnItemEx("CalebBulletSpark_Lightless",0,0,0,0,0,0,0,0)	  
		Stop		
	}
}
	
Actor UsingPitchfork : Inventory { inventory.amount 1 inventory.maxamount 1 }
Actor RemoveVodoo : Inventory { inventory.amount 1 inventory.maxamount 1 }
Actor PitchforkStuff_DM : PitchforkStuff { Species "" -THRUSPECIES }