actor "Assault Cannon" : SamsaraDarkForcesWeapon
{
    Inventory.PickupMessage "Assault Cannon"
    Weapon.UpSound "katarn/wepslct"
    Weapon.SlotNumber 7
    Weapon.SelectionOrder 2800
    Weapon.AmmoType1 "Shell"
    Weapon.AmmoGive1 80
    Weapon.AmmoUse1 1
    Weapon.AmmoType2 "RocketAmmo"
    Weapon.AmmoGive2 10
    Weapon.AmmoUse2 5
    Obituary "%k destroyed %o with the Assault Cannon."
    Tag "Assault Cannon"
    +ALT_AMMO_OPTIONAL
    +BFG
    +EXPLOSIVE
    +NOALERT
    //+NOAUTOFIRE
    States
    {
      Spawn:
        IASS Z -1
        stop

      Ready:
        KASS A 1 A_WeaponReady
        loop

      Deselect:
        TNT1 A 0 A_Lower
        KASS A 1 A_Lower
        loop

      Select:
        TNT1 A 0 A_Raise
        KASS A 1 A_Raise
        loop

      Fire:
        TNT1 A 0 A_AlertMonsters
        TNT1 A 0 A_JumpIfInventory("SamsaraHasPermaWeaponLevel2",1,"FireFast")
        TNT1 A 0 A_JumpIfInventory("KatarnFireRateUp",1,"FireFast")
      FireNormal:
        TNT1 A 0 A_GunFlash
        KASS B 5 Bright A_GiveInventory("SamsaraDarkForcesAssaultCannonPrimaryAttackHandler")
        KASS B 2
        KASS A 2 A_ReFire
        goto Ready

      FireFast:
        TNT1 A 0 A_GunFlash
        KASS B 3 Bright A_GiveInventory("SamsaraDarkForcesAssaultCannonPrimaryAttackHandler")
        KASS B 1
        KASS A 1 A_ReFire
        goto Ready

      AltFire:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"AltFireStart")
        TNT1 A 0 A_JumpIfInventory("RocketAmmo",5,"AltFireStart")
        goto AltFireDry

      AltFireDry:
        TNT1 A 0 A_ClearReFire
        TNT1 A 0 A_PlaySound("katarn/repempt",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SamsaraHasPermaWeaponLevel2",1,"AltFireDryFast")
        TNT1 A 0 A_JumpIfInventory("KatarnFireRateUp",1,"AltFireDryFast")
      AltFireDryNormal:
        KASS A 40
        goto Ready

      AltFireDryFast:
        KASS A 20
        goto Ready

      AltFireStart:
        TNT1 A 0 A_AlertMonsters
        TNT1 A 0 A_JumpIfInventory("SamsaraHasPermaWeaponLevel2",1,"AltFireFast")
        TNT1 A 0 A_JumpIfInventory("KatarnFireRateUp",1,"AltFireFast")
      AltFireNormal:
        KASS C 7 Bright
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("RocketAmmo",5,1)
        goto AltFireNormalContinue
        TNT1 A 0 A_GunFlash
        goto AltFireNormalContinue

      AltFireNormalContinue:
        KASS D 22 Bright A_GiveInventory("SamsaraDarkForcesAssaultCannonSecondaryAttackHandler")
        KASS A 1 Bright
        KASS A 13
        KASS A 2 A_ReFire
        goto Ready

      AltFireFast:
        KASS C 4 Bright
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("RocketAmmo",5,1)
        goto AltFireFastContinue
        TNT1 A 0 A_GunFlash
        goto AltFireFastContinue

      AltFireFastContinue:
        KASS D 11 Bright A_GiveInventory("SamsaraDarkForcesAssaultCannonSecondaryAttackHandler")
        KASS A 1 Bright
        KASS A 7
        KASS A 1 A_ReFire
        goto Ready

      AltFlash:
        TNT1 A 1 A_Light2
        TNT1 A 1 A_Light1
        goto LightDone
    }
}

actor SamsaraDarkForcesAssaultCannonPrimaryAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Shell",1,1)
        stop
        TNT1 A 0 A_TakeInventory("Shell",1)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("katarn/assshot",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireCustomMissile("plasma_shot",frandom(-0.5,0.5),false,8,0,0,frandom(-0.5,0.5))
        stop

      PickupAttackCoop:
        TNT1 A 0 A_FireCustomMissile("plasma_shot2",frandom(-0.5,0.5),false,8,0,0,frandom(-0.5,0.5))
        stop
    }
}

actor SamsaraDarkForcesAssaultCannonSecondaryAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("RocketAmmo",5,1)
        stop
        TNT1 A 0 A_TakeInventory("RocketAmmo",5)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("katarn/asssho2",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireCustomMissile("assault_missile",frandom(-0.5,0.5),false,8,2,0,frandom(-0.5,0.5))
        stop

      PickupAttackCoop:
        TNT1 A 0 A_FireCustomMissile("assault_missile2",frandom(-0.5,0.5),false,8,2,0,frandom(-0.5,0.5))
        stop
    }
}

actor plasma_shot
{
    Projectile
    Radius 4
    Height 4
    Speed 50
    Damage (random(60,85))
    DamageType "DoomPlasma"
    Scale 0.95
    Obituary "%k destroyed %o with the Assault Cannon."
    States
    {
      Spawn:
        PASS ABCD 3 Bright
        loop

      Death:
        TNT1 A 0 A_PlaySound("katarn/exmort",CHAN_VOICE)
        TNT1 A 1 A_SpawnItemEx("plasma_puff",0,0,0,0,0,0,0,SXF_CLIENTSIDE)
        stop
    }
}

actor plasma_shot2 : plasma_shot
{
    Species "Player"
    +THRUSPECIES
}

actor plasma_puff : spark_puff
{
    States
    {
      Spawn:
        EXAS ABC 2 Bright
        EXAS DEF 2
        stop
    }
}

actor assault_missile
{
    Projectile
    Radius 5
    Height 5
    Speed 25
    Damage (196)
    DamageType "Explosive"
    Scale 0.85
    Obituary "%o met a worthy opponent in %k. Too worthy."
    +FORCERADIUSDMG
    States
    {
      Spawn:
        PMIS A 2 Bright NoDelay A_PlaySound("katarn/missfly",CHAN_VOICE,1.0,true)
        PMIS BABABA 2 Bright
        PMIS B 1 Bright
        loop

      Death:
        TNT1 A 0 A_PlaySound("katarn/exmiss",CHAN_VOICE)
        TNT1 A 0 A_SpawnItemEx("assmissile_explosion",0,0,0,0,0,0,0,SXF_CLIENTSIDE)
        TNT1 A 1 A_Explode(500,200)
        stop
    }
}

actor assault_missile2 : assault_missile
{
    Species "Player"
    +THRUSPECIES
}

actor assmissile_explosion
{
    Radius 6
    Height 6
    Scale 2
    +CLIENTSIDEONLY
    +NOINTERACTION
    States
    {
      Spawn:
        EXMI ABC 2 Bright
        EXMI DEFGHIJKL 2 Bright
        stop
    }
}
