actor "Hammer of Retribution" : FWeapHammer
{
	Weapon.SelectionOrder 900
	Weapon.SlotNumber 5
	+BLOODSPLATTER
	+WEAPON.AMMO_OPTIONAL
	+WEAPON.MELEEWEAPON
	+INVENTORY.UNDROPPABLE
	Weapon.AmmoUse1 3
	Weapon.AmmoGive1 25
	Weapon.KickBack 150
	Weapon.YAdjust -10
	Weapon.AmmoType1 "Cell"
	Inventory.PickupMessage "You got the Hammer of Retribution!"
	Inventory.PickupSound "hexen/weaponget"
	Obituary "$SAMSARA_HEXEN_OB_SLOT5_1"
	Inventory.RestrictedTo "HexenPlayer"
	Tag "Ice Fang"
	States
	{
	  Spawn:
		WFHM A -1
		Stop

	  Select:
		TNT1 A 0 A_JumpIfInventory("HexenClassMode", 2, "SelectFighter")
		TNT1 A 0 A_JumpIfInventory("HexenClassMode", 1, "SelectMage")
		H117 B 1 A_Raise
		Loop

	  SelectFighter:
		FHMR A 1 A_Raise
		Loop

	  SelectMage:
		H107 B 1 A_Raise
		Loop

	  Deselect:
		TNT1 A 0 A_JumpIfInventory("HexenClassMode", 2, "DeselectFighter")
		TNT1 A 0 A_JumpIfInventory("HexenClassMode", 1, "DeselectMage")
		H117 B 1 A_Lower
		Loop

	  DeselectFighter:
		FHMR A 1 A_Lower
		Loop

	  DeselectMage:
		H107 B 1 A_Lower
		Loop

	  Ready:
		TNT1 A 0 A_JumpIfInventory("HexenClassMode", 2, "ReadyFighter")
		TNT1 A 0 A_JumpIfInventory("HexenClassMode", 1, "ReadyMage")
		H117 B 1 A_WeaponReady
		Loop

	  ReadyFighter:	
		FHMR A 1 A_WeaponReady
		Goto Ready

	  ReadyMage:
		H107 B 1 A_WeaponReady
		Goto Ready

	  Fire:
		TNT1 A 0 A_JumpIfInventory("HexenClassMode", 2, "FireFighter")
		TNT1 A 0 A_JumpIfInventory("HexenClassMode", 1, "FireMage")
		H107 D 0 A_JumpIfInventory("Cell", 3, 1)
		Goto SwitchWeapon
		H117 B 3 Bright
		H117 C 4 Bright
		H117 D 5 Bright
		H117 E 0 Bright A_SpawnItemEx("FrostFangFog", 16, 0, 32, 0.5, 0, FRandom(-0.5, 0.5), Random(-50, 50), 128)
		H117 E 0 Bright A_SpawnItemEx("FrostFangFog", 16, 0, 32, 0.5, 0, FRandom(-0.5, 0.5), Random(-50, 50), 128)
		H117 E 0 Bright A_SpawnItemEx("FrostFangFog", 16, 0, 32, 0.5, 0, FRandom(-0.5, 0.5), Random(-50, 50), 128)
		H117 E 0 Bright A_SpawnItemEx("FrostFangFog", 16, 0, 32, 0.5, 0, FRandom(-0.5, 0.5), Random(-50, 50), 128)
		H117 E 0 Bright A_PlayWeaponSound("Hexen/FrostFang/Fire")
		H117 E 0 Bright A_JumpIfInventory("CoopModeOn",1,"CoopFire")
		H117 E 0 Bright A_FireCustomMissile("FrostFangBall", 0, 1, 4, 0)
		H117 E 6 Bright A_FireCustomMissile("FrostFangBall", 0, 1, -4, 0)
		Goto FireEnd
	  CoopFire:
		H117 E 0 Bright A_FireCustomMissile("FrostFangBallCoop", 0, 1, 4, 0)
		H117 E 6 Bright A_FireCustomMissile("FrostFangBallCoop", 0, 1, -4, 0)
		Goto FireEnd
	  FireEnd:
		H117 D 1 Bright Offset (0, 60)
		H117 D 1 Bright Offset (0, 55)
		H117 D 1 Bright Offset (0, 50)
		H117 D 1 Bright Offset (0, 45)
		H117 D 1 Bright Offset (0, 40)
		H117 D 1 Bright Offset (0, 35)
		H117 C 5 Bright
		H117 B 4 Bright
		Goto Ready

	  FireMage:
		H107 D 0 A_JumpIfInventory("Cell", 3, 1)
		Goto SwitchWeapon
		H107 D 4 A_PlaySound("BloodScepter/Charge", CHAN_7)
		H107 EFG 4 Bright
		H107 H 0 Bright A_JumpIfInventory("CoopModeOn", 1, "CoopFireMage")
		H107 H 0 Bright A_FireCustomMissile("BloodScepterBall")
		Goto FireMageEnd
	  CoopFireMage:
		H107 H 0 Bright A_FireCustomMissile("BloodScepterBallCoop")
		Goto FireMageEnd
	  FireMageEnd:
		H107 H 6 Bright
		H107 G 0 Bright A_PlaySound("BloodScepter/CoolDown", CHAN_6)
		H107 GF 4 Bright
		H107 B 20
		Goto Ready

	  FireFighter:
		H105 B 6 Offset (5, 0)
		TNT1 A 0 A_JumpIfCloser(96,"HammerMeleeFighter")
		H105 C 3 Offset (5, 0) A_PlayWeaponSound("FighterHammerMiss")
		H105 D 3 Offset (5, 0)
		FHMR E 2 Offset (5, 0)
		FHMR E 10 Offset (5, 150) A_JumpIfInventory("Cell",1,"FireHammer")
		Goto HammerMeleeEnd

	  HammerMelee:
		TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"CoopHammerMelee")
		TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"CoopHammerMelee")
		TNT1 A 0 A_CustomPunch(random(60,113),1,0,"HammerPuff",96)// A_FHammerAttack - Deals 60-123 damage.
		Goto HammerMelee2
	  CoopHammerMelee:
		TNT1 A 0 A_CustomPunch(random(60,113),1,0,"HammerPuffCoop",96)
		Goto HammerMelee2

	  HammerMelee2:
		FHMR CD 3 Offset (5, 0)
		FHMR E 2 Offset (5, 0)
		FHMR E 10 Offset (5, 150) //A_FHammerThrow
		Goto HammerMeleeEnd

	  HammerMeleeEnd:
		FHMR A 1 Offset (0, 60)
		FHMR A 1 Offset (0, 55)
		FHMR A 1 Offset (0, 50)
		FHMR A 1 Offset (0, 45)
		FHMR A 1 Offset (0, 40)
		FHMR A 1 Offset (0, 35)
		FHMR A 1
		Goto Ready

	  HammerMeleeFighter:
		TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"CoopHammerMeleeFighter")
		TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"CoopHammerMeleeFighter")
		TNT1 A 0 A_CustomPunch(random(60,113),1,0,"HammerPuff",96)// A_FHammerAttack - Deals 60-123 damage.
		Goto HammerMeleeFighterEnd
	  CoopHammerMeleeFighter:
		TNT1 A 0 A_CustomPunch(random(60,113),1,0,"HammerPuffCoop",96)
		Goto HammerMeleeFighterEnd

	  HammerMeleeFighterEnd:
		H105 CD 3 Offset (5, 0)
		FHMR E 2 Offset (5, 0)
		FHMR E 10 Offset (5, 150) //A_FHammerThrow
		Goto HammerMeleeEnd

	  FireHammer:
		TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"CoopFireHammer")
		TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"CoopFireHammer")
		TNT1 A 0 A_JumpIfInventory("RJingModeOn",1,"FireHammerRJ")
		TNT1 A 0 A_FireCustomMissile("HammerMissileDM") //A_FHammerThrow
		Goto FireHammerEnd
	  CoopFireHammer:
		TNT1 A 0 A_JumpIfInventory("RJingModeOn",1,"CoopFireHammerRJ")
		TNT1 A 0 A_FireCustomMissile("HammerMissile2")
		Goto FireHammerEnd
	  FireHammerRJ:
		TNT1 A 0 A_FireCustomMissile("HammerMissileDMRJ")
		Goto FireHammerEnd
	  CoopFireHammerRJ:
		TNT1 A 0 A_FireCustomMissile("HammerMissile2RJ")
		Goto FireHammerEnd
	
	  FireHammerEnd:
		FHMR E 10 Offset (5, 150)
		Goto HammerMeleeEnd

	  SwitchWeapon:
		TNT1 A 0 ACS_NamedExecuteWithResult("Hexen_SwitchPriority")
		TNT1 A 0
		Goto Ready
	}
}

actor HammerMissile2 : HammerMissile replaces HammerMissile
{
	Obituary "$SAMSARA_HEXEN_OB_SLOT5_2"
	Decal Scorch
	Damage 10
    +BLOODSPLATTER
	+THRUGHOST
	+FORCERADIUSDMG
	+SKYEXPLODE
	Species "Player"
	+THRUSPECIES
	DamageType "FireExplosive"
}

actor HammerMissile2RJ : HammerMissile2
{
	States
	{
	  Death:
		FHFX I 3 Bright A_SetTranslucent(1, 1)
		FHFX J 3 Bright
		FHFX K 3 Bright A_Explode(128, 128)
		FHFX LM 3 Bright
		FHFX N 3
		FHFX OPQR 3 Bright
		Stop
	}
}

actor HammerMissileDM : HammerMissile
{
	Obituary "$SAMSARA_HEXEN_OB_SLOT5_2"
	Decal Scorch
	Damage 10
    +BLOODSPLATTER
	+THRUGHOST
	+SKYEXPLODE
	DamageType "FireExplosive"
	States
	{
	  Death:
		FHFX I 3 Bright A_SetTranslucent(1, 1)
		FHFX J 3 Bright
		FHFX K 3 Bright A_Explode(128, 128, 0)
		FHFX LM 3 Bright
		FHFX N 3
		FHFX OPQR 3 Bright
		Stop
	}
}

actor HammerMissileDMRJ : HammerMissileDM
{
	States
	{
	  Death:
		FHFX I 3 Bright A_SetTranslucent(1, 1)
		FHFX J 3 Bright
		FHFX K 3 Bright A_Explode(128, 128)
		FHFX LM 3 Bright
		FHFX N 3
		FHFX OPQR 3 Bright
		Stop
	}
}

Actor BloodScepterBall
{
  Speed 30
  Radius 8
  Height 12
  Damage 10
  DamageType "BloodScepter"
  Projectile
  SeeSound "BloodScepter/PowerFire"
  DeathSound "BloodScepter/PowerHit"
  Scale 1.5
  Decal BigBloodSmear
  +BLOODSPLATTER
  States
  {
  Spawn:
	H108 AABBCC 1 Bright //A_SpawnItemEx("BloodScepterTrail", Random(-5, 5), Random(-6, 6), Random(-6, 6), 0, 0, 0, 0, 128)
	H108 DDEEFF 1 Bright //A_SpawnItemEx("BloodScepterTrail", Random(-5, 5), Random(-6, 6), Random(-6, 6), 0, 0, 0, 0, 128)
	Loop
  XDeath:
	CSSF A 0 A_JumpIf(ACS_NamedExecuteWithResult("SamsaraDecorate", 22), "InfiniteDrain")
	TNT1 A 0 A_GivetoTarget("FiniteHealth200", random(2,4))
	TNT1 A 0 A_PlaySound("ClericCStaffHitThing", 5)
  Crash:	
  Death:
	TNT1 A 0 A_Explode(200, 200, 0)
	TNT1 A 1 A_SpawnItemEx("BloodScepterBallDeath", 0, 0, 0, 0, 0, 0, 0, 128)
	Stop
  InfiniteDrain:
	TNT1 A 0 A_GivetoTarget("InfiniteHealth", random(2,4))
	TNT1 A 0 A_PlaySound("ClericCStaffHitThing", 5)
	Goto Death
  }
}

Actor BloodScepterBallCoop : BloodScepterBall { +THRUSPECIES +DONTHARMSPECIES +DONTHARMCLASS +MTHRUSPECIES Species "Player" }

Actor BloodScepterBallDeath
{
  Radius 1
  Height 1
  +NoGravity
  +NoClip
  RenderStyle Translucent
  Alpha 0.8
  Scale 0.50
  States
  {
  Spawn:
	H108 GHIJKL 3 Bright
  Fade:
	H108 L 2 Bright A_FadeOut(0.05)
	Loop
  }
}

Actor FrostFangFog
{
  Radius 1
  Height 1
  +NoGravity
  +NoClip
  RenderStyle Add
  Alpha 0.2
  XScale 0.4
  YScale 0.2
  States
  {
  Spawn:
	H118 A 4 A_FadeOut(0.1)
	Loop
  }
}

Actor FrostFangBall : IceGuyFX
{
	Damage 5
	Speed 20
	DamageType "HexenIce"
	DeathSound "Hexen/FrostFang/Hit"
	+ACTIVATEIMPACT
	+ACTIVATEPCROSS
    +BLOODSPLATTER
	States
	{
		Death:
			ICPR D 4 Bright A_Explode(80, 128, 0)
			ICPR E 4 Bright A_CustomMissile("FrostMissileDM", 8, 0, 0, CMF_TRACKOWNER)
			ICPR E 0 Bright A_CustomMissile("FrostMissileDM", 8, 0, 45, CMF_TRACKOWNER)
			ICPR E 0 Bright A_CustomMissile("FrostMissileDM", 8, 0, 90, CMF_TRACKOWNER)
			ICPR E 0 Bright A_CustomMissile("FrostMissileDM", 8, 0, 135, CMF_TRACKOWNER)
			ICPR E 0 Bright A_CustomMissile("FrostMissileDM", 8, 0, 180, CMF_TRACKOWNER)
			ICPR E 0 Bright A_CustomMissile("FrostMissileDM", 8, 0, -45, CMF_TRACKOWNER)
			ICPR E 0 Bright A_CustomMissile("FrostMissileDM", 8, 0, -90, CMF_TRACKOWNER)
			ICPR E 0 Bright A_CustomMissile("FrostMissileDM", 8, 0, -135, CMF_TRACKOWNER)
			ICPR FG 4 Bright
			ICPR H 3 Bright
			Stop
	}
}

Actor FrostFangBallCoop : FrostFangBall
{
	+THRUSPECIES
	Species "Player"
	+DONTHARMSPECIES
	+DONTHARMCLASS
	States
	{
		Death:
			ICPR D 4 Bright A_Explode(80, 128, 0)
			ICPR E 4 Bright A_CustomMissile("FrostMissile2_1", 8, 0, 0, CMF_TRACKOWNER)
			ICPR E 0 Bright A_CustomMissile("FrostMissile2_1", 8, 0, 45, CMF_TRACKOWNER)
			ICPR E 0 Bright A_CustomMissile("FrostMissile2_1", 8, 0, 90, CMF_TRACKOWNER)
			ICPR E 0 Bright A_CustomMissile("FrostMissile2_1", 8, 0, 135, CMF_TRACKOWNER)
			ICPR E 0 Bright A_CustomMissile("FrostMissile2_1", 8, 0, 180, CMF_TRACKOWNER)
			ICPR E 0 Bright A_CustomMissile("FrostMissile2_1", 8, 0, -45, CMF_TRACKOWNER)
			ICPR E 0 Bright A_CustomMissile("FrostMissile2_1", 8, 0, -90, CMF_TRACKOWNER)
			ICPR E 0 Bright A_CustomMissile("FrostMissile2_1", 8, 0, -135, CMF_TRACKOWNER)
			ICPR FG 4 Bright
			ICPR H 3 Bright
			Stop
	}
}
