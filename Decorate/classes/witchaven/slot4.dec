Actor WTHalberdHold : WTBoolean {}
Actor "Ice Halberd" : WTWeapon
{
	Weapon.Slotnumber 4
	Weapon.SelectionOrder 2000
	Weapon.BobStyle Alpha
	Weapon.BobRangeX 0.2
	Weapon.BobRangeY 0.6
	Weapon.BobSpeed 0.8	
	+WEAPON.MELEEWEAPON
	+WEAPON.AMMO_OPTIONAL
	+WEAPON.NOALERT	
	Weapon.Ammotype "Clip"
	Weapon.AmmoUse 1
	Weapon.AmmoGive 25
    Inventory.PickupSound "gpickup/gen"
    Inventory.PickupMessage "You got the ice halberd!"
	Inventory.RestrictedTo "GrondovalPlayer"
    Tag "Ice Halberd"
	States
	{
	Spawn:
		PWHD ABCDEFGH 5
		Loop
	Select:
		TNT1 A 0 A_TakeInventory("WTWeaponActive",1)
		//TNT1 A 0 A_TakeInventory("WTFistActive",1)
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_Raise
		TNT1 A 1 A_Raise
		goto Select+1
	Deselect:
		WW40 D 0 A_TakeInventory("WTSlotIVClip",999)
		WW40 D 1 A_Lower
		goto Deselect+1		
	Ready:
	Draw:
		WW40 ABC 4 A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
		goto WIdle		
	WIdle:
		WW40 D 1 A_Weaponready
		loop		
	Altfire:
		WW40 D 0 A_JumpIfInventory("WTSlotIVClip",1,"TakeClip")
	GiveClip:
		WW40 D 12 A_GiveInventory("WTSlotIVClip",1)
		goto WIdle
	TakeClip:
		WW40 D 12 A_TakeInventory("WTSlotIVClip",999)
		goto WIdle
	Fire:
		WW40 D 0 A_JumpIfInventory("WTSlotIVClip",1,"NFire")
		TNT1 A 0 A_JumpIfInventory("Clip",1,"MegaFire")
	NFire:
		WW40 A 0 A_JumpIfInventory("WTAttackClip",1,2)
		WW40 A 0 A_GiveInventory("WTAttackClip",1)
		goto Attack1
		WW40 A 0 A_TakeInventory("WTAttackClip",1)
		goto Attack2
	MegaFire:
		goto MegaAttack1		
	Attack1:
		TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"CoopAttack1")
		WW40 IJ 4
		WW40 K 4 A_PlayWeaponSound("ggen/swing4")
		WW40 L 5 A_FireCustomMissile("WTIceHalberdProjectile",0,0,4,12)
		WW40 KJI 4
		goto WIdle
	CoopAttack1:
		WW40 IJ 4
		WW40 K 4 A_PlayWeaponSound("ggen/swing4")
		WW40 L 5 A_FireCustomMissile("WTIceHalberdProjectile_2",0,0,4,12)
		WW40 KJI 4
		goto WIdle		
	Attack2:
		TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"CoopAttack2")	
		WW40 E 4
		WW40 F 4 A_PlayWeaponSound("ggen/swing4")
		WW40 G 4 A_FireCustomMissile("WTIceHalberdProjectile_Weak",0,0,4,12)
		WW40 H 4
		TNT1 A 4
		WW40 H 4
		WW40 G 4 A_PlayWeaponSound("ggen/swing4")
		WW40 F 4 A_FireCustomMissile("WTIceHalberdProjectile_Weak",0,0,4,12)
		WW40 E 4
		goto WIdle
	CoopAttack2:
		WW40 E 4
		WW40 F 4 A_PlayWeaponSound("ggen/swing4")
		WW40 G 4 A_FireCustomMissile("WTIceHalberdProjectile_Weak_2",0,0,4,12)
		WW40 H 4
		TNT1 A 4
		WW40 H 4
		WW40 G 4 A_PlayWeaponSound("ggen/swing4")
		WW40 F 4 A_FireCustomMissile("WTIceHalberdProjectile_Weak_2",0,0,4,12)
		WW40 E 4
		goto WIdle
	MegaAttack1:
		WW41 I 1 bright A_JumpIfInventory("WTHalberdHold",1,"MegaAttack1Hold")
		WW41 I 3 bright A_GiveInventory("WTHalberdHold",1)
		WW41 JJKK 2 bright
	MegaAttack1Hold:
		TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"CoopMegaAttackHold")	
		TNT1 A 0 A_AlertMonsters		
		TNT1 A 0 A_JumpIfInventory("Clip",1,1)
		goto MegaAttack1Done
		WW41 L 2 bright A_PlayWeaponSound("ggen/buzzloop")
		WW41 M 2 bright A_FireCustomMissile("WTIceHalberdIceBall",random(-2,2),1,4,6,0,random(-2,2))
		WW41 N 2 bright 
		WW41 L 2 bright A_Refire
		goto MegaAttack1Done
	CoopMegaAttackHold:
		TNT1 A 0 A_AlertMonsters	
		TNT1 A 0 A_JumpIfInventory("Clip",1,1)
		goto MegaAttack1Done	
		WW41 L 2 bright A_PlayWeaponSound("ggen/buzzloop")
		WW41 M 2 bright A_FireCustomMissile("WTIceHalberdIceBall_2",random(-2,2),1,4,6,0,random(-2,2))
		WW41 N 2 bright 
		WW41 L 2 bright A_Refire
		goto MegaAttack1Done	
	MegaAttack1Done:
		TNT1 A 0 A_PlayWeaponSound("DSNONE")
		WW41 K 4 bright A_TakeInventory("WTHalberdHold",1)
		WW41 JJII 2 bright
		goto WIdle
	}
}

Actor WTIceHalberdProjectile : WTGigaMeleeProjectile
{
	Speed 70
	Damage (random(75,120))
	Obituary "$SAMSARA_WITCHAVEN_OB_SLOT_IV"
	DamageType "WTMeleeIce"
}

Actor WTIceHalberdProjectile_2 : WTIceHalberdProjectile { Species "Player" +THRUSPECIES +DONTHARMCLASS +DONTHARMSPECIES +MTHRUSPECIES +FORCEPAIN }

Actor WTIceHalberdProjectile_Weak : WTMegaMeleeProjectile
{
	Speed 70
	Damage (random(45,90))
	Obituary "$SAMSARA_WITCHAVEN_OB_SLOT_IV"
	DamageType "WTMeleeIce"
}

Actor WTIceHalberdProjectile_Weak_2 : WTIceHalberdProjectile_Weak { Species "Player" +THRUSPECIES +DONTHARMCLASS +DONTHARMSPECIES +MTHRUSPECIES +FORCEPAIN }

Actor WTIceHalberdIceBall //: FastProjectile
{
	PROJECTILE
	Speed 45
	Radius 7
	Height 10
	Damagetype "WTRangedIce"
	Damage (Random(13,25))
	Obituary "$SAMSARA_WITCHAVEN_OB_SLOT_IV_MEGA"
	scale 0.5
	SeeSound "ghalb/ice1"
	DeathSound "ghalb/ice2"
	//renderstyle add
	States
	{
	Spawn:
		//2 seconds
		WP40 EF 3 bright A_SpawnItemEx("WTIceHalberdTrail",random(-3,3),random(-3,3),random(-3,3),random(-2,2),random(-2,2),random(-2,0))
		WP40 CD 3 bright A_SpawnItemEx("WTIceHalberdTrail",random(-3,3),random(-3,3),random(-3,3),random(-2,2),random(-2,2),random(-2,0))
	PIdle:
		WP40 AB 2 bright A_SpawnItemEx("WTIceHalberdTrail",random(-3,3),random(-3,3),random(-3,3),random(-2,2),random(-2,2),random(-2,0))
		WP40 AB 2 bright
		loop
	Death:
		TNT1 AAAAAAA 0 A_SpawnItemEx("WTIceHalberdTrail",random(-3,3),random(-3,3),random(-3,3),random(-5,5),random(-5,5),random(-5,5))
		TNT1 A 1
		stop
	}
}

Actor WTIceHalberdTrail
{
	+CLIENTSIDEONLY
	-SOLID
	+NOINTERACTION
	//renderstyle add
	gravity 0.7
	states
	{
	Spawn:
		WP40 GHIJ 3 bright
		stop
	}
}

Actor WTIceHalberdIceBall_2 : WTIceHalberdIceBall { Species "Player" +THRUSPECIES +DONTHARMCLASS +DONTHARMSPECIES +MTHRUSPECIES }