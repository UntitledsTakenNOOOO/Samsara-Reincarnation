actor "Zorch Launcher" : Weapon
{
    Weapon.SelectionOrder 2500
    Weapon.SlotNumber 5
    Inventory.PickUpSound "chex/weaponget"
    Weapon.AmmoUse 1
    Weapon.AmmoGive 2
    Weapon.AmmoType "RocketAmmo"
    +WEAPON.NOAUTOFIRE
    //+AMMO_OPTIONAL
    +INVENTORY.UNDROPPABLE
    Inventory.PickupMessage "You got the Zorch Propulsor!"
	Inventory.RestrictedTo "ChexWarrior"
	Obituary "$SAMSARA_CHEX_OB_SLOT5S"
    Tag "Zorch Launcher"
    States
    {
      Spawn:
        LAUC B -1
        stop

      Ready:
        MISC J 1 A_WeaponReady
        loop

      Deselect:
        MISC J 1 A_Lower
        loop

      Select:
        MISC J 1 A_Raise
        loop

      Fire:
        MISC K 8 A_GunFlash
        MISC K 12 A_GiveInventory("SamsaraChexZorchLauncherAttackHandler")
        MISC J 6
        TNT1 A 0 A_ReFire
        goto Ready

      Flash:
        MCSF Q 3 Bright A_Light1
        MCSF R 4 Bright
        MCSF ST 4 Bright A_Light2
        goto LightDone
    }
}

actor SamsaraChexZorchLauncherAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("RocketAmmo",1,1)
        stop
        TNT1 A 0 A_TakeInventory("RocketAmmo",1)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireCustomMissile("ZorchGrenade",0,false,0,0,0,6.328125)
        stop

      PickupAttackCoop:
        TNT1 A 0 A_FireCustomMissile("ZorchGrenade2",0,false,0,0,0,6.328125)
        stop
    }
}

actor ZorchGrenade
{
	Projectile
	Radius 8
	Height 8
	Speed 25
	Damage 20
	DamageType "Zorch"
	ReactionTime 87
	Gravity 0.25
	BounceType "Doom" // replaces +DOOMBOUNCE in zan, plays sounds as intended
	SeeSound "chex/grenade"
	BounceSound "chex/grbnce" // no longer uses +USESTBOUNCESOUND from zan
	DeathSound "chex/grenlx"
	Obituary "%k played zorch grenade catch with %o."
	//SelfObituary "%o tripped %p own zorch grenade."
	+DEHEXPLOSION
	+EXPLODEONDEATH
	+GRENADETRAIL
	-NOGRAVITY
	+RANDOMIZE
	+BLOODLESSIMPACT
	+NOEXTREMEDEATH
	States
	{
	  Spawn:
		CMIS I 1 Bright A_Countdown
		loop

	  Death:
		TNT1 A 0 A_ChangeFlag("NOGRAVITY",true)
		TNT1 A 0 A_Explode(128,128,0)
	  DeathFinish:
		CMIS B 8 Bright
		CMIS C 6 Bright
		CMIS D 4 Bright
		stop
	}
}

actor ZorchGrenade2 : ZorchGrenade { +THRUSPECIES Species "Player" }
