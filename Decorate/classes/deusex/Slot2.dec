actor SamsaraDeusExShotgunAmmoType : Boolean {}

Actor DeusEx_Shotgun : DeusExWeapon
{
	Weapon.SelectionOrder 1600
	Weapon.SlotNumber 2
	Weapon.SlotPriority 1
	Weapon.AmmoType1 "DeusEx_ShotgunMagazine"
	Weapon.AmmoType2 "Shell"
	Weapon.AmmoUse1 1
	Weapon.AmmoGive2 8
	Tag "Sawed-Off Shotgun"
	Inventory.PickupMessage "You found a Sawed-Off Shotgun."
	Obituary "%o was shotgunned by %k."
	States
	{
		Spawn:
			DX99 A -1
			stop

		ReadyActual:
			//DX00 A 0 A_TakeInventory("DeusEx_SawedOffSpread",1)
			DX00 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"ReadyActualReloadable")
		ReadyActualNonReloadable:
			DX00 A 1 A_WeaponReady
			goto ReadyActualJump

		ReadyActualReloadable:
			DX00 A 1 A_WeaponReady(WRF_ALLOWRELOAD)
			goto ReadyActualJump

		ReadyActualJump:
			DX00 A 0 A_Jump(4,"ReadyActualIdle1","ReadyActualIdle2","ReadyActualIdle3")
			goto ReadyActualAmmoCheck

		ReadyActualIdle1:
			DX01 V 0 A_JumpIfInventory("SamsaraReloadMode",1,"ReadyActualIdle1Reloadable")
		ReadyActualIdle1NonReloadable:
			DX01 "VWXYZ[\]" 5 A_WeaponReady
			goto ReadyActual

		ReadyActualIdle1Reloadable:
			DX01 "VWXYZ[\]" 5 A_WeaponReady(WRF_ALLOWRELOAD)
			goto ReadyActual

		ReadyActualIdle2:
			DX02 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"ReadyActualIdle2Reloadable")
		ReadyActualIdle2NonReloadable:
			DX02 ABCDEFGH 5 A_WeaponReady
			goto ReadyActual

		ReadyActualIdle2Reloadable:
			DX02 ABCDEFGH 5 A_WeaponReady(WRF_ALLOWRELOAD)
			goto ReadyActual

		ReadyActualIdle3:
			DX00 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"ReadyActualIdle3Reloadable")
		ReadyActualIdle3NonReloadable:
			DX00 A 5 A_WeaponReady
			DX02 IJKLMNOP 5 A_WeaponReady
			goto ReadyActual

		ReadyActualIdle3Reloadable:
			DX00 A 5 A_WeaponReady(WRF_ALLOWRELOAD)
			DX02 IJKLMNOP 5 A_WeaponReady(WRF_ALLOWRELOAD)
			goto ReadyActual

		ReadyActualAmmoCheck:
			DX00 A 0 A_JumpIfInventory("SamsaraReloadMode",1,1)
			goto ReadyActual
			DX00 A 0 A_JumpIfInventory("SamsaraBottomlessMagazines",1,"ReadyActual")
			DX00 A 0 A_JumpIfInventory("DeusEx_ShotgunMagazine",1,"ReadyActual")
			goto ReloadCheck

		Deselect:
			DX01 P 0 A_TakeInventory("DeusEx_SawedOffSpread")
			DX01 P 0 A_GiveInventory("DeusEx_Reloading")
			DX01 P 3 A_JumpIfHealthLower(1,"DeathDeselect")
			DX01 QRST 3
			DX01 U 0
			goto DeselectFinish

		DeathDeselect:
			TNT1 AAAAA 3
			goto DeselectFinish

		DeselectFinish:
			TNT1 A 0 A_TakeInventory("DeusEx_ShotgunEquipped")
			goto DeselectInstant

		Ready: // our "Select" state
			DX00 B 0 A_PlaySound("DeusEx/Shotgun/Select")
			DX00 B 3 A_GiveInventory("DeusEx_ShotgunEquipped")
			DX00 CDEFGHIJKLM 3
			DX00 A 0 A_TakeInventory("DeusEx_Reloading")
			goto ReadyActual

		Fire:
			DX00 N 0 A_JumpIfInventory("SamsaraReloadMode",1,"FireReloadable")
		FireNonReloadable:
			DX00 N 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireWaterCheck")
			DX00 N 0 A_JumpIfInventory("Shell",1,"FireWaterCheck")
			goto FireDry

		FireReloadable:
			DX00 N 0 A_JumpIfInventory("SamsaraBottomlessMagazines",1,"FireWaterCheck")
			DX00 N 0 A_JumpIfInventory("DeusEx_ShotgunMagazine",1,"FireWaterCheck")
		FireReloadableDryCheck:
			DX00 N 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
			DX00 N 0 A_JumpIfInventory("Shell",1,1)
			goto FireDry
			DX00 "]" 0 A_TakeInventory("SamsaraDeusExSwappedAmmo")
			goto ReloadStart

		FireWaterCheck:
			DX00 N 0 A_JumpIfInventory("SamsaraCurrentWaterLevel",0,"FireDry")
			goto FireStart

		FireDry:
			DX00 A 0 A_PlaySound("DeusEx/DryFire",CHAN_6)
			DX00 A 0 A_ClearReFire
			DX00 A 4 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			goto ReadyActualAmmoCheck

		FireStart:
			DX00 N 0 A_GiveInventory("DeusEx_Firing")
			DX00 N 0 A_AlertMonsters(1100)
			DX00 N 0 A_SpawnItemEx("DeusEx_ShellCasing3",8,-16,39,random(1,3),0,3,-90,SXF_NOCHECKPOSITION|SXF_MULTIPLYSPEED|SXF_TRANSFERPITCH)
			DX00 N 0 A_GunFlash
			DX00 N 0 A_GiveInventory("SamsaraDeusExShotgunAttackHandler")
			DX00 NO 4 Bright A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			DX00 PQR 3 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			DX00 S 0 A_PlaySound("DeusEx/Shotgun/Pump")
			DX00 ST 3 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			//DX00 U 0 A_SpawnItemEx("DeusEx_ShellCasing3",8,-16,39,random(1,3),0,3,-90,SXF_NOCHECKPOSITION|SXF_MULTIPLYSPEED|SXF_TRANSFERPITCH)
			DX00 UVWXY 3 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			DX00 Z 0 A_ReFire
			DX00 Z 3 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			DX00 "\" 0 A_ReFire
			DX00 "\" 3 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			DX00 "]" 0 A_ReFire
			DX00 "]" 3 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			goto ReadyActual

		Flash:
			DX02 Q 1 Bright
			stop

		AltFire:
			DX00 "]" 0 A_JumpIfInventory("SamsaraReloadMode",1,"AltFireReloadable")
		AltFireNonReloadable:
			DX00 "]" 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"AltFireSwap")
			DX00 "]" 0 A_JumpIfInventory("Shell",1,"AltFireSwap")
			goto ReadyActual

		AltFireReloadable:
			DX00 "]" 0 A_JumpIfInventory("SamsaraBottomlessMagazines",1,"AltFireSwap")
			DX00 "]" 0 A_JumpIfInventory("DeusEx_ShotgunMagazine",1,"AltFireSwap")
		AltFireReloadableDryCheck:
			DX00 "]" 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
			DX00 "]" 0 A_JumpIfInventory("Shell",1,1)
			goto ReadyActual
			DX00 "]" 0 A_TakeInventory("SamsaraDeusExSwappedAmmo")
			goto ReloadStart

		AltFireSwap:
			DX00 "]" 0 A_GiveInventory("SamsaraDeusExSwappedAmmo")
			DX00 "]" 0 A_JumpIfInventory("SamsaraDeusExShotgunAmmoType",1,"AltFireSwapBuckshot")
		AltFireSwapSabot:
			DX00 "]" 0 A_GiveInventory("SamsaraDeusExShotgunAmmoType")
			DX00 "]" 0 A_Print("Sawed-Off Shotgun now has 12 Gauge Sabot Shells loaded.",2)
			goto ReloadStart

		AltFireSwapBuckshot:
			DX00 "]" 0 A_TakeInventory("SamsaraDeusExShotgunAmmoType")
			DX00 "]" 0 A_Print("Sawed-Off Shotgun now has 12 Gauge Buckshot Shells loaded.",2)
			goto ReloadStart

		Reload:
		ReloadCheck:
			DX00 A 0 A_JumpIfInventory("DeusEx_ShotgunMagazine",0,"ReadyActual")
			DX00 A 0 A_JumpIfInventory("SamsaraBottomlessMagazines",1,2)
			DX00 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
			DX00 A 0 A_JumpIfInventory("Shell",1,1)
			goto ReadyActual
			DX00 "]" 0 A_TakeInventory("SamsaraDeusExSwappedAmmo")
			goto ReloadStart

		ReloadStart:
			DX00 "]" 0 A_GiveInventory("DeusEx_Reloading")
			DX00 "]" 0 A_PlaySound("DeusEx/Shotgun/Reload1")
			DX00 "]" 3 A_WeaponReady(WRF_NOFIRE)
			DX01 ABC 3 A_WeaponReady(WRF_NOFIRE)
			DX01 D 0
			DX01 E 0 A_JumpIfInventory("SPModeOn",1,2)
			DX01 E 0 A_JumpIfInventory("CoopModeOn",1,1)
			goto ReloadContinueAdvanced
			DX01 E 0 A_JumpIfInventory("DeusEx_PistolSkill",0,"ReloadContinueMaster")
			DX01 E 0 A_JumpIfInventory("DeusEx_PistolSkill",3,"ReloadContinueAdvanced")
			DX01 E 0 A_JumpIfInventory("DeusEx_PistolSkill",2,"ReloadContinueProficient")
			DX01 E 0 A_JumpIfInventory("DeusEx_PistolSkill",1,"ReloadContinueTrained")
		ReloadContinueUntrained:
			DX01 EFGHIJK 2 A_WeaponReady(WRF_NOFIRE)
		ReloadContinueTrained:
			DX01 EFGHIJK 2 A_WeaponReady(WRF_NOFIRE)
		ReloadContinueProficient:
			DX01 EFGHIJK 2 A_WeaponReady(WRF_NOFIRE)
		ReloadContinueAdvanced:
			DX01 EFGHIJK 2 A_WeaponReady(WRF_NOFIRE)
		ReloadContinueMaster:
			DX01 L 0 A_TakeInventory("DeusEx_Reloading")
			DX01 L 0 A_PlaySound("DeusEx/Shotgun/Reload2")
			DX01 LMNO 3 A_WeaponReady(WRF_NOFIRE)
			DX00 A 0 A_JumpIfInventory("SamsaraDeusExSwappedAmmo",1,"ReloadDone")
			DX00 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"ReloadAmmo")
			goto ReloadDone

		ReloadDone:
			DX00 A 0
			goto ReadyActual

		ReloadAmmo:
			DX00 A 0 A_JumpIfInventory("DeusEx_ShotgunMagazine",0,"ReloadDone")
			DX00 A 0 A_JumpIfInventory("SamsaraBottomlessMagazines",1,2)
			DX00 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
			DX00 A 0 A_JumpIfInventory("Shell",1,1)
			goto ReloadDone
			DX00 A 0 A_JumpIfInventory("SamsaraBottomlessMagazines",1,2)
			DX00 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
			DX00 A 0 A_TakeInventory("Shell",1)
			DX00 A 0 A_GiveInventory("DeusEx_ShotgunMagazine")
			loop
	}
}

actor SamsaraDeusExShotgunAttackHandler : Trigger
{
	States
	{
	// Ammo checking/taking stuff
	Pickup:
		TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"PickupReloadable")
	PickupNonReloadable:
		TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
		TNT1 A 0 A_JumpIfInventory("Shell",1,1)
		stop
		TNT1 A 0 A_TakeInventory("Shell",1)
		goto PickupAttack
	PickupReloadable:
		TNT1 A 0 A_JumpIfInventory("SamsaraBottomlessMagazines",1,"PickupAttack")
		TNT1 A 0 A_JumpIfInventory("DeusEx_ShotgunMagazine",1,1)
		stop
		TNT1 A 0 A_TakeInventory("DeusEx_ShotgunMagazine",1)
		goto PickupAttack

	// Attack stuff
	PickupAttack:
		TNT1 A 0 A_PlaySound("DeusEx/Shotgun/Fire",CHAN_WEAPON)
		TNT1 A 0 A_JumpIfInventory("SamsaraDeusExShotgunAmmoType",1,"PickupAttackSabot")
	// Buckshot
	PickupAttackBuckshot:
		TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackBuckshotCoop")
		TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackBuckshotCoop")
	PickupAttackBuckshotDM:
		TNT1 AAAAA 0 A_FireCustomMissile("DeusEx_ShotgunBullet",frandom(-2.2,2.2)+CallACS("DeusEx_Decorate", 5, 0, 6)*frandom(-1.0,1.0),false,4,16,0,frandom(-1.8,1.8)+CallACS("DeusEx_Decorate", 5, 0, 6)*frandom(-0.5,0.5))
		goto PickupFinish
	PickupAttackBuckshotCoop:
		TNT1 AAAAA 0 A_FireCustomMissile("DeusEx_ShotgunBulletCoop",frandom(-2.2,2.2)+CallACS("DeusEx_Decorate", 5, 0, 6)*frandom(-1.0,1.0),false,4,16,0,frandom(-1.8,1.8)+CallACS("DeusEx_Decorate", 5, 0, 6)*frandom(-0.5,0.5))
		goto PickupFinish

	// Sabot
	PickupAttackSabot:
		TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackSabotCoop")
		TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackSabotCoop")
	PickupAttackSabotDM:
		TNT1 AAAAA 0 A_FireCustomMissile("DeusEx_SabotBullet",frandom(-2.2,2.2)+CallACS("DeusEx_Decorate", 5, 0, 6)*frandom(-1.0,1.0),false,4,16,0,frandom(-1.8,1.8)+CallACS("DeusEx_Decorate", 5, 0, 6)*frandom(-0.5,0.5))
		goto PickupFinish
	PickupAttackSabotCoop:
		TNT1 AAAAA 0 A_FireCustomMissile("DeusEx_SabotBulletCoop",frandom(-2.2,2.2)+CallACS("DeusEx_Decorate", 5, 0, 6)*frandom(-1.0,1.0),false,4,16,0,frandom(-1.8,1.8)+CallACS("DeusEx_Decorate", 5, 0, 6)*frandom(-0.5,0.5))
		goto PickupFinish

	// Finishing stuff
	PickupFinish:
		TNT1 A 0 A_GiveInventory("DeusEx_SawedOffSpread",14)
		TNT1 A 0 ACS_NamedExecuteWithResult("DeusExRecoil", 18, 6, 1)
		stop
	}
}

Actor DeusEx_ShotgunMagazine : Ammo
{
	Inventory.MaxAmount 4
	Tag "Sawed-Off Shotgun Magazine"
	+IGNORESKILL
}

Actor DeusEx_SawedOffSpread : Ammo
{
	Inventory.MaxAmount 90
	Inventory.InterHubAmount 0
	+IGNORESKILL
	+UNDROPPABLE
}

Actor DeusEx_ShotgunBullet : DeusExBullet
{
	Damage (CallACS("DeusEx_WeaponSkillDamage", 1, 6)) // was 5
	DamageType "DeusExPistol"
	States
	{
		Crash:
			TNT1 A 0 A_PlaySound("DeusEx/Bullet/HitRobot", CHAN_BODY)
			TNT1 A 1 A_Explode(CallACS("DeusEx_DamageModifiers",CallACS("DeusEx_WeaponSkillDamage", 1, 6)),8,!XF_HURTSOURCE,0,8)
			stop
		XDeath:
			//TNT1 A 0 A_PlaySound("DeusEx/Bullet/HitFlesh", CHAN_BODY)
			TNT1 A 0 A_Explode(CallACS("DeusEx_DamageModifiers",CallACS("DeusEx_WeaponSkillDamage", 1, 6)),8,!XF_HURTSOURCE,0,8)
			TNT1 A 0 ACS_NamedExecuteWithResult("DeusEx_BloodGenerator_Offsets")
			TNT1 A 1 A_GiveInventory("DeusEx_BloodGenerator",1,AAPTR_TRACER)
			stop
	}
}

Actor DeusEx_ShotgunBulletCoop : DeusEx_ShotgunBullet { +THRUSPECIES Species "Player" }

Actor DeusEx_SabotBullet : DeusExBullet
{
	DamageType "DeusExSabotPistol"
	Damage (0)
	States
	{
		Crash:
			TNT1 A 0 A_PlaySound("DeusEx/Bullet/HitRobot", CHAN_BODY)
			TNT1 A 1 A_Explode(CallACS("DeusEx_WeaponSkillDamage", 1, 18),32,!XF_HURTSOURCE,0,32)
			stop
		XDeath:
			//TNT1 A 0 A_PlaySound("DeusEx/Bullet/HitFlesh", CHAN_BODY)
			TNT1 A 0 A_Explode(CallACS("DeusEx_WeaponSkillDamage", 1, 18),32,!XF_HURTSOURCE,0,32)
			TNT1 A 0 ACS_NamedExecuteWithResult("DeusEx_BloodGenerator_Offsets")
			TNT1 A 1 A_GiveInventory("DeusEx_BloodGenerator",1,AAPTR_TRACER)
			stop
	}
}

Actor DeusEx_SabotBulletCoop : DeusEx_SabotBullet { +THRUSPECIES +DONTHARMSPECIES +MTHRUSPECIES Species "Player" }
