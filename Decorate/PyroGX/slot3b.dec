actor SamsaraDescentHomingMissileAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Shell",1,1)
        stop
        TNT1 A 0 A_TakeInventory("Shell",1)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("Descent/Missile/Fire",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("DescentMissileSwitcher",1,"PickupAttackLeft")
      // Right
      PickupAttackRight:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackRightCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackRightCoop")
      PickupAttackRightDM:
        TNT1 A 0 A_FireCustomMissile("DescentHomingMissile",0,false,8)
        goto PickupFinishRight

      PickupAttackRightCoop:
        TNT1 A 0 A_FireCustomMissile("DescentHomingMissileCoop",0,false,8)
        goto PickupFinishRight

      // Left
      PickupAttackLeft:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackLeftCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackLeftCoop")
      PickupAttackLeftDM:
        TNT1 A 0 A_FireCustomMissile("DescentHomingMissile",0,false,-8)
        goto PickupFinishLeft

      PickupAttackLeftCoop:
        TNT1 A 0 A_FireCustomMissile("DescentHomingMissileCoop",0,false,-8)
        goto PickupFinishLeft

      // Finishing stuff
      // Right
      PickupFinishRight:
        TNT1 A 0 A_GiveInventory("DescentMissileSwitcher")
        stop

      // Left
      PickupFinishLeft:
        TNT1 A 0 A_TakeInventory("DescentMissileSwitcher")
        stop
    }
}

actor DescentHomingMissile : DescentMissile 
{
	Speed 18
	+SCREENSEEKER
	+SEEKERMISSILE
	States
	{
		Spawn:
			DS00 A 0
			DS00 A 0 A_RearrangePointers(AAPTR_TARGET,AAPTR_NULL,AAPTR_NULL)
			DS00 A 0 A_GiveToTarget("DescentMissileCamera")
			DS00 A 0 ACS_NamedExecuteWithResult("DescentMissileCamera")
		SpawnLoop:
			DS00 A 1 A_SeekerMissile(2,2,SMF_LOOK|SMF_PRECISE)
			loop

		Death:
			TNT1 A 0 A_RemoveMaster
			TNT1 A 0 A_Explode(64,80,0)
			TNT1 A 0 A_GiveToTarget("DescentMissileStaticGiver")
			TNT1 A 0 A_TakeFromTarget("DescentMissileCamera",1)
			TNT1 A 0 A_PlaySound("Descent/Laser/Explode",CHAN_VOICE)
			TNT1 A 1 A_SpawnItemEx("DescentExplosion",0,0,0,0,0,0,0,SXF_CLIENTSIDE)
			stop
	}
}

actor DescentHomingMissileCoop : DescentHomingMissile
{
    Speed 20
    Species "Player"
    +THRUSPECIES
    States
    {
      Death:
        TNT1 A 0 A_RemoveMaster
        TNT1 A 0 A_Explode(100,80,0)
        TNT1 A 0 A_GiveToTarget("DescentMissileStaticGiver")
        TNT1 A 0 A_TakeFromTarget("DescentMissileCamera",1)
        TNT1 A 0 A_PlaySound("Descent/Laser/Explode",CHAN_VOICE)
        TNT1 A 1 A_SpawnItemEx("DescentExplosion",0,0,0,0,0,0,0,SXF_CLIENTSIDE)
        stop
    }
}
