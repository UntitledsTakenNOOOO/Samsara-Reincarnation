Actor WTBowUpgrade : WTBoolean {}
Actor "Bow and Arrows" : WTWeapon
{
	Weapon.Slotnumber 1
	Weapon.SelectionOrder 3500
	+WEAPON.AMMO_OPTIONAL
	+WEAPON.NOALERT
	Weapon.AmmoType "Clip"
	Weapon.AmmoUse 2
	Weapon.AmmoGive 20
	Weapon.BobStyle Alpha
	Weapon.BobRangeX 0.2
	Weapon.BobRangeY 0.6
	Weapon.BobSpeed 0.8
    Inventory.PickupSound "gpickup/gen"
    Inventory.PickupMessage "You got the bow!"
	+INVENTORY.UNDROPPABLE
	Inventory.RestrictedTo "GrondovalPlayer"	
	States
	{
	Spawn:
		TNT1 A -1
		stop
	Select:
		TNT1 A 0 A_TakeInventory("WTWeaponActive",1)
		TNT1 A 0 A_TakeInventory("WTFistActive",1)
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_Raise
		TNT1 A 1 A_Raise
		loop
	Deselect:
		WWI0 K 1 A_Lower
		loop
	
	Ready:
		WWI0 A 0 A_JumpIfInventory("WTBowUpgrade",1,"MegaDraw")
	Draw:
		WWI0 ABCDEFGHIJ 3 A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
		goto WIdle
	MegaDraw:
		WWI1 ABCDEFGHIJ 2 bright
		goto MegaWIdle
		
	WIdle:
		WWI0 A 0 A_JumpIfInventory("WTBowUpgrade",1,"MegaWIdle")
		WWI0 K 2 A_WeaponReady
		loop
	MegaWIdle:
		WWI1 A 0 A_JumpIfInventory("Clip", 2, 1)
		Goto TakeClip
		WWI1 A 0 A_PlayWeaponSound("ggen/fireloop")
		WWI1 KLMNO 4 bright A_WeaponReady
		WWI1 A 0 A_JumpIfInventory("WTBowUpgrade",1,"MegaWIdle")
		WWI1 A 0 A_PlayWeaponSound("DSNONE")
		goto WIdle
		
	Fire:
		WWI0 A 0 A_JumpIfInventory("WTBowUpgrade",1,"MegaFire")
	NFire:
		goto Attack1
	MegaFire:
		goto MegaAttack1
		
	Attack1:
		WWI0 A 0 A_FireBullets(0,0, 1, 0, "SamsaraBlankPuff", FBF_NORANDOM)
		TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"CoopAttack1")
		WWI0 LM 3
		WWI0 N 1 A_PlayWeaponSound("gbow/shot")
		WWI0 O 1 A_FireCustomMissile("WTBowArrow",0,0,3,6) //Dont need ammo for normal arrows, but theyre really weak...
		WWI0 P 4
		WWI0 HIJK 3
		WWI0 K 3 A_Refire
		goto WIdle
	CoopAttack1:
		WWI0 LM 3
		WWI0 N 1 A_PlayWeaponSound("gbow/shot")
		WWI0 O 1 A_FireCustomMissile("WTBowArrow_2",0,0,3,6) //Dont need ammo for normal arrows, but theyre really weak...
		WWI0 P 4
		WWI0 HIJK 3
		WWI0 K 3 A_Refire
		goto WIdle
		
	MegaAttack1:
		TNT1 A 0 A_JumpIfInventory("Clip", 2, 1)
		Goto TakeClip
		WWI0 A 0 A_FireBullets(0,0, 1, 0, "SamsaraBlankPuff", FBF_NORANDOM)
		TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"CoopMegaAttack1")	
		WWI1 PQ 3 bright
		WWI1 R 3 bright A_PlayWeaponSound("gbow/shot")
		WWI1 S 1 bright A_FireCustomMissile("WTMegaBowArrow",0,1,3,6)
		WWI1 T 4 bright
		WWI1 HIJO 3 bright
		WWI1 K 3 bright A_Refire
		goto WIdle
	CoopMegaAttack1:
		WWI1 PQ 3 bright
		WWI1 R 3 bright A_PlayWeaponSound("gbow/shot")
		WWI1 S 1 bright A_FireCustomMissile("WTMegaBowArrow_2",0,1,3,6)
		WWI1 T 4 bright
		WWI1 HIJO 3 bright
		WWI1 K 3 bright A_Refire
		goto WIdle		
		
	Altfire:
		WW10 L 0 A_JumpIfInventory("WTBowUpgrade",1,"TakeClip")
	GiveClip:
		WWI0 K 0 A_GiveInventory("WTBowUpgrade",1)
		WWI0 K 12 A_JumpIfInventory("Clip",2,1)
		goto WIdle
		WWI1 A 0 A_PlayWeaponSound("ggen/fireloop")
		WWI1 KLMNO 3 bright
		goto MegaWIdle			
	TakeClip:
		WWI1 H 0 A_PlayWeaponSound("DSNONE")	
		WWI0 K 12 A_TakeInventory("WTBowUpgrade",999)
		goto WIdle	
	}
}

Actor WTBowArrow : FastProjectile
{
	PROJECTILE
	+BLOODSPLATTER
	+NOEXTREMEDEATH
	Speed 150
	Height 4
	Radius 3
	Damage (random(15,30))
	Obituary "$SAMSARA_WITCHAVEN_OB_SLOT_I"
	scale 0.3
	Deathsound "gbow/arrow"
	DamageType "WTRanged"
	States
	{
	Spawn:
		WPI0 A 1
		loop
	Death:
		TNT1 A 0 A_ChangeFlag(MOVEWITHSECTOR,1)
		WPI0 AAAAA 36 //5 sec
		stop
	XDeath:
	Crash:
		TNT1 A 1
		stop
	}
}

Actor WTBowArrow_2 : WTBowArrow { Species "Player" +THRUSPECIES +DONTHARMCLASS +DONTHARMSPECIES +MTHRUSPECIES  }

Actor WTMegaBowArrow : FastProjectile
{
	PROJECTILE
	+BLOODSPLATTER
	+NOEXTREMEDEATH
	Speed 150
	Height 4
	Radius 3
	Damage (random(30,60))
	DamageType "WTRangedFire"
	Obituary "$SAMSARA_WITCHAVEN_OB_SLOT_I_MEGA"
	scale 0.3
	Deathsound "gbow/arrow"
	States
	{
	Spawn:
		WPI0 A 1 bright
		loop
	Death:
		WPI0 A 0 A_SpawnItemEx("WTBowSmallFire")
		WPI0 AAAAA 36 bright//5 sec
		stop
	XDeath:
	Crash:
		TNT1 A 1
		stop
	}
}

Actor WTMegaBowArrow_2 : WTMegaBowArrow { Species "Player" +THRUSPECIES +DONTHARMCLASS +DONTHARMSPECIES +MTHRUSPECIES  }

Actor WTBowSmallFire
{
	+NOGRAVITY
	+NOINTERACTION
	+NOBLOCKMAP
	-SOLID
	+ALLOWCLIENTSPAWN
	+CLIENTSIDEONLY
	scale 0.6
	states
	{
	Spawn:
		WPI1 ABCDEFGHIJKL 2 bright //22 total
		WPI1 ABCDEFGHIJKL 2 bright
		WPI1 ABCDEFGHIJKL 2 bright
		WPI1 ABCDEFGHIJKL 2 bright
		WPI1 ABCDEFGHIJKL 2 bright
		WPI1 ABCDEFGHIJKL 2 bright
		WPI1 ABCDEFGHIJKL 2 bright
		WPI1 ABCDEFGHIJKL 2 bright
		stop
	}
}
