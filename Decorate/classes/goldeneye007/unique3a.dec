actor Goldeneye_WatchMagnet : SamsaraGoldenEyeWeapon
{
    Weapon.SlotNumber 0
    Weapon.SelectionOrder 25
    Tag "Watch (Magnet Attract)"
    States
    {
      Spawn:
        PIST A -1
        stop

      // Weapon
      Ready:
        TNT1 A 0 A_JumpIfInventory("BondWatchQueue",1,"WatchRaise")
        TNT1 A 0 A_JumpIfInventory("BondWatchToken",1,"WatchReady")
      ReadyStart:
        TNT1 A 0 A_JumpIfInventory("Goldeneye_WatchDetonator",1,3)
        TNT1 A 0 A_JumpIfInventory("Goldeneye_WatchLaser",1,2)
        GE00 A 1 A_WeaponReady(WRF_NOSECONDARY)
        goto Ready
        GE00 A 1 A_WeaponReady
        goto Ready

      Deselect:
        GE00 A 0 A_TakeInventory("BondFireDelay")
        GE00 A 0 A_TakeInventory("BondAlternateFire")
        GE00 A 0 A_TakeInventory("BondDualWieldToken")
        GE00 A 0 A_TakeInventory("BondAltToggle")
        GE00 A 0 A_TakeInventory("BondZoomToken")
        GE00 A 0 A_TakeInventory("BondZoomAmountModifyActive")
        GE00 A 0 A_TakeInventory("BondDualWieldSpamProtector")
        GE00 A 0 A_TakeInventory("BondDryFireSpamProtector1")
        GE00 A 0 A_TakeInventory("BondDryFireSpamProtector2")
        GE00 A 0 A_TakeInventory("BondAltActivationToken")
        GE00 A 1 A_Lower
        loop

      Select:
        GE00 A 0 A_GiveInventory("Goldeneye_UnarmedToken")
        GE00 A 0 A_TakeInventory("Goldeneye_PP7Token")
        GE00 A 0 A_TakeInventory("Goldeneye_KnifeToken")
        GE00 A 0 A_TakeInventory("Goldeneye_KF7Token")
        GE00 A 0 A_TakeInventory("Goldeneye_AutoShotgunToken")
        GE00 A 0 A_TakeInventory("Goldeneye_AR33Token")
        GE00 A 0 A_TakeInventory("Goldeneye_RocketLauncherToken")
        GE00 A 0 A_TakeInventory("Goldeneye_RCP90Token")
        GE00 A 0 A_TakeInventory("Goldeneye_LaserToken")
        GE00 A 0 A_TakeInventory("Goldeneye_GoldenGunToken")
        GE00 A 0 A_TakeInventory("Goldeneye_SniperToken")
        GE00 A 0 A_TakeInventory("Goldeneye_D5KToken")
        GE00 A 1 A_Raise
        loop

      Fire:
        GE01 A 0 A_GiveInventory("BondFiringToken")
        GE01 AB 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        GE01 C 0 A_PlaySound("Goldeneye/WatchAttract",CHAN_WEAPON)
        GE01 C 0 A_GiveInventory("SamsaraGoldenEyeWatchMagnetAttackHandler")
        GE01 C 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        GE01 D 9 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        GE01 D 0 A_TakeInventory("BondFiringToken")
        GE01 D 0 A_ReFire(1)
        goto FireContinue
        GE01 D 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        goto FireLoop

      FireContinue:
        GE01 DCBA 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        goto FireFinish

      FireLoop:
        GE01 D 0 A_GiveInventory("BondFiringToken")
        GE01 D 5 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        GE01 D 0 A_PlaySound("Goldeneye/WatchAttract",CHAN_WEAPON)
        GE01 D 5 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        GE01 D 0 A_GiveInventory("SamsaraGoldenEyeWatchMagnetAttackHandler")
        GE01 D 5 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        GE01 D 0 A_TakeInventory("BondFiringToken")
        GE01 D 0 A_ReFire(1)
        goto FireLoopContinue
        GE01 D 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        loop

      FireLoopContinue:
        GE01 D 6 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        GE01 DCBA 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        goto FireFinish

      FireFinish:
        TNT1 A 0 A_TakeInventory("BondFiringToken")
        goto Ready

      AltFire:
        TNT1 A 0 A_JumpIfInventory("Goldeneye_WatchLaser",1,2)
        TNT1 A 0 A_SelectWeapon("Goldeneye_WatchDetonator")
        goto Deselect
        TNT1 A 0 A_SelectWeapon("Goldeneye_WatchLaser")
        goto Deselect

      // Watch
      WatchLower:
        GE13 T 0 A_TakeInventory("BondWatchActivate")
        GE13 T 0 A_GiveInventory("BondWatchUIDeinit")
      WatchLowerStart:
        GE13 T 0 ACS_NamedExecuteAlways("BondWatchLookUpAndDown")
        GE13 T 12 A_WeaponReady(WRF_NOBOB|WRF_NOFIRE|WRF_DISABLESWITCH)
        GE13 SRQPONMLKJIHGFEDCBA 2 A_WeaponReady(WRF_NOBOB|WRF_NOFIRE|WRF_DISABLESWITCH)
        GE02 IHGFEDCBA 1 A_WeaponReady(WRF_NOFIRE|WRF_DISABLESWITCH)
        goto WatchLowerFinish

      WatchRaise:
        GE02 A 0 A_ZoomFactor(1)
        GE02 A 0 A_TakeInventory("BondAltToggle")
        GE02 A 0 A_TakeInventory("BondZoomToken")
        GE02 A 0 A_TakeInventory("BondZoomAmountModifyActive")
        GE02 A 0 A_JumpIfInventory("BondWatchToken",1,"WatchLower")
        GE02 A 0 A_GiveInventory("BondWatchActivate")
        GE02 ABCDEFGHI 1 A_WeaponReady(WRF_NOFIRE|WRF_DISABLESWITCH)
        goto WatchRaiseStart
    }
}

// Attack handler

actor SamsaraGoldenEyeWatchMagnetAttackHandler : Trigger
{
    States
    {
      // Attack stuff
      Pickup:
        TNT1 A 0 ACS_NamedExecuteAlways("BondWatchMagnet", 0, 0)
        stop
    }
}
