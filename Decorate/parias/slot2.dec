actor "Frost Shards" : MWeapFrost
{
    Weapon.SelectionOrder 1700
    Weapon.SlotNumber 2
    Weapon.AmmoUse1 0
    Weapon.AmmoGive1 25
    Weapon.KickBack 150
    Weapon.YAdjust 20
    DamageType "HexenIce"
    Weapon.AmmoType1 "Clip"
    Inventory.PickupMessage "You got the Frost Shards!"
    Obituary "$SAMSARA_HEXEN_OB_SLOT2"
    Inventory.PickupSound "hexen/weaponget"
	Inventory.RestrictedTo "HexenPlayer"
    +BLOODSPLATTER
    -ICEDAMAGE
    +INVENTORY.UNDROPPABLE
    States
    {
      Spawn:
        H114 A 8 Bright
        Loop

      Select:
		TNT1 A 0 A_JumpIfInventory("HexenClassMode", 2, "SelectFighter")
		TNT1 A 0 A_JumpIfInventory("HexenClassMode", 1, "SelectMage")
        H114 B 1 A_Raise
        Loop

	  SelectMage:
        CONE H 1 A_Raise
        Loop	  

	  SelectFighter:
		H101 A 0 A_JumpIfInventory("Clip", 3, "SelectFighterGlow")
		H101 A 1 A_Raise
		Loop
	  SelectFighterGlow:
		H101 B 1 Bright A_Raise
		Goto Select

      Deselect:
		TNT1 A 0 A_JumpIfInventory("HexenClassMode", 2, "DeselectFighter")
		TNT1 A 0 A_JumpIfInventory("HexenClassMode", 1, "DeselectMage")
        H114 B 1 A_Lower
        Loop

	  DeselectMage:
        CONE H 1 A_Lower
        Loop
		
	  DeselectFighter:
		H101 A 0 A_JumpIfInventory("Clip", 3, "DeselectFighterGlow")
		H101 A 1 A_Lower
		Loop
	  DeselectFighterGlow:
		H101 B 1 Bright A_Lower
		Goto Deselect

      Ready:
		TNT1 A 0 A_JumpIfInventory("HexenClassMode", 2, "ReadyFighter")
		TNT1 A 0 A_JumpIfInventory("HexenClassMode", 1, "ReadyMage")
		H114 BBBFFF 1 A_WeaponReady
		Loop
		
	  ReadyMage:
        CONE H 1 A_WeaponReady
        Goto Ready	
		
		
	  ReadyFighter:
		H101 A 0 A_JumpIfInventory("Clip", 3, "ReadyFighterGlow")
		H101 A 1 A_WeaponReady
		Goto Ready
	  ReadyFighterGlow:
		H101 BCDC 5 Bright A_WeaponReady
		Goto Ready

      Fire:
		TNT1 A 0 A_JumpIfInventory("HexenClassMode", 2, "FireFighter")
		TNT1 A 0 A_JumpIfInventory("HexenClassMode", 1, "FireMage")
		TNT1 A 0 A_JumpIfInventory("Clip",3,1)
		Goto SwitchWeapon
		H114 C 3
		H114 D 3 A_PlayWeaponSound("Hexen/BowString")

      Hold:
		TNT1 A 0 A_JumpIfInventory("Clip",3,1)
		Goto SwitchWeapon
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"CoopHold")
		H114 EG 3
		TNT1 A 0 A_Refire
		TNT1 A 0 A_FireCustomMissile("HexenShockArrow",0,1,5,0)
		TNT1 A 0 A_TakeInventory("Clip",3,TIF_NOTAKEINFINITE)
		H114 H 3 A_PlayWeaponSound("Hexen/ElectricArrow")
		H114 I 3
		H114 CB 3
		Goto Ready

	  FireMage:
		TNT1 A 0 A_JumpIfInventory("Clip",2,1)
		Goto SwitchWeapon
		CONE I 3
        CONE J 4

      HoldMage:
		TNT1 A 0 A_JumpIfInventory("Clip",2,1)
		Goto SwitchWeapon
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"CoopFireMage")
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"CoopFireMage")
        CONE K 3
        TNT1 A 0 A_JumpIfCloser(92,"FrostMelee")
        CONE L 5 BRIGHT
        TNT1 A 0 A_PlayWeaponSound("MageShardsFire")
        // THIS HERE AREA WAS A DICK
        TNT1 A 0 A_FireCustomMissile("FrostMissileDM",14,0,6,1)
        TNT1 A 0 A_FireCustomMissile("FrostMissileDM",9,0,4,1)
        TNT1 A 0 A_FireCustomMissile("FrostMissileDM",5,0,2,1)
        TNT1 A 0 A_FireCustomMissile("FrostMissileDM",-14,0,-6,1)
        TNT1 A 0 A_FireCustomMissile("FrostMissileDM",-9,0,-4,1)
        TNT1 A 0 A_FireCustomMissile("FrostMissileDM",-5,0,-2,1)
        
        TNT1 A 0 A_FireCustomMissile("FrostMissileDM2",0,0,0,8,0,0)
        TNT1 A 0 A_FireCustomMissile("FrostMissileDM3",0,0,0,16,0,0)
        TNT1 A 0 A_FireCustomMissile("FrostMissileDM4",0,0,0,24,0,0)
        TNT1 A 0 A_FireCustomMissile("FrostMissileDM2",0,0,0,-2,0,0)
        TNT1 A 0 A_FireCustomMissile("FrostMissileDM3",0,0,0,-3,0,0)
        TNT1 A 0 A_FireCustomMissile("FrostMissileDM4",0,0,0,-4,0,0)
        
        TNT1 A 0 A_FireCustomMissile("FrostMissileDM2",4,0,1,16,0,0)
        TNT1 A 0 A_FireCustomMissile("FrostMissileDM2",4,0,1,-2,0,0)
        TNT1 A 0 A_FireCustomMissile("FrostMissileDM2",-4,0,-1,16,0,0)
        TNT1 A 0 A_FireCustomMissile("FrostMissileDM2",-4,0,-1,-2,0,0)
        //Y'ALL MOTHERFUCKERS BETTER APPRECIATE IT
        TNT1 A 0 A_FireCustomMissile("FrostMissileDM",0,0)
		TNT1 A 0 A_TakeInventory("Clip", 2, TIF_NOTAKEINFINITE)
        CONE M 4 BRIGHT //A_FireConePL1
        CONE N 4
        CONE H 5 
		TNT1 A 0 A_JumpIfInventory("Clip",2,1)
		Goto SwitchWeapon
        CONE H 7 A_ReFire("HoldMage")
        Goto Ready
		
      FrostMeleeMage:
        CONE L 5 BRIGHT
        TNT1 A 0 A_PlayWeaponSound("MageShardsFire")
		TNT1 A 0 A_TakeInventory("Clip", 2, TIF_NOTAKEINFINITE)
        CONE M 4 BRIGHT A_FireCustomMissile("FrostMissileMelee2",0,0)//A_CustomPunch(3,0,0,"BlankPuff3",84)//A_Saw("silence", "silence", 4, "BlankPuff")
        CONE N 4
        CONE H 5 
		TNT1 A 0 A_JumpIfInventory("Clip",2,1)
		Goto SwitchWeapon
        CONE H 7 A_ReFire("HoldMage")
        Goto Ready		

	  CoopHold:
		H114 EG 3
		TNT1 A 0 A_Refire
		TNT1 A 0 A_FireCustomMissile("HexenShockArrow2",0,1,5,0)
		TNT1 A 0 A_TakeInventory("Clip",3,TIF_NOTAKEINFINITE)
		H114 H 3 A_PlayWeaponSound("Hexen/ElectricArrow")
		H114 I 3
		H114 CB 3
		Goto Ready	  
		
      CoopFireMage:
        CONE K 3
        CONE L 5 BRIGHT
        TNT1 A 0 A_FireConePL1
        TNT1 A 0 A_FireCustomMissile("FrostMissileMelee",0,0)//A_CustomPunch(3,0,0,"BlankPuff3",84)//A_Saw("silence", "silence", 4, "BlankPuff")
        TNT1 A 0 A_TakeInventory("Clip", 2, TIF_NOTAKEINFINITE)
		CONE M 4 BRIGHT //A_FireConePL1
        CONE N 4
        CONE H 5 
		TNT1 A 0 A_JumpIfInventory("Clip",2,1)
		Goto SwitchWeapon
        CONE H 7 A_ReFire("HoldMage")
        Goto Ready
		
	  FireFighter:
		TNT1 A 0 A_JumpIfCloser(128,"FighterMelee")
		H101 A 0 A_JumpIfInventory("Clip", 3, "FireFighterGlow")
		H101 E 4 
		H101 F 4 
		H101 G 3
		H101 H 2 A_PlayWeaponSound("FighterHammerMiss")
		H101 I 2 A_FireCustomMissile("Hexen_FSpearFX1",0,0,8,16)
		H101 JKLM 2 
		TNT1 A 16
		H101 A 3 Offset(0,92)
		H101 A 3 Offset(0,72)
		H101 A 3 Offset(0,52)
		Goto Ready
	  FireFighterGlow:
		H101 N 4 
		H101 O 4 
		H101 P 3 
		H101 Q 2 A_PlayWeaponSound("FighterHammerMiss")
		TNT1 A 0 A_TakeInventory("Clip",3,TIF_NOTAKEINFINITE)
		H101 I 2 A_FireCustomMissile("Hexen_FSpearFX2",0,0,8,16)
		H101 JKLM 2 
		TNT1 A 16
		H101 A 0 A_JumpIfInventory("Clip", 3, 4)
		H101 A 3 Offset(0,92)
		H101 A 3 Offset(0,72)
		H101 A 3 Offset(0,52)
		Goto Ready
		H101 A 3 Offset(0,92)
		H101 D 3 Offset(0,72)
		H101 C 3 Offset(0,52)
		Goto Ready	
		
	  FighterMelee:
		H101 A 0 A_JumpIfInventory("Clip", 3, "FighterMeleeGlow")
		H101 A 2 Offset(-32,34)
		H101 A 2 Offset(-64,38)
		H101 A 2 Offset(-96,42) A_CustomPunch(random(24,48),1,1,"AxePuff2",128)
		H101 A 2 Offset(-64,38)
		H101 A 2 Offset(-32,34)
		H101 A 8 Offset(0,32)
		Goto Ready
		
	  FighterMeleeGlow:
		H101 B 2 Bright Offset(-32,34)
		H101 B 2 Bright Offset(-64,38)
		H101 B 2 Bright Offset(-96,42) A_CustomPunch(random(48,96),1,1,"SpearPuffGlow",128)
		H101 B 2 Bright Offset(-64,38)
		H101 B 2 Bright Offset(-32,34)
		H101 B 8 Bright Offset(0,32)
		Goto Ready
		
	  SwitchWeapon:
		TNT1 A 0 A_JumpIf(CallACS("Hexen_SwitchPriority")==1,1)
		TNT1 A 0
		Goto Ready
    }
}

ACTOR Hexen_FSpearFX1
{
	Speed 50
	Radius 6
	Height 6
	Damage 10
	Projectile
	DeathSound "Hexen/weapons/spearhit"
	Obituary "%o was impaled by %k's throwing spears."
	States
	{
		Spawn:
			H101 R 1 A_SpawnItemEx("Hexen_FSpearFX3",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
			Loop
		Death:
			TNT1 A 2
			Stop
	}
}

ACTOR Hexen_FSpearFX2
{
	Speed 90
	Radius 4
	Height 6
	Damage 35
	Projectile
	DeathSound "Hexen/weapons/spearhit"
	Obituary "%o was impaled by %k's throwing spears."
	States
	{
		Spawn:
			H101 S 1 Bright A_SpawnItemEx("Hexen_FSpearFX4",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
			Loop
		Death:
			TNT1 AAAAAAAAA 0 A_SpawnItemEx("Hexen_FSpearFX5",0,0,2,frandom(-4,4),frandom(-4,4),random(-2,6),0,SXF_NOCHECKPOSITION)
			Stop
	}
}

ACTOR Hexen_FSpearFX3
{
	Radius 2
	Height 2
	+NOINTERACTION
	+CLIENTSIDEONLY
	States
	{
		Spawn:
			H101 R 2
			H101 RRRR 2 A_FadeOut(0.2)
			Stop
	}
}

ACTOR Hexen_FSpearFX4
{
	Radius 2
	Height 2
	+NOINTERACTION
	+CLIENTSIDEONLY
	States
	{
		Spawn:
			H101 S 2 Bright
			H101 SSSS 2 Bright A_FadeOut(0.2)
			Stop
	}
}

ACTOR Hexen_FSpearFX5
{
	Radius 2
	Height 2
	Damage 0
	PROJECTILE
	-NOGRAVITY
	+RIPPER
	+DONTSPLASH
	RenderStyle Add
	Alpha 0.85
	Obituary "%o was impaled by %k's throwing spears."
	States
	{
		Spawn:
			WRBL J 1 Bright NoDelay A_Jump(64,"Spawn2")
			WRBL K 2 Bright
			WRBL L 1 Bright 
			WRBL M 2 Bright
			WRBL N 1 Bright 
			WRBL O 2 Bright
			WRBL P 1 Bright 
			Stop
		Spawn2:
			WRBL Q 4 Bright 
			WRBL R 3 Bright
			WRBL S 2 Bright
			TNT1 A 0 A_Jump(128,1)
			Stop
			WRBL J 1 Bright
			WRBL K 2 Bright
			WRBL L 1 Bright 
			WRBL M 2 Bright
			WRBL N 1 Bright 
			WRBL O 2 Bright
			WRBL P 1 Bright 
			Stop
	}
}

actor FrostMissileMelee2 : FastProjectile
{
    Damage (random(70,95)) // Previously 90-115
    Radius 6
    Height 6
    Speed 20
    DamageType "HexenIce"
    Projectile
    -BLOODSPLATTER
    States
    {
      Spawn:
        TNT1 A 1
        TNT1 A 1
        Stop

      Death:
        TNT1 A 1 A_Stop
        Stop
    }
}

actor FrostMissileMelee : FastProjectile
{
    Damage 6
    Radius 1
    Height 1
    Speed 20
    DamageType "HexenIce"
    Projectile
    +STRIFEDAMAGE
    -BLOODSPLATTER
    States
    {
      Spawn:
        TNT1 A 1
        TNT1 A 1
        Stop

      Death:
        TNT1 A 1 A_Stop
        Stop
    }
}

actor FrostMissileDM : FrostMissile
{
    Damage (random(1,4))
    Obituary "$SAMSARA_HEXEN_OB_SLOT2"
    -ICEDAMAGE
    DamageType "HexenIce"
    +THRUGHOST
    +BLOODSPLATTER
}

actor FrostMissileDM2 : FrostMissileDM
{
    Speed 23
}

actor FrostMissileDM3 : FrostMissileDM
{
    Speed 21
}

actor FrostMissileDM4 : FrostMissileDM
{
    Speed 19
}

actor FrostMissile2 : FrostMissile replaces FrostMissile
{
    Obituary "$SAMSARA_HEXEN_OB_SLOT2"
    -ICEDAMAGE
    DamageType "HexenIce"
    +THRUGHOST
    +THRUSPECIES
    +BLOODSPLATTER
    Species "Player"
}

actor BlankPuff3 : BlankPuff
{
    -ICEDAMAGE
    DamageType "HexenIce"
}

actor SpearPuffGlow : AxePuffGlow
{
    DamageType "TimonsAxe"
    +PUFFGETSOWNER
    States
    {
    Spawn:
         TNT1 A 0
         Stop
    Melee:
         TNT1 A 1
         TNT1 A 1 A_TakeFromTarget("Clip",3,TIF_NOTAKEINFINITE)
         H101 TUVWXYZ 4 Bright
         goto Spawn
    Crash:
         FHFX STUVW 4
         stop
    }
}

Actor HexenShockArrow 
{
	+DONTREFLECT
	Damage 21
	Radius 5
	Height 5
	PROJECTILE
	Obituary "%o was shocked by one of %k's thunder arrows"
	DamageType "Electric"
	DeathSound "Hexen/ArrowHit"
	Speed 50
	States
	{
		Spawn:
			H115 A 1 bright A_SpawnItem("HexenElectroTrail",0,0,0)
			Loop
		Death:
			TNT1 A 1 A_AlertMonsters
			H115 BCDEFGH 4 Bright
			Stop
	}
}

Actor HexenShockArrow2 : HexenShockArrow { Species "Player" +THRUSPECIES }

Actor HexenElectroTrail
{
	Renderstyle Add
	+CLIENTSIDEONLY
	+NOGRAVITY
	Scale 0.2
	States
	{
		Spawn:
			H115 EFGH 2 Bright 
			Stop
	}
}