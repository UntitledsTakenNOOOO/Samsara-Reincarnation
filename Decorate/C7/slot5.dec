actor "AlienPlasmaRifle" : Corridor7WeaponBase
{
    Weapon.SelectionOrder 1500
    Weapon.SlotNumber 5
	Weapon.BobStyle InverseAlpha
	Weapon.BobRangeX 0.2
	Weapon.BobRangeY 0.2
	Weapon.BobSpeed 4.0	
    Inventory.PickUpSound "C7/AmmoPickup"
    Weapon.AmmoUse 1
    Weapon.AmmoGive 5
    Weapon.AmmoType "RocketAmmo"
	+WEAPON.ALT_AMMO_OPTIONAL
	+WEAPON.NOALERT
	Weapon.AmmoType2 "C7ProxyMineAmmo"
	Weapon.AmmoUse2 1
	Weapon.AmmoGive2 0
    Inventory.PickupMessage "You got the Alien Plasma Rifle!"
	Tag "Alien Plasma Rifle"	
    Weapon.YAdjust 11
	Inventory.RestrictedTo "Corridor7Player"	
    States
    {
      Spawn:
        7RL2 A -1
        Stop
      Ready:
        C7RL A 1 A_WeaponReady
	  Ready2:
	    C7RL BCDEFGHA 1 A_WeaponReady
        Loop
      Fire:
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"CoopFire")
		TNT1 A 0 A_AlertMonsters
        C7RL I 4 BRIGHT A_FireCustomMissile("C7Plasma")
        C7RL JK 5 BRIGHT
        C7RL L 4 
		C7RL L 0 A_Refire
        Goto Ready
      CoopFire:
		TNT1 A 0 A_AlertMonsters
        C7RL I 4 BRIGHT A_FireCustomMissile("C7Plasma2")
        C7RL JK 5 BRIGHT
        C7RL L 4 
		C7RL L 0 A_Refire
        Goto Ready	
    }
}

actor C7Plasma
{
    Radius 8
    Height 6
    Speed 20
    Damage (0)
    Projectile
    +FORCERADIUSDMG
	+NODAMAGETHRUST
    Decal Scorch
    SeeSound "C7/AlienPlasmaRifle"
    DeathSound "C7/Explosion"
    Obituary "%o was deep fried by %k's Alien Plasma Rifle."
	var int user_distance;
    States
    {
      Spawn:
        7MIS A 2 Bright A_SetUserVar(user_distance, user_distance+10)
		7MIS B 2 Bright A_SetUserVar(user_distance, user_distance+5)
		7MIS C 2 Bright A_SetUserVar(user_distance, user_distance+10)
		7MIS D 2 Bright A_SetUserVar(user_distance, user_distance+5)
		7MIS E 2 Bright A_SetUserVar(user_distance, user_distance+10)
		7MIS F 2 Bright A_SetUserVar(user_distance, user_distance+5)
		7MIS G 2 Bright A_SetUserVar(user_distance, user_distance+10)
		7MIS H 2 Bright A_SetUserVar(user_distance, user_distance+5)
        Loop
      Death:
        7MIS I 8 Bright A_Explode(100+user_distance,128,0, true, 16)
        7MIS J 6 Bright
        7MIS K 4 Bright
        Stop
    }
}

actor C7Plasma2 : C7Plasma { +THRUSPECIES Species "Player" }