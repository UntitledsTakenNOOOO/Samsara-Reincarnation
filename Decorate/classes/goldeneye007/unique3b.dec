actor Goldeneye_WatchLaser : SamsaraGoldenEyeWeapon
{
    Inventory.Icon "GEWWTC01"
    Weapon.SlotNumber 0
    Weapon.SelectionOrder 6
    Obituary "%o was killed by %k's watch laser."
    Tag "Watch (Laser)"
    States
    {
      Spawn:
        PIST A -1
        stop

      // Weapon
      Ready:
        TNT1 A 0 A_JumpIfInventory("BondWatchQueue",1,"WatchRaise")
        TNT1 A 0 A_JumpIfInventory("BondWatchToken",1,"WatchReady")
      ReadyStart:
        TNT1 A 0 A_JumpIfInventory("Goldeneye_WatchDetonator",1,3)
        TNT1 A 0 A_JumpIfInventory("Goldeneye_WatchMagnet",1,2)
        GE00 A 1 A_WeaponReady(WRF_NOSECONDARY)
        goto Ready
        GE00 A 1 A_WeaponReady
        goto Ready

      Deselect:
        GE00 A 0 A_TakeInventory("BondFireDelay")
        GE00 A 0 A_TakeInventory("BondAlternateFire")
        GE00 A 0 A_TakeInventory("BondDualWieldToken")
        GE00 A 0 A_TakeInventory("BondAltToggle")
        GE00 A 0 A_TakeInventory("BondZoomToken")
        GE00 A 0 A_TakeInventory("BondZoomAmountModifyActive")
        GE00 A 0 A_TakeInventory("BondDualWieldSpamProtector")
        GE00 A 0 A_TakeInventory("BondDryFireSpamProtector1")
        GE00 A 0 A_TakeInventory("BondDryFireSpamProtector2")
        GE00 A 0 A_TakeInventory("BondAltActivationToken")
        GE00 A 1 A_Lower
        loop

      Select:
        GE00 A 0 A_GiveInventory("Goldeneye_UnarmedToken")
        GE00 A 0 A_TakeInventory("Goldeneye_PP7Token")
        GE00 A 0 A_TakeInventory("Goldeneye_KnifeToken")
        GE00 A 0 A_TakeInventory("Goldeneye_KF7Token")
        GE00 A 0 A_TakeInventory("Goldeneye_AutoShotgunToken")
        GE00 A 0 A_TakeInventory("Goldeneye_AR33Token")
        GE00 A 0 A_TakeInventory("Goldeneye_RocketLauncherToken")
        GE00 A 0 A_TakeInventory("Goldeneye_RCP90Token")
        GE00 A 0 A_TakeInventory("Goldeneye_LaserToken")
        GE00 A 0 A_TakeInventory("Goldeneye_GoldenGunToken")
        GE00 A 0 A_TakeInventory("Goldeneye_SniperToken")
        GE00 A 0 A_TakeInventory("Goldeneye_D5KToken")
        GE00 A 1 A_Raise
        loop

      Fire:
        GE01 A 0 A_GiveInventory("BondFiringToken")
        GE01 A 0 A_AlertMonsters(128)
        GE01 ABCD 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        GE01 D 0 A_GunFlash("WatchBeam",GFF_NOEXTCHANGE)
        GE01 D 0 A_GiveInventory("SamsaraGoldenEyeWatchLaserSoundHandler")
        GE01 D 0 A_GiveInventory("SamsaraGoldenEyeWatchLaserAttackHandler")
        GE01 D 0 A_TakeInventory("BondFiringToken")
        GE01 D 0 A_ReFire(1)
        goto FireContinue
        GE01 D 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        goto FireLoop

      FireContinue:
        GE01 DCB 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        goto FireFinish

      FireLoop:
        GE01 D 0 A_GiveInventory("BondFiringToken")
        GE01 D 0 A_AlertMonsters(128)
        GE01 D 0 A_GunFlash("WatchBeam",GFF_NOEXTCHANGE)
        GE01 D 0 A_GiveInventory("SamsaraGoldenEyeWatchLaserSoundHandler")
        GE01 D 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        GE01 D 0 A_GiveInventory("SamsaraGoldenEyeWatchLaserAttackHandler")
        GE01 D 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        GE01 D 0 A_GiveInventory("SamsaraGoldenEyeWatchLaserAttackHandler")
        GE01 D 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        GE01 D 0 A_TakeInventory("BondFiringToken")
        GE01 D 0 A_ReFire(1)
        goto FireLoopContinue
        GE01 D 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        loop

      FireLoopContinue:
        GE01 DCBA 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        goto FireFinish

      FireFinish:
        TNT1 A 0 A_TakeInventory("BondFiringToken")
        goto Ready

      WatchBeam:
        GE01 E 3 Bright
        stop

      AltFire:
        TNT1 A 0 A_JumpIfInventory("Goldeneye_WatchDetonator",1,2)
        TNT1 A 0 A_SelectWeapon("Goldeneye_WatchMagnet")
        goto Deselect
        TNT1 A 0 A_SelectWeapon("Goldeneye_WatchDetonator")
        goto Deselect

      // Watch
      WatchLower:
        GE14 T 0 A_TakeInventory("BondWatchActivate")
        GE14 T 0 A_GiveInventory("BondWatchUIDeinit")
      WatchLowerStart:
        GE14 T 0 ACS_NamedExecuteAlways("BondWatchLookUpAndDown")
        GE14 T 12 A_WeaponReady(WRF_NOBOB|WRF_NOFIRE|WRF_DISABLESWITCH)
        GE14 SRQPONMLKJIHGFEDCBA 2 A_WeaponReady(WRF_NOBOB|WRF_NOFIRE|WRF_DISABLESWITCH)
        GE02 IHGFEDCBA 1 A_WeaponReady(WRF_NOFIRE|WRF_DISABLESWITCH)
        goto WatchLowerFinish

      WatchRaise:
        GE02 A 0 A_ZoomFactor(1)
        GE02 A 0 A_TakeInventory("BondAltToggle")
        GE02 A 0 A_TakeInventory("BondZoomToken")
        GE02 A 0 A_TakeInventory("BondZoomAmountModifyActive")
        GE02 A 0 A_JumpIfInventory("BondWatchToken",1,"WatchLower")
        GE02 A 0 A_GiveInventory("BondWatchActivate")
        GE02 ABCDEFGHI 1 A_WeaponReady(WRF_NOFIRE|WRF_DISABLESWITCH)
        goto WatchRaiseStart
    }
}

// Sound handler

actor SamsaraGoldenEyeWatchLaserSoundHandler : Trigger
{
    States
    {
      // Sound playing stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraAlternateSoundToken",1,"PickupSource")
      // Original sounds
      PickupN64:
        TNT1 A 0 A_PlaySound("Goldeneye/WatchLaser",CHAN_WEAPON)
        stop

      // Source sounds
      PickupSource:
        TNT1 A 0 A_PlaySound("GoldeneyeSource/WatchLaser",CHAN_WEAPON)
        stop
    }
}

// Attack handler

actor SamsaraGoldenEyeWatchLaserAttackHandler : Trigger
{
    States
    {
      // Attack stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraAlternateSoundToken",1,"PickupSource")
      // Original sounds
      PickupN64:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupN64Coop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupN64Coop")
      PickupN64DM:
        TNT1 A 0 A_FireCustomMissile("Goldeneye_InvisibleRay",0,false,0,16)
        stop

      PickupN64Coop:
        TNT1 A 0 A_FireCustomMissile("Goldeneye_InvisibleRayCoop",0,false,0,16)
        stop

      // Source sounds
      PickupSource:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupSourceCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupSourceCoop")
      PickupSourceDM:
        TNT1 A 0 A_FireCustomMissile("Goldeneye_InvisibleRaySoundSource",0,false,0,16)
        stop

      PickupSourceCoop:
        TNT1 A 0 A_FireCustomMissile("Goldeneye_InvisibleRayCoopSoundSource",0,false,0,16)
        stop
    }
}

actor Goldeneye_LaserRay : Goldeneye_BulletTracer
{
    Speed 0
    RenderStyle "Translucent"
    States
    {
      Death:
        TNT1 A 1
        stop

      XDeath:
        TNT1 A 0 A_SetUserVar("user_damage",5)
        TNT1 A 0 A_Explode(CallACS("BondHitBox", user_damage),8,0,false,8)
        TNT1 A 0 A_SpawnItemEx("Goldeneye_BulletImpact",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
        goto Death
		
	  Crash:
        TNT1 A 0 A_SetUserVar("user_damage",5)
        goto Super::Crash
    }
}

actor Goldeneye_InvisibleRay : Goldeneye_BulletTracerThruGhost
{
    Speed 128
    States
    {
      Spawn:
        TNT1 A 0
        stop

      Death:
      Crash:
		TNT1 A 0 A_SetUserVar("user_damage",5)
		TNT1 A 0 A_Explode(CallACS("BondHitBox", user_damage),8,0,false,8)
        TNT1 AAAAAAAA 0 A_SpawnItemEx("Goldeneye_Debris",random(-1,1),random(-1,1),random(-1,1),random(-1,1),random(-1,1),random(-1,1),random(-360,360),SXF_NOCHECKPOSITION)
      DeathFinish:
        TNT1 A 0 A_PlaySound("Goldeneye/Zap")
        TNT1 A 1 A_SpawnItemEx("Goldeneye_BulletImpact",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
        stop

      XDeath:
        TNT1 A 0 A_SetUserVar("user_damage",5)
        TNT1 A 0 A_Explode(CallACS("BondHitBox", user_damage),8,0,false,8)
        goto DeathFinish
    }
}

actor Goldeneye_InvisibleRayCoop : Goldeneye_InvisibleRay
{
    Species "Player"
    +THRUSPECIES
}

actor Goldeneye_InvisibleRaySoundSource : Goldeneye_InvisibleRay
{
    States
    {
      Death:
      Crash:
		TNT1 A 0 A_SetUserVar("user_damage",5)
		TNT1 A 0 A_Explode(CallACS("BondHitBox", user_damage),8,0,false,8)
        TNT1 AAAAAAAA 0 A_SpawnItemEx("Goldeneye_Debris",random(-1,1),random(-1,1),random(-1,1),random(-1,1),random(-1,1),random(-1,1),random(-360,360),SXF_NOCHECKPOSITION)
      DeathFinish:
        TNT1 A 0 A_PlaySound("GoldeneyeSource/Zap")
        TNT1 A 1 A_SpawnItemEx("Goldeneye_BulletImpact",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
        stop

      XDeath:
        TNT1 A 0 A_SetUserVar("user_damage",5)
        TNT1 A 0 A_Explode(CallACS("BondHitBox", user_damage),8,0,false,8)
        goto DeathFinish
    }
}

actor Goldeneye_InvisibleRayCoopSoundSource : Goldeneye_InvisibleRaySoundSource
{
    Species "Player"
    +THRUSPECIES
}
