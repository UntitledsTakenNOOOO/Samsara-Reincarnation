actor " Pistol " : Weapon// : Pistol  doom 64 pistol fires slightly faster, otherwise there are no differences
{
    Weapon.SelectionOrder 1900
    Decal BulletChip
    Weapon.SlotNumber 1  
    Weapon.SlotPriority 1
    Weapon.AmmoUse 0
    Weapon.AmmoGive 0
    Weapon.AmmoType ""
    +WEAPON.WIMPY_WEAPON
    +INVENTORY.UNDROPPABLE
    DamageType Pistol
    Inventory.Pickupmessage "i eat you"
    Obituary "$SAMSARA_DOOMGUY_OB_PISTOL"
	Inventory.RestrictedTo "DoomguyPlayer"
    States
    {
      Ready:
		TNT1 A 0 A_JumpIfInventory("Doom64Mode", 1, "Ready64")
        DPIS A 1 A_WeaponReady
        Loop
		
      Ready64:
        64PS A 1 A_WeaponReady
        Goto Ready		

      Deselect:
		TNT1 A 0 A_JumpIfInventory("Doom64Mode", 1, "Deselect64")
        DPIS A 1 A_Lower
        Loop
		
      Deselect64:
        64PS A 1 A_Lower
        Loop		

      Select:
		TNT1 A 0 A_JumpIfInventory("Doom64Mode", 1, "Select64")
        DPIS A 1 A_Raise
        Loop
		
      Select64:
        64PS A 1 A_Raise
        Loop			

      Fire:
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,"CheckForAmmo")
      Fire2:
		TNT1 A 0 A_JumpIfInventory("Doom64Mode", 1, "Fire64")	 
        DPIS A 4
		TNT1 A 0 A_JumpIfInventory("VanillaDoom", 1, "VanillaFire")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn", 1, "CoopFire")
        TNT1 A 0 A_PlayWeaponSound("doomguy/pistol")
        TNT1 A 0 A_FireBullets (5.6, 0, 1, Random(6,8)*2, "DoomBulletPuff2", FBF_NORANDOM)
        goto FireAnim
		
      CoopFire:
        TNT1 A 0
        TNT1 A 0 A_PlayWeaponSound("doomguy/pistol")
        TNT1 A 0 A_FireBullets (5.6, 0, 1, Random(6,8)*2, "DoomBulletPuff2_Coop", FBF_NORANDOM)
        Goto FireAnim		
		
      CoopFireVanilla:
        TNT1 A 0
        TNT1 A 0 A_PlayWeaponSound("doomguy/pistol")
        TNT1 A 0 A_FireBullets (5.6, 0, 1, 5, "DoomBulletPuff2_Coop")
        Goto FireAnim		
		
	  Fire64:	
		TNT1 A 0 A_JumpIfInventory("VanillaDoom", 1, "VanillaFire64")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn", 1, "CoopFire64")
        TNT1 A 0 A_FireBullets (5.6, 0, 1, Random(5,7)*2, "Doom64BulletPuff2", FBF_NORANDOM)
        TNT1 A 0 A_PlayWeaponSound("doom64guy/pistol")
        goto Animation64	
		
	  VanillaFire64:
        TNT1 A 0 A_JumpIfInventory("CoopModeOn", 1, "CoopFire64Vanilla")
        TNT1 A 0 A_FireBullets (5.6, 0, 1, 5, "Doom64BulletPuff2")
        TNT1 A 0 A_PlayWeaponSound("doom64guy/pistol")
        goto Animation64	  
		
      CoopFire64:
        TNT1 A 0 A_FireBullets (5.6, 0, 1, Random(5,7)*2, "Doom64BulletPuff2_Coop", FBF_NORANDOM)
        TNT1 A 0 A_PlayWeaponSound("doom64guy/pistol")
        goto Animation64		
        
      CoopFire64Vanilla:
        TNT1 A 0 A_FireBullets (5.6, 0, 1, 5, "Doom64BulletPuff2_Coop")
        TNT1 A 0 A_PlayWeaponSound("doom64guy/pistol")
        goto Animation64		
		
      CheckForAmmo:
		TNT1 A 0 A_JumpIfInventory("Doom64Mode", 1, "CheckAmmo64")	  
        TNT1 A 0 A_JumpIfInventory("Clip",1,"Fire2")
        TNT1 A 0 A_SelectWeapon(" Fist ")
        goto Deselect
		
	  CheckAmmo64:
		TNT1 A 0 A_JumpIfInventory("Clip",1,2)
		TNT1 A 0 A_SelectWeapon(" Fist ")
		goto Deselect64
		TNT1 A 0 A_TakeInventory("Clip",1)
		goto Fire64			

      FireAnim:
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,"TakePistolAmmo")
      FireAnim2:
        TNT1 A 0 A_GunFlash
        TNT1 A 0 A_JumpIfInventory("RuneDoubleFiringSpeed", 1, "RageAnim")
        DPIS B 2 bright
        DPIS C 1 bright
        DPIS CEEDDBB 1
        DPIS A 5 A_ReFire
        Goto Ready
		
      Animation64:		
        64PS B 4 BRIGHT A_GunFlash("Flash64")
        64PS C 9
        64PS B 3 A_ReFire
        Goto Ready		

      RageAnim:
        DPIS BC 1 bright
        DPIS EDB 1
        DPIS A 3 A_Refire
        goto Ready
        
      VanillaFire:
		TNT1 A 0 A_JumpIfInventory("CoopModeOn", 1, "CoopFireVanilla")
		TNT1 A 0 A_PlayWeaponSound("doomguy/pistol")
		TNT1 A 0 A_FireBullets (5.6, 0, 1, 5, "DoomBulletPuff2")
        DPIS F 6 BRIGHT A_GunFlash
        DPIS G 4
        DPIS H 5 A_ReFire
        Goto Ready
        
      TakePistolAmmo:
        TNT1 A 0 A_TakeInventory("Clip",1)
        goto FireAnim2

      Hold:
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,"CheckForAmmoHold")
      Hold2:
		TNT1 A 0 A_JumpIfInventory("Doom64Mode", 1, "Hold64")	
        DPIS A 4
		TNT1 A 0 A_JumpIfInventory("VanillaDoom", 1, "VanillaFire")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn", 1, "CoopHold")
        TNT1 A 0 A_PlayWeaponSound("doomguy/pistol")
        TNT1 A 0 A_FireBullets (5.6, 0, -1, Random(6,8)*2, "DoomBulletPuff2", FBF_NORANDOM)
        Goto FireAnim

      CoopHold:
        TNT1 A 0
        TNT1 A 0 A_PlayWeaponSound("doomguy/pistol")
        TNT1 A 0 A_FireBullets (5.6, 0, -1, Random(6,8)*2, "DoomBulletPuff2_Coop", FBF_NORANDOM)
        Goto FireAnim
		
	  Hold64:
        TNT1 A 0 A_JumpIfInventory("CoopModeOn", 1, "CoopHold64")
		TNT1 A 0 A_JumpIfInventory("VanillaDoom", 1, "VanillaFire64")		
        TNT1 A 0 A_FireBullets (5.6, 0, 1, Random(5,7)*2, "Doom64BulletPuff2", FBF_NORANDOM)
        TNT1 A 0 A_PlayWeaponSound("doom64guy/pistol")
        Goto Animation64		
      
      CoopHold64:
		TNT1 A 0 A_JumpIfInventory("VanillaDoom", 1, "CoopFire64Vanilla")
        TNT1 A 0 A_FireBullets (5.6, 0, 1, Random(5,7)*2, "Doom64BulletPuff2_Coop", FBF_NORANDOM)
        TNT1 A 0 A_PlayWeaponSound("doom64guy/pistol")
        Goto Animation64	  
	  
      CheckForAmmoHold:
		TNT1 A 0 A_JumpIfInventory("Doom64Mode", 1, "CheckAmmoHold64")	 
        TNT1 A 0 A_JumpIfInventory("Clip",1,"Hold2")
        TNT1 A 0 A_SelectWeapon(" Fist ")
        goto Deselect     
	
	CheckAmmoHold64:
		TNT1 A 0 A_JumpIfInventory("Clip",1,2)
		TNT1 A 0 A_SelectWeapon(" Fist ")
		goto Deselect
		TNT1 A 0 A_TakeInventory("Clip",1)
		goto Hold64		
        
      Flash:
        TNT1 A 0 A_JumpIfInventory("VanillaDoom", 1, "VanillaFlash")
        DPSF A 2 Bright A_Light1
        TNT1 A 2 A_Light1
        Goto LightDone
		
      Flash64:
        64PM A 4 Bright
        TNT1 A 2
        Goto LightDone		
      
      VanillaFlash:
        TNT1 A 3 Bright A_Light1
        TNT1 A 2 A_Light1
        Goto LightDone

      Spawn:
        WPST A -1
        Stop
    }
}

actor DoomBulletPuff2 : DoomBulletPuff { DamageType "DoomDamageType" Species "Player" }
actor DoomBulletPuff2_Coop : DoomBulletPuff2 { +THRUSPECIES +MTHRUSPECIES Species "Player" }

actor DoomBullet2: FastProjectile
{
    Damage (Random(1, 3)*5)
    Radius 1
    Height 1
    Speed 320
    Projectile
    DamageType "DoomDamageType"
    Renderstyle "Normal"
    Species "Player"
    +BLOODSPLATTER
    +THRUSPECIES
    +NOTIMEFREEZE
    Alpha 1.0
    Decal Bulletchip
    States
    {
      Spawn:
        TNT1 A 1
        loop

      Crash:
      Death:
        TNT1 A 1 A_SpawnItem("DoomBulletPuff2")
        stop

      XDeath:
        TNT1 A 1
        stop
    }
}


actor Doom64BulletPuff2 : Doom64BulletPuff { DamageType Pistol }
actor Doom64BulletPuff2_Coop : Doom64BulletPuff2 { Species "Player" +THRUSPECIES +MTHRUSPECIES }

actor Doom64Bullet2: DoomBullet2
{
    Decal BulletChip
    States
    {
	  Crash:
      Death:
        TNT1 A 1 A_SpawnItem("Doom64BulletPuff2")
        stop
    }
}