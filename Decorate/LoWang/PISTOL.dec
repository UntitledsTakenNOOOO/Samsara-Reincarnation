//Shuriken://///////////////////////////////////////////////////////////////////
Actor SWShuriken : Weapon
{
	+WEAPON.WIMPY_WEAPON
	+WEAPON.NOALERT
	+INVENTORY.UNDROPPABLE
    Weapon.BobRangeX 0.75
    Weapon.BobRangeY 0.95
    Weapon.BobSpeed 1.1	
	Weapon.SelectionOrder 3800
	Weapon.Slotnumber 1
	Inventory.RestrictedTo "LoWangPlayer"		
	Tag "Shurikens"
	states
	{
	Spawn:
		SWP0 Z -1
		stop
	Select:
		TNT1 A 0 A_GiveInventory("SWFistUsed", 1)
		TNT1 A 0 A_Jump(160,"SelectCheck")
		TNT1 A 0 A_JumpIfInventory("LoWangTauntCooldown",1,"SelectCheck")		
		TNT1 A 0 A_JumpIfInventory("LoWangBallgag",1,"SelectCheck")		
		TNT1 A 0 A_PlaySound("LoWang/Shuriken", CHAN_VOICE)
		TNT1 A 0 A_GiveInventory("LoWangTauntCooldown",5)
		TNT1 A 0 ACS_NamedExecuteAlways("LoWangTauntCooldown", 0)
	SelectCheck:	
		TNT1 A 0 A_JumpIfInventory("SWLoWangBloody",1,"Raise1")
		goto Raise2
	Raise1:
		TNT1 A 0 A_Raise	
		SHUR I 1 A_Raise
		loop
	Raise2:
		TNT1 A 0 A_Raise	
		SHUR A 1 A_Raise
		loop
	Deselect:
		TNT1 A 0 A_TakeInventory("SWFistUsed", 1)
		TNT1 A 0 A_JumpIfInventory("SWLoWangBloody",1,"Lower1")
		goto Lower2
	Lower1:
		TNT1 A 0 A_Lower	
		SHUR I 1 A_Lower
		loop
	Lower2:
		TNT1 A 0 A_Lower	
		SHUR A 1 A_Lower
		loop
	Ready:
		TNT1 A 0 A_TakeInventory("SWFistYell", 0)
		TNT1 A 0 A_JumpIfInventory("LoWangReady",1,"IntroTaunt")	
		TNT1 A 0 A_JumpIfInventory("SWLoWangBloody",1,"Idle1")
		goto Idle2
	Idle1:
		SHUR I 1 A_WeaponReady
		loop
	Idle2:
		SHUR A 1 A_WeaponReady
		loop
	Fire:
		TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,"CheckAmmo")
	Fire2:
		SHUR ABC 1
		TNT1 A 1
	Hold:
		TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,"CheckAmmoHold")	
	Hold2:
		TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"CoopHold")	
		TNT1 A 0 A_GiveInventory("SWKatanaYell",1)
		TNT1 A 0 A_JumpIfInventory("SWKatanaYell",9,2)
		TNT1 A 0 A_Jump(256,7)
		TNT1 A 0 A_TakeInventory("SWKatanaYell",999)
		TNT1 A 0 A_JumpIfInventory("LoWangTauntCooldown", 1, 5)
		TNT1 A 0 A_JumpIfInventory("LoWangBallgag", 1, 4)
		TNT1 A 0 A_PlaySound("sw/yell2",CHAN_VOICE)
		TNT1 A 0 A_GiveInventory("LoWangTauntCooldown",2)
		TNT1 A 0 ACS_NamedExecuteAlways("LoWangTauntCooldown", 0)
		SHUR CDE 1 A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
		TNT1 A 0 A_PlaySound("sw/swing")
		TNT1 A 0 A_FireCustomMissile("SWThrownShuriken",0,0,0,8)
		TNT1 A 0 A_FireCustomMissile("SWThrownShuriken",5,0,6,8)
		TNT1 A 0 A_FireCustomMissile("SWThrownShuriken",-5,0,-6,8)
		SHUR FGH 1 A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
		TNT1 A 1 A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
		SHUR CBA 1 A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
		TNT1 A 0 A_Refire
		goto Ready
	  CoopHold:
		TNT1 A 0 A_GiveInventory("SWKatanaYell",1)
		TNT1 A 0 A_JumpIfInventory("SWKatanaYell",9,2)
		TNT1 A 0 A_Jump(256,7)
		TNT1 A 0 A_TakeInventory("SWKatanaYell",999)
		TNT1 A 0 A_JumpIfInventory("LoWangTauntCooldown", 1, 5)
		TNT1 A 0 A_JumpIfInventory("LoWangBallgag", 1, 4)
		TNT1 A 0 A_PlaySound("sw/yell2",CHAN_VOICE)
		TNT1 A 0 A_GiveInventory("LoWangTauntCooldown",2)
		TNT1 A 0 ACS_NamedExecuteAlways("LoWangTauntCooldown", 0)
		SHUR CDE 1 A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
		TNT1 A 0 A_PlaySound("sw/swing")
		TNT1 A 0 A_FireCustomMissile("SWThrownShurikenCoop",0,0,0,8)
		TNT1 A 0 A_FireCustomMissile("SWThrownShurikenCoop",5,0,6,8)
		TNT1 A 0 A_FireCustomMissile("SWThrownShurikenCoop",-5,0,-6,8)
		SHUR FGH 1 A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
		TNT1 A 1 A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
		SHUR CBA 1 A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
		TNT1 A 0 A_Refire
		goto Ready	
	IntroTaunt:
		TNT1 A 0 A_JumpIfInventory("LoWangTauntCooldown",1,3)	
		TNT1 A 0 A_JumpIfInventory("LoWangBallgag",1,2)
		TNT1 A 0 A_PlaySoundEx("LoWang/Enter", "SoundSlot7", 0, 0)
		TNT1 A 0 A_TakeInventory("LoWangReady", 1)
		TNT1 A 0 A_GiveInventory("LoWangTauntCooldown",5)
		TNT1 A 0 ACS_NamedExecuteAlways("LoWangTauntCooldown", 0)
		Goto Ready		
	CheckAmmo:
		SHUR A 0 A_JumpIfInventory("Clip",1,2)
		SHUR A 0 A_SelectWeapon("SWFists")
		Goto Deselect
		Goto Fire2
	CheckAmmoHold:
		SHUR A 0 A_JumpIfInventory("Clip",1,2)
		SHUR A 0 A_SelectWeapon("SWKatana")
		goto Deselect
		SHUR A 0 A_TakeInventory("Clip",1)
		goto Hold2	
	}
}

Actor SWThrownShuriken //: FastProjectile
{
	PROJECTILE
	+HEXENBOUNCE
	+SKYEXPLODE
	+BLOODSPLATTER
	+FORCEXYBILLBOARD
	+HITTRACER
	bouncetype "grenade"
	Bouncecount 2
	DamageType "SWShuriken"
	Speed 50
	Radius 4
	Height 4
	Scale 0.3
	Damage (random(5,10))
	states
	{
	Spawn:
		SPP0 ABCD 3
		loop
	Death:
		TNT1 A 0 A_PlaySound("sw/shuriken_clank",CHAN_BODY)
		TNT1 A 0 A_ChangeFlag("MOVEWITHSECTOR",1)
		SPP0 E 36
		stop
	Crash:
		TNT1 A 0 A_PlaySound("sw/shuriken_clank",CHAN_BODY)		
		TNT1 A 0 A_ChangeFlag("MOVEWITHSECTOR",1)		
		TNT1 A 0 A_ChangeFlag("SEEKERMISSILE",1)
		SPP0 E 36 A_SeekerMissile(90,90)
		stop
	XDeath:
		TNT1 A 1 A_GiveInventory("SWBloodGeneratorShuriken", 1, AAPTR_TRACER)
		stop
	}
}

Actor SWThrownShurikenCoop : SWThrownShuriken { Species "Player" +THRUSPECIES }