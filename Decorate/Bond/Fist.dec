ACTOR Goldeneye_Slapper : Weapon
{
  Weapon.SelectionOrder 20
  Weapon.UpSound ""
  Weapon.SlotNumber 0
  Weapon.BobStyle InverseSmooth
  Weapon.BobSpeed 1.6
  Weapon.BobRangeX 0.3
  Weapon.BobRangeY 0.4  
  Obituary "%o got slapped to death."
  +WEAPON.MELEEWEAPON
  +WEAPON.NOALERT
  +WEAPON.AMMO_OPTIONAL
  +INVENTORY.UNDROPPABLE
  Inventory.Pickupmessage "Slapper"
  Inventory.RestrictedTo "JamesBond"
  Tag "Unarmed"
  Height 16
  Radius 8
  States
  {
  Spawn:
    PIST A -1
    stop
  Ready:
	TNT1 A 0 A_JumpIfInventory("BondWatchQueue", 1, "RaiseWatch")
	TNT1 A 0 A_JumpIfInventory("BondWatchToken", 1, "WatchReady")
	Goto Center
 
  Center:
    TNT1 A 1 A_WeaponReady
    Goto Ready

  WatchReady:
	GE13 T 0 A_JumpIfInventory("BondWatchQueue", 1, "RaiseWatch")
	GE13 T 4 A_WeaponReady(WRF_DISABLESWITCH|WRF_NOFIRE)
	Loop
	
  Deselect:
	GE00 A 0 A_TakeInventory("BondFireDelay", 3)
	GE00 A 0 A_TakeInventory("BondAlternateFire", 1)
	GE00 A 0 A_TakeInventory("BondDualWieldToken", 1)
	GE00 A 0 A_TakeInventory("Goldeneye_UnarmedToken", 1)
	GE00 A 0 A_TakeInventory("BondAltToggle", 2)
	GE00 A 0 A_TakeInventory("BondZoomToken", 1)
	GE00 A 0 A_TakeInventory("BondDualWieldSpamProtector", 1)
	GE00 A 0 A_TakeInventory("BondDryFireSpamProtector1", 1)
	GE00 A 0 A_TakeInventory("BondDryFireSpamProtector2", 1)
    GE00 A 1 A_Lower
    Loop
	
  Select:	
	GE00 A 0 A_GiveInventory("Goldeneye_UnarmedToken", 1)
	GE00 A 0 A_TakeInventory("Goldeneye_PP7Token", 1)
	GE00 A 0 A_TakeInventory("Goldeneye_KnifeToken", 1)
	GE00 A 0 A_TakeInventory("Goldeneye_KF7Token", 1)
	GE00 A 0 A_TakeInventory("Goldeneye_AutoShotgunToken", 1)
	GE00 A 0 A_TakeInventory("Goldeneye_AR33Token", 1)
	GE00 A 0 A_TakeInventory("Goldeneye_RocketLauncherToken", 1)
	GE00 A 0 A_TakeInventory("Goldeneye_RCP90Token", 1)
	GE00 A 0 A_TakeInventory("Goldeneye_LaserToken", 1)
	GE00 A 0 A_TakeInventory("Goldeneye_GoldenGunToken", 1)
	GE00 A 0 A_TakeInventory("Goldeneye_SniperToken", 1)
	GE00 A 0 A_TakeInventory("Goldeneye_D5KToken", 1)
    GE00 A 1 A_Raise
    Loop
	
  Fire:
	GE00 A 0 A_GiveInventory("BondFiringToken", 1)
	GE00 A 0 A_Jump(128,"FireRight")
	Goto FireLeft
	
  FireLeft:
	GE00 B 2 A_PlaySound("Goldeneye/Slapper", CHAN_WEAPON)
    GE00 CDEF 2
    GE00 G 0  A_JumpIfInventory("CoopModeOn", 1, 2)
	GE00 G 3 A_FireCustomMissile("Goldeneye_SlapperTracer")
    Goto FireLeftEnd
    GE00 G 3 A_FireCustomMissile("Goldeneye_SlapperTracerCoop")
    Goto FireLeftEnd

  FireLeftEnd:
  	GE00 H 3
	TNT1 A 5
	GE00 A 0 A_TakeInventory("BondFiringToken", 1)
    Goto Ready

  FireRight:
	GE00 I 2 A_PlaySound("Goldeneye/Slapper", CHAN_WEAPON)
	GE00 JKLMN 2
    GE00 O 0  A_JumpIfInventory("CoopModeOn", 1, 2)
	GE00 O 3 A_FireCustomMissile("Goldeneye_SlapperTracer")
    Goto FireRightEnd
	GE00 O 3 A_FireCustomMissile("Goldeneye_SlapperTracerCoop")
    Goto FireRightEnd

  FireRightEnd:
	GE00 P 3
	TNT1 A 5
	TNT1 A 0 A_TakeInventory("BondFiringToken", 1)
    Goto Ready

  RaiseWatch:
	GE13 A 0 A_ZoomFactor(1.0)
	GE13 A 0 A_TakeInventory("BondAltToggle", 2)
	GE13 A 0 A_WeaponReady(WRF_DISABLESWITCH|WRF_NOFIRE)
	GE13 A 0 A_JumpIfInventory("BondWatchToken", 1, "LowerWatch")
	GE13 A 0 A_GiveInventory("BondWatchActivate", 1)
	GE13 A 0 A_JumpIfInventory("BondDualWieldToken", 1, 1)
	Goto RaiseWatch+8
	GE13 A 0 A_GunFlash("DualLower",GFF_NOEXTCHANGE)
	GE00 A 12
	GE13 A 0 A_TakeInventory("BondPitchTokens", 32768)
	GE13 A 0 A_TakeInventory("BondAngleTokens", 131072)
	GE13 A 0 SetPlayerProperty(0, 1, PROP_Frozen)
	GE13 A 0 ACS_NamedExecuteAlways("BondWatchLookUpAndDown")
	//GE13 T 0 A_GiveInventory("Goldeneye_TimeFreezer", 1)
	GE13 ABCDEFGHIJKLMNOPQRST 2
	GE13 T 0 A_Zoomfactor(7.5)
	GE13 T 0 A_GiveInventory("BondWatchUIInit", 1)
	GE13 T 0 A_GiveInventory("BondWatchToken", 1)
	GE13 T 0 ACS_NamedExecuteAlways("BondWatchInterface")
	GE13 T 0 A_TakeInventory("BondWatchQueue", 1)
	Goto Ready
	
  LowerWatch:
	GE13 T 0 A_Zoomfactor(1.0)
	GE13 T 0 A_TakeInventory("BondWatchActivate", 1)
	GE13 T 0 A_GiveInventory("BondWatchUIDeinit", 1)
	GE13 T 0 ACS_NamedExecuteAlways("BondWatchLookUpAndDown")
	GE13 T 10
	GE13 T 0 A_WeaponReady(WRF_DISABLESWITCH|WRF_NOFIRE)
	GE13 TSRQPONMLKJIHGFEDCBA 2
	GE13 A 0 A_JumpIfInventory("BondDualWieldToken", 1, 1)
	Goto LowerWatch+29
	GE13 A 0 A_GunFlash("DualRaise",GFF_NOEXTCHANGE)
	GE00 A 12
	GE13 A 0 A_TakeInventory("BondWatchQueue", 1)
	GE13 A 0 SetPlayerProperty(0, 0, PROP_Frozen)
	GE13 A 0 A_TakeInventory ("PowerTimeFreezer", 0x7fffffff) 
	GE13 A 0 A_TakeInventory("BondWatchToken", 1)
	GE13 A 0 A_TakeInventory("BondWatchUIInit", 1)
	GE13 A 0 ACS_NamedExecuteAlways("BondWeaponSwitch")
	Goto Ready 
  }
}

Actor Goldeneye_SlapperTracer : FastProjectile
{
	Projectile
	Speed 24
	Damage (15)
	Height 4
	Radius 2
	var int user_damage;
	ExplosionDamage 0
	+NOEXTREMEDEATH
	+HITTRACER
	+FORCERADIUSDMG
	+NODAMAGETHRUST
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 A 1 
			Stop
		Death:
			TNT1 A 2
			Stop
		XDeath:
			TNT1 A 0 A_Playsound("Goldeneye/Punch", CHAN_WEAPON)
			TNT1 A 0 A_SetUserVar("user_damage",15)
			TNT1 A 0 A_Explode(CallACS("BondHitBox",user_damage),8,0,0,8)
			TNT1 A 2
			Stop
		Crash:
			TNT1 A 0 A_Playsound("Goldeneye/SlapperMetal", CHAN_WEAPON)
			TNT1 A 2
			Stop
	}
}

Actor Goldeneye_SlapperTracerCoop : Goldeneye_SlapperTracer { +THRUSPECIES Species "Player" }