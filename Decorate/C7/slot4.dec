actor "AlienDualBlaster" : InstantSwitchWeapon
{
    Weapon.SelectionOrder 1600
    Weapon.SlotNumber 4
	Weapon.BobStyle InverseAlpha
	Weapon.BobRangeX 0.2
	Weapon.BobRangeY 0.2
	Weapon.BobSpeed 4.0	
    Inventory.PickUpSound "C7/AmmoPickup"
    Weapon.AmmoUse 1
    Weapon.AmmoGive 30
    Weapon.AmmoType "Clip"
	+WEAPON.ALT_AMMO_OPTIONAL
	+WEAPON.NOALERT
	Weapon.AmmoType2 "C7ProxyMineAmmo"
	Weapon.AmmoUse2 1
	Weapon.AmmoGive2 0
    Inventory.PickupMessage "You got the Alien Dual-Blaster!"
    Obituary "%o was on the wrong side of %k's dual-blaster."
	Tag "Alien Dual-Blaster"	
    Weapon.YAdjust 11
	Inventory.RestrictedTo "Corridor7Player"	
    States
    {
      Spawn:
        7DU2 A -1
        Loop
      Ready:
        TNT1 A 0 SetPlayerProperty(0,1,2)
        C7DU A 1 A_WeaponReady
        goto Ready+1
      Fire:
        C7DU AEFG 2
	  Hold:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"CoopFire")	  	  
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"CoopFire")
        TNT1 A 0 A_PlayWeaponSound("C7/DualBlaster")
		TNT1 A 0 A_AlertMonsters
        C7DU C 3 BRIGHT A_FireBullets(0.3,0.3,1,Random(1,32),"BlankPuffC7",FBF_NORANDOM|FBF_USEAMMO)
        TNT1 A 0 A_PlayWeaponSound("C7/DualBlaster")
        C7DU D 3 BRIGHT A_FireBullets(0.3,0.3,1,Random(1,32),"BlankPuffC7",FBF_NORANDOM|FBF_USEAMMO)
        TNT1 A 0 A_Refire
        C7DU GFEA 2 A_ClearRefire
        Goto Ready
      CoopFire:
        TNT1 A 0 A_PlayWeaponSound("C7/DualBlaster")
		TNT1 A 0 A_AlertMonsters
        C7DU C 3 BRIGHT A_FireCustomMissile("C7DBBullet",frandom(1.0,-1.0),1,0,1,0,frandom(1.0,-1.0))
        TNT1 A 0 A_PlayWeaponSound("C7/DualBlaster")
        C7DU D 3 BRIGHT A_FireCustomMissile("C7DBBullet",frandom(1.0,-1.0),1,0,1,0,frandom(1.0,-1.0))
        TNT1 A 0 A_Refire
        C7DU GFEA 2 A_ClearRefire
        Goto Ready
	  AltFire:   	  
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"AltCoopFire")		
		C7DU E 0 A_JumpIfNoAmmo("Ready")
		C7DU E 5 A_SpawnItemEx("C7ProxyMine",0,0,0,0,0,0,0)
		C7DU E 5 A_TakeInventory("C7ProxyMineAmmo", 1)
		C7DU E 5 A_Print("Mine placed")
		Goto Ready
      AltCoopFire:	
		C7DU E 0 A_JumpIfNoAmmo("Ready")
		C7DU E 5 A_SpawnItemEx("C7CoopProxyMine",0,0,0,0,0,0,0,SXF_SETMASTER)
		C7DU E 5 A_TakeInventory("C7ProxyMineAmmo", 1)
		C7DU E 5 A_Print("Mine placed")
		Goto Ready	  
    }
} 

actor C7DBBullet : FastProjectile
{
    Damage (Random(1,32))
    Radius 1
    Height 1
    Speed 350
    Projectile
    Renderstyle "Normal"
    Species "Player"
    +BLOODSPLATTER
    +THRUSPECIES
    +NOTIMEFREEZE
	+NOEXTREMEDEATH
	+NODAMAGETHRUST
    Alpha 1.0
    States
    {
      Spawn:
        TNT1 A 1
        loop

      Death:
        TNT1 A 1
        stop
    }
}