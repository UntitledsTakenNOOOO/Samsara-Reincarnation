actor Hellstaff : Weapon
{
    Weapon.SelectionOrder 200
    Weapon.AmmoUse1 1
    Weapon.AmmoGive1 50
    Weapon.YAdjust 16
    Weapon.AmmoType1 "Cell"
    Weapon.SlotNumber 6
    //+AMMO_OPTIONAL
    +INVENTORY.UNDROPPABLE
    +NOALERT
    Inventory.PickUpSound "heretic/weaponget"
    Inventory.PickupMessage "You got the Hellstaff!"
    Obituary "$SAMSARA_CORVUS_OB_SLOT6"
    Inventory.RestrictedTo "CorvusPlayer"
    Tag "Hellstaff"
    States
    {
      Spawn:
        WSKL A -1
        stop

      Ready:
		TNT1 A 0 A_TakeInventory("CorvusHoldingSlot")
		TNT1 A 0 A_GiveInventory("CorvusHoldingSlot",5)
        HROD A 1 A_WeaponReady
        loop

      Deselect:
        HROD A 1 A_Lower
        loop

      Select:
        HROD A 1 A_Raise
        loop

      // Unpowered
      Fire:
        TNT1 A 0 A_JumpIfInventory("PowerHereticTome",1,"FirePowered")
		TNT1 A 0 A_JumpIfInventory("SamsaraHasPermaWeaponLevel2",1,"FirePowered")
      FireUnpowered:
        TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_GiveInventory("CorvusShootCloneTrigger")
        HROD AB 4 Bright A_GiveInventory("SamsaraHereticSkullRodAttackHandler")
        TNT1 A 0 A_ReFire
        goto Ready

      // Powered
      FirePowered:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("Cell",5,1)
        goto NoAmmoSwitch
        TNT1 A 0 A_AlertMonsters
        HROD C 2 A_GiveInventory("CorvusShootCloneTrigger")
        HROD D 3
        HROD E 2 Bright
        HROD F 3 Bright
		HROD G 4 Bright A_GiveInventory("SamsaraHereticSkullRodPoweredAttackHandler")
        HROD F 2 Bright
        HROD E 3
        HROD D 2
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("Cell",5,1)
        goto NoAmmoSwitch
        HROD C 2 A_ReFire
        goto Ready

      // "No ammo" switch
      NoAmmoSwitch:
        TNT1 A 0 A_ClearReFire
        TNT1 A 0 A_SelectWeapon("Elven Wand")
        goto Deselect
    }
}

// Unpowered

actor SamsaraHereticSkullRodAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Cell",1,1)
        stop
        TNT1 A 0 A_TakeInventory("Cell",1)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireCustomMissile("HellstaffShot",0,false) //A_FireSkullRodPL1
        stop

      PickupAttackCoop:
        TNT1 A 0 A_FireCustomMissile("HellstaffShot2",0,false)
        stop
    }
}

actor HellstaffShot
{
    Radius 12
    Height 8
    Speed 22
    Damage 5 //original was 3
    Projectile
    +BLOODSPLATTER
    +WINDTHRUST
    -NOBLOCKMAP
    DamageType "CorvusHellstaff"
    Decal PlasmaScorchLower
    RenderStyle Add
    SeeSound "heretic/hornrodshoot"
    DeathSound "heretic/hornrodhit"
    States
    {
      Spawn:
        FX00 A 0 Bright Light("HELLSTAFFBALL")
        TNT1 A 0 A_Jump(128,2)
      SpawnLoop:
        FX00 AB 6 Bright Light("HELLSTAFFBALL")
        loop

      Death:
        FX00 H 5 Bright Light("HELLSTAFFBALL_X1")
        FX00 I 5 Bright Light("HELLSTAFFBALL_X2")
        FX00 J 4 Bright Light("HELLSTAFFBALL_X3")
        FX00 K 4 Bright Light("HELLSTAFFBALL_X4")
        FX00 L 3 Bright Light("HELLSTAFFBALL_X5")
        FX00 M 3 Bright
        stop
    }
}

actor HellstaffShot2 : HellstaffShot
{
    Species "Player"
    +THRUSPECIES
}

// Powered

actor SamsaraHereticSkullRodPoweredAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Cell",5,1)
        stop
        TNT1 A 0 A_TakeInventory("Cell",5)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_JumpIfTargetInLOS("PickupAttackHoming")
      // Dumbfire
      PickupAttackDumbfire:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackDumbfireCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackDumbfireCoop")
      PickupAttackDumbfireDM:
        TNT1 A 0 A_FireCustomMissile("HornRodFX2DM",0,false)
        stop

      PickupAttackDumbfireCoop:
        TNT1 A 0 A_FireCustomMissile("HornRodFX2A",0,false)
        stop

      // Homing
      PickupAttackHoming:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackHomingCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackHomingCoop")
      PickupAttackHomingDM:
        TNT1 A 0 A_FireCustomMissile("HornRodFX2DMHoming",0,false)
        stop

      PickupAttackHomingCoop:
        TNT1 A 0 A_FireCustomMissile("HornRodFX2AHoming",0,false)
        stop
    }
}

actor HornRodFX2DM : HornRodFX2
{
    Alpha 0.9
    SeeSound "heretic/hornrodpowshoot"
    DeathSound "heretic/hornrodpowhit"
    DamageType "CorvusHellstaff"
    Obituary "$SAMSARA_CORVUS_OB_SLOT6"
    +BLOODSPLATTER
    +NOTIMEFREEZE
    States
    {
      Spawn:
        FX00 C 0 Bright Light("HELLSTAFFBALL") NoDelay A_PlaySound("heretic/hornrodpowshoot",CHAN_WEAPON)
        TNT1 A 0 A_GiveInventory("SamsaraProjectileSpawnInit")
        TNT1 A 0 A_ChangeFlag("NOTIMEFREEZE",false)
      SpawnLoop:
        FX00 C 3 Bright Light("HELLSTAFFBALL")
        FX00 D 3 Bright Light("HELLSTAFFBALL") A_SeekerMissile(10,30)
        FX00 E 3 Bright Light("HELLSTAFFBALL")
        FX00 F 3 Bright Light("HELLSTAFFBALL") A_SeekerMissile(10,30)
        loop

      Death:
        TNT1 A 0 A_JumpIfInventory("SamsaraProjectileSpawnInit",1,"DeathStart")
        TNT1 A 0 A_PlaySound("heretic/hornrodpowshoot",CHAN_WEAPON)
        TNT1 A 0 A_GiveInventory("SamsaraProjectileSpawnInit")
      DeathStart:
        FX00 H 5 Bright Light("HELLSTAFFBALL_X1") A_ChangeFlag("NOTIMEFREEZE",false) //A_AddPlayerRain
        FX00 I 5 Bright Light("HELLSTAFFBALL_X2")
        FX00 J 4 Bright Light("HELLSTAFFBALL_X3")
        FX00 K 3 Bright Light("HELLSTAFFBALL_X4")
        FX00 L 3 Bright Light("HELLSTAFFBALL_X5")
        FX00 M 3 Bright
        TNT1 A 1 A_Stop // invisible
        TNT1 A 0 A_PlaySound("heretic/hornrodpowrain",CHAN_BODY,1,true)
        TNT1 AAAAAAAAAA 1 A_SpawnItemEx("HellstaffRain",random(-64,64),random(-64,64))
        TNT1 A 0 A_JumpIfHealthLower(20,"Death2")
        TNT1 AAAAAAAAAA 1 A_SpawnItemEx("HellstaffRain",random(-64,64),random(-64,64))
        TNT1 A 0 A_JumpIfHealthLower(20,"Death2")
        TNT1 AAAAAAAAAA 1 A_SpawnItemEx("HellstaffRain",random(-64,64),random(-64,64))
        TNT1 A 0 A_JumpIfHealthLower(20,"Death2")
        TNT1 AAAAAAAAAA 1 A_SpawnItemEx("HellstaffRain",random(-64,64),random(-64,64))
        TNT1 A 0 A_JumpIfHealthLower(20,"Death2")
        TNT1 AAAAAAAAAA 1 A_SpawnItemEx("HellstaffRain",random(-64,64),random(-64,64))
        TNT1 A 0 A_JumpIfHealthLower(20,"Death2")
        TNT1 AAAAAAAAAA 1 A_SpawnItemEx("HellstaffRain",random(-64,64),random(-64,64))
        TNT1 A 0 A_JumpIfHealthLower(20,"Death2")
        TNT1 AAAAAAAAAA 1 A_SpawnItemEx("HellstaffRain",random(-64,64),random(-64,64))
        TNT1 A 0 A_JumpIfHealthLower(20,"Death2")
        TNT1 AAAAAAAAAA 1 A_SpawnItemEx("HellstaffRain",random(-64,64),random(-64,64))
        TNT1 A 0 A_JumpIfHealthLower(20,"Death2")
        TNT1 AAAAAAAAAA 1 A_SpawnItemEx("HellstaffRain",random(-64,64),random(-64,64))
        TNT1 A 0 A_JumpIfHealthLower(20,"Death2")
        TNT1 AAAAAAAAAA 1 A_SpawnItemEx("HellstaffRain",random(-64,64),random(-64,64))
        TNT1 A 0 A_JumpIfHealthLower(20,"Death2")
        TNT1 AAAAAAAAAA 1 A_SpawnItemEx("HellstaffRain",random(-64,64),random(-64,64))
        TNT1 A 0 A_JumpIfHealthLower(20,"Death2")
        TNT1 AAAAAAAAAA 1 A_SpawnItemEx("HellstaffRain",random(-64,64),random(-64,64))
        TNT1 A 0 A_JumpIfHealthLower(20,"Death2")
        TNT1 AAAAAAAAAA 1 A_SpawnItemEx("HellstaffRain",random(-64,64),random(-64,64))
        TNT1 A 0 A_JumpIfHealthLower(20,"Death2")
        TNT1 AAAAAAAAAA 1 A_SpawnItemEx("HellstaffRain",random(-64,64),random(-64,64))
      DeathFinish:
        TNT1 A 0 A_TakeInventory("Health",100)
        TNT1 A 0 A_PlaySound("silence")
        TNT1 A 5 A_StopSound(CHAN_BODY) //A_SkullRodStorm
        stop

      Death2:
        TNT1 AAAAAAAAAAAAAAAA 1 A_SpawnItemEx("HellstaffRain",random(-64,64),random(-64,64)) // When the Heretic rain pillar "dies", its health is actually set at 16 and gives 16 more raindrops before going away.
        goto DeathFinish
    }
}

actor HornRodFX2DMHoming : HornRodFX2DM { +SEEKERMISSILE }

actor HornRodFX2A : HornRodFX2DM
{
    Species "Player"
    +THRUSPECIES
    States
    {
      Death:
        TNT1 A 0 A_JumpIfInventory("SamsaraProjectileSpawnInit",1,"DeathStart")
        TNT1 A 0 A_PlaySound("heretic/hornrodpowshoot",CHAN_WEAPON)
        TNT1 A 0 A_GiveInventory("SamsaraProjectileSpawnInit")
      DeathStart:
        FX00 H 5 Bright Light("HELLSTAFFBALL_X1") A_ChangeFlag("NOTIMEFREEZE",false) //A_AddPlayerRain
        FX00 I 5 Bright Light("HELLSTAFFBALL_X2")
        FX00 J 4 Bright Light("HELLSTAFFBALL_X3")
        FX00 K 3 Bright Light("HELLSTAFFBALL_X4")
        FX00 L 3 Bright Light("HELLSTAFFBALL_X5")
        FX00 M 3 Bright
        TNT1 A 1 A_Stop // invisible
        TNT1 A 0 A_PlaySound("heretic/hornrodpowrain",CHAN_BODY,1,true)
        TNT1 AAAAAAAAAA 1 A_SpawnItemEx("HellstaffRain2",random(-64,64),random(-64,64))
        TNT1 A 0 A_JumpIfHealthLower(20,"Death2")
        TNT1 AAAAAAAAAA 1 A_SpawnItemEx("HellstaffRain2",random(-64,64),random(-64,64))
        TNT1 A 0 A_JumpIfHealthLower(20,"Death2")
        TNT1 AAAAAAAAAA 1 A_SpawnItemEx("HellstaffRain2",random(-64,64),random(-64,64))
        TNT1 A 0 A_JumpIfHealthLower(20,"Death2")
        TNT1 AAAAAAAAAA 1 A_SpawnItemEx("HellstaffRain2",random(-64,64),random(-64,64))
        TNT1 A 0 A_JumpIfHealthLower(20,"Death2")
        TNT1 AAAAAAAAAA 1 A_SpawnItemEx("HellstaffRain2",random(-64,64),random(-64,64))
        TNT1 A 0 A_JumpIfHealthLower(20,"Death2")
        TNT1 AAAAAAAAAA 1 A_SpawnItemEx("HellstaffRain2",random(-64,64),random(-64,64))
        TNT1 A 0 A_JumpIfHealthLower(20,"Death2")
        TNT1 AAAAAAAAAA 1 A_SpawnItemEx("HellstaffRain2",random(-64,64),random(-64,64))
        TNT1 A 0 A_JumpIfHealthLower(20,"Death2")
        TNT1 AAAAAAAAAA 1 A_SpawnItemEx("HellstaffRain2",random(-64,64),random(-64,64))
        TNT1 A 0 A_JumpIfHealthLower(20,"Death2")
        TNT1 AAAAAAAAAA 1 A_SpawnItemEx("HellstaffRain2",random(-64,64),random(-64,64))
        TNT1 A 0 A_JumpIfHealthLower(20,"Death2")
        TNT1 AAAAAAAAAA 1 A_SpawnItemEx("HellstaffRain2",random(-64,64),random(-64,64))
        TNT1 A 0 A_JumpIfHealthLower(20,"Death2")
        TNT1 AAAAAAAAAA 1 A_SpawnItemEx("HellstaffRain2",random(-64,64),random(-64,64))
        TNT1 A 0 A_JumpIfHealthLower(20,"Death2")
        TNT1 AAAAAAAAAA 1 A_SpawnItemEx("HellstaffRain2",random(-64,64),random(-64,64))
        TNT1 A 0 A_JumpIfHealthLower(20,"Death2")
        TNT1 AAAAAAAAAA 1 A_SpawnItemEx("HellstaffRain2",random(-64,64),random(-64,64))
        TNT1 A 0 A_JumpIfHealthLower(20,"Death2")
        TNT1 AAAAAAAAAA 1 A_SpawnItemEx("HellstaffRain2",random(-64,64),random(-64,64))
        goto DeathFinish

      Death2:
        TNT1 AAAAAAAAAAAAAAAA 1 A_SpawnItemEx("HellstaffRain2",random(-64,64),random(-64,64)) // When the Heretic rain pillar "dies", its health is actually set at 16 and gives 16 more raindrops before going away.
        goto DeathFinish
    }
}

actor HornRodFX2AHoming : HornRodFX2A { +SEEKERMISSILE }

actor HellstaffRain
{
    Radius 3 //Original is 5
    Height 4 //Original is 12
    Speed 12
    Damage 5
    ReactionTime 1
    Mass 10 //Original is 5
    RenderStyle Add
    DamageType "CorvusHellstaffTomed"
    +BLOODSPLATTER
    +CEILINGHUGGER //+SPAWNCEILING
    +DONTSPLASH // hmm
    +MISSILE
    +NOTELEPORT
    States
    {
      Spawn:
        TNT1 A 2
        TNT1 A 0 A_ChangeFlag("CEILINGHUGGER",false)
      SpawnLoop:
        FX22 A 1 Bright Light("REDRAIN") A_JumpIf(velz == 0, "ForceDeath")
        loop

      ForceDeath:
        FX22 A 1 Bright Light("REDRAIN") A_Countdown
        goto SpawnLoop

      Death:
        TNT1 A 0 A_NoGravity
        FX22 B 4 Bright Light("REDRAIN") A_JumpIf(z > floorz, "NotFloor")
        FX22 C 4 Bright Light("REDRAIN_X1")
        FX22 D 4 Bright Light("REDRAIN_X2")
        FX22 E 4 Bright Light("REDRAIN_X3")
        FX22 F 4 Bright Light("REDRAIN_X4")
        stop

      NotFloor:
        FX22 GHI 4 Bright
        stop
    }
}

actor HellstaffRain2 : HellstaffRain
{
    Species "Player"
    +THRUSPECIES
}

/*actor RainPillar2 : RainPillar
{
    DamageType "CorvusHellstaffTomed"
    +BLOODSPLATTER
    +SPAWNCEILING
}*/

// Lesser powered (unused)

Actor HellStaffPuff : BulletPuff
{
	Scale 0.5
	DamageType "CorvusHellstaff"
	States
	{
		Spawn:
			FX00 HI 5 Bright
			FX00 JK 4 Bright
			FX00 LM 3 Bright
			Stop
	}
}
