actor "Phoenix Rod" : Weapon
{
    Weapon.SelectionOrder 2600
    Weapon.Kickback 150
    Weapon.YAdjust 16
    Weapon.AmmoUse 1
    Weapon.AmmoGive 2
    Weapon.SlotNumber 5
    //+AMMO_OPTIONAL
    +INVENTORY.UNDROPPABLE
    Weapon.AmmoType "RocketAmmo"
    Inventory.PickUpSound "heretic/weaponget"
    Inventory.PickupMessage "You got the Phoenix Rod!"
    Obituary "$SAMSARA_CORVUS_OB_SLOT5"
    Inventory.RestrictedTo "CorvusPlayer"
    Tag "Phoenix Rod"
    States
    {
      Spawn:
        WPHX A -1
        Stop

      Ready:
		TNT1 A 0 A_TakeInventory("CorvusHoldingSlot2",1)
		TNT1 A 0 A_TakeInventory("CorvusHoldingSlot3",1)
		TNT1 A 0 A_TakeInventory("CorvusHoldingSlot4",1)
		TNT1 A 0 A_GiveInventory("CorvusHoldingSlot5",1)
		TNT1 A 0 A_TakeInventory("CorvusHoldingSlot6",1)
		TNT1 A 0 A_TakeInventory("CorvusHoldingSlot7",1)
        PHNX A 1 A_WeaponReady
        Loop

      Deselect:
		TNT1 A 0 A_StopSound(CHAN_WEAPON)
        PHNX A 1 A_Lower
        Loop

      Select:
        PHNX A 1 A_Raise
        Loop

      Fire:
		TNT1 A 0 A_GiveInventory("CorvusShootCloneTrigger",1)
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"CoopFire")
        TNT1 A 0 A_StopSound(CHAN_WEAPON)
        TNT1 A 0 A_ClearRefire
        TNT1 A 0 A_JumpIfInventory("PowerHereticTome",1,"FireTomed")
		TNT1 A 0 A_JumpIfInventory("PowerHereticTomePerma",1,"FireTomed")
        PHNX B 5 BRIGHT
        PHNX C 0 A_Recoil(4)
        PHNX C 0 A_FireCustomMissile("PhoenixShot")
        PHNX C 7 BRIGHT
        PHNX DB 4
        PHNX B 0 A_Refire
        Goto Ready

      CoopFire:
        TNT1 A 0 A_StopSound(CHAN_WEAPON)
        TNT1 A 0 A_ClearRefire
        TNT1 A 0 A_JumpIfInventory("PowerHereticTome",1,"FireTomed")
		TNT1 A 0 A_JumpIfInventory("PowerHereticTomePerma",1,"FireTomed")
        PHNX B 5 BRIGHT
        PHNX C 0 A_Recoil(4)
        PHNX C 0 A_FireCustomMissile("PhoenixShot2")
        PHNX C 7 BRIGHT
        PHNX DB 4
        PHNX B 0 A_Refire
        Goto Ready

      FireTomed:
        PHNX B 3

      Hold:
        PHNX A 0 A_JumpIfInventory("PowerHereticTome",1,"HoldTomed")
		PHNX A 0 A_JumpIfInventory("PowerHereticTomePerma",1,"HoldTomed")
        PHNX A 0 A_ClearRefire
        goto Fire

      HoldTomed:
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"CoopHoldTomed")
        TNT1 A 0 A_JumpIfInventory("PhoenixRodCount",280,"Powerdown")
        PHNX C 0 A_JumpIfInventory("PhoenixRodCount",1,1)
        PHNX C 0 A_PlaySound("weapons/phoenixpowshoot",CHAN_WEAPON,1,true)
        TNT1 A 0
        TNT1 A 0 A_GiveInventory("PhoenixRodCount",1)
        PHNX C 0 A_FireCustomMissile("PhoenixFX2DM",random(-2,2),0,0,0,2,2)//,26,2,2)
        PHNX C 1 BRIGHT //A_FirePhoenixPL2
        PHNX B 4 BRIGHT A_ReFire
        TNT1 A 0 A_TakeInventory("RocketAmmo",1, TIF_NOTAKEINFINITE)
        TNT1 A 0 A_TakeInventory("PhoenixRodCount",360)
        TNT1 A 0 A_StopSound(CHAN_WEAPON)
        goto Ready

      CoopHoldTomed:
        TNT1 A 0 A_JumpIfInventory("PhoenixRodCount",280,"Powerdown")
        PHNX C 0 A_JumpIfInventory("PhoenixRodCount",1,1)
        PHNX C 0 A_PlaySound("weapons/phoenixpowshoot",CHAN_WEAPON,1,true)
        TNT1 A 0
        TNT1 A 0 A_GiveInventory("PhoenixRodCount",1)
        PHNX C 0 A_FireCustomMissile("PhoenixFX2A",random(-2,2),0,0,0,2,2)//,26,2,2)
        PHNX C 1 BRIGHT //A_FirePhoenixPL2
        PHNX B 4 BRIGHT A_ReFire
        TNT1 A 0 A_TakeInventory("RocketAmmo",1, TIF_NOTAKEINFINITE)
        TNT1 A 0 A_TakeInventory("PhoenixRodCount",360)
        TNT1 A 0 A_StopSound(CHAN_WEAPON)
        goto Ready

      Powerdown:
        TNT1 A 0 A_StopSound(CHAN_WEAPON)
        TNT1 A 0 A_TakeInventory("RocketAmmo",1, TIF_NOTAKEINFINITE)
        TNT1 A 0 A_TakeInventory("PhoenixRodCount",360)
        PHNX B 4 A_Refire
        TNT1 A 0 A_ClearRefire
        Goto Ready
    }
}

actor PhoenixRodCount : inventory { -INVBAR +INVENTORY.UNDROPPABLE inventory.maxamount 280 }

actor PhoenixShot : PhoenixFX1
{
    -SPECIALFIREDAMAGE
    DamageType "FireExplosive"
    Decal Scorch
    Obituary "$SAMSARA_CORVUS_OB_SLOT5"
    //Damage 100
}

actor PhoenixShot2 : PhoenixShot { +THRUSPECIES Species "Player" }

actor PhoenixFX2DM : PhoenixFX2
{
	damage 4
}

actor PhoenixFX2A : PhoenixFX2
{
    +THRUSPECIES
    +FIREDAMAGE
    Species "Player"
    DECAL PhoenixThrowerScorch
}

actor PhoenixPuff2 : PhoenixPuff replaces PhoenixPuff
{
    DECAL PhoenixThrowerScorch
    +CLIENTSIDEONLY
}

Actor PheonixBlastFX : VolcanoBlast
{
	Damage 12
	Gravity 0.525
	DamageType "FireExplosive"
	SeeSound "weapons/phoenixshoot"
	Speed 20
	Scale 1.2
	Radius 10
	Height 10
	States
	{
		Spawn:
			VFBL AB 4 A_SpawnItemEx("PhoenixPuff2", 0, 0, 0, 0)
			Loop
		Death:
			FBMB F 4 BRight A_Explode(50, 50, 1)
			TNT1 AAAA 0 A_SpawnItemEX("VolcanoTBlast22", 0,0,0,0.7,0.7, Random(0,2), Random(0,360), SXF_TRANSFERPOINTERS)
			FBMB GHIJK 4 Bright
			Stop
	}		
}	

Actor VolcanoTBlast22 : VolcanoTBlast { Scale 1.2 Damage 3 }