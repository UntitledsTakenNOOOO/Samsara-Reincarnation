script "SamsaraRecoil" (int degrees, int ticsup, int ticsdown) clientside //312 -- SAMSARA_RECOIL
{
    if (degrees == 0) { terminate; }
    if (defaultCVar("samsara_cl_norecoil", 0) == 1 && ticsup >= 0) { terminate; }
    if (GetCVar("sv_nofreelook")) { terminate; }

    degrees = itof(degrees);
    if (degrees < 0) { degrees /= -100; }

    int qCurve, oldPitch, newPitch, pitchDiff, i;
    int amplitude = degrees / 2;
    int doDownRecoil = 1;
    ticsup = abs(ticsup);

    if (ticsdown < 0)
    {
        if (GetCVar("freelook")) { doDownRecoil = 0; }
        else { ticsdown = -ticsdown; }
    }

    if (ticsup > 0)
    {
        qCurve = degrees / pow(ticsup, 2);  // this is a
        newPitch = qCurve * pow(ticsup, 2);

        for (i = 0; i < ticsup; i++)
        {
            oldPitch = newPitch;
            // y = a(x-h)**2 + k
            newPitch = qCurve * pow((i+1)-ticsup, 2);
            pitchDiff = (newPitch - oldPitch) / 360;

            SetActorPitch(0, GetActorPitch(0) + pitchDiff);
            Delay(1);
        }
    }
    else
    {
        SetActorPitch(0, GetActorPitch(0) - (degrees / 360));
    }

    if (doDownRecoil)
    {
        if (ticsdown > 0)
        {
            if (defaultCVar("samsara_cl_sinerecoil", 0) == 0)
            {
                qCurve = -degrees / pow(ticsdown, 2); // this is also a
                newPitch = qCurve * pow(ticsdown, 2);

                for (i = 0; i < ticsdown; i++)
                {
                    oldPitch = newPitch;
                    // y = a(x-h)**2 + k
                    newPitch = qCurve * pow((i+1)-ticsdown, 2);
                    pitchDiff = (newPitch - oldPitch) / 360;

                    SetActorPitch(0, GetActorPitch(0) + pitchDiff);
                    Delay(1);
                }
            }
            else
            {
                // y = (a/2) * sin(x) + (a/2)
                newPitch = 0;

                for (i = 0; i <= ticsdown; i++)
                {
                    oldPitch = newPitch;
                    newPitch = -FixedMul(amplitude, cos(itof(i) / (ticsdown*2))) + amplitude;
                    pitchDiff = (newPitch - oldPitch) / 360;

                    SetActorPitch(0, GetActorPitch(0) + pitchDiff);
                    Delay(1);
                }
            }
        }
        else
        {
            SetActorPitch(0, GetActorPitch(0) + (degrees / 360));
        }
    }
}

script "QuakeGrenade" (int x, int y, int z) //583
{
    if (GetActorZ(0) - GetActorFloorZ(0) > 4.0)
    {
        SetResultValue(1);
    }
    else
    {
        SetResultValue(sqrt(x*x + y*y + z*z) );
    }
}

script "QuakeParticle" (int divI, int divF, int divF1) //586
{
    int div = percFloat2(divI, divF, divF1);

    int x = GetActorVelX(0);
    int y = GetActorVelY(0);
    int z = GetActorVelZ(0);

    SetActorVelocity(0, FixedMul(x, div), FixedMul(y, div), FixedMul(z, div), 0, 1);
}
