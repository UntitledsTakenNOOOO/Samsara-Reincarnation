actor UsingAlienWeapon: Boolean {}
actor AlienWeaponFired: Counter {}
actor AlienWeaponFiredP: Counter {}
actor AlienWeaponFiredS: Counter {}
actor AlienWeaponSecondarySide: Boolean {}

actor "Alien Weapon" : SamsaraMarathonWeapon
{
    Inventory.PickupMessage "system error 0xfded"
    Weapon.SlotNumber 8
    Weapon.SelectionOrder 500
    Weapon.AmmoType "UnknownResonator1"
    Tag "Alien Weapon"
    States
    {
      Spawn:
        AWPN A -1
        stop

      Ready:
        TNT1 A 0 A_JumpIfInventory("AlienWeaponFired",4,"ReadyFired")
        TNT1 A 0 A_JumpIfInventory("AlienWeaponFiredP",1,"ReadyNoSwitch")
        TNT1 A 0 A_JumpIfInventory("AlienWeaponFiredS",1,"ReadyNoSwitch")
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("UnknownAmmo",1,1)
        goto NoAmmoSwitch
        AWP1 A 1 A_WeaponReady
        loop

      ReadyNoSwitch:
        AWP1 A 1 A_WeaponReady(WRF_NOSWITCH)
        goto Ready

      ReadyFired:
        TNT1 A 0 A_JumpIfInventory("AlienWeaponFiredP",1,"ReadyFiredNoSwitch")
        TNT1 A 0 A_JumpIfInventory("AlienWeaponFiredS",1,"ReadyFiredNoSwitch")
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("UnknownAmmo",1,1)
        goto NoAmmoSwitch
        AWP1 B 1 Bright A_WeaponReady
        goto Ready

      ReadyFiredNoSwitch:
        AWP1 B 1 Bright A_WeaponReady(WRF_NOSWITCH)
        goto Ready

      Deselect:
        TNT1 A 0 A_TakeInventory("AlienWeaponFired")
        TNT1 A 0 A_TakeInventory("AlienWeaponFiredP")
        TNT1 A 0 A_TakeInventory("AlienWeaponFiredS")
        TNT1 A 0 A_TakeInventory("AlienWeaponSecondarySide")
        TNT1 A 0 A_JumpIfHealthLower(1,"DeselectInstant")
        TNT1 A 0 A_Lower
        AWP1 A 1 A_Lower
        loop

      Select:
        TNT1 A 0 A_TakeInventory("UsingFists")
        TNT1 A 0 A_TakeInventory("UsingPistols")
        TNT1 A 0 A_TakeInventory("UsingDualPistols")
        TNT1 A 0 A_TakeInventory("UsingSMG")
        TNT1 A 0 A_TakeInventory("UsingPacifier")
        TNT1 A 0 A_TakeInventory("UsingShotguns")
        TNT1 A 0 A_TakeInventory("UsingDualShotguns")
        TNT1 A 0 A_TakeInventory("UsingFusionPistol")
        TNT1 A 0 A_TakeInventory("UsingAssaultRifle")
        TNT1 A 0 A_TakeInventory("UsingSpanker")
        TNT1 A 0 A_TakeInventory("UsingToasty")
        TNT1 A 0 A_TakeInventory("UsingWMC")
        //TNT1 A 0 A_TakeInventory("UsingAlienWeapon")
        TNT1 A 0 A_TakeInventory("UsingAlienWeapon2")
        TNT1 A 0 A_TakeInventory("UsingMarathonInstagib")

        TNT1 A 0 A_GiveInventory("UsingAlienWeapon")
        TNT1 A 0 A_TakeInventory("AlienWeaponFired")
        TNT1 A 0 A_TakeInventory("AlienWeaponFiredP")
        TNT1 A 0 A_TakeInventory("AlienWeaponFiredS")
        // AlienWeaponSecondarySide is kept during map change
        TNT1 A 0 A_Raise
        AWP1 A 1 A_Raise
        loop

      Fire:
        TNT1 A 0 A_JumpIfInventory("AlienWeaponFiredP",1,"CheckAltFire")
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireStart")
        TNT1 A 0 A_JumpIfInventory("UnknownAmmo",1,"FireStart")
        goto CheckAltFire

      FireStart:
        TNT1 A 0 A_JumpIfInventory("SamsaraCurrentWaterLevel",0,"CheckAltFire")
        TNT1 A 0 A_AlertMonsters
        TNT1 A 0 A_JumpIfInventory("AlienWeaponFired",1,2)
        TNT1 A 0 A_GiveInventory("AlienWeaponFired",7)
        TNT1 A 0 A_TakeInventory("AlienWeaponFiredP") // reset
        TNT1 A 0 A_GiveInventory("AlienWeaponFiredP",7)
        TNT1 A 0 A_GiveInventory("SamsaraMarathonAlienWeapon2PrimaryAttackHandler")
        goto CheckAltFire

      CheckAltFire:
        TNT1 A 0 A_JumpIfInventory("SynthFireRight",1,"AltFire")
        goto Ready

      AltFire:
        TNT1 A 0 A_JumpIfInventory("AlienWeaponFiredS",1,"Ready")
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"AltFireStart")
        TNT1 A 0 A_JumpIfInventory("UnknownAmmo",1,"AltFireStart")
        goto Ready

      AltFireStart:
        TNT1 A 0 A_JumpIfInventory("SamsaraCurrentWaterLevel",0,"Ready")
        TNT1 A 0 A_AlertMonsters
        TNT1 A 0 A_JumpIfInventory("AlienWeaponFired",1,2)
        TNT1 A 0 A_GiveInventory("AlienWeaponFired",7)
        TNT1 A 0 A_TakeInventory("AlienWeaponFiredS") // reset
        TNT1 A 0 A_GiveInventory("AlienWeaponFiredS",7)
        TNT1 A 0 A_GiveInventory("SamsaraMarathonAlienWeapon2SecondaryAttackHandler")
        goto Ready

      // "No ammo" switch
      NoAmmoSwitch:
        TNT1 A 0 A_ClearReFire
        TNT1 A 0 A_SelectWeapon(".44 Magnum Mega Class A1")
        goto Deselect
    }
}

actor SamsaraMarathonAlienWeapon2PrimaryAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("UnknownAmmo",1,1)
        stop
        TNT1 A 0 A_Jump(256,1,1,1,2)
        TNT1 A 0
        goto PickupAttack
        TNT1 A 0 A_TakeInventory("UnknownAmmo",1)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("enforcer/shoot",CHAN_AUTO)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireCustomMissile("EnforcerShot",random(-1,1),false,0,0,0,random(-1,1))
        stop

      PickupAttackCoop:
        TNT1 A 0 A_FireCustomMissile("EnforcerShotCoop",random(-1,1),false,0,0,0,random(-1,1))
        stop
    }
}

actor SamsaraMarathonAlienWeapon2SecondaryAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("UnknownAmmo",1,1)
        stop
        TNT1 A 0 A_Jump(256,1,1,1,2)
        TNT1 A 0
        goto PickupAttack
        TNT1 A 0 A_TakeInventory("UnknownAmmo",1)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("enforcer/shoot",CHAN_AUTO)
        TNT1 A 0 A_JumpIfInventory("AlienWeaponSecondarySide",1,"PickupAttackRight")
      // Left
      PickupAttackLeft:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackLeftCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackLeftCoop")
      PickupAttackLeftDM:
        TNT1 A 0 A_FireCustomMissile("EnforcerShot",22,false,0,0,0,random(-1,1)) // [tv50] had no pitch prior to the synthfire update in 0.27, no mention of the pitch change in that version's changelog
        goto PickupFinishLeft

      PickupAttackLeftCoop:
        TNT1 A 0 A_FireCustomMissile("EnforcerShotCoop",22,false,0,0,0,random(-1,1)) // [tv50] had no pitch in any version, chalking that up to a possible oversight
        goto PickupFinishLeft

      // Right
      PickupAttackRight:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackRightCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackRightCoop")
      PickupAttackRightDM:
        TNT1 A 0 A_FireCustomMissile("EnforcerShot",-22,false,0,0,0,random(-1,1)) // [tv50] had no pitch prior to the synthfire update in 0.27, no mention of the pitch change in that version's changelog
        goto PickupFinishRight

      PickupAttackRightCoop:
        TNT1 A 0 A_FireCustomMissile("EnforcerShotCoop",-22,false,0,0,0,random(-1,1)) // [tv50] had no pitch in any version, chalking that up to a possible oversight
        goto PickupFinishRight

      // Finishing stuff
      // Left
      PickupFinishLeft:
        TNT1 A 0 A_GiveInventory("AlienWeaponSecondarySide")
        stop

      // Right
      PickupFinishRight:
        TNT1 A 0 A_TakeInventory("AlienWeaponSecondarySide")
        stop
    }
}

actor UnknownAmmo : Ammo 
{
    Inventory.MaxAmount 50
    Tag "Alien Weapon Ammo"
    +IGNORESKILL
}

actor UnknownResonator1: Ammo
{
    Inventory.MaxAmount 100
    Tag "Alien Weapon Ammo"
    +IGNORESKILL
}

actor EnforcerShot
{
    Projectile
    Radius 6
    Height 8
    Speed 15
    Damage (random(30,38)) // Formerly 20,28
    DamageType "MarathonPlasmaFire"
    Scale 0.35
    DeathSound "enforcer/hit"
    Decal "Scorch"
    Obituary "$SAMSARA_MARATHON_OB_BERSERK1"
    +BLOODLESSIMPACT
    +NOEXTREMEDEATH
    States
    {
      Spawn:
        PLAS ABCDEF 2 Bright
        loop

      Death:
        PHIT ABCD 2 Bright
        stop
    }
}

actor EnforcerShotCoop : EnforcerShot
{
    Species "Player"
    +THRUSPECIES
}
