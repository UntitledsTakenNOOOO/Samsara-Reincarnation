ACTOR Bazooka : weapon 
{
     Weapon.SlotNumber 3
     Weapon.SelectionOrder 1300
     Weapon.UpSound "gen/wepswitch"
     Inventory.PickupSound "gen/pickup"
     Inventory.Pickupmessage "You Bagged a Bazooka!"
     Weapon.AmmoGive 15
     Weapon.AmmoUse 1 
	 +IGNORESKILL
     Weapon.AmmoType "RottMissiles"
     Obituary "%o \cbwas on the wrong end of\c- %k's \cbbazooka\c-"
	 Inventory.RestrictedTo "IPFreeley"	 
     States 
     { 
     Spawn: 
        PBAZ A -1 
        Stop 
     Ready:
		TNT1 A 0 A_TakeInventory("UsingFirearm", 1)
        BAZO A 1 A_WeaponReady 
        Goto Ready+1 
     Deselect: 
        BAZO A 1 A_Lower 
        Loop
     Select:
      	BAZO A 1 A_Raise 
        Loop
     Fire: 
	    TNT1 A 0 A_PlaySound("bazooka/shoot")
        BAZO E 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
	 	TNT1 A 0 A_JumpIfInventory("CoopModeOn", 1, "CoopFire")
	 	TNT1 A 0 A_FireCustomMissile("CompRottM",0,8,0,-8) // -16
     Continue:
	 	BAZO F 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
        BAZO G 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
        BAZO B 1 Bright A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
        BAZO C 1 Bright A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
        BAZO D 1 Bright A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
        BAZO D 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)		
        BAZO H 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
        BAZO A 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
        BAZO A 6 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
        BAZO A 0 A_Refire
        Goto Ready+1
	 CoopFire:
		TNT1 A 0 A_FireCustomMissile("RottMissile",0,8,0,-8) // -16
		Goto Continue
	 } 
}  

actor RottMissile
{
    Radius 11
    Height 8
    Speed 50
    Damage (35)
    SCALE 0.50
    PROJECTILE
	+WINDTHRUST
    +EXTREMEDEATH 
	+NOBLOOD
	+BLOODLESSIMPACT
    DamageType "RottExplosive"
//	ActiveSound "missile/fly"	
    DeathSound "god/bang"
	+THRUSPECIES
	Species "Player"
	Decal Scorch
    States
    {
    Spawn:
		TNT1 A 0
        TNT1 A 0 A_PlaySound("missile/fly",5,1.0,1,ATTN_NORM)
	SpawnLoop:			
        MISS AAAAAAAAAAAAAA 1 Bright A_CustomMissile("MissileTrail",0,0,0,0)     
        Loop
    Death:
	XDeath:
		TNT1 A 0 A_StopSound(5)
		TNT1 A 0 A_PlaySound("gen/expl2",6,1.0,0,ATTN_NORM)	
    	EXPL A 2 Bright A_Explode(64,48)
        EXPL BCDEFGHIJKLMNOPQRST 2 Bright
        Stop
	Crash:
		TNT1 A 0 A_StopSound(5)	
		TNT1 A 0 A_PlaySound("gen/expl2",6,1.0,0,ATTN_NORM)
		TNT1 A 0 A_Jump(250,2)
		TNT1 A 0 A_PlaySound("rmissile/cook",5,1.0,0,ATTN_NORM)	
		TNT1 A 0
    	EXPL A 2 Bright A_Explode(64,48)
        EXPL BCDEFGHIJKLMNOPQRST 2 Bright
		Stop
    }
}

Actor CompRottM : RottMissile { Speed 48 -THRUSPECIES Species "" Decal Scorch }

ACTOR MissileTrail
{
  Radius 2
  Height 2
  Speed 0
  Scale 0.65
  PROJECTILE
  +NOGRAVITY
  +RANDOMIZE
  +CLIENTSIDEONLY
  States
  {
  Spawn:
     TNT1 A 4
     MTRL AAAA 1 Bright
     goto misc     
  MISC:   
     MTRL BBCCDD 1 Bright A_fadeout
     stop
   
  }
}