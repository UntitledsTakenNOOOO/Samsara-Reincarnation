ACTOR CMLaserPistol : Weapon
{
	Weapon.AmmoGive 40
	Weapon.AmmoUse 0
	Weapon.SlotNumber 1
	Weapon.BobStyle InverseAlpha
	Weapon.BobRangeX 0.5
	Weapon.BobRangeY 0.6
	Weapon.BobSpeed 2.3
	Weapon.SelectionOrder 3800
	+FLOORCLIP
	+Inventory.UNDROPPABLE
	+WEAPON.NOAUTOFIRE
	+WEAPON.WIMPY_WEAPON
	Tag "Laser Pistol"
	Inventory.PickupSound "misc/w_pkup"
	Inventory.PickupMessage "Picked up a laser pistol!"
	Obituary "%o was phased by %k's laser pistol."
	Weapon.YAdjust 6
	Inventory.RestrictedTo "CMPlayer"
	States
	{ 
	Spawn:
		CMPS F -1
		Loop
	Ready: 
		CMPS A 1 A_WeaponReady 
		Loop 
	Deselect: 
		TNT1 A 0 A_Lower
		CMPS G 1 A_Lower 
		Loop 
	Select: 
		TNT1 A 0 A_Raise
		CMPS G 1 A_Raise
		Loop
	Fire:
		TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,"CheckAmmo")	
	Fire2:
		CMPS D 1 BRIGHT A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		CMPS C 2 BRIGHT A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		TNT1 A 0 A_PlaySound("CM/LaserFire")
        TNT1 A 0 A_FireBullets(0,0,1,0,"SamsaraBlankPuff",FBF_NORANDOM|FBF_NOFLASH)
		TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,2)
		TNT1 A 0 A_FireCustomMissile("CMLaserProj",0,1,-5,0)
		Goto A
		TNT1 A 0 A_FireCustomMissile("CMLaserProj_2",0,1,-5,0)		
	A:
		CMPS D 1 BRIGHT A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
//		CMPS E 6 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		TNT1 A 0 A_ReFire("Ready")
//		CMPS B 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		goto Ready
	CheckAmmo:
		TNT1 A 0 A_JumpIfInventory("Clip",1,2)
		TNT1 A 0 A_SelectWeapon("MagicFist")
		goto Deselect
		TNT1 A 0 A_TakeInventory("Clip",1)
		Goto Fire2
	}
}

actor CMLaserProj
{
	Height 3
	Radius 2
	Speed 85
	Damage (Random(9,12))
	PROJECTILE
	RENDERSTYLE ADD
	ALPHA 0.95
	DeathSound "CM/LaserPuff"
	-SKYEXPLODE
	+BLOODSPLATTER
	States
	{
   Spawn:
	  CMLZ B 4 Bright
	  Loop
   Death:
   	  CMLZ B 1 Bright A_FadeOut
	  Loop
	}
}

Actor CMLaserProj_2 : CMLaserProj { Species "Player" +THRUSPECIES +DONTHARMCLASS +DONTHARMSPECIES +MTHRUSPECIES }