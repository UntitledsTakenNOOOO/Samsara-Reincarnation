actor DGHasChaingun : Boolean {}
actor SamsaraDoom64ChaingunSoundChannelCount : Counter { Inventory.MaxAmount 3 }
actor SamsaraDoomguyStrChaingunFiringSoundCheck : Boolean {}
actor SamsaraDoomguyStrChaingunPoweredAttackAmount : Counter { Inventory.MaxAmount 2 }

actor " Chaingun " : Weapon
{
	Weapon.SelectionOrder 700
	Weapon.AmmoUse 1
	Weapon.AmmoGive 0
	Weapon.AmmoType "Clip"
	Weapon.SlotNumber 4
	Tag "Chaingun"
	Inventory.PickupMessage "You got the Chaingun!"
	+ALT_USES_BOTH
	+UNDROPPABLE
	Obituary "$SAMSARA_DOOMGUY_OB_SLOT4"
	Inventory.RestrictedTo "DoomguyPlayer"
	States
	{
	Spawn:
		WCGN A -1
		stop

	// Original
	Ready:
		TNT1 A 0 A_TakeInventory("SamsaraDoomguyStrChaingunFiringSoundCheck")
		TNT1 A 0 A_JumpIfInventory("DoomClassMode",2,"ReadyStr")
		TNT1 A 0 A_JumpIfInventory("DoomClassMode",1,"Ready64")
		DCHG A 1 A_WeaponReady(WRF_NOSECONDARY)
		loop

	Deselect:
		TNT1 A 0 A_GiveInventory("SamsaraDoomguyStrChaingunFiringSoundStopper")
		TNT1 A 0 A_JumpIfInventory("DoomClassMode",2,"DeselectStr")
		TNT1 A 0 A_JumpIfInventory("DoomClassMode",1,"Deselect64")
		DCHG A 1 A_Lower
		goto Deselect

	Select:
		TNT1 A 0 A_TakeInventory("SamsaraDoomguyStrChaingunFiringSoundCheck")
		TNT1 A 0 A_JumpIfInventory("DoomClassMode",2,"SelectStr")
		TNT1 A 0 A_JumpIfInventory("DoomClassMode",1,"Select64")
		DCHG A 1 A_Raise
		loop

	Fire:
		TNT1 A 0 A_JumpIfInventory("DoomClassMode",2,"FireStr")
		TNT1 A 0 A_GiveInventory("SamsaraDoomguyStrChaingunFiringSoundStopper")
		TNT1 A 0 A_JumpIfInventory("DoomClassMode",1,"Fire64")
		TNT1 A 0 A_JumpIfInventory("SamsaraUsingSmoothAnims",1,"FireSmooth")
	FireVanilla:
		TNT1 A 0 A_GunFlash
		DCHG A 4 offset(0,32) Bright A_GiveInventory("SamsaraDoomguyChaingunAttackHandler")
		TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireVanilla2Attack")
		TNT1 A 0 A_JumpIfInventory("Clip",1,"FireVanilla2Attack")
		goto FireVanilla2

	FireVanilla2Attack:
		TNT1 A 0 A_GunFlash("Flash2")
	FireVanilla2:
		DCHG B 4 offset(0,32) Bright A_GiveInventory("SamsaraDoomguyChaingunAttackHandler")
	FireFinish:
		TNT1 A 0 A_ReFire
		goto Ready

	FireSmooth:
		TNT1 A 0 A_JumpIfInventory("SamsaraHasDoubleFiringSpeed",1,"FireSmoothRage")
		TNT1 A 0 A_GunFlash("FlashSmooth")
		PKCG A 1 offset(0,38) A_GiveInventory("SamsaraDoomguyChaingunAttackHandler")
		PKCG BCD 1
		TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireSmooth2Attack")
		TNT1 A 0 A_JumpIfInventory("Clip",1,"FireSmooth2Attack")
		goto FireSmooth2

	FireSmoothRage:
		TNT1 A 0 A_GunFlash("FlashSmoothRage")
		PKCG A 1 offset(0,38) A_GiveInventory("SamsaraDoomguyChaingunAttackHandler")
		PKCG C 1
		TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireSmooth2Attack")
		TNT1 A 0 A_JumpIfInventory("Clip",1,"FireSmooth2Attack")
		goto FireSmoothRage2

	FireSmooth2Attack:
		TNT1 A 0 A_JumpIfInventory("SamsaraHasDoubleFiringSpeed",1,"FireSmoothRage2Attack")
		TNT1 A 0 A_GunFlash("FlashSmooth2")
	FireSmooth2:
		TNT1 A 0 A_GiveInventory("SamsaraDoomguyChaingunAttackHandler")
		PKCG A 1 offset(0,38)
		PKCG BCD 1
		goto FireFinish

	FireSmoothRage2Attack:
		TNT1 A 0 A_GunFlash("FlashSmoothRage2")
	FireSmoothRage2:
		TNT1 A 0 A_GiveInventory("SamsaraDoomguyChaingunAttackHandler")
		PKCG B 1 offset(0,38)
		PKCG D 1
		goto FireFinish

	Flash:
		DCHF A 4 Bright A_Light1
		goto LightDone

	Flash2:
		DCHF B 4 Bright A_Light2
		goto LightDone
		
	FlashSmooth:
		PKCF A 1 Bright A_Light2
		PKCF B 1 Bright A_Light1
		goto LightDone

	FlashSmoothRage:
		PKCF A 1 Bright A_Light2
		PKCF B 0 Bright A_Light1
		goto LightDone

	FlashSmooth2:
		PKCF C 1 Bright A_Light2
		PKCF D 1 Bright A_Light1
		goto LightDone

	FlashSmoothRage2:
		PKCF C 1 Bright A_Light2
		PKCF D 0 Bright A_Light1
		goto LightDone

	// Doom 64 (fires slightly faster and has recoil visual)
	Ready64:
		64CG A 1 A_WeaponReady(WRF_NOSECONDARY)
		goto Ready

	Deselect64:
		64CG A 1 A_Lower
		goto Deselect

	Select64:
		64CG A 1 A_Raise
		goto Select

	Fire64:
		TNT1 A 0 A_GunFlash("Flash64")
		TNT1 A 0 A_JumpIfInventory("SamsaraUsingSmoothAnims",1,"Fire64Smooth")
	Fire64Vanilla:
		64CG A 4 offset(0,32) A_GiveInventory("SamsaraDoomguy64ChaingunAttackHandler")
		TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"Fire64Vanilla2Attack")
		TNT1 A 0 A_JumpIfInventory("Clip",1,"Fire64Vanilla2Attack")
		goto Fire64Vanilla2

	Fire64Vanilla2Attack:
		TNT1 A 0 A_GunFlash("Flash64_2")
	Fire64Vanilla2:
		64CG C 3 offset(0,32) A_GiveInventory("SamsaraDoomguy64ChaingunAttackHandler")
		goto FireFinish

	Fire64Smooth:
		64CG A 2 A_GiveInventory("SamsaraDoomguy64ChaingunAttackHandler")
		64CG B 2
		TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"Fire64Smooth2Attack")
		TNT1 A 0 A_JumpIfInventory("Clip",1,"Fire64Smooth2Attack")
		goto Fire64Smooth2

	Fire64Smooth2Attack:
		TNT1 A 0 A_GunFlash("Flash64_2")
	Fire64Smooth2:
		64CG C 2 A_GiveInventory("SamsaraDoomguy64ChaingunAttackHandler")
		64CG D 1
		goto FireFinish

	Flash64:
		64CM A 3 Bright A_Light1
		goto LightDone

	Flash64_2:
		64CM B 2 Bright A_Light2
		goto LightDone

	// Stronghold
	// Unpowered
	ReadyStr:
		TNT1 A 0 A_JumpIfInventory("PowerSamsaraDoomguyStrWeaponPower",1,"ReadyStrPowered")
		TNT1 A 0 A_JumpIfInventory("SamsaraHasPermaWeaponLevel2",1,"ReadyStrPowered")
		DCHG H 1 A_WeaponReady(WRF_NOSECONDARY)
		goto Ready

	DeselectStr:
		TNT1 A 0 A_JumpIfInventory("PowerSamsaraDoomguyStrWeaponPower",1,"DeselectStrPowered")
		TNT1 A 0 A_JumpIfInventory("SamsaraHasPermaWeaponLevel2",1,"DeselectStrPowered")
	DeselectStrContinue:
		TNT1 A 0 A_Lower
		DCHG H 1 A_Lower
		goto Deselect

	SelectStr:
		TNT1 A 0 A_JumpIfInventory("PowerSamsaraDoomguyStrWeaponPower",1,"SelectStrPowered")
		TNT1 A 0 A_JumpIfInventory("SamsaraHasPermaWeaponLevel2",1,"SelectStrPowered")
	SelectStrContinue:
		TNT1 A 0 A_Raise
		DCHG H 1 A_Raise
		goto Select

	FireStr:
		TNT1 A 0 A_JumpIfInventory("PowerSamsaraDoomguyStrWeaponPower",1,"FireStrPowered")
		TNT1 A 0 A_JumpIfInventory("SamsaraHasPermaWeaponLevel2",1,"FireStrPowered")
		TNT1 A 0 A_JumpIfInventory("SamsaraImprovedDoomguyBalance",1,"FireStrImproved")
		TNT1 A 0 A_JumpIfInventory("SamsaraUsingSmoothAnims",1,"FireStrSmooth")
	FireStrVanilla:
		TNT1 A 0 A_GunFlash("FlashStr")
		DCHG J 2 offset(0,32) Bright A_GiveInventory("SamsaraDoomguyStrChaingunAttackHandler")
		DCHG K 2 offset(0,32) Bright
		TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireStr2AttackVanilla")
		TNT1 A 0 A_JumpIfInventory("Clip",1,"FireStr2AttackVanilla")
		goto FireStr2Vanilla

	FireStr2AttackVanilla:
		TNT1 A 0 A_GunFlash("FlashStr")
	FireStr2Vanilla:
		DCHG L 2 offset(0,32) Bright A_GiveInventory("SamsaraDoomguyStrChaingunAttackHandler")
		DCHG M 2 offset(0,32) Bright
	FireStrFinish:
		TNT1 A 0 A_ReFire
        TNT1 A 0 A_GiveInventory("SamsaraDoomguyStrChaingunFiringEndSoundPlayer")
		goto Ready

	FireStrSmooth:
		TNT1 A 0 A_GunFlash("FlashStr")
		DCHG J 1 offset(0,38) Bright A_GiveInventory("SamsaraDoomguyStrChaingunAttackHandler")
		DCHG P 1 offset(0,36) Bright
		DCHG K 1 offset(0,34) Bright
		DCHG Q 1 offset(0,32) Bright
		TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireStr2AttackSmooth")
		TNT1 A 0 A_JumpIfInventory("Clip",1,"FireStr2AttackSmooth")
		goto FireStr2Smooth

	FireStr2AttackSmooth:
		TNT1 A 0 A_GunFlash("FlashStr")
	FireStr2Smooth:
		DCHG J 1 offset(0,38) Bright A_GiveInventory("SamsaraDoomguyStrChaingunAttackHandler")
		DCHG P 1 offset(0,36) Bright
		DCHG K 1 offset(0,34) Bright
		DCHG Q 1 offset(0,32) Bright
		goto FireStrFinish

	FlashStr:
		TNT1 A 2 Bright A_Light1
		TNT1 A 2 Bright A_Light2
		goto LightDone

	FireStrImproved:
		TNT1 A 0 A_JumpIfInventory("SamsaraUsingSmoothAnims",1,"FireStrImprovedSmooth")
		TNT1 A 0 A_GunFlash("FlashStrImproved")
		DCHG J 2 Bright A_GiveInventory("SamsaraDoomguyStrChaingunAttackHandler")
		DCHG K 1 Bright
		TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireStr2AttackImproved")
		TNT1 A 0 A_JumpIfInventory("Clip",1,"FireStr2AttackImproved")
		goto FireStr2Improved

	FireStr2AttackImproved:
		TNT1 A 0 A_GunFlash("FlashStrImproved")
	FireStr2Improved:
		DCHG L 2 Bright A_GiveInventory("SamsaraDoomguyStrChaingunAttackHandler")
		DCHG M 1 Bright
		goto FireStrFinish
		
	FireStrImprovedSmooth:
		TNT1 A 0 A_GunFlash("FlashStrImproved")
		DCHG J 1 Bright offset(0,38) A_GiveInventory("SamsaraDoomguyStrChaingunAttackHandler")
		DCHG P 1 Bright offset(0,35)
		DCHG K 1 Bright offset(0,32)
		TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireStr2AttackImprovedSmooth")
		TNT1 A 0 A_JumpIfInventory("Clip",1,"FireStr2AttackImprovedSmooth")
		goto FireStr2ImprovedSmooth

	FireStr2AttackImprovedSmooth:
		TNT1 A 0 A_GunFlash("FlashStrImproved")
	FireStr2ImprovedSmooth:
		DCHG J 1 Bright offset(0,38) A_GiveInventory("SamsaraDoomguyStrChaingunAttackHandler")
		DCHG P 1 Bright offset(0,35)
		DCHG K 1 Bright offset(0,32)
		goto FireStrFinish

	FlashStrImproved:
		TNT1 A 2 Bright A_Light1
		TNT1 A 1 Bright A_Light2
		goto LightDone

	// Powered
	ReadyStrPowered:
		DCHG H 1 A_WeaponReady
		goto Ready

	DeselectStrPowered:
		TNT1 A 0 A_Lower
		goto DeselectStrContinue

	SelectStrPowered:
		TNT1 A 0 A_Raise
		goto SelectStrContinue

	FireStrPowered:
		TNT1 A 0 A_TakeInventory("SamsaraDoomguyStrChaingunPoweredAttackAmount") // reset
	FireStrPoweredLoop:
		TNT1 A 0 A_GiveInventory("SamsaraDoomguyStrChaingunPoweredAttackAmount")
		TNT1 A 0 A_JumpIfInventory("SamsaraUsingSmoothAnims",1,"FireStrPoweredLoopSmooth")
	FireStrPoweredLoopVanilla:
		TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireStrPoweredLoopVanilla1Attack")
		TNT1 A 0 A_JumpIfInventory("Clip",1,"FireStrPoweredLoopVanilla1Attack")
		goto FireStrPoweredLoopVanilla1

	FireStrPoweredLoopVanilla1Attack:
		TNT1 A 0 A_GunFlash("FlashStrPowered")
	FireStrPoweredLoopVanilla1:
		TNT1 A 0 A_TakeInventory("SamsaraTakingAmmo")
		DCHG J 2 Bright A_GiveInventory("SamsaraDoomguyStrChaingunPoweredPrimaryAttackHandler")
		DCHG K 1 Bright
		TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireStrPoweredLoopVanilla2Attack")
		TNT1 A 0 A_JumpIfInventory("Clip",1,"FireStrPoweredLoopVanilla2Attack")
		goto FireStrPoweredLoopVanilla2

	FireStrPoweredLoopVanilla2Attack:
		TNT1 A 0 A_GunFlash("FlashStrPowered")
	FireStrPoweredLoopVanilla2:
		TNT1 A 0 A_GiveInventory("SamsaraTakingAmmo")
		DCHG L 2 Bright A_GiveInventory("SamsaraDoomguyStrChaingunPoweredPrimaryAttackHandler")
		DCHG M 1 Bright
		TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrChaingunPoweredAttackAmount",0,"FireStrFinish")
		goto FireStrPoweredLoop

	FireStrPoweredLoopSmooth:
		TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireStrPoweredLoopSmooth1Attack")
		TNT1 A 0 A_JumpIfInventory("Clip",1,"FireStrPoweredLoopSmooth1Attack")
		goto FireStrPoweredLoopSmooth1

	FireStrPoweredLoopSmooth1Attack:
		TNT1 A 0 A_GunFlash("FlashStrPowered")
	FireStrPoweredLoopSmooth1:
		TNT1 A 0 A_TakeInventory("SamsaraTakingAmmo")
		DCHG J 1 Bright offset(0,38) A_GiveInventory("SamsaraDoomguyStrChaingunPoweredPrimaryAttackHandler")
		DCHG P 1 Bright offset(0,35)
		DCHG K 1 Bright offset(0,32)
		TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireStrPoweredLoopSmooth2Attack")
		TNT1 A 0 A_JumpIfInventory("Clip",1,"FireStrPoweredLoopSmooth2Attack")
		goto FireStrPoweredLoopSmooth2

	FireStrPoweredLoopSmooth2Attack:
		TNT1 A 0 A_GunFlash("FlashStrPowered")
	FireStrPoweredLoopSmooth2:
		TNT1 A 0 A_GiveInventory("SamsaraTakingAmmo")
		DCHG J 1 Bright offset(0,38) A_GiveInventory("SamsaraDoomguyStrChaingunPoweredPrimaryAttackHandler")
		DCHG P 1 Bright offset(0,35)
		DCHG K 1 Bright offset(0,32)
		TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrChaingunPoweredAttackAmount",0,"FireStrFinish")
		goto FireStrPoweredLoop

	AltFire:
		TNT1 A 0 A_TakeInventory("SamsaraDoomguyStrChaingunPoweredAttackAmount") // reset
	AltFireLoop:
		TNT1 A 0 A_GiveInventory("SamsaraDoomguyStrChaingunPoweredAttackAmount")
		TNT1 A 0 A_JumpIfInventory("SamsaraUsingSmoothAnims",1,"AltFireLoopSmooth")
	AltFireLoopVanilla:
		TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"AltFireLoopVanilla1Attack")
		TNT1 A 0 A_JumpIfInventory("Clip",1,"AltFireLoopVanilla1Attack")
		goto AltFireLoopVanilla1

	AltFireLoopVanilla1Attack:
		TNT1 A 0 A_GunFlash("FlashStrPowered")
	AltFireLoopVanilla1:
		DCHG J 2 Bright A_GiveInventory("SamsaraDoomguyStrChaingunPoweredSecondaryAttackHandler")
		DCHG K 1 Bright
		TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"AltFireLoopVanilla2Attack")
		TNT1 A 0 A_JumpIfInventory("Clip",1,"AltFireLoopVanilla2Attack")
		goto AltFireLoopVanilla2

	AltFireLoopVanilla2Attack:
		TNT1 A 0 A_GunFlash("FlashStrPowered")
	AltFireLoopVanilla2:
		DCHG L 2 Bright A_GiveInventory("SamsaraDoomguyStrChaingunPoweredSecondaryAttackHandler")
		DCHG M 1 Bright
		TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrChaingunPoweredAttackAmount",0,"AltFireFinish")
		goto AltFireLoop

	AltFireLoopSmooth:
		TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"AltFireLoopSmooth1Attack")
		TNT1 A 0 A_JumpIfInventory("Clip",1,"AltFireLoopSmooth1Attack")
		goto AltFireLoopSmooth1

	AltFireLoopSmooth1Attack:
		TNT1 A 0 A_GunFlash("FlashStrPowered")
	AltFireLoopSmooth1:
		DCHG J 1 Bright offset(0,38) A_GiveInventory("SamsaraDoomguyStrChaingunPoweredSecondaryAttackHandler")
		DCHG P 1 Bright offset(0,35)
		DCHG K 1 Bright offset(0,32)
		TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"AltFireLoopSmooth2Attack")
		TNT1 A 0 A_JumpIfInventory("Clip",1,"AltFireLoopSmooth2Attack")
		goto AltFireLoopSmooth2

	AltFireLoopSmooth2Attack:
		TNT1 A 0 A_GunFlash("FlashStrPowered")
	AltFireLoopSmooth2:
		DCHG J 1 Bright offset(0,38) A_GiveInventory("SamsaraDoomguyStrChaingunPoweredSecondaryAttackHandler")
		DCHG P 1 Bright offset(0,35)
		DCHG K 1 Bright offset(0,32)
		TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrChaingunPoweredAttackAmount",0,"AltFireFinish")
		goto AltFireLoop

	AltFireFinish:
		TNT1 A 0 A_JumpIfInventory("PowerSamsaraDoomguyStrWeaponPower",1,"AltFireFinishReFire")
		TNT1 A 0 A_JumpIfInventory("SamsaraHasPermaWeaponLevel2",1,"AltFireFinishReFire")
		TNT1 A 0 A_CheckReload
        TNT1 A 0 A_GiveInventory("SamsaraDoomguyStrChaingunFiringEndSoundPlayer")
		goto Ready

	AltFireFinishReFire:
		TNT1 A 0 A_JumpIfInventory("DoomClassMode",2,"FireStrFinish")
		TNT1 A 0 A_CheckReload
        TNT1 A 0 A_GiveInventory("SamsaraDoomguyStrChaingunFiringEndSoundPlayer")
		goto Ready

	FlashStrPowered:
		TNT1 A 2 Bright A_Light1
		TNT1 A 1 Bright A_Light2
		goto LightDone
	}
}

actor SamsaraDoomguyChaingunAttackHandler : Trigger
{
	States
	{
	// Ammo checking/taking stuff
	Pickup:
		TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupSound")
		TNT1 A 0 A_JumpIfInventory("Clip",1,1)
		stop
		TNT1 A 0 A_TakeInventory("Clip",1)
		goto PickupSound

	// Sound stuff
	PickupSound:
		TNT1 A 0 A_JumpIfInventory("SamsaraAlternateSoundToken",2,"PickupSoundPerkristan")
		TNT1 A 0 A_JumpIfInventory("SamsaraAlternateSoundToken",1,"PickupSoundPSX")
		TNT1 A 0 A_PlaySound("doomguy/chngun",CHAN_WEAPON)
		goto PickupAttack
	PickupSoundPSX:
		TNT1 A 0 A_PlaySound("doom64guy/chngun",CHAN_WEAPON)
		goto PickupAttack
	PickupSoundPerkristan:
		TNT1 A 0 A_PlaySound("doomguy/perkristan/chngun",CHAN_WEAPON)
		goto PickupAttack

	// Attack stuff
	PickupAttack:
		TNT1 A 0 A_JumpIfInventory("SamsaraImprovedDoomguyBalance",1,"PickupAttackImproved")
	// Vanilla
	PickupAttackVanilla:
		TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackVanillaCoop")
		TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackVanillaCoop")
	PickupAttackVanillaDM:
		TNT1 A 0 A_FireBullets(5.6,0,1,5,"DoomBulletPuff2ThruGhost",FBF_NOFLASH)
		stop

	PickupAttackVanillaCoop:
		TNT1 A 0 A_FireBullets(5.6,0,1,5,"DoomBulletPuff2ThruGhost_Coop",FBF_NOFLASH)
		stop

	// Improved
	PickupAttackImproved:
		TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackImprovedCoop")
		TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackImprovedCoop")
	PickupAttackImprovedDM:
		TNT1 A 0 A_FireBullets(5.6,0,1,random(8,10)*2,"DoomBulletPuff2ThruGhost",FBF_NORANDOM|FBF_NOFLASH)
		stop

	PickupAttackImprovedCoop:
		TNT1 A 0 A_FireBullets(5.6,0,1,random(8,10)*2,"DoomBulletPuff2ThruGhost_Coop",FBF_NORANDOM|FBF_NOFLASH)
		stop
	}
}

actor SamsaraDoomguy64ChaingunAttackHandler : Trigger
{
	States
	{
	// Ammo checking/taking stuff
	Pickup:
		TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupPlaySound")
		TNT1 A 0 A_JumpIfInventory("Clip",1,1)
		stop
		TNT1 A 0 A_TakeInventory("Clip",1)
		goto PickupPlaySound

	// Sound stuff
	PickupPlaySound:
		TNT1 A 0 A_JumpIfInventory("SamsaraDoom64ChaingunSoundChannelCount",0,"PickupPlaySound7")
		TNT1 A 0 A_JumpIfInventory("SamsaraDoom64ChaingunSoundChannelCount",2,"PickupPlaySound6")
		TNT1 A 0 A_JumpIfInventory("SamsaraDoom64ChaingunSoundChannelCount",1,"PickupPlaySound4")
	PickupPlaySound1:
		TNT1 A 0 A_PlaySound("doom64guy/chngun",CHAN_WEAPON)
		TNT1 A 0 A_GiveInventory("SamsaraDoom64ChaingunSoundChannelCount")
		goto PickupAttack

	PickupPlaySound4:
		TNT1 A 0 A_PlaySound("doom64guy/chngun")
		TNT1 A 0 A_GiveInventory("SamsaraDoom64ChaingunSoundChannelCount")
		goto PickupAttack

	PickupPlaySound6:
		TNT1 A 0 A_PlaySound("doom64guy/chngun",CHAN_6)
		TNT1 A 0 A_GiveInventory("SamsaraDoom64ChaingunSoundChannelCount")
		goto PickupAttack

	PickupPlaySound7:
		TNT1 A 0 A_PlaySound("doom64guy/chngun",CHAN_7)
		TNT1 A 0 A_TakeInventory("SamsaraDoom64ChaingunSoundChannelCount")
		goto PickupAttack

	// Attack stuff
	PickupAttack:
		TNT1 A 0 A_JumpIfInventory("SamsaraImprovedDoomguyBalance",1,"PickupAttackImproved")
	// Vanilla
	PickupAttackVanilla:
		TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackVanillaCoop")
		TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackVanillaCoop")
	PickupAttackVanillaDM:
		TNT1 A 0 A_FireBullets(5.6,0,1,random(1,4)*4,"Doom64BulletPuff2ThruGhost",FBF_NORANDOM|FBF_NOFLASH)
		goto PickupFinish

	PickupAttackVanillaCoop:
		TNT1 A 0 A_FireBullets(5.6,0,1,random(1,4)*4,"Doom64BulletPuff2ThruGhost_Coop",FBF_NORANDOM|FBF_NOFLASH)
		goto PickupFinish

	// Improved
	PickupAttackImproved:
		TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackImprovedCoop")
		TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackImprovedCoop")
	PickupAttackImprovedDM:
		TNT1 A 0 A_FireBullets(5.6,0,1,random(6,9)*2,"Doom64BulletPuff2ThruGhost",FBF_NORANDOM|FBF_NOFLASH)
		goto PickupFinish

	PickupAttackImprovedCoop:
		TNT1 A 0 A_FireBullets(5.6,0,1,random(6,9)*2,"Doom64BulletPuff2ThruGhost_Coop",FBF_NORANDOM|FBF_NOFLASH)
		goto PickupFinish

	// Finishing stuff
	PickupFinish:
		TNT1 A 0 ACS_NamedExecuteAlways("SamsaraRecoil", 0, 1, 0, 5)
		stop
	}
}

// Unpowered attack handler

actor SamsaraDoomguyStrChaingunAttackHandler : Trigger
{
	States
	{
	// Ammo checking/taking stuff
	Pickup:
		TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupSound")
		TNT1 A 0 A_JumpIfInventory("Clip",1,1)
		stop
		TNT1 A 0 A_TakeInventory("Clip",1)
		goto PickupSound

	// Sound stuff
	PickupSound:
		TNT1 A 0 A_JumpIfInventory("SamsaraAlternateSoundToken",1,"PickupSoundDE")
		TNT1 A 0 A_PlaySound("doomguy/stronghold/chnglp",CHAN_WEAPON,1,true)
		TNT1 A 0 A_GiveInventory("SamsaraDoomguyStrChaingunFiringSoundCheck")
		goto PickupAttack
	PickupSoundDE:
		TNT1 A 0 A_GiveInventory("SamsaraDoomguyStrChaingunFiringSoundStopper")
		TNT1 A 0 A_PlaySound("doomguy/demoneclipse/chngun",CHAN_WEAPON)
		goto PickupAttack

	// Attack stuff
	PickupAttack:
		TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
		TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
	PickupAttackDM:
		TNT1 A 0 A_FireBullets(4,2.3,1,5,"SamsaraDoomguyStrBulletPuffBallisticThruGhost",FBF_NOFLASH)
		stop

	PickupAttackCoop:
		TNT1 A 0 A_FireBullets(4,2.3,1,5,"SamsaraDoomguyStrBulletPuffBallisticThruGhostCoop",FBF_NOFLASH)
		stop
	}
}

actor SamsaraDoomguyStrChaingunFiringEndSoundPlayer : Trigger
{
	States
	{
	// Sound playing stuff
	Pickup:
		TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrChaingunFiringSoundCheck",1,1)
		stop
		TNT1 A 0 A_PlaySound("doomguy/stronghold/chngdn",CHAN_WEAPON)
		TNT1 A 0 A_TakeInventory("SamsaraDoomguyStrChaingunFiringSoundCheck")
		stop
	}
}

actor SamsaraDoomguyStrChaingunFiringSoundStopper : Trigger
{
	States
	{
	// Sound stopping stuff
	Pickup:
		TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrChaingunFiringSoundCheck",1,1)
		stop
		TNT1 A 0 A_PlaySound("silence",CHAN_WEAPON)
		TNT1 A 0 A_StopSound(CHAN_WEAPON)
		TNT1 A 0 A_TakeInventory("SamsaraDoomguyStrChaingunFiringSoundCheck")
		stop
	}
}

// Powered attack handlers

actor SamsaraDoomguyStrChaingunPoweredPrimaryAttackHandler : Trigger
{
	States
	{
	// Ammo checking/taking stuff
	Pickup:
		TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
		TNT1 A 0 A_JumpIfInventory("Clip",1,1)
		stop
		TNT1 A 0 A_JumpIfInventory("SamsaraTakingAmmo",1,1)
		goto PickupAttack
		TNT1 A 0 A_TakeInventory("Clip",1)
		goto PickupAttack

	// Attack stuff
	PickupAttack:
		TNT1 A 0 A_JumpIfInventory("SamsaraAlternateSoundToken",1,"PickupAttackDE")
	PickupAttackStr:
		TNT1 A 0 A_PlaySound("doomguy/stronghold/chnglp",CHAN_WEAPON,1,true)
		TNT1 A 0 A_GiveInventory("SamsaraDoomguyStrChaingunFiringSoundCheck")
		TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackStrCoop")
		TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackStrCoop")
	PickupAttackStrDM:
		TNT1 A 0 A_FireBullets(0.75,0.75,-1,20,"SamsaraDoomguyStrElectricalPuff",FBF_NOFLASH)
		stop

	PickupAttackStrCoop:
		TNT1 A 0 A_FireBullets(0.75,0.75,-1,20,"SamsaraDoomguyStrElectricalPuffCoop",FBF_NOFLASH)
		stop

	PickupAttackDE:
		TNT1 A 0 A_GiveInventory("SamsaraDoomguyStrChaingunFiringSoundStopper")
		TNT1 A 0 A_PlaySound("doomguy/demoneclipse/chngun",CHAN_WEAPON)
		TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackDECoop")
		TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackDECoop")
	PickupAttackDEDM:
		TNT1 A 0 A_FireBullets(0.75,0.75,-1,20,"SamsaraDoomguyStrElectricalPuffSoundDE",FBF_NOFLASH)
		stop

	PickupAttackDECoop:
		TNT1 A 0 A_FireBullets(0.75,0.75,-1,20,"SamsaraDoomguyStrElectricalPuffCoopSoundDE",FBF_NOFLASH)
		stop
	}
}

actor SamsaraDoomguyStrChaingunPoweredSecondaryAttackHandler : Trigger
{
	States
	{
	// Ammo checking/taking stuff
	Pickup:
		TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupSound")
		TNT1 A 0 A_JumpIfInventory("Clip",1,1)
		stop
		TNT1 A 0 A_TakeInventory("Clip",1)
		goto PickupSound

	// Sound stuff
	PickupSound:
		TNT1 A 0 A_JumpIfInventory("SamsaraAlternateSoundToken",1,"PickupSoundDE")
		TNT1 A 0 A_PlaySound("doomguy/stronghold/chnglp",CHAN_WEAPON,1,true)
		TNT1 A 0 A_GiveInventory("SamsaraDoomguyStrChaingunFiringSoundCheck")
		goto PickupAttack
	PickupSoundDE:
		TNT1 A 0 A_GiveInventory("SamsaraDoomguyStrChaingunFiringSoundStopper")
		TNT1 A 0 A_PlaySound("doomguy/demoneclipse/chngun",CHAN_WEAPON)
		goto PickupAttack

	// Attack stuff
	PickupAttack:
		TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
		TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
	PickupAttackDM:
		TNT1 A 0 A_FireBullets(7.5,3,-1,16,"SamsaraDoomguyStrRepPuffNoDamage",FBF_NOFLASH)
		stop

	PickupAttackCoop:
		TNT1 A 0 A_FireBullets(7.5,3,-1,16,"SamsaraDoomguyStrRepPuffNoDamageCoop",FBF_NOFLASH)
		stop
	}
}
