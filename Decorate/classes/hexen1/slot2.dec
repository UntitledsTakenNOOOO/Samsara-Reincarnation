// Parias (Reincarnation)

actor "Crusader's Longbow" : SamsaraHexenWeapon
{
    Inventory.PickupMessage "Crusader's Longbow"
    Weapon.SlotNumber 2
    Weapon.SelectionOrder 1700
    Weapon.YAdjust 20
    Weapon.AmmoType "Clip"
    //Weapon.AmmoGive 25
    Weapon.AmmoUse 3
    Obituary "$SAMSARA_HEXEN_OB_SLOT2"
    Tag "Crusader's Longbow"
    +BLOODSPLATTER
    +NOALERT
    States
    {
      Spawn:
        H114 A -1
        stop

      Ready:
        H114 BBBFFF 1 A_WeaponReady
        loop

      Deselect:
        H114 B 1 A_Lower
        loop

      Select:
        H114 B 1 A_Raise
        loop

      Fire:
        H114 C 3
        H114 D 3 A_PlaySound("Hexen/BowString",CHAN_WEAPON)
        H114 E 3
      Hold:
        H114 G 1
        TNT1 A 0 A_ReFire
        H114 H 3 A_GiveInventory("SamsaraHexenCrusadersLongbowAttackHandler")
        H114 ICB 3
        goto Ready
    }
}

actor SamsaraHexenCrusadersLongbowAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Clip",3,1)
        stop
        TNT1 A 0 A_TakeInventory("Clip",3)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("Hexen/ElectricArrow",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireCustomMissile("HexenShockArrow",0,false,5,0)
        stop

      PickupAttackCoop:
        TNT1 A 0 A_FireCustomMissile("HexenShockArrow2",0,false,5,0)
        stop
    }
}

actor HexenShockArrow
{
    Projectile
    Radius 5
    Height 5
    Speed 50
    Damage 21
    DamageType "Electric"
    RenderStyle "Add"
    DeathSound "Hexen/ArrowHit"
    Obituary "%o was shocked by one of %k's thunder arrows."
    +BLOODSPLATTER
    +DONTREFLECT
    States
    {
      Spawn:
        H115 A 1 Bright A_SpawnItemEx("HexenElectroTrail")
        loop

      Death:
        FAXE R 4 Bright A_AlertMonsters
        FAXE STUVWX 4 Bright
        stop
    }
}

actor HexenShockArrow2 : HexenShockArrow
{
    Species "Player"
    +THRUSPECIES
}

actor HexenElectroTrail
{
    RenderStyle "Add"
    Scale 0.2
    +CLIENTSIDEONLY
    +NOGRAVITY
    States
    {
      Spawn:
        FAXE UVWX 2 Bright
        stop
    }
}

// Parias (Classic)/Daedolon

actor "Frost Shards" : SamsaraHexenWeapon
{
    Inventory.PickupMessage "Frost Shards"
    Weapon.SlotNumber 2
    Weapon.SelectionOrder 1700
    Weapon.YAdjust 20
    Weapon.AmmoType "Clip"
    //Weapon.AmmoGive 25
    Weapon.AmmoUse 2
    Obituary "$SAMSARA_HEXEN_OB_SLOT2"
    Tag "Frost Shards"
    +BLOODSPLATTER
    States
    {
      Spawn:
        WFRS ABC 8 Bright
        loop

      // Parias (Classic)
      Ready:
        TNT1 A 0 A_JumpIfInventory("HexenClassMode",2,"ReadyMage")
      ReadyClericClassic:
        CONE A 1 A_WeaponReady
        goto Ready

      Deselect:
        TNT1 A 0 A_JumpIfInventory("HexenClassMode",2,"DeselectMage")
      DeselectClericClassic:
        CONE A 1 A_Lower
        goto Deselect

      Select:
        TNT1 A 0 A_JumpIfInventory("HexenClassMode",2,"SelectMage")
      SelectClericClassic:
        CONE A 1 A_Raise
        goto Select

      Fire:
        TNT1 A 0 A_JumpIfInventory("HexenClassMode",2,"FireMage")
      FireClericClassic:
        CONE B 3
        CONE C 4
        goto HoldClericClassic

      Hold:
        TNT1 A 0 A_JumpIfInventory("HexenClassMode",2,"HoldMage")
      HoldClericClassic:
        CONE D 3
        CONE E 5 Bright
        CONE F 4 Bright A_GiveInventory("SamsaraHexenFrostShardsAttackHandler")
        CONE G 4
        CONE A 5
        CONE A 7 A_ReFire
        goto Ready

      // Daedolon
      ReadyMage:
        CONE H 1 A_WeaponReady
        goto Ready

      DeselectMage:
        CONE H 1 A_Lower
        goto Deselect

      SelectMage:
        CONE H 1 A_Raise
        goto Select

      FireMage:
        CONE I 3
        CONE J 4
      HoldMage:
        CONE K 3
        CONE L 5 Bright
        CONE M 4 Bright A_GiveInventory("SamsaraHexenFrostShardsAttackHandler")
        CONE N 4
        CONE H 5
        CONE H 7 A_ReFire
        goto Ready
    }
}

actor SamsaraHexenFrostShardsAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Clip",2,1)
        stop
        TNT1 A 0 A_TakeInventory("Clip",2)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("MageShardsFire",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfCloser(92,"PickupAttackMelee")
        goto PickupAttackRanged

      // Melee
      PickupAttackMelee:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackMeleeCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackMeleeCoop")
      PickupAttackMeleeDM:
        TNT1 A 0 A_FireCustomMissile("FrostMissileMelee",0,false)
        stop

      PickupAttackMeleeCoop:
        TNT1 A 0 A_FireCustomMissile("FrostMissileMeleeCoop",0,false)
        stop

      // Ranged
      // THIS HERE AREA WAS A DICK
      PickupAttackRanged:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackRangedCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackRangedCoop")
      PickupAttackRangedDM:
      PickupAttackRangedDMPattern1:
        TNT1 A 0 A_FireCustomMissile("FrostMissileDM",14,false,6,1)
        TNT1 A 0 A_FireCustomMissile("FrostMissileDM",9,false,4,1)
        TNT1 A 0 A_FireCustomMissile("FrostMissileDM",5,false,2,1)
        TNT1 A 0 A_FireCustomMissile("FrostMissileDM",-14,false,-6,1)
        TNT1 A 0 A_FireCustomMissile("FrostMissileDM",-9,false,-4,1)
        TNT1 A 0 A_FireCustomMissile("FrostMissileDM",-5,false,-2,1)
      PickupAttackRangedDMPattern2:
        TNT1 A 0 A_FireCustomMissile("FrostMissileDM2",0,false,0,8)
        TNT1 A 0 A_FireCustomMissile("FrostMissileDM3",0,false,0,16)
        TNT1 A 0 A_FireCustomMissile("FrostMissileDM4",0,false,0,24)
        TNT1 A 0 A_FireCustomMissile("FrostMissileDM2",0,false,0,-2)
        TNT1 A 0 A_FireCustomMissile("FrostMissileDM3",0,false,0,-3)
        TNT1 A 0 A_FireCustomMissile("FrostMissileDM4",0,false,0,-4)
      PickupAttackRangedDMPattern3:
        TNT1 A 0 A_FireCustomMissile("FrostMissileDM2",4,false,1,16)
        TNT1 A 0 A_FireCustomMissile("FrostMissileDM2",4,false,1,-2)
        TNT1 A 0 A_FireCustomMissile("FrostMissileDM2",-4,false,-1,16)
        TNT1 A 0 A_FireCustomMissile("FrostMissileDM2",-4,false,-1,-2)
      PickupAttackRangedDMPattern4:
        TNT1 A 0 A_FireCustomMissile("FrostMissileDM",0,false)
        stop

      PickupAttackRangedCoop:
      PickupAttackRangedCoopPattern1:
        TNT1 A 0 A_FireCustomMissile("FrostMissile2",14,false,6,1)
        TNT1 A 0 A_FireCustomMissile("FrostMissile2",9,false,4,1)
        TNT1 A 0 A_FireCustomMissile("FrostMissile2",5,false,2,1)
        TNT1 A 0 A_FireCustomMissile("FrostMissile2",-14,false,-6,1)
        TNT1 A 0 A_FireCustomMissile("FrostMissile2",-9,false,-4,1)
        TNT1 A 0 A_FireCustomMissile("FrostMissile2",-5,false,-2,1)
      PickupAttackRangedCoopPattern2:
        TNT1 A 0 A_FireCustomMissile("FrostMissile2_2",0,false,0,8)
        TNT1 A 0 A_FireCustomMissile("FrostMissile2_3",0,false,0,16)
        TNT1 A 0 A_FireCustomMissile("FrostMissile2_4",0,false,0,24)
        TNT1 A 0 A_FireCustomMissile("FrostMissile2_2",0,false,0,-2)
        TNT1 A 0 A_FireCustomMissile("FrostMissile2_3",0,false,0,-3)
        TNT1 A 0 A_FireCustomMissile("FrostMissile2_4",0,false,0,-4)
      PickupAttackRangedCoopPattern3:
        TNT1 A 0 A_FireCustomMissile("FrostMissile2_2",4,false,1,16)
        TNT1 A 0 A_FireCustomMissile("FrostMissile2_2",4,false,1,-2)
        TNT1 A 0 A_FireCustomMissile("FrostMissile2_2",-4,false,-1,16)
        TNT1 A 0 A_FireCustomMissile("FrostMissile2_2",-4,false,-1,-2)
      PickupAttackRangedCoopPattern4:
        TNT1 A 0 A_FireCustomMissile("FrostMissile2",0,false)
        stop
      // Y'ALL MOTHERFUCKERS BETTER APPRECIATE IT
    }
}

// Melee

actor FrostMissileMelee : FastProjectile
{
    Radius 6
    Height 6
    Speed 20
    Damage (random(70,95)) // Previously 90-115
    DamageType "HexenIce"
    +BLOODLESSIMPACT
    States
    {
      Spawn:
        TNT1 AA 1
        stop

      Death:
        TNT1 A 1
        stop
    }
}

actor FrostMissileMeleeCoop : FrostMissileMelee
{
    Species "Player"
    +THRUSPECIES
}

// Ranged

actor FrostMissileDM : FrostMissile
{
    Damage (random(1,4))
    DamageType "HexenIce"
    Obituary "$SAMSARA_HEXEN_OB_SLOT2"
    +BLOODSPLATTER
    +THRUGHOST
}

actor FrostMissileDM2 : FrostMissileDM { Speed 23 }
actor FrostMissileDM3 : FrostMissileDM { Speed 21 }
actor FrostMissileDM4 : FrostMissileDM { Speed 19 }

actor FrostMissile2 : FrostMissile
{
    DamageType "HexenIce"
    Obituary "$SAMSARA_HEXEN_OB_SLOT2"
    Species "Player"
    +BLOODSPLATTER
    +THRUGHOST
    +THRUSPECIES
}

actor FrostMissileEnemy : FrostMissile // dark side version
{
    DamageType "HexenIce"
    Obituary "$SAMSARA_HEXEN_OB_SLOT2"
    +BLOODSPLATTER
    +THRUGHOST
}

actor FrostMissile2_1 : FrostMissileDM
{
    Species "Player"
    +THRUSPECIES
}

actor FrostMissile2_2 : FrostMissileDM2
{
    Species "Player"
    +THRUSPECIES
}

actor FrostMissile2_3 : FrostMissileDM3
{
    Species "Player"
    +THRUSPECIES
}

actor FrostMissile2_4 : FrostMissileDM4
{
    Species "Player"
    +THRUSPECIES
}

// Baratus

actor "Javelin of Zeal" : SamsaraHexenWeapon
{
    Inventory.PickupMessage "Javelin of Zeal"
    Weapon.SlotNumber 2
    Weapon.SelectionOrder 1700
    Weapon.YAdjust 20
    Weapon.AmmoType "Clip"
    //Weapon.AmmoGive 25
    Weapon.AmmoUse 3
    Obituary "$SAMSARA_HEXEN_OB_SLOT2"
    Tag "Javelin of Zeal"
    +AMMO_OPTIONAL
    +BLOODSPLATTER
    +NOALERT
    States
    {
      Spawn:
        H102 Q -1 Bright
        stop

      Ready:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"ReadyAmmo")
        TNT1 A 0 A_JumpIfInventory("Clip",3,"ReadyAmmo")
        goto ReadyNoAmmo

      ReadyNoAmmo:
        H101 A 1 A_WeaponReady
        goto Ready

      ReadyAmmo:
        H101 B 1 Bright A_WeaponReady
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("Clip",3,1)
        goto Ready
        H101 B 1 Bright A_WeaponReady
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("Clip",3,1)
        goto Ready
        H101 B 1 Bright A_WeaponReady
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("Clip",3,1)
        goto Ready
        H101 B 1 Bright A_WeaponReady
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("Clip",3,1)
        goto Ready
        H101 B 1 Bright A_WeaponReady
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("Clip",3,1)
        goto Ready
        H101 C 1 Bright A_WeaponReady
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("Clip",3,1)
        goto Ready
        H101 C 1 Bright A_WeaponReady
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("Clip",3,1)
        goto Ready
        H101 C 1 Bright A_WeaponReady
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("Clip",3,1)
        goto Ready
        H101 C 1 Bright A_WeaponReady
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("Clip",3,1)
        goto Ready
        H101 C 1 Bright A_WeaponReady
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("Clip",3,1)
        goto Ready
        H101 D 1 Bright A_WeaponReady
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("Clip",3,1)
        goto Ready
        H101 D 1 Bright A_WeaponReady
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("Clip",3,1)
        goto Ready
        H101 D 1 Bright A_WeaponReady
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("Clip",3,1)
        goto Ready
        H101 D 1 Bright A_WeaponReady
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("Clip",3,1)
        goto Ready
        H101 D 1 Bright A_WeaponReady
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("Clip",3,1)
        goto Ready
        H101 C 1 Bright A_WeaponReady
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("Clip",3,1)
        goto Ready
        H101 C 1 Bright A_WeaponReady
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("Clip",3,1)
        goto Ready
        H101 C 1 Bright A_WeaponReady
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("Clip",3,1)
        goto Ready
        H101 C 1 Bright A_WeaponReady
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("Clip",3,1)
        goto Ready
        H101 C 1 Bright A_WeaponReady
        goto Ready

      Deselect:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"DeselectAmmo")
        TNT1 A 0 A_JumpIfInventory("Clip",3,"DeselectAmmo")
        goto DeselectNoAmmo

      DeselectNoAmmo:
        H101 A 1 A_Lower
        goto Deselect

      DeselectAmmo:
        H101 B 1 Bright A_Lower
        goto Deselect

      Select:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"SelectAmmo")
        TNT1 A 0 A_JumpIfInventory("Clip",3,"SelectAmmo")
        goto SelectNoAmmo

      SelectNoAmmo:
        H101 A 1 A_Raise
        goto Select

      SelectAmmo:
        H101 B 1 Bright A_Raise
        goto Select

      Fire:
        TNT1 A 0 A_JumpIfCloser(128,"FireMelee")
        goto FireRanged

      FireMelee:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireMeleeAmmo")
        TNT1 A 0 A_JumpIfInventory("Clip",3,"FireMeleeAmmo")
        goto FireMeleeNoAmmo

      FireMeleeNoAmmo:
        H101 A 2 Offset(-32,34)
        H101 A 2 Offset(-64,38)
        goto FireMeleeContinue

      FireMeleeAmmo:
        H101 B 2 Bright Offset(-32,34)
        H101 B 2 Bright Offset(-64,38)
        goto FireMeleeContinue

      FireMeleeContinue:
        TNT1 A 0 A_GiveInventory("SamsaraHexenJavelinOfZealMeleeAttackHandler")
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireMeleeFinishAmmo")
        TNT1 A 0 A_JumpIfInventory("Clip",3,"FireMeleeFinishAmmo")
        goto FireMeleeFinishNoAmmo

      FireMeleeFinishNoAmmo:
        H101 A 2 Offset(-96,42)
        H101 A 2 Offset(-64,38)
        H101 A 2 Offset(-32,34)
        H101 "[" 8 Offset(-32,32) // offset hack via TEXTURES since 0 in either position (especially X) implies the default
        goto Ready

      FireMeleeFinishAmmo:
        H101 B 2 Bright Offset(-96,42)
        H101 B 2 Bright Offset(-64,38)
        H101 B 2 Bright Offset(-32,34)
        H101 "\" 8 Bright Offset(-32,32) // offset hack via TEXTURES since 0 in either position (especially X) implies the default
        goto Ready

      FireRanged:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireRangedAmmo")
        TNT1 A 0 A_JumpIfInventory("Clip",3,"FireRangedAmmo")
        goto FireRangedNoAmmo

      FireRangedNoAmmo:
        H201 UV 4
        H201 WX 3
        H201 Y 2 A_PlaySound("FighterHammerMiss",CHAN_WEAPON)
        goto FireRangedContinue

      FireRangedAmmo:
        H101 EF 4 Bright
        H101 GH 3 Bright
        H101 I 2 Bright A_PlaySound("FighterHammerMiss",CHAN_WEAPON)
        goto FireRangedContinue

      FireRangedContinue:
        H101 J 2 A_GiveInventory("SamsaraHexenJavelinOfZealRangedAttackHandler")
        H101 KLM 2
        TNT1 A 18
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireRangedFinishAmmo")
        TNT1 A 0 A_JumpIfInventory("Clip",3,"FireRangedFinishAmmo")
        goto FireRangedFinishNoAmmo

      FireRangedFinishNoAmmo:
        H101 A 3 Offset(0,92)
        H101 A 3 Offset(0,72)
        H101 A 3 Offset(0,52)
        goto Ready

      FireRangedFinishAmmo:
        H101 B 3 Bright Offset(0,92)
        H101 B 3 Bright Offset(0,72)
        H101 B 3 Bright Offset(0,52)
        goto Ready
    }
}

// Melee

actor SamsaraHexenJavelinOfZealMeleeAttackHandler : Trigger
{
    States
    {
      // Ammo checking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttackStrong")
        TNT1 A 0 A_JumpIfInventory("Clip",3,"PickupAttackStrong")
        goto PickupAttackWeak

      // Attack stuff
      // Weak
      PickupAttackWeak:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackWeakCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackWeakCoop")
      PickupAttackWeakDM:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasSpread",1,"PickupAttackWeakDMSpread")
        TNT1 A 0 A_CustomPunch(random(24,48),true,0,"HexenSpearPuff",128)
        stop

      PickupAttackWeakDMSpread:
        TNT1 A 0 A_CustomPunch(random(24,48)*3,true,0,"HexenSpearPuff",128)
        stop

      PickupAttackWeakCoop:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasSpread",1,"PickupAttackWeakCoopSpread")
        TNT1 A 0 A_CustomPunch(random(24,48),true,0,"HexenSpearPuffCoop",128)
        stop

      PickupAttackWeakCoopSpread:
        TNT1 A 0 A_CustomPunch(random(24,48)*3,true,0,"HexenSpearPuffCoop",128)
        stop

      // Strong
      PickupAttackStrong:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttackStrongInfinite")
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackStrongCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackStrongCoop")
      PickupAttackStrongDM:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasSpread",1,"PickupAttackStrongDMSpread")
        TNT1 A 0 A_CustomPunch(random(48,96),true,0,"HexenSpearPuffGlow",128)
        stop

      PickupAttackStrongDMSpread:
        TNT1 A 0 A_CustomPunch(random(48,96)*3,true,0,"HexenSpearPuffGlow",128)
        stop

      PickupAttackStrongCoop:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasSpread",1,"PickupAttackStrongCoopSpread")
        TNT1 A 0 A_CustomPunch(random(48,96),true,0,"HexenSpearPuffGlowCoop",128)
        stop

      PickupAttackStrongCoopSpread:
        TNT1 A 0 A_CustomPunch(random(48,96)*3,true,0,"HexenSpearPuffGlowCoop",128)
        stop

      PickupAttackStrongInfinite:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackStrongInfiniteCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackStrongInfiniteCoop")
      PickupAttackStrongInfiniteDM:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasSpread",1,"PickupAttackStrongInfiniteDMSpread")
        TNT1 A 0 A_CustomPunch(random(48,96),true,0,"HexenSpearPuffGlowInfinite",128)
        stop

      PickupAttackStrongInfiniteDMSpread:
        TNT1 A 0 A_CustomPunch(random(48,96)*3,true,0,"HexenSpearPuffGlowInfinite",128)
        stop

      PickupAttackStrongInfiniteCoop:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasSpread",1,"PickupAttackStrongInfiniteCoopSpread")
        TNT1 A 0 A_CustomPunch(random(48,96),true,0,"HexenSpearPuffGlowInfiniteCoop",128)
        stop

      PickupAttackStrongInfiniteCoopSpread:
        TNT1 A 0 A_CustomPunch(random(48,96)*3,true,0,"HexenSpearPuffGlowInfiniteCoop",128)
        stop
    }
}

// Weak

actor HexenSpearPuff : AxePuff {}
actor HexenSpearPuffCoop : HexenSpearPuff { +MTHRUSPECIES }

// Strong

actor HexenSpearPuffGlow : AxePuffGlow
{
    DamageType "TimonsAxe"
    +PUFFGETSOWNER
    States
    {
      Spawn:
      Crash:
        H101 TUVWXYZ 4 Bright
        stop

      Melee:
        TNT1 A 0 A_TakeInventory("Clip",3,0,AAPTR_TARGET)
        goto Spawn
    }
}

actor HexenSpearPuffGlowCoop : HexenSpearPuffGlow { +MTHRUSPECIES }

actor HexenSpearPuffGlowInfinite : HexenSpearPuffGlow
{
    States
    {
      Melee:
        goto Spawn
    }
}

actor HexenSpearPuffGlowInfiniteCoop : HexenSpearPuffGlowInfinite { +MTHRUSPECIES }

// Ranged

actor SamsaraHexenJavelinOfZealRangedAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttackStrong")
        TNT1 A 0 A_JumpIfInventory("Clip",3,1)
        goto PickupAttackWeak
        TNT1 A 0 A_TakeInventory("Clip",3)
        goto PickupAttackStrong

      // Attack stuff
      // Weak
      PickupAttackWeak:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackWeakCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackWeakCoop")
      PickupAttackWeakDM:
        TNT1 A 0 A_FireCustomMissile("Hexen_FSpearFX1",0,false,8,16)
        stop

      PickupAttackWeakCoop:
        TNT1 A 0 A_FireCustomMissile("Hexen_FSpearFX1Coop",0,false,8,16)
        stop

      // Strong
      PickupAttackStrong:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackStrongCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackStrongCoop")
      PickupAttackStrongDM:
        TNT1 A 0 A_FireCustomMissile("Hexen_FSpearFX2",0,false,8,16)
        stop

      PickupAttackStrongCoop:
        TNT1 A 0 A_FireCustomMissile("Hexen_FSpearFX2Coop",0,false,8,16)
        stop
    }
}

// Weak

actor Hexen_FSpearFX1
{
    Projectile
    Radius 6
    Height 6
    Speed 50
    Damage 10
    Gravity 0.33
    DeathSound "Hexen/weapons/spearhit"
    Obituary "%o was impaled by %k's throwing spears."
    +BLOODSPLATTER
    -NOGRAVITY
    States
    {
      Spawn:
        H101 R 1 A_SpawnItemEx("Hexen_FSpearFX3",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
        loop

      Death:
        TNT1 A 1 A_AlertMonsters
        stop
    }
}

actor Hexen_FSpearFX1Coop : Hexen_FSpearFX1
{
    Species "Player"
    +THRUSPECIES
}

actor Hexen_FSpearFX3
{
    Radius 2
    Height 2
    +CLIENTSIDEONLY
    +NOINTERACTION
    States
    {
      Spawn:
        H101 R 2
        H101 RRRR 2 A_FadeOut(0.2)
        stop
    }
}

// Strong

actor Hexen_FSpearFX2 : Hexen_FSpearFX1
{
    Radius 4
    Height 6
    Speed 90
    Damage 20
    States
    {
      Spawn:
        H101 S 1 Bright A_SpawnItemEx("Hexen_FSpearFX4",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
        loop

      Death:
        TNT1 A 0 A_AlertMonsters
        TNT1 A 1 A_SpawnItemEx("Hexen_FSpearFX5Spawner",0,0,0,0,0,0,0,SXF_CLIENTSIDE)
        stop
    }
}

actor Hexen_FSpearFX2Coop : Hexen_FSpearFX2
{
    Species "Player"
    +THRUSPECIES
}

actor Hexen_FSpearFX2Enemy : Hexen_FSpearFX2
{
    States
    {
      Death:
        TNT1 A 1 A_SpawnItemEx("Hexen_FSpearFX5Spawner",0,0,0,0,0,0,0,SXF_CLIENTSIDE)
        stop
    }
}

actor Hexen_FSpearFX4 : Hexen_FSpearFX3
{
    States
    {
      Spawn:
        H101 S 2 Bright
        H101 SSSS 2 Bright A_FadeOut(0.2)
        stop
    }
}

actor Hexen_FSpearFX5Spawner
{
    +CLIENTSIDEONLY
    +NOINTERACTION
    States
    {
      Spawn:
        TNT1 A 0
        TNT1 AAAAAAAAA 0 A_SpawnItemEx("Hexen_FSpearFX5",0,0,2,frandom(-4,4),frandom(-4,4),random(-2,6),0,SXF_NOCHECKPOSITION|SXF_CLIENTSIDE)
        TNT1 A 1
        stop
    }
}

actor Hexen_FSpearFX5
{
    Radius 2
    Height 2
    RenderStyle "Add"
    Alpha 0.85
    +CLIENTSIDEONLY
    +DONTSPLASH
    States
    {
      Spawn:
        TNT1 A 0
        TNT1 A 0 A_Jump(64,"Spawn2")
      Spawn1:
        H123 A 1 Bright
        H123 B 2 Bright
        H123 C 1 Bright
        H123 D 2 Bright
        H123 E 1 Bright
        H123 F 2 Bright
        H123 G 1 Bright
        stop

      Spawn2:
        H123 H 4 Bright
        H123 I 3 Bright
        H123 J 2 Bright
        TNT1 A 0 A_Jump(128,"Spawn1")
        stop
    }
}
