Actor Q2Blaster : Q2Weapon
{
	Weapon.SelectionOrder 900
	+INVENTORY.UNDROPPABLE
	Tag "Blaster"
	Weapon.SlotNumber 1
	Inventory.PickupMessage "Blaster"
	Inventory.RestrictedTo "Bitterman"	
	States
	{
	Spawn:
		Q2WP A -1
		Stop
	Select:
		TNT1 A 0 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		TNT1 A 0 A_TakeInventory(Q2WeaponIndex)
		BLS1 AB 5
		BLS1 CDE 4
		Goto Ready+1
	Ready:
		TNT1 A 0
		BLS2 AAA 6 A_WeaponReady
		TNT1 A 0 A_Jump(64,1)
		Loop
		BLS2 ABCDEFGHIJKL 6 A_WeaponReady
		BLS2 LLL 6 A_WeaponReady
		TNT1 A 0 A_Jump(64,1)
		Goto Ready+17
		BLS2 MNOPQRSTUV 6 A_WeaponReady
		Goto Ready+1
	Deselect:
		BLS2 A 3
		BLS1 JKL 4
		Goto InstantDeselect
	InstantDeselect:
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_Lower
		TNT1 A 1 A_Lower
		Wait
	Fire:
		TNT1 A 0 A_JumpIfInventory("CoopModeOn", 1, "CoopFire")
		TNT1 A 0 A_JumpIfInventory(PowerSilencer,1,"SilentFire")
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_PlaySound("Q2Weapons/BlasterFire",CHAN_WEAPON)
		TNT1 A 0 A_FireCustomMissile(BlasterProjectile,0,0,8,4)
//		TNT1 A 0 A_FireCustomMissile(WeaponFlash,0,0)
		TNT1 A 0 A_GunFlash
		BLS1 FGHI 4
		BLS2 A 2
		Goto Ready
	SilentFire:
		TNT1 A 0 A_PlaySound("Q2Weapons/BlasterFire",CHAN_WEAPON,0.25,0,3)
		TNT1 A 0 A_FireCustomMissile(BlasterProjectile,0,0,8,4)
//		TNT1 A 0 A_FireCustomMissile(WeaponFlash,0,0)
		TNT1 A 0 A_GunFlash
		BLS1 FGHI 4
		BLS2 A 2
		Goto Ready
	CoopFire:
		TNT1 A 0 A_JumpIfInventory(PowerSilencer,1,"SilentCoopFire")
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_PlaySound("Q2Weapons/BlasterFire",CHAN_WEAPON)
		TNT1 A 0 A_FireCustomMissile(BlasterProjectile_Coop,0,0,8,4)
//		TNT1 A 0 A_FireCustomMissile(WeaponFlash,0,0)
		TNT1 A 0 A_GunFlash
		BLS1 FGHI 4
		BLS2 A 2
		Goto Ready
	SilentCoopFire:
		TNT1 A 0 A_PlaySound("Q2Weapons/BlasterFire",CHAN_WEAPON,0.25,0,3)
		TNT1 A 0 A_FireCustomMissile(BlasterProjectile_Coop,0,0,8,4)
//		TNT1 A 0 A_FireCustomMissile(WeaponFlash,0,0)
		TNT1 A 0 A_GunFlash
		BLS1 FGHI 4
		BLS2 A 2
		Goto Ready
	Flash:
		TNT1 A 0 A_Light2
		TNT1 A 0 A_SetPitch(Pitch-3.0)
		TNT1 AAAAA 1 A_SetPitch(Pitch+0.2)
		TNT1 A 0 A_Light1
		TNT1 AAAAA 1 A_SetPitch(Pitch+0.2)
		TNT1 A 0 A_Light0
		TNT1 AAAAA 1 A_SetPitch(Pitch+0.2)
		Stop
	}
}
Actor BlasterProjectile : FastProjectile
{
	Radius 3
	Height 6
	Speed 32
	Damage 4
	MissileHeight 8
	MissileType BlasterTrailSpawner
	DeathSound "Q2Weapons/LaserHit"
	DamageType "BittermanDamage"
	+BLOODSPLATTER
	Decal BlasterScorch
	States
	{
	Spawn:
		NULL A 1 Light(BlasterProjectile) Bright A_PlaySound("Q2Weapons/LaserFly",CHAN_VOICE,0.5,1)
		Loop
	Death:
		TNT1 A 0 A_SpawnItem(BlasterProjectileImpact)
		TNT1 A 3 Light(BlasterProjectile_X1)
		TNT1 A 3 Light(BlasterProjectile_X2)
		TNT1 A 3 Light(BlasterProjectile_X3)
		TNT1 A 3 Light(BlasterProjectile_X4)
		Stop
	XDeath:
		TNT1 A 0
		Stop
	}
}
Actor BlasterProjectile_Coop : BlasterProjectile { Species "Player" +THRUSPECIES +MTHRUSPECIES }

Actor BlasterProjectileImpact : Q2EffectBase
{
	Height 6
	+BRIGHT
	RenderStyle Normal
	Translation "80:111=208:223"
	States
	{
	Spawn:
		TNT1 AAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx(BlasterImpactParticle,0,0,0,FRandom(0,1),0,FRandom(-0.25,3),Random(1,360),1)
		TNT1 A 0 A_CheckFloor("Floor")
		TNT1 A 0 A_CheckCeiling(1)
		Goto Air
		TNT1 A 0 A_SetScale(-1)
	Floor:
		NULL ABC 3
		NULL D 1 A_FadeOut(0.25)
		Wait
	Air:
		NULL EFG 3
		NULL H 1 A_FadeOut(0.25)
		Wait
	}
}
Actor BlasterTrailSpawner : Q2EffectBase
{
	Scale 0.66
	States
	{
	Spawn:
		TNT1 AA 0 A_SpawnItemEx(BlasterTrail,FRandom(0,2),0,FRandom(-2,2),FRandom(0,0.15),0,FRandom(-0.15,0.15),Random(1,360),128|SXF_TRANSFERSCALE)
		Stop
	}
}
Actor BlasterTrail : Q2BulletPuffParticle { Translation "80:111=208:223" Scale 0.75 }
Actor BlasterImpactParticle : BlasterTrail
{
	Projectile
	-NOINTERACTION -NOGRAVITY
	Gravity 0.125
	States
	{
	Fade:
		"----" A 10
		"----" A 1 A_FadeOut
		Wait
	}
}