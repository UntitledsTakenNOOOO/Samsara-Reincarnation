actor SamsaraDeusExMiniXBowAmmoType : Counter { Inventory.MaxAmount 2 }

Actor DeusEx_MiniCrossbow : DeusExWeapon
{
	Weapon.SelectionOrder 1600
	Weapon.SlotNumber 1
	Weapon.SlotPriority 3
	Weapon.AmmoType1 "DeusEx_MiniXBowMagazine"
	Weapon.AmmoType2 "Shell"
	Weapon.AmmoUse1 1
	Weapon.AmmoGive2 16
	Tag "Mini-Crossbow"
	Inventory.PickupMessage "You found a Mini-Crossbow."
	Obituary "%o was punctured with darts by %k."
	States
	{
		Spawn:
			DX99 A -1
			stop

		ReadyActual:
			DX00 A 0 A_JumpIfInventory("SamsaraReloadMode",2,"ReadyActualReloadable")
		ReadyActualNonReloadable:
			DX00 A 1 A_WeaponReady
			goto ReadyActualJump

		ReadyActualReloadable:
			DX00 A 1 A_WeaponReady(WRF_ALLOWRELOAD)
			goto ReadyActualJump

		ReadyActualJump:
			DX00 A 0 A_Jump(3,"ReadyActualIdle1","ReadyActualIdle2","ReadyActualIdle3")
			goto ReadyActualAmmoCheck

		ReadyActualIdle1:
			DX00 A 0 A_JumpIfInventory("SamsaraReloadMode",2,"ReadyActualIdle1Reloadable")
		ReadyActualIdle1NonReloadable:
			DX00 A 6 A_WeaponReady
			DX01 "VWYZ[\]" 6 A_WeaponReady
			goto ReadyActual

		ReadyActualIdle1Reloadable:
			DX00 A 6 A_WeaponReady(WRF_ALLOWRELOAD)
			DX01 "VWYZ[\]" 6 A_WeaponReady(WRF_ALLOWRELOAD)
			goto ReadyActual

		ReadyActualIdle2:
			DX00 A 0 A_JumpIfInventory("SamsaraReloadMode",2,"ReadyActualIdle2Reloadable")
		ReadyActualIdle2NonReloadable:
			DX00 A 6 A_WeaponReady
			DX02 ABCDEFGH 6 A_WeaponReady
			goto ReadyActual

		ReadyActualIdle2Reloadable:
			DX00 A 6 A_WeaponReady(WRF_ALLOWRELOAD)
			DX02 ABCDEFGH 6 A_WeaponReady(WRF_ALLOWRELOAD)
			goto ReadyActual

		ReadyActualIdle3:
			DX00 A 0 A_JumpIfInventory("SamsaraReloadMode",2,"ReadyActualIdle3Reloadable")
		ReadyActualIdle3NonReloadable:
			DX00 A 6 A_WeaponReady
			DX02 IJKLMNOPQRST 6 A_WeaponReady
			goto ReadyActual

		ReadyActualIdle3Reloadable:
			DX00 A 6 A_WeaponReady(WRF_ALLOWRELOAD)
			DX02 IJKLMNOPQRST 6 A_WeaponReady(WRF_ALLOWRELOAD)
			goto ReadyActual

		ReadyActualAmmoCheck:
			DX00 A 0 A_JumpIfInventory("SamsaraReloadMode",2,1)
			goto ReadyActual
			DX00 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"ReadyActual")
			DX00 A 0 A_JumpIfInventory("DeusEx_MiniXBowMagazine",1,"ReadyActual")
			goto ReloadCheck

		Deselect:
			DX01 P 0 A_GiveInventory("DeusEx_Reloading")
			DX01 P 2 A_JumpIfHealthLower(1,"DeathDeselect")
			DX01 QRSTU 2
			goto DeselectFinish

		DeathDeselect:
			TNT1 AAAAAA 2
			goto DeselectFinish

		DeselectFinish:
			TNT1 A 0 A_TakeInventory("DeusEx_MiniXBowEquipped")
			goto DeselectInstant

		Ready: // our "Select" state
			DX00 B 0 A_PlaySound("DeusEx/Crossbow/Select")
			DX00 B 2 A_GiveInventory("DeusEx_MiniXBowEquipped")
			DX00 CDEFGHIJKLM 2
			DX00 A 0 A_TakeInventory("DeusEx_Reloading")
			goto ReadyActual

		Fire:
			DX00 N 0 A_JumpIfInventory("SamsaraReloadMode",2,"FireReloadable")
		FireNonReloadable:
			DX00 N 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireStart")
			DX00 N 0 A_JumpIfInventory("Shell",1,"FireStart")
			goto FireDry

		FireReloadable:
			DX00 N 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"FireStart")
			DX00 N 0 A_JumpIfInventory("DeusEx_MiniXBowMagazine",1,"FireStart")
		FireReloadableDryCheck:
			DX00 N 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
			DX00 N 0 A_JumpIfInventory("Shell",1,1)
			goto FireDry
			DX01 A 0 A_TakeInventory("SamsaraDeusExSwappedAmmo")
			goto ReloadStart

		FireDry:
			DX00 A 0 A_PlaySound("DeusEx/DryFire",CHAN_6)
			DX00 A 0 A_ClearReFire
			DX00 A 4 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			goto ReadyActualAmmoCheck

		FireStart:
			DX00 N 0 A_GiveInventory("DeusEx_Firing")
			DX00 N 0 A_JumpIfInventory("DeusEx_PistolSkill",0,"FireContinueMaster")
			DX00 N 0 A_JumpIfInventory("DeusEx_PistolSkill",3,"FireContinueAdvanced")
			DX00 N 0 A_JumpIfInventory("DeusEx_PistolSkill",2,"FireContinueProficient")
			DX00 N 0 A_JumpIfInventory("DeusEx_PistolSkill",1,"FireContinueTrained")
		FireContinueUntrained:
			DX00 N 0 A_GiveInventory("SamsaraDeusExMiniXBowAttackHandler")
			DX00 "NOPQRSTUVWXYZ[\]" 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			goto ReadyActual

		FireContinueTrained:
			DX00 N 0 A_GiveInventory("SamsaraDeusExMiniXBowAttackHandler")
			DX00 NOPQRSTUVWX 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			DX00 "YZ[\]" 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			goto ReadyActual

		FireContinueProficient:
			DX00 N 0 A_GiveInventory("SamsaraDeusExMiniXBowAttackHandler")
			DX00 NOPQRSTU 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			DX00 "VWXYZ[\]" 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			goto ReadyActual

		FireContinueAdvanced:
			DX00 N 0 A_GiveInventory("SamsaraDeusExMiniXBowAttackHandler")
			DX00 NOPQR 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			DX00 "STUVWXYZ[\]" 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			goto ReadyActual

		FireContinueMaster:
			DX00 N 0 A_GiveInventory("SamsaraDeusExMiniXBowAttackHandler")
			DX00 N 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			DX00 "OPQRSTUVWXYZ[\]" 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			goto ReadyActual

		AltFire:
			DX01 A 0 A_JumpIfInventory("SamsaraReloadMode",2,"AltFireReloadable")
		AltFireNonReloadable:
			DX01 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"AltFireSwap")
			DX01 A 0 A_JumpIfInventory("Shell",1,"AltFireSwap")
			goto ReadyActual

		AltFireReloadable:
			DX01 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"AltFireSwap")
			DX01 A 0 A_JumpIfInventory("DeusEx_MiniXBowMagazine",1,"AltFireSwap")
		AltFireReloadableDryCheck:
			DX01 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
			DX01 A 0 A_JumpIfInventory("Shell",1,1)
			goto ReadyActual
			DX01 A 0 A_TakeInventory("SamsaraDeusExSwappedAmmo")
			goto ReloadStart

		AltFireSwap:
			DX01 A 0 A_GiveInventory("SamsaraDeusExSwappedAmmo")
			DX01 A 0 A_JumpIfInventory("SamsaraDeusExMiniXBowAmmoType",0,"AltFireSwapTranq")
			DX01 A 0 A_JumpIfInventory("SamsaraDeusExMiniXBowAmmoType",1,"AltFireSwapFlare")
		AltFireSwapDarts:
			DX01 A 0 A_GiveInventory("SamsaraDeusExMiniXBowAmmoType")
			DX01 A 0 A_Print("Mini-Crossbow now has Darts loaded.",2)
			goto ReloadStart

		AltFireSwapFlare:
			DX01 A 0 A_GiveInventory("SamsaraDeusExMiniXBowAmmoType")
			DX01 A 0 A_Print("Mini-Crossbow now has Flare Darts loaded.",2)
			goto ReloadStart

		AltFireSwapTranq:
			DX01 A 0 A_TakeInventory("SamsaraDeusExMiniXBowAmmoType")
			DX01 A 0 A_Print("Mini-Crossbow now has Tranquilizer Darts loaded.",2)
			goto ReloadStart

		Reload:
		ReloadCheck:
			DX00 A 0 A_JumpIfInventory("DeusEx_MiniXBowMagazine",0,"ReadyActual")
			DX00 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
			DX00 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
			DX00 A 0 A_JumpIfInventory("Shell",1,1)
			goto ReadyActual
			DX01 A 0 A_TakeInventory("SamsaraDeusExSwappedAmmo")
			goto ReloadStart

		ReloadStart:
			DX01 A 0 A_GiveInventory("DeusEx_Reloading")
			DX01 A 0 A_PlaySound("DeusEx/Crossbow/Reload1")
			DX01 ABCD 2 A_WeaponReady(WRF_NOFIRE)
			DX01 E 0 A_JumpIfInventory("SPModeOn",1,2)
			DX01 E 0 A_JumpIfInventory("CoopModeOn",1,1)
			goto ReloadContinueAdvanced
			DX01 E 0 A_JumpIfInventory("DeusEx_PistolSkill",0,"ReloadContinueMaster")
			DX01 E 0 A_JumpIfInventory("DeusEx_PistolSkill",3,"ReloadContinueAdvanced")
			DX01 E 0 A_JumpIfInventory("DeusEx_PistolSkill",2,"ReloadContinueProficient")
			DX01 E 0 A_JumpIfInventory("DeusEx_PistolSkill",1,"ReloadContinueTrained")
		ReloadContinueUntrained: // never called, but here just in case
			DX01 EFGHIJK 4 A_WeaponReady(WRF_NOFIRE)
		ReloadContinueTrained:
			DX01 EFGHIJK 4 A_WeaponReady(WRF_NOFIRE)
		ReloadContinueProficient:
			DX01 EFGHIJK 4 A_WeaponReady(WRF_NOFIRE)
		ReloadContinueAdvanced:
			DX01 EFGHIJK 4 A_WeaponReady(WRF_NOFIRE)
		ReloadContinueMaster:
			DX01 L 0 A_TakeInventory("DeusEx_Reloading")
			DX01 L 0 A_PlaySound("DeusEx/Crossbow/Reload2")
			DX01 LMNO 2 A_WeaponReady(WRF_NOFIRE)
			DX00 A 0 A_JumpIfInventory("SamsaraDeusExSwappedAmmo",1,"ReloadDone")
			DX00 A 0 A_JumpIfInventory("SamsaraReloadMode",2,"ReloadAmmo")
			goto ReloadDone

		ReloadDone:
			DX00 A 0
			goto ReadyActual

		ReloadAmmo:
			DX00 A 0 A_JumpIfInventory("DeusEx_MiniXBowMagazine",0,"ReloadDone")
			DX00 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
			DX00 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
			DX00 A 0 A_JumpIfInventory("Shell",1,1)
			goto ReloadDone
			DX00 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
			DX00 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
			DX00 A 0 A_TakeInventory("Shell",1)
			DX00 A 0 A_GiveInventory("DeusEx_MiniXBowMagazine")
			loop
	}
}

actor SamsaraDeusExMiniXBowAttackHandler : Trigger
{
	States
	{
	// Ammo checking/taking stuff
	Pickup:
		TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",2,"PickupReloadable")
	PickupNonReloadable:
		TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
		TNT1 A 0 A_JumpIfInventory("Shell",1,1)
		stop
		TNT1 A 0 A_TakeInventory("Shell",1)
		goto PickupAttack
	PickupReloadable:
		TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"PickupAttack")
		TNT1 A 0 A_JumpIfInventory("DeusEx_MiniXBowMagazine",1,1)
		stop
		TNT1 A 0 A_TakeInventory("DeusEx_MiniXBowMagazine",1)
		goto PickupAttack

	// Attack stuff
	PickupAttack:
		TNT1 A 0 A_PlaySound("DeusEx/Crossbow/Fire",CHAN_WEAPON)
		TNT1 A 0 A_JumpIfInventory("SamsaraDeusExMiniXBowAmmoType",0,"PickupAttackFlare")
		TNT1 A 0 A_JumpIfInventory("SamsaraDeusExMiniXBowAmmoType",1,"PickupAttackDarts")
	// Tranquilizer darts
	PickupAttackTranq:
		TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttackTranqInfinite")
		TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackTranqCoop")
		TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackTranqCoop")
	PickupAttackTranqDM:
		TNT1 A 0 A_FireCustomMissile("DeusEx_TranqDart",random(-2,2),false,5,12,0,random(-1,1))
		stop
	PickupAttackTranqCoop:
		TNT1 A 0 A_FireCustomMissile("DeusEx_TranqDartCoop",random(-2,2),false,5,12,0,random(-1,1))
		stop

	PickupAttackTranqInfinite:
		TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackTranqInfiniteCoop")
		TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackTranqInfiniteCoop")
	PickupAttackTranqInfiniteDM:
		TNT1 A 0 A_FireCustomMissile("DeusEx_TranqDartInfinite",random(-2,2),false,5,12,0,random(-1,1))
		stop
	PickupAttackTranqInfiniteCoop:
		TNT1 A 0 A_FireCustomMissile("DeusEx_TranqDartInfiniteCoop",random(-2,2),false,5,12,0,random(-1,1))
		stop

	// Darts
	PickupAttackDarts:
		TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttackDartsInfinite")
		TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackDartsCoop")
		TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackDartsCoop")
	PickupAttackDartsDM:
		TNT1 A 0 A_FireCustomMissile("DeusEx_Dart",random(-2,2),false,5,12,0,random(-1,1))
		stop
	PickupAttackDartsCoop:
		TNT1 A 0 A_FireCustomMissile("DeusEx_DartCoop",random(-2,2),false,5,12,0,random(-1,1))
		stop

	PickupAttackDartsInfinite:
		TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackDartsInfiniteCoop")
		TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackDartsInfiniteCoop")
	PickupAttackDartsInfiniteDM:
		TNT1 A 0 A_FireCustomMissile("DeusEx_DartInfinite",random(-2,2),false,5,12,0,random(-1,1))
		stop
	PickupAttackDartsInfiniteCoop:
		TNT1 A 0 A_FireCustomMissile("DeusEx_DartInfiniteCoop",random(-2,2),false,5,12,0,random(-1,1))
		stop

	// Flare darts
	PickupAttackFlare:
		TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttackFlareInfinite")
		TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackFlareCoop")
		TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackFlareCoop")
	PickupAttackFlareDM:
		TNT1 A 0 A_FireCustomMissile("DeusEx_FlareDart",random(-2,2),false,5,12,0,random(-1,1))
		stop
	PickupAttackFlareCoop:
		TNT1 A 0 A_FireCustomMissile("DeusEx_FlareDartCoop",random(-2,2),false,5,12,0,random(-1,1))
		stop

	PickupAttackFlareInfinite:
		TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackFlareInfiniteCoop")
		TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackFlareInfiniteCoop")
	PickupAttackFlareInfiniteDM:
		TNT1 A 0 A_FireCustomMissile("DeusEx_FlareDartInfinite",random(-2,2),false,5,12,0,random(-1,1))
		stop
	PickupAttackFlareInfiniteCoop:
		TNT1 A 0 A_FireCustomMissile("DeusEx_FlareDartInfiniteCoop",random(-2,2),false,5,12,0,random(-1,1))
		stop
	}
}

Actor DeusEx_MiniXBowMagazine : Ammo
{
	Inventory.MaxAmount 6 // was 4
	Tag "Mini-Crossbow Magazine"
	+IGNORESKILL
}

Actor DeusEx_XBowSpread : Ammo
{
	Inventory.MaxAmount 40
	Inventory.InterHubAmount 0
	+IGNORESKILL
	+UNDROPPABLE
}

Actor DeusEx_Dart : DeusExBullet
{
	Speed 75
	Damage(CallACS("DeusEx_WeaponSkillDamage", 1, 20))
	DamageType "DeusExPistol"
	RenderStyle Normal
	-DONTBLAST
	-NOTIMEFREEZE
	+THRUGHOST
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 A 0 ACS_NamedExecuteWithResult("DeusEx_ProjectilePitch")
			TNT1 A 0 A_GiveInventory("DeusEx_PointBlank",1)
			TNT1 A 0 A_SetUserVar(user_damage, CallACS("DeusEx_WeaponSkillDamage", 1, 20))
		SpawnLoop:
			DX00 A 1
			Loop
		Death:
			TNT1 A 0 A_JumpIfInventory("DeusEx_PointBlank",1,2)
			TNT1 A 0 ACS_NamedExecuteWithResult("DeusEx_ProjectilePitch")
			TNT1 A 0 A_PlaySound("DeusEx/Bullet/HitFlesh", CHAN_BODY)
			TNT1 A 0 A_AlertMonsters(64)
			DX00 A 1 A_ChangeFlag("NOTIMEFREEZE",true) //A_SpawnItemEx("BulletPuff",0,0,0,0,0,1,0,0)
			Goto DeathLoop
		DeathLoop:
			DX00 A 1 A_JumpIfInTargetInventory("DeusExClass",1,1)
			loop
			DX00 A 0 A_JumpIfInTargetInventory("Shell",0,2)
			DX00 A 0 A_JumpIfCloser(48,"TakeShell")
			DX00 A 1
			loop
		TakeShell:
			TNT1 A 2 A_GiveInventory("SamsaraDeusExMiniXBowDartPickupAmmoGiver",1,AAPTR_TARGET)
			stop
		Crash:
			TNT1 A 0 A_PlaySound("DeusEx/Bullet/HitRobot", CHAN_BODY)
			TNT1 A 1 A_Explode(CallACS("DeusEx_DamageModifiers",CallACS("DeusEx_WeaponSkillDamage", 1, 20)),8,!XF_HURTSOURCE,0,8) //was 15
			stop
		XDeath:
			TNT1 A 0 A_PlaySound("DeusEx/Bullet/HitFlesh", CHAN_BODY)
			TNT1 A 0 A_Explode(CallACS("DeusEx_DamageModifiers",CallACS("DeusEx_WeaponSkillDamage", 1, 20)),8,!XF_HURTSOURCE,0,8)
			TNT1 A 0 ACS_NamedExecuteWithResult("DeusEx_BloodGenerator_Offsets")
			TNT1 A 1 A_GiveInventory("DeusEx_BloodGenerator",1,AAPTR_TRACER)
			stop
	}
}

Actor DeusEx_DartCoop : DeusEx_Dart { Species "Player" +THRUSPECIES }

Actor DeusEx_DartInfinite : DeusEx_Dart
{
	States
	{
		Death:
			TNT1 A 0 A_JumpIfInventory("DeusEx_PointBlank",1,2)
			TNT1 A 0 ACS_NamedExecuteWithResult("DeusEx_ProjectilePitch")
			TNT1 A 0 A_PlaySound("DeusEx/Bullet/HitFlesh", CHAN_BODY)
			TNT1 A 0 A_AlertMonsters(64)
			DX00 A 1 //A_SpawnItemEx("BulletPuff",0,0,0,0,0,1,0,0)
		DeathLoop:
			DX00 A 1 A_FadeOut(0.04)
			loop
	}
}

Actor DeusEx_DartInfiniteCoop : DeusEx_DartInfinite { Species "Player" +THRUSPECIES }

Actor DeusEx_TranqDart : DeusEx_Dart
{
	DamageType "DeusExKnockedOut"
	Damage (CallACS("DeusEx_WeaponSkillDamage", 1, 8))
	PoisonDamageType "DeusExPoisonEffect"
	PoisonDamage 8, 16, 70 //can't scale up poison damage =(
	+ADDITIVEPOISONDURATION
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 A 0 ACS_NamedExecuteWithResult("DeusEx_ProjectilePitch")
			TNT1 A 0 A_GiveInventory("DeusEx_PointBlank",1)
		SpawnLoop:
			DX00 A 1
			Loop
		Death:
			TNT1 A 0 A_JumpIfInventory("DeusEx_PointBlank",1,2)
			TNT1 A 0 ACS_NamedExecuteWithResult("DeusEx_ProjectilePitch")
			TNT1 A 0 A_PlaySound("DeusEx/Bullet/HitFlesh", CHAN_BODY)
			TNT1 A 0 A_AlertMonsters(64)
			DX00 A 1 A_ChangeFlag("NOTIMEFREEZE",true) //A_SpawnItemEx("BulletPuff",0,0,0,0,0,1,0,0)
			Goto DeathLoop
		DeathLoop:
			DX00 A 1 A_JumpIfInTargetInventory("DeusExClass",1,1)
			loop
			DX00 A 0 A_JumpIfInTargetInventory("Shell",0,2)
			DX00 A 0 A_JumpIfCloser(48,"TakeShell")
			DX00 A 1
			loop
		TakeShell:
			TNT1 A 2 A_GiveInventory("SamsaraDeusExMiniXBowDartPickupAmmoGiver",1,AAPTR_TARGET)
			stop
		Crash:
			TNT1 A 0 A_PlaySound("DeusEx/Bullet/HitRobot", CHAN_BODY)
			TNT1 A 1 A_Explode(CallACS("DeusEx_DamageModifiers",CallACS("DeusEx_WeaponSkillDamage", 1, 8)),8,!XF_HURTSOURCE,0,8) //was 5
			stop
		XDeath:
			TNT1 A 0 A_PlaySound("DeusEx/Bullet/HitFlesh", CHAN_BODY)
			TNT1 A 0 A_Explode(CallACS("DeusEx_DamageModifiers",CallACS("DeusEx_WeaponSkillDamage", 1, 8)),8,!XF_HURTSOURCE,0,8) // was 5
			TNT1 A 0 ACS_NamedExecuteWithResult("DeusEx_BloodGenerator_Offsets")
			TNT1 A 1 A_GiveInventory("DeusEx_BloodGenerator",1,AAPTR_TRACER)
			stop
	}
}

Actor DeusEx_TranqDartCoop : DeusEx_TranqDart { Species "Player" +THRUSPECIES }

Actor DeusEx_TranqDartInfinite : DeusEx_TranqDart
{
	States
	{
		Death:
			TNT1 A 0 A_JumpIfInventory("DeusEx_PointBlank",1,2)
			TNT1 A 0 ACS_NamedExecuteWithResult("DeusEx_ProjectilePitch")
			TNT1 A 0 A_PlaySound("DeusEx/Bullet/HitFlesh", CHAN_BODY)
			TNT1 A 0 A_AlertMonsters(64)
			DX00 A 1 //A_SpawnItemEx("BulletPuff",0,0,0,0,0,1,0,0)
		DeathLoop:
			DX00 A 1 A_FadeOut(0.04)
			loop
	}
}

Actor DeusEx_TranqDartInfiniteCoop : DeusEx_TranqDartInfinite { Species "Player" +THRUSPECIES }

actor DeusEx_FlareDartTimer : Counter { Inventory.MaxAmount 20 }
actor DeusEx_FlareDartSubTimer : Counter { Inventory.MaxAmount 35 }

Actor DeusEx_FlareDart : DeusEx_Dart
{
	DamageType "DeusExPistolFire"
	Damage (CallACS("DeusEx_WeaponSkillDamage", 1, 5))
	PoisonDamageType "DeusExPistolFire"
	PoisonDamage 5, 5, 12 //can't scale up poison damage =(
	ReactionTime 60
	+ADDITIVEPOISONDURATION
	+BRIGHT
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 A 0 ACS_NamedExecuteWithResult("DeusEx_ProjectilePitch")
			TNT1 A 0 A_GiveInventory("DeusEx_PointBlank",1)
		SpawnLoop:
			DX00 A 1
			Loop
		Death:
			TNT1 A 0 A_JumpIfInventory("DeusEx_PointBlank",1,2)
			TNT1 A 0 ACS_NamedExecuteWithResult("DeusEx_ProjectilePitch")
			TNT1 A 0 A_PlaySound("DeusEx/Bullet/HitFlesh", CHAN_BODY)
			TNT1 A 0 A_AlertMonsters(64)
			DX00 A 1 A_ChangeFlag("NOTIMEFREEZE",true) //A_SpawnItemEx("BulletPuff",0,0,0,0,0,1,0,0)
			Goto DeathLoop
		DeathLoop:
			/*DX00 A 0 A_GiveInventory("DeusEx_FlareDartSubTimer")
			DX00 A 0 A_JumpIfInventory("DeusEx_FlareDartSubTimer",0,"GiveTimer")*/
			DX00 A 1 A_JumpIfInTargetInventory("DeusExClass",1,1)
			loop
			/*DX00 A 0 A_GiveInventory("DeusEx_FlareDartSubTimer")
			DX00 A 0 A_JumpIfInventory("DeusEx_FlareDartSubTimer",0,"GiveTimer")*/
			DX00 A 0 A_JumpIfInTargetInventory("Shell",0,2)
			DX00 A 0 A_JumpIfCloser(48,"TakeShell")
			DX00 A 1
			loop
		GiveTimer:
			DX00 A 0 A_TakeInventory("DeusEx_FlareDartSubTimer", 35)
			DX00 A 0 A_GiveInventory("DeusEx_FlareDartTimer", 1)
			DX00 A 0 A_JumpIfInventory("DeusEx_FlareDartTimer", 0, "Fade")
			Goto DeathLoop
		Fade:
			DX00 AAAAA 1 A_FadeOut(0.2)
			Stop
		TakeShell:
			TNT1 A 2 A_GiveInventory("SamsaraDeusExMiniXBowDartPickupAmmoGiver",1,AAPTR_TARGET)
			stop
		Crash:
			TNT1 A 0 A_PlaySound("DeusEx/Bullet/HitRobot", CHAN_BODY)
			TNT1 A 1 A_Explode(CallACS("DeusEx_DamageModifiers",CallACS("DeusEx_WeaponSkillDamage", 1, 5)),8,!XF_HURTSOURCE,0,8) //was 5
			stop
		XDeath:
			TNT1 A 0 A_PlaySound("DeusEx/Bullet/HitFlesh", CHAN_BODY)
			TNT1 A 0 A_Explode(CallACS("DeusEx_DamageModifiers",CallACS("DeusEx_WeaponSkillDamage", 1, 5)),8,!XF_HURTSOURCE,0,8) // was 5
			TNT1 A 0 ACS_NamedExecuteWithResult("DeusEx_BloodGenerator_Offsets")
			TNT1 A 1 A_GiveInventory("DeusEx_BloodGenerator",1,AAPTR_TRACER)
			stop
	}
}

Actor DeusEx_FlareDartCoop : DeusEx_FlareDart { Species "Player" +THRUSPECIES }

Actor DeusEx_FlareDartInfinite : DeusEx_FlareDart
{
	States
	{
		Death:
			TNT1 A 0 A_JumpIfInventory("DeusEx_PointBlank",1,2)
			TNT1 A 0 ACS_NamedExecuteWithResult("DeusEx_ProjectilePitch")
			TNT1 A 0 A_PlaySound("DeusEx/Bullet/HitFlesh", CHAN_BODY)
			TNT1 A 0 A_AlertMonsters(64)
			DX00 A 1 //A_SpawnItemEx("BulletPuff",0,0,0,0,0,1,0,0)
		DeathLoop:
			/*DX00 A 0 A_GiveInventory("DeusEx_FlareDartSubTimer")
			DX00 A 0 A_JumpIfInventory("DeusEx_FlareDartSubTimer",0,"GiveTimer")*/
			DX00 A 1 A_FadeOut(0.04)
			loop
	}
}

Actor DeusEx_FlareDartInfiniteCoop : DeusEx_FlareDartInfinite { Species "Player" +THRUSPECIES }

actor SamsaraDeusExMiniXBowDartPickupAmmoGiver : Trigger
{
	States
	{
	Pickup:
		TNT1 A 0 A_PlaySound("DeusEx/WeaponPickup",CHAN_ITEM|CHAN_MAYBE_LOCAL)
		TNT1 A 0 A_GiveInventory("Ammo2IgnoreSkill",1)
		stop
	}
}
