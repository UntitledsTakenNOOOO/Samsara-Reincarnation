Actor "Battle Axe" : WTWeapon
{
	Weapon.Slotnumber 3
	Weapon.SelectionOrder 3000
	Weapon.BobStyle Alpha
	Weapon.BobRangeX 0.2
	Weapon.BobRangeY 0.6
	Weapon.BobSpeed 0.8	
	+WEAPON.MELEEWEAPON
	+WEAPON.NOALERT //The buzz sound alerts the monsters
	+WEAPON.AMMO_OPTIONAL
	+THRUGHOST
	Weapon.Ammotype "Shell"
	Weapon.AmmoUse 1
	Weapon.AmmoGive 15
    Inventory.PickupSound "gpickup/gen"
    Inventory.PickupMessage "You got the Battle Axe!"
	Inventory.RestrictedTo "GrondovalPlayer"	
	States
	{
	Spawn:
		PWHB A -1
		stop
	Select:
		TNT1 A 0 A_TakeInventory("WTWeaponActive",1)
		TNT1 A 0 A_TakeInventory("WTFistActive",1)
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_Raise
		TNT1 A 1 A_Raise
		loop
	Deselect:
		WW20 L 0 A_TakeInventory("WTSlotIIClip",999)
		WW20 L 1 A_Lower
		loop
	
	Ready: //No shield
		WW20 A 0 A_JumpIfInventory("Shell",1,"MegaDraw")
	Draw:
		WW20 ABCDEFGHIJK 4 A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
		goto WIdle
	MegaDraw:
		WW22 A 0 A_PlayWeaponSound("ggen/buzzloop")
		WW22 ABCDEFGHIJK 3 bright A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
		goto MegaWIdle
		
	WIdle:
		WW20 L 0 A_JumpIfInventory("WTSlotIIClip",1,2)
		WW20 L 0 A_JumpIfInventory("Shell",1,"MegaWIdle") //A_JumpIfNoAmmo
		WW20 L 2 A_WeaponReady
		loop
	MegaWIdle:
		WW22 A 0 A_PlayWeaponSound("ggen/buzzloop")
		WW22 LMNOPQ 3 bright A_WeaponReady
		WW22 L 0 A_JumpIfInventory("Shell",1,"MegaWIdle")
		WW22 H 0 A_PlayWeaponSound("DSNONE")
		goto WIdle
	
	Altfire:
		WW20 L 0 A_JumpIfInventory("WTSlotIIClip",1,"TakeClip")
	GiveClip:
		WW22 H 0 A_PlayWeaponSound("DSNONE")
		WW20 L 12 A_GiveInventory("WTSlotIIClip",1)
		goto WIdle
	TakeClip:
		WW20 L 0 A_TakeInventory("WTSlotIIClip",999)
		WW20 L 12 A_JumpIfInventory("Shell",1,1)
		goto WIdle
		WW22 A 0 A_PlayWeaponSound("ggen/buzzloop")
		WW22 LMNO 3 bright
		goto MegaWIdle
	/*Altfire:
		WW10 H 0 A_JumpIfInventory("Shell",1,"MegaAlt")
	AltNormal:
		WW20 L 1 A_ChangeFlag("PICKUP",0)
		TNT1 A 0 ACS_ExecuteAlways(707,0,1) //Throws potion if you got
		WW20 L 18
		WW00 A 0 A_ChangeFlag("PICKUP",1)
		goto WIdle
	MegaAlt:
		WW22 L 1 bright A_ChangeFlag("PICKUP",0)
		TNT1 A 0 ACS_ExecuteAlways(707,0,1) //Throws potion if you got
		WW22 LMNOPQLMN 3 bright
		WW00 A 0 A_ChangeFlag("PICKUP",1)
		goto MegaWIdle*/
		
	Fire:
		WW20 L 0 A_JumpIfInventory("WTSlotIIClip",1,"NFire")
		WW20 A 0 A_JumpIfInventory("Shell",1,"MegaFire") //A_JumpIfNoAmmo
		//goto MegaFire
	NFire:
		WW20 A 0 A_JumpIfInventory("WTAttackClip",1,2)
		WW20 A 0 A_GiveInventory("WTAttackClip",1)
		goto Attack1
		WW20 A 0 A_TakeInventory("WTAttackClip",1)
		goto Attack2
	MegaFire:
		WW22 A 0 A_JumpIfInventory("WTAttackClip",1,2)
		WW22 A 0 A_GiveInventory("WTAttackClip",1)
		goto MegaAttack1
		WW22 A 0 A_TakeInventory("WTAttackClip",1)
		goto MegaAttack2
		
	Attack1: 
		TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"CoopAttack1")
		WW20 MNOPQR 3
		WW20 S 3 A_PlayWeaponSound("ggen/swing4")
		WW20 T 3
		WW20 U 3 A_FireCustomMissile("WTBattleAxeProjectile",0,0,4,7)
		WW20 V 3
		TNT1 A 6
		WW20 J 2 offset(0,37) A_Refire
		WW20 JK 2 offset(0,32) A_Refire
		goto WIdle
	CoopAttack1: 
		WW20 MNOPQR 3
		WW20 S 3 A_PlayWeaponSound("ggen/swing4")
		WW20 T 3
		WW20 U 3 A_FireCustomMissile("WTBattleAxeProjectile_2",0,0,4,7)
		WW20 V 3
		TNT1 A 6
		WW20 J 2 offset(0,37) A_Refire
		WW20 JK 2 offset(0,32) A_Refire
		goto WIdle		
	Attack2:
		TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"CoopAttack2")	
		WW20 WXYZ 3
		WW21 A 3 A_PlayWeaponSound("ggen/swing4")
		WW21 B 3
		WW21 C 3 A_FireCustomMissile("WTBattleAxeProjectile_Weak",0,0,2,7)
		WW21 D 3
		TNT1 A 6
		WW20 J 2 offset(0,37) A_Refire
		WW20 JK 2 offset(0,32) A_Refire
		goto WIdle
	CoopAttack2:
		WW20 WXYZ 3
		WW21 A 3 A_PlayWeaponSound("ggen/swing4")
		WW21 B 3
		WW21 C 3 A_FireCustomMissile("WTBattleAxeProjectile_Weak_2",0,0,2,7)
		WW21 D 3
		TNT1 A 6
		WW20 J 2 offset(0,37) A_Refire
		WW20 JK 2 offset(0,32) A_Refire
		goto WIdle
		
	MegaAttack1:
		TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"CoopMegaAttack1")		
		TNT1 A 0 A_AlertMonsters		
		WW22 RSTUV 2 bright
		WW22 W 2 bright A_PlayWeaponSound("ggen/insect")
		WW22 X 2 bright
		WW22 Y 2 bright A_FireCustomMissile("WTMegaBattleAxeProjectile",0,0,4,9)
		WW22 Z 2 bright
		TNT1 A 4
		WW20 J 2 bright offset(0,37) A_Refire
		WW20 JK 2 bright offset(0,32) A_Refire
		goto MegaWIdle
	CoopMegaAttack1:
		TNT1 A 0 A_AlertMonsters	
		WW22 RSTUV 2 bright
		WW22 W 2 bright A_PlayWeaponSound("ggen/insect")
		WW22 X 2 bright
		WW22 Y 2 bright A_FireCustomMissile("WTMegaBattleAxeProjectile_2",0,0,4,9)
		WW22 Z 2 bright
		TNT1 A 4
		WW20 J 2 bright offset(0,37) A_Refire
		WW20 JK 2 bright offset(0,32) A_Refire
		goto MegaWIdle		
	MegaAttack2:
		TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"CoopMegaAttack2")
		TNT1 A 0 A_AlertMonsters		
		WW23 ABCD 2 bright
		WW23 E 2 bright A_PlayWeaponSound("ggen/insect")
		WW23 F 2 bright
		WW23 G 2 bright A_FireCustomMissile("WTMegaBattleAxeProjectile_Weak",0,0,4,9)
		WW23 H 2 bright
		TNT1 A 4
		WW20 J 2 bright offset(0,37) A_Refire
		WW20 JK 2 bright offset(0,32) A_Refire
		goto MegaWIdle
	CoopMegaAttack2:
		TNT1 A 0 A_AlertMonsters	
		WW23 ABCD 2 bright
		WW23 E 2 bright A_PlayWeaponSound("ggen/insect")
		WW23 F 2 bright
		WW23 G 2 bright A_FireCustomMissile("WTMegaBattleAxeProjectile_Weak_2",0,0,4,9)
		WW23 H 2 bright
		TNT1 A 4
		WW20 J 2 bright offset(0,37) A_Refire
		WW20 JK 2 bright offset(0,32) A_Refire
		goto MegaWIdle		
	}
}

Actor WTBattleaxeAmmoDrain : WTACSTrigger
{
	states
	{
	//Pickup:
		//stop
	Use:
		TNT1 A 1 A_TakeInventory("Shell",1,TIF_NOTAKEINFINITE)
		stop
	}
}

Actor WTBattleaxeProjectile : WTMegaMeleeProjectile
{
	Speed 96
	Damage (random(75,90))
	Obituary "$SAMSARA_WITCHAVEN_OB_SLOT_II"
    +THRUGHOST
}

Actor WTBattleaxeProjectile_2 : WTBattleaxeProjectile { Species "Player" +THRUSPECIES +DONTHARMCLASS +DONTHARMSPECIES +MTHRUSPECIES +FORCEPAIN }

Actor WTBattleaxeProjectile_Weak : WTMegaMeleeProjectile
{
	Speed 96
	Damage (random(60,75))
	Obituary "$SAMSARA_WITCHAVEN_OB_SLOT_II"
    +THRUGHOST
}

Actor WTBattleaxeProjectile_Weak_2 : WTBattleaxeProjectile_Weak { Species "Player" +THRUSPECIES +DONTHARMCLASS +DONTHARMSPECIES +MTHRUSPECIES +FORCEPAIN }

Actor WTMegaBattleaxeProjectile : WTMegaMeleeProjectile
{
	Speed 96
	Damage (random(150,180))
	Obituary "$SAMSARA_WITCHAVEN_OB_SLOT_II_MEGA"
    +THRUGHOST
	states
	{
	Crash:
		TNT1 A 0 A_GiveToTarget("WTBattleaxeAmmoDrain",1)
		goto Super::Crash
	XDeath:
		TNT1 A 0 A_GiveToTarget("WTBattleaxeAmmoDrain",1)
		goto Super::XDeath
	}
}

Actor WTMegaBattleaxeProjectile_2 : WTMegaBattleaxeProjectile { Species "Player" +THRUSPECIES +DONTHARMCLASS +DONTHARMSPECIES +MTHRUSPECIES +FORCEPAIN }


Actor WTMegaBattleaxeProjectile_Weak : WTMegaMeleeProjectile
{
	Speed 96
	Damage (random(120,150))
	Obituary "$SAMSARA_WITCHAVEN_OB_SLOT_II_MEGA"
    +THRUGHOST
	states
	{
	Crash:
		TNT1 A 0 A_GiveToTarget("WTBattleaxeAmmoDrain",1)
		goto Super::Crash
	XDeath:
		TNT1 A 0 A_GiveToTarget("WTBattleaxeAmmoDrain",1)
		goto Super::XDeath
	}
}

Actor WTMegaBattleaxeProjectile_Weak_2 : WTMegaBattleaxeProjectile_Weak { Species "Player" +THRUSPECIES +DONTHARMCLASS +DONTHARMSPECIES +MTHRUSPECIES +FORCEPAIN }
