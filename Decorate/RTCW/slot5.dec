Actor RTCW_Panzerfaust : RTCW_Weapon
{
	Weapon.SlotNumber 5
	Weapon.SelectionOrder 1500
	Weapon.AmmoType2 "RocketAmmo"
	Weapon.AmmoType1 "RTCW_PanzerfaustMagazine"
	Weapon.AmmoUse1 1
	Weapon.AmmoGive1 1
	Weapon.AmmoGive2 3
	Obituary "%o was killed (rl) by %k."
	Inventory.Pickupmessage "Panzerfaust"
	Inventory.PickupSound "RTCW/WeaponPickup"
	Tag "Panzerfaust"
	+WEAPON.AMMO_OPTIONAL
	States
	{
		Spawn:
			RW98 A -1
			Stop
		Deselect:
			RW00 V 0 A_PlaySound("RTCW/Change",CHAN_7)
			RW00 V 0 A_Gunflash("DeselectFlash")
			RW00 "VWXYZ[\]" 2
			RW01 ABCDEFGHIJKL 2
			Goto Super::Deselect
		DeselectFlash:
			RW05 "VWXYZ[\]" 2
			RW06 ABCDEFGHIJKL 2
			Stop
		Ready:	
			RW00 A 0 A_JumpIfInventory("RTCW_PanzerfaustMagazine",1,2)
			RW00 A 0 A_JumpIfInventory("RocketAmmo",1,"LoadPanzerfaust")
			Goto DeselectNoAmmo
			RW00 A 0 A_TakeInventory("RTCW_WeaponToken",256)
			RW00 A 0 A_GiveInventory("RTCW_WeaponToken",13)
			RW00 A 0 A_Gunflash("ReadyFlash")
			RW01 MNOPQRSTU 2
			RW02 AB 2
		ReadyLoop:
			RW02 C 0 A_JumpIfInventory("RTCW_Kicking",1,"Kicking")
			RW02 C 0 A_Gunflash("LoopFlash")
			RW02 C 1 A_WeaponReady
			Loop
		ReadyFlash:
			RW06 MNOPQRSTU 2
			RW07 AB 2
			Stop
		LoopFlash:
			RW07 C 2
			Stop
		Fire:
			RW00 A 0 A_GiveInventory("RTCW_FiringToken",1)
			RW00 A 0 A_GiveInventory("RTCW_BlazkoWeaponAlert",1)
			RW00 A 0 A_FireCustomMissile("RTCW_PanzerfaustShell",-random(85,105),0,8,4,0,-4)
			RW00 A 0 A_Quake(3,15,0,128)
			RW00 A 0 A_PlaySound("RTCW/Panzerfaust/Fire",CHAN_WEAPON)
			RW00 A 0 A_JumpIfInventory("CoopModeOn",1,2)
			RW00 A 0 A_FireCustomMissile("RTCW_PanzerfaustRocket",0,1,8)
			Goto FireContinue
			RW00 A 0 A_FireCustomMissile("RTCW_PanzerfaustRocketCoop",0,1,8)
			Goto FireContinue
		FireContinue:
			RW00 A 0 A_JumpIfInventory("RocketAmmo",1,1)
			Goto FireLast
			RW00 A 0 A_Gunflash("FireFlash")
		LoadPanzerfaust:
			RW00 ABCDEFGHIJKLMNOP 2
			RW00 P 0
			RW01 A 0 A_Gunflash("ReloadFlash")
			RW01 "GHIJKLMNOPQRSTUVWXYZ[\]" 2
			RW02 AB 2
			RW02 A 0 A_GiveInventory("RTCW_PanzerfaustMagazine",1)
			RW02 A 0 A_TakeInventory("RocketAmmo",1,TIF_NOTAKEINFINITE)
			Goto ReadyLoop	
		FireLast:
			RW02 C 0 A_Gunflash("FireFlashLast")
			RW02 C 0 A_PlaySound("RTCW/NoAmmo",CHAN_5)
		DeselectNoAmmo:
			RW02 A 0 A_PlaySound("RTCW/Change",CHAN_7)
			RW02 CDEFGHIJKL 2
			TNT1 "MNOPQRSTUVWXYZ[\]" 2
			TNT1 ABCDEFGHIJKLM 2
			TNT1 A 0 A_SelectWeapon("RTCW_Knife")
			Goto Super::Deselect
		FireFlash:
			RW04 A 2 Bright
			Stop
		FireFlashLast:
			RW04 B 2 Bright
			Stop
		ReloadFlash:
			RW06 "GHIJKLMNOPQRSTUVWXYZ[\]" 2
			RW07 AB 2
			Stop
		Kicking:
			TNT1 A 0 A_Gunflash("KickingFoot")
		KickingDelay:
			RW09 A 18
			RW09 A 0 A_TakeInventory("RTCW_Kicking",1)
			Goto ReadyLoop
	}
}

Actor RTCW_PanzerfaustMagazine : Ammo
{
	Inventory.MaxAmount 1
	Ammo.BackpackMaxAmount 1
}

//Was 100 impact 120 splash, now 240 splash, 200 impact
Actor RTCW_PanzerfaustRocket
{
	Height 10
	Radius 5
	Damage (250)
	Speed 32
	Projectile
	+SKYEXPLODE
	DamageType "RTCWExplosive"
	States
	{
		Spawn:
			RW00 A 0 NoDelay A_PlaySound("RTCW/Panzerfaust/Fly",CHAN_5,0.5,1)
			RW00 A 0 ACS_NamedExecuteWithResult("RTCW_SpawnRocketFlare")
		SpawnLoop:
			RW00 A 1 Bright
			Loop
		Crash:
		Death:
		XDeath:
			RW00 AAAA 0 A_SpawnItemEx("RTCW_PanzerfaustSmokeExplosion",0,0,0,frandom(-0.5,0.5),frandom(-0.5,0.5),1)
			RW00 AAAA 0 A_SpawnItemEx("RTCW_ExplosionSmokeDebris")
			RW00 A 0 A_Quake(3,15,0,128)
			RW00 A 0 A_GiveInventory("RTCW_BlazkoWeaponAlert",1,AAPTR_Target)
			RW00 A 0 A_Explode(300,120,XF_HURTSOURCE,1,0)
			RW00 A 0 A_SpawnItemEx("RTCW_PanzerfaustRocketExplosion")
			TNT1 A 15
			Stop
	}
}

Actor RTCW_PanzerfaustRocketCoop : RTCW_PanzerfaustRocket { +THRUSPECIES Species "Player" }

Actor RTCW_PanzerfaustShell : RTCW_Casing
{
	Scale 2.0
	BounceType "None"
	States
	{
		Spawn:
			RW00 A 0 NoDelay A_ChangeVelocity(frandom(-0.75,0.75),frandom(-0.75,0.75),0,CVF_RELATIVE)
			RW00 A 0 ThrustThingZ(0,random(8,12),0,1)
		SpawnLoop:
			RW00 A 5 A_SpawnItemEx("RTCW_SmokePuff",0,-16,0,0,0,frandom(0.25,0.5))
			Loop
		Death:
			RW00 AAAAAAAAAAAAAAAAAAAA 5 A_SpawnItemEx("RTCW_SmokePuff",0,-16,0,0,0,frandom(0.25,0.5))
			RW00 AAAAAAAA 1 A_Fadeout(0.1)
			Stop
	}
}

Actor RTCW_PanzerfaustRocketFlare
{
	Height 10
	Radius 5
	Renderstyle Add
	+NOINTERACTION
	States
	{
		Spawn:
			RW00 A 5 Bright
		SpawnLoop:
			RW00 A 0 A_SpawnItemEx("RTCW_PanzerfaustSmoke",-16*cos(velz/24),0,-16*sin(velz/24))
			RW00 A 0 A_SpawnItemEx("RTCW_PanzerfaustSmoke",-8*cos(velz/24),0,-8*sin(velz/24))
			RW00 A 1 Bright A_SpawnItemEx("RTCW_PanzerfaustSmoke")
			Loop
	}
}

Actor RTCW_ExplosionSmokeDebris
{
	Height 4
	Radius 2
	Gravity 0.33
	Damage (0)
	Projectile
	Scale 0.1
	BounceType "Doom"
	BounceCount 3
	-NOGRAVITY
	+THRUACTORS
	States
	{
		Spawn:
			TNT1 A 0 NoDelay A_ChangeVelocity(((2*random(0,1))-1)*frandom(1.0,2.0),((2*random(0,1))-1)*frandom(1.0,2.0),frandom(3.0,5.0),CVF_REPLACE)
		SpawnLoop:
			TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 1 A_SpawnItemEx("RTCW_PanzerfaustSmoke",0,0,0,0,0,0,0,SXF_TRANSFERSCALE)
			Stop	
	}
}

Actor RTCW_PanzerfaustRocketExplosion
{
	Height 4
	Radius 2
	Scale 2.0
	+NOINTERACTION
	States
	{
		Spawn:
			//We have to duplicate the sprites because of a zandronum bug
			RW06 A 0 NoDelay A_Quake(3,15,0,256)
			RW06 A 0 A_PlaySound("RTCW/Panzerfaust/Explode",CHAN_5)
			RW06 ABCDEFGHIJKLMNOPQRSTUVWX 2 Bright
			Stop
	}
}

Actor RTCW_PanzerfaustSmoke
{
	Radius 1
	Height 1
	Alpha 0.2
	Scale 0.33
	RenderStyle Shaded
	+NOINTERACTION
	+CLIENTSIDEONLY
	+FORCEXYBILLBOARD
	States
	{
		Spawn:
			RW05 ABCDEFGHIJKLMNOPQRSTUVWX 2
			Stop
	}
}

Actor RTCW_PanzerfaustSmokeExplosion : RTCW_PanzerfaustSmoke { Scale 2.0 Alpha 0.33 }