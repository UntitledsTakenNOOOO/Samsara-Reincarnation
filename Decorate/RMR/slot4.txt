ACTOR "Cyclops Particle Accelerator" : Weapon
{
  Weapon.SelectionOrder 300
  Weapon.SlotNumber 4 
  Weapon.BobStyle Inverse
  Weapon.BobRangeX 0.85
  Weapon.BobSpeed 1.1  
  Weapon.AmmoUse 1
  Weapon.AmmoGive 25
  Weapon.AmmoType "Clip"
  Inventory.PickupSound "RMR/pickup" 	  
  Inventory.PickupMessage "You found the Cyclops Mark VI Particle Accelerator!"
  Inventory.RestrictedTo "RebelMoonCommando"
  Tag "Cyclops Particle Accelerator"
  States
  {
  Ready:
    RM7T A 1 A_WeaponReady
    Loop
  Deselect:
	TNT1 A 0 A_Lower
    RM7T A 1 A_Lower
	Loop
  Select:
	TNT1 A 0 A_Raise
    RM7T A 1 A_Raise
    Loop
  Fire:
    TNT1 A 0 A_PlaySound("RMR/SpinningP")
    TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"CoopFire")  
	TNT1 A 0 A_JumpIfInventory("DMModeOn",1,"DMFire")  
	RM7T B 1 bright
	TNT1 A 0 A_FireCustomMissile("RMRSpinningPlasma",0,1,0,0)
	RM7T CC 1 Bright 	
	RM7T DD 1 Bright
    RM7T DD 1 Bright A_Refire	
    Goto Ready
  CoopFire:
	RM7T B 1 bright
	TNT1 A 0 A_FireCustomMissile("RMRSpinningPlasmaCoop",0,1,0,0)
	RM7T CC 1 Bright	
	RM7T DD 1 Bright
    RM7T DD 1 Bright A_Refire
	Goto Ready
  DMFire:
	RM7T B 1 bright
	TNT1 A 0 A_FireCustomMissile("RMRSpinningPlasmaDM",0,1,0,0)
	RM7T CC 1 Bright	
	RM7T DD 1 Bright
    RM7T DD 1 Bright A_Refire
	Goto Ready
  Spawn:
	RM7T E -1 Bright
	Loop
  }
}

Actor RMRSpinningPlasma : FastProjectile
{
    Damage (18)
    Radius 5
    Height 5
    Speed 180
    Projectile
	+WINDTHRUST
	+NOBLOOD
	+BLOODLESSIMPACT	
	-EXTREMEDEATH
	+NOEXTREMEDEATH
	-SKYEXPLODE	
	DeathSound "RMR/SpinningB"
    DamageType "RMR-CyanPlasma"	
	Obituary "%o saw %k's shiny fireworks."	
	Scale 0.50
    States
    {
      Spawn:
        RMZ2 PQ 1 bright
        loop
      Death:
	  Crash:	  
        RMZ2 RST 4 bright
		RMZ2 U 3 bright A_FadeOut
        stop
	  XDeath:
		TNT1 A 0 A_SpawnItemEx("RMRPlasmaSplash",0,0,0,0,0,0,0,128,0)
		TNT1 A 5 A_PlaySound("RMR/SpinningB")	  
		stop				
    }
}

Actor RMRSpinningPlasmaDM : RMRSpinningPlasma { Damage (14) }

Actor RMRSpinningPlasmaCoop : RMRSpinningPlasma { Species "Player" +THRUSPECIES  }
