actor DiscOfRepulsionCooldown : inventory { -INVBAR +INVENTORY.UNDROPPABLE inventory.maxamount 60 }

actor PortMysticAmbit : CustomInventory
{
    tag "Mystic Ambit Incant"
    +FLOATBOB
    Inventory.MaxAmount 16
    +INVENTORY.INVBAR
    +INVENTORY.PICKUPFLASH
    +INVENTORY.FANCYPICKUPSOUND
    +INVENTORY.UNDROPPABLE
    Inventory.Icon "ARTICOMB"
    Inventory.PickupSound "hexen/artiget"
    //Inventory.UseSound "hexen/ambituse"
    Inventory.PickupMessage "You got the Mystic Ambit Incant!"
	Inventory.RestrictedTo "HexenPlayer"
    States
    {
      Spawn:
        HRAD ABCDEFGHIJKLMNOP 4 Bright
        Loop

      Use:
		TNT1 A 0 A_JumpIfInventory("HexenClassMode", 2, "UseFighter")
		TNT1 A 0 A_JumpIfInventory("HexenClassMode", 1, "UseMage")
        TNT1 A 0 A_JumpIfHealthLower(100,"UseSuccess")
        TNT1 A 1 ACS_NamedExecuteAlways("SamsaraClientDecorate",0,1)//A_Print("You cannot use this unless injured.")
        fail

      UseSuccess:
        TNT1 A 1 ACS_NamedExecuteAlways("SamsaraClientDecorate",0,2)//A_Print(" ")
        TNT1 A 0 A_GiveInventory("PortMysticAmbit2")
        TNT1 A 0 A_GiveInventory("MysticAmbitArmor")
        TNT1 A 0 A_PlaySoundEx("hexen/ambituse","body",0,0)
        TNT1 AAAAAAAAAAAAAAA 1 A_SpawnItemEx("ArmorScrap",0,0,random(60,90),random(-4,4),random(-4,4),random(1,7),48,0)
        stop
	  UseMage:
	    TNT1 A 0 A_JumpIfInventory("DiscOfRepulsionCooldown",1,"UseFailMage")
		TNT1 A 0 A_GiveInventory("DiscOfRepulsionCooldown",3)
		TNT1 A 0 ACS_NamedExecuteAlways("DiscOfRepulsionCooldown")
		TNT1 A 1 A_Blast(0,255,255,20.0,"BlastEffect2")
		fail
	  UseFighter:
	    TNT1 A 0 A_JumpIfInventory("DiscOfRepulsionCooldown",1,"UseFailFighter")
		TNT1 A 0 A_GiveInventory("DiscOfRepulsionCooldown",60)
		TNT1 A 0 ACS_NamedExecuteAlways("DiscOfRepulsionCooldown")
		TNT1 A 1 A_FireCustomMissile("SummoningDoll2")
		fail
	
	  UseFailMage:
        TNT1 A 1 A_Print("$DORCCOOLDOWN")
        fail
	  UseFailFighter:
        TNT1 A 1 A_Print("$DSCCOOLDOWN")
        fail
    }
}

actor PortMysticAmbit2 : ArtiHealingRadius
{
    tag "Mystic Ambit Incant"
    +INVENTORY.AUTOACTIVATE
	Inventory.RestrictedTo "HexenPlayer"
}

actor MysticAmbitArmor : BasicArmorBonus
{
    Armor.Savepercent 33.33333
    Armor.MaxSaveAmount 300
    Armor.Saveamount 50
}

Actor SummoningDoll2 : SummoningDoll
{
	Speed 20
	+NOBLOCKMAP
	+DROPOFF
	+MISSILE
	+NOTELEPORT
	States
	{
	Spawn:
		H125 I 4
		Loop
	Death:
		TNT1 A 0 A_RearrangePointers(AAPTR_TARGET,AAPTR_NULL,AAPTR_TARGET)
		TNT1 A 0 A_JumpIf(CallACS("DarkServantAutoRemoval")==0,"Remove")
		H125 II 4
		H125 I 4 A_Summon
		Stop
	Remove:
		TNT1 A 0 A_TakeFromTarget("DiscOfRepulsionCooldown",60)
		TNT1 A 1 A_SpawnItemEx("MinotaurSmoke2",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
		Stop
	}
}
Actor MinotaurFriend2 : MinotaurFriend replaces MinotaurFriend 
{
	States
	{
		Spawn:
			H126 A 15
			H126 A 15 A_SetTranslucent(0.66, 0)
			H126 A 3 A_SetTranslucent(1, 0)
		SpawnLoop:
			H126 AB 10 A_MinotaurLook
			Loop
		Roam:
			H126 ABCD 5 A_MinotaurRoam
			Loop
		See:
			H126 ABCD 5 A_MinotaurChase
			Loop
		Melee:
			H126 V 10 A_FaceTarget
			H126 W 7 A_FaceTarget
			H126 X 12 A_MinotaurAtk1
			Goto See
		Missile:
			H126 V 10 A_MinotaurDecide
			H126 Y 4 A_FaceTarget
			H126 Z 9 A_MinotaurAtk2
			Goto See
		Hammer:
			H126 V 10 A_FaceTarget
			H126 W 7 A_FaceTarget
			H126 X 12 A_MinotaurAtk3
			Goto See
		HammerLoop:
			H126 X 12
			Goto Hammer
		Charge:
			H126 U 2 A_MinotaurCharge
			Loop
		Pain:
			H126 E 3
			H126 E 6 A_Pain
			Goto See
		//I could go to fadeout, but I honestly kinda found that dumb
		Death:
			TNT1 A 0 A_JumpIfInventory("PowerMinotaur",1,1,AAPTR_TRACER)
			Goto FadeOut
			H126 F 6 //A_MinotaurDeath
			H126 G 5
			H126 H 6 A_Scream
			H126 I 5
			H126 J 6
			H126 K 5
			H126 L 6
			H126 M 5 A_NoBlocking
			H126 N 6
			H126 O 5
			H126 P 6
			H126 Q 5
			H126 R 6
			H126 S 5
			H126 T -1 A_BossDeath
			Stop
		FadeOut:
			H126 E 6
			H126 E 2 A_Scream
			H126 E 5 A_SpawnItemEx("MinotaurSmoke2")
			H126 E 5
			H126 E 5 A_NoBlocking
			H126 E 5
			H126 E 5 A_SetTranslucent(0.66, 0)
			H126 E 5 A_SetTranslucent(0.33, 0)
			H126 E 10 A_BossDeath
			Stop
	}
}

ACTOR BlastEffect2
{
	+NOBLOCKMAP
	+NOGRAVITY
	+NOCLIP
	+NOTELEPORT
	RenderStyle Translucent
	Alpha 0.666
	States
	{
	Spawn:
		H125 ABCDEFGHI 4
		Stop
	}
}

ACTOR MinotaurSmoke2 replaces MinotaurSmoke
{
	+NOBLOCKMAP
	+NOGRAVITY
	+NOTELEPORT
	RenderStyle Translucent
	Alpha 0.6
	States
	{
	Spawn:
		H127 ABCDEFGHIJKLMNOPQ 3
		Stop
	}
}