Actor PSFlamethrower : Weapon
{
    Inventory.PickupSound "PS/Ipickup2"
	Weapon.selectionorder 900
	Weapon.AmmoUse 1
	Weapon.BobStyle Smooth
	Weapon.BobRangeX 0.75
	Weapon.BobRangeY 0.28
	Weapon.BobSpeed 1.4		
	Weapon.AmmoGive 30
	Weapon.AmmoType "Shell"
	Weapon.SlotNumber 3
	Inventory.PickupMessage "You got the Flamethrower!"
	Weapon.UpSound "Jon/FTUp"
	Obituary "%k made a bbq out of %o."
	Inventory.RestrictedTo "PSPlayer"
	Tag "Flamethrower"  	
	States
	{
	Ready:
		TNT1 A 0 A_JumpIfNoAmmo("ReadyAmmoless")
		EXF0 RRRSSSTTTUUU 1 A_WeaponReady
		Loop
	ReadyAmmoless:
		TNT1 A 0 A_JumpIfInventory("Shell",1,"Ready")
		PSFT A 1 A_WeaponReady
		Loop
	Deselect:
		TNT1 A 0 A_PlaySound("Jon/FTDown",CHAN_WEAPON)
		PSFT A 1 A_Lower
		Goto Deselect+1
	Select:
		PSFT A 1 A_Raise
		Loop
	Fire:
	    PSFT E 1
		PSFT F 1
	Hold:	
		TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"CoopHold")
		TNT1 A 0 A_PlaySoundEx("Jon/FTFire1","Weapon",1) //Loop sound
		PSFT B 2 offset(1,38) Bright A_FireCustomMissile("PSFTProjA",0,1,0,3)
		PSFT A 2 offset(0,54) Bright A_FireCustomMissile("PSFTProjA",0,0,0,3)
		PSFT B 2 offset(-1,38)Bright A_FireCustomMissile("PSFTProjA",0,0,0,3)
		PSFT B 2 offset(0,37) Bright A_FireCustomMissile("PSFTProjA",0,0,0,3)
		PSFT A 1 Bright A_ReFire
		PSFT A 1 Bright A_StopSoundEx("Weapon") //Prevents firing sound from looping.
		Goto FireFinish
	CoopHold:
		TNT1 A 0 A_PlaySoundEx("Jon/FTFire1","Weapon",1) //Loop sound
		PSFT B 2 offset(1,38) Bright A_FireCustomMissile("PSFTProjA_2",0,1,0,3)
		PSFT A 2 offset(0,54) Bright A_FireCustomMissile("PSFTProjA_2",0,0,0,3)
		PSFT B 2 offset(-1,38)Bright A_FireCustomMissile("PSFTProjA_2",0,0,0,3)
		PSFT B 2 offset(0,37) Bright A_FireCustomMissile("PSFTProjA_2",0,0,0,3)
		PSFT A 1 Bright A_ReFire
		PSFT A 1 Bright A_StopSoundEx("Weapon") //Prevents firing sound from looping.
		Goto FireFinish	
	FireFinish:
		TNT1 A 0 A_PlaySound("Jon/FTSTEAM",CHAN_WEAPON)
		EXF0 LL 1 Bright A_WeaponReady(WRF_NOBOB|WRF_NOFIRE|WRF_NOSWITCH)
		EXF0 MM 1 Bright A_WeaponReady(WRF_NOBOB|WRF_NOFIRE|WRF_NOSWITCH)
		EXF0 NN 1 Bright A_WeaponReady(WRF_NOBOB|WRF_NOFIRE|WRF_NOSWITCH)
		EXF0 OO 1 Bright A_WeaponReady(WRF_NOBOB|WRF_NOFIRE|WRF_NOSWITCH)
		EXF0 PP 1 Bright A_WeaponReady(WRF_NOBOB|WRF_NOFIRE|WRF_NOSWITCH)
		EXF0 QQ 1 Bright A_WeaponReady(WRF_NOBOB|WRF_NOFIRE|WRF_NOSWITCH)
		PSFT AA 1 Bright A_WeaponReady(WRF_NOBOB|WRF_NOFIRE|WRF_NOSWITCH)
		Goto Ready
	Spawn:
		WFTR A -1
		Stop
	}
}

Actor PSFTProjA
{
	Radius 3
	Height 4
	Scale 0.2
//	Alpha 0.3
	Speed 20
	Damage (5)
	Projectile
//	RenderStyle Add
	DamageType "PSFire"
	+FIREDAMAGE
//	+PAINLESS
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_Jump(224,"SpawnLoop")
		TNT1 A 0 A_ChangeFlag(PAINLESS,1)
	SpawnLoop:
		EXF0 AAAAAAAAAAAAA 1 Bright
		EXFO A 1 Bright A_SpawnItemEx("PSFTProjB",0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_CLIENTSIDE)
		Stop
	Death:
		TNT1 A 0 A_Explode(15, 15, 0, 1)
		TNT1 A 5 A_SpawnItemEx("PSFireDeath",0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_CLIENTSIDE)
		Stop
	}
}

Actor PSFTProjA_2 : PSFTProjA { Species "Player" +THRUSPECIES +DONTHARMCLASS +DONTHARMSPECIES +MTHRUSPECIES }

Actor PSFireDeath 
{
  +NOINTERACTION
  +CLIENTSIDEONLY
  -SOLID
  Scale 0.4
  states
  {
  Spawn:
		EXF0 BCDEFG 2 Bright  
		Stop
	}
}

Actor PSFTProjB
{
	Radius 3
	Height 4
	Scale 0.3
	Alpha 0.9
	Speed 20
	Projectile
	RenderStyle Add
	+CLIENTSIDEONLY
	States
	{
	Spawn:
		EXF0 HIJK 2 Bright
		Stop
	}
}