// This would normally be in ReMixer, but the code for this enemy's attack
// is too embedded in Reincarnation, so it's been moved here.

Actor ChokingHand : SamsaraMonsterBeastTotalGib
{
	Tag "Hell Hand"
	Monster
	Height 10
	Radius 11
	Mass 30
	Speed 12
	Health 4
	MeleeRange 32
	MeleeDamage 1
	DamageType "ChokeEM"
	DamageFactor "Fire", 2
	DamageFactor "MarathonFire", 2
	DamageFactor "MarathonPlasmaFire", 2
	Species "BloodEnemies"
	+THRUSPECIES
	seesound "Hand/See1"
	deathsound "Hand/Die"
	activesound "Hand/Roam"
	painSound "Hand/Pain"
	Scale 0.56
	Obituary "%o was bare choked by a hand!"
	BloodType "NashGore_Blood_Monolith"
	+DROPOFF
	+FLOORCLIP
	+NOINFIGHTING
	+THRUGHOST
	-COUNTKILL
	+NOBLOODDECALS
	PainChance "ShrinkerDMG", 0
	PainChance "MightyBoot", 255
	PainChance "MarathonStun", 255
	States
	{
	Idle:
		7H9N A 2 A_Wander
		7H9N A 0 A_Look
		7H9N B 2 A_Wander
		7H9N A 0 A_Look
		7H9N C 2 A_Wander
		7H9N A 0 A_Look
		7H9N D 2 A_Wander
		7H9N A 0 A_Look
		7H9N E 2 A_Wander
		7H9N A 0 A_Look
		loop
	Spawn:
		7H9N A 0
		7H9N A 0 A_JumpIf(CallACS("SamsaraDecorate", 50) == 0, "Spawn2")
		7H9N A 1 A_JumpIfInventory("AlreadySpawned",1,"Spawn2")
		7H9N A 1 ACS_NamedExecuteAlways("ReMixerSpawnScript",0,1,0)
		7H9N A 0 A_GiveInventory("AlreadySpawned",1)
	Spawn2:
		7H9N A 5 A_Look
		Loop
	See:
		7H9N AABBCCDDEEAABBCCDDEE 2 A_Chase
		7H9N A 0 A_JumpIfCloser(250, "Jump")
		7H9N A 0 A_Jump(8,"GoWild")
		Loop
	Jump:
		TNT1 A 0 A_ChangeFlag("NOPAIN",0)
		7H9N FG 1 A_FaceTarget
		7H9N HI 2 ThrustThingZ(0,30,0,0)
		7H9N JK 2 A_Recoil(-10)
		Goto See
	GoWild:
		7H9N AABBCCDDEEAABBCCDDEEAABBCCDDEE 1 A_Wander
		7H9N A 0 A_Look
		Goto See
	Pain:
	Pain.Shock:
	Pain.Tesla:
		7H9N A 2 A_Pain
		Goto See
	Pain.MightyBoot:
		TNT1 A 0 A_ChangeFlag("NOPAIN",1)
		TNT1 A 0 A_PlaySoundEx("duke/kickhit","body",0,0)
		7H9N A 2 A_FaceTarget
		TNT1 A 0 A_Recoil(14)
		7H9N A 16 A_Pain
		Goto See
	Melee:
		TNT1 A 0 A_ChangeFlag("NOPAIN",0)
		TNT1 A 0 A_JumpIfInTargetInventory("AmAlreadyChoked",1,"GoWild")
		TNT1 A 0 A_JumpIfInTargetInventory("GettingSlimed",1,"GoWild")
		TNT1 A 0 A_JumpIfInTargetInventory("God Hand",1,"GoWild")
		TNT1 A 0 A_JumpIfInTargetInventory("Doggie",1,"GoWild")
		TNT1 A 0 A_JumpIfInTargetInventory("RedneckMotorcycle",1,"GoWild")
		TNT1 A 0 A_JumpIfInTargetInventory("ROTTNetNoWeapon",1,"GoWild")
		TNT1 A 0 A_GiveToTarget("SamsaraMixerChokingHandDamageOnLatch",1)
		7H9N A 1 A_GiveToTarget("ChokeMe",1)
		7H9N A 1 A_FaceTarget
		TNT1 A 0 Thing_Remove(0)
		Goto Death2
	Death:
		TNT1 A 0 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)
		TNT1 A 0 A_SpawnItem("UniversalBloodyHellGoreDeathEffect")
		TNT1 A 0 A_Jump(128,1,2)
		TNT1 AAA 0 A_SpawnItemEx("NashGore_FlyingBlood_Monolith", 0, 0, 0, FRandom(0.1, 6.0) * Random(-1, 1), FRandom(0.1, 6.0) * Random(-1, 1), FRandom((Random(3.0,1.0)), (Random(8.0,6.0))) * FRandom((Random(3,2)), 1), 0, NASHGORE_MONOLITH_BLOODFLAGS1, 64)
	XDeath2:
		7H9N T 5 A_Scream
		7H9N U 5 A_Fall
		7H9N VWXY 4
		7H9N Y -1
		Stop
	Death2:
		TNT1 A 1 A_UnsetSolid
		Stop
	Death.CalebFire:
	Death.LifeLeech:
		TNT1 A 0 A_Jump(218, "Burn")
		TNT1 A 0 A_GiveToTarget("CalebDMTaunt1", 1)
	Burn:
		TNT1 A 0 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)
		7H9N L 0 A_Scream
		TNT1 A 0 A_PlaySound("Blood/BurningBody",CHAN_BODY,1.0,0,ATTN_NORM)
	LoopBurn:
		7H9N LMNOLMNOLMNOLMNOLMNO 4
		7H9N A 0 A_Jump(120,"LoopBurn")
		TNT1 A 0 A_PlaySound("Blood/CrispyBody",CHAN_BODY,1.0,0,ATTN_NORM)
		7H9N P 4 A_Fall
		7H9N QRS 4
		7H9N S -1
		Stop
	Death.CalebNapalm:
	Death.Vodoo:
	Death.VoodooPuff:
	Death.DynamiteCoop:
	Death.Dynamite:
	Death.Shock:
	Death.Tesla:
	Death.CalebBullet:
		TNT1 A 0 A_Jump(218, "Death")
		TNT1 A 0 A_GiveToTarget("CalebDMTaunt1", 1)
		Goto Death
	}
}

Actor AmAlreadyChoked : Inventory {	Inventory.Amount 1 +AUTOACTIVATE +ALWAYSPICKUP }
Actor ChokingHandSpammer : Counter { Inventory.MaxAmount 3 Inventory.InterHubAmount 0 }
actor SamsaraMixerChokingHandDamageOnLatch : Boolean {} // makes sure damage isn't dealt right away on map change, or bad things happen

Actor ChokeMe : Weapon
{
	Weapon.SelectionOrder 1
	Inventory.Amount 1
	Inventory.MaxAmount 1
    Tag "Hell Hand Choke"
	+INVENTORY.UNDROPPABLE
	+WEAPON.CHEATNOTWEAPON
	+WEAPON.NOALERT
	+AUTOACTIVATE
	+ALWAYSPICKUP
	states
	{
	Ready:
		TNT1 A 0 A_JumpIfInventory("AmAlreadyChoked",1,"ReadyLoopCheck")
		TNT1 A 0 A_GiveInventory("AmAlreadyChoked",1)
		TNT1 A 0 A_Print("Spam either firing button to take it off!")
	ReadyLoopCheck:
        TNT1 A 0 A_JumpIfInventory("SamsaraMixerChokingHandDamageOnLatch",1,"ReadyLoopDamage")
        goto ReadyLoop

	ReadyLoopDamage:
        TNT1 A 0 A_TakeInventory("SamsaraMixerChokingHandDamageOnLatch")
		TNT1 A 0 DamageThing(5)
	ReadyLoop:
		//TNT1 A 0 A_PlaySound("Hand/Choke",CHAN_WEAPON)
		7H9X AB 3 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
		7H9X CD 3 A_WeaponReady(WRF_NOSWITCH)
		7H9X ED 3 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
		7H9X CB 3 A_WeaponReady(WRF_NOSWITCH)
		7H9X AF 3 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
		7H9X GF 3 A_WeaponReady(WRF_NOSWITCH)
		goto ReadyLoopDamage

	Select:
		// TNT1 A 0 A_SetDamageType("chokeEM")
		TNT1 A 0 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
	SelectLoop:
		7H9X ABCDEFGABCDEFG 3 A_Raise
		Loop
	Deselect:
		TNT1 A 0 A_TakeInventory("AmAlreadyChoked",1)
		TNT1 A 0 A_SpawnItemEx("Chokinghand_2",0,0,0,0,0,0,SXF_NOCHECKPOSITION)
		TNT1 A 0 A_TakeInventory("ChokeMe",1)
		7H9X ABCDEFGABCDEFG 3 A_Lower
		Stop
	Fire:	
	AltFire:
		TNT1 A 0 A_JumpIfInventory("ChokingHandSpammer",0,"Throw")
		TNT1 A 0 A_GiveInventory("ChokingHandSpammer")
		Goto ReadyLoop
	Throw:
		TNT1 A 0 A_TakeInventory("ChokingHandSpammer")
		TNT1 A 1 A_FireCustomMissile("Flying_BloodHand",0,false)
		TNT1 A 1 A_TakeInventory("AmAlreadyChoked",1)
		TNT1 A 1 A_TakeInventory("ChokeMe",1)
		Stop
	}
}

actor Flying_BloodHand
{
	radius 11
	height 10
	speed 22
	Damage 0
	Scale 0.5
	BounceCount 3
	Species "Enemies"
    Tag "Hell Hand"
	+THRUSPECIES
	+DONTHARMCLASS
	+DONTHARMSPECIES
	+RANDOMIZE
	-NOGRAVITY
	+DONTSPLASH
	+BOUNCEONACTORS
	+DOOMBOUNCE
	+CANBOUNCEWATER
	DamageType "ChokeEM"
	states
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_PlaySound("Hand/Pain",CHAN_VOICE)
		7H9N A 1 ThrustThingZ(0,Random(29,40),0,0)
	See:
		7H9N AABBCCDDEE 1 A_CheckFloor("SpawnHand")
		loop
	Death:
	SpawnHand:
		TNT1 A 1 A_SpawnItemEx("Chokinghand_2",0,0,0,0,0,0,SXF_NOCHECKPOSITION)
		stop
	}
}

Actor ChokingHand_2 : ChokingHand { +LOOKALLAROUND }

actor ChokingHandDisintegrate : ZombiemanDisintegrate
{
	Scale 0.56
	States
	{
	Spawn:
		TNT1 A 0
		7H9N A 3 A_SpawnItem("DisintEffectSmall",0,10)
		7H9N A 3 A_SetTranslucent(0.9,0)
		7H9N A 3 A_SetTranslucent(0.8,0)
		7H9N A 3 A_SetTranslucent(0.7,0)
		7H9N A 3 A_SetTranslucent(0.6,0)
		7H9N A 3 A_SetTranslucent(0.5,0)
		7H9N A 2 A_SetTranslucent(0.4,0)
		7H9N A 2 A_SetTranslucent(0.3,0)
		7H9N A 2 A_SetTranslucent(0.2,0)
		7H9N A 2 A_SetTranslucent(0.1,0)
		Stop
	}
}