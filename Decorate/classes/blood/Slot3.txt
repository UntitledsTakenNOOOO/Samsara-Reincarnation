Actor Sawedoff : CalebWeapon
{
	Obituary "%k gave %o a heap of buckshot."
	Weapon.AmmoType "Shell"
	Weapon.AmmoType2 "Shell"
	Inventory.PickupMessage "You got the Sawed off!"
	Inventory.PickupSound "Caleb/Weaponup"
	Weapon.SelectionOrder 80
	Tag "Sawed-Off Shotgun"
	Weapon.AmmoGive 8
	Weapon.AmmoUse 1
	Weapon.AmmoUse2 1
	Weapon.SlotNumber 3
	+THRUGHOST
	+WEAPON.NOALERT
	Decal ZBloodBulletChip1
	States
	{
	Spawn:
		BSOP A -1
		Stop
	Ready:
		TNT1 A 0 A_TakeInventory("UsingPitchfork", 1)
		TNT1 A 0 A_JumpIfInventory("CalebReady",1,"IntroTaunt")
		TNT1 A 0 A_JumpIfInventory("CalebWaiting",60,"WaitingTaunt")
		TNT1 A 0 A_JumpIfInventory("PowerSamsaraBloodGunsAkimbo",1,"AkimboReady")
		TNT1 A 0 A_JumpIfInventory("SamsaraBloodGunsAkimboToggled",1,"AkimboReady")
		TNT1 A 0 A_JumpIfInventory("SawedOffCheck",2,"Ready2ShellsFired")
		TNT1 A 0 A_JumpIfInventory("SawedOffCheck",1,"Ready1ShellFired")
		BSWN A 1 A_WeaponReady
		Goto Ready+1
	Ready1ShellFired:
		BSWN B 1 A_WeaponReady
		Goto Ready+1
	Ready2ShellsFired:
		BSWN C 1 A_WeaponReady
		Goto Ready+1
	AkimboReady:
		BSW2 G 1 A_WeaponReady
		Goto Ready+1
	Deselect:
		TNT1 A 0 A_JumpIfInventory("PowerSamsaraBloodGunsAkimbo",1,"AkimboDeselect")
		TNT1 A 0 A_JumpIfInventory("SamsaraBloodGunsAkimboToggled",1,"AkimboDeselect")
		TNT1 A 0 A_JumpIfInventory("SawedOffCheck",2,"Deselect2ShellsFired")
		BSWN A 0 A_JumpIfInventory("SawedOffCheck",1,"Deselect1ShellFired")
		Goto Super::DeselectLoop
	Deselect1ShellFired:
		BSWN B 0
		Goto Super::DeselectLoop
	Deselect2ShellsFired:
		BSWN C 0
		Goto Super::DeselectLoop
	AkimboDeselect:
		BSW2 G 0
		Goto Super::DeselectLoop
	Select:
		TNT1 A 0 A_JumpIfInventory("CalebLighterReadyDummy",1,"DeselectLighter")
		TNT1 A 0 A_JumpIfInventory("PowerSamsaraBloodGunsAkimbo",1,"AkimboSelect")
		TNT1 A 0 A_JumpIfInventory("SamsaraBloodGunsAkimboToggled",1,"AkimboSelect")
		TNT1 A 0 A_JumpIfInventory("SawedOffCheck",2,"Select2ShellsFired")
		BSWN A 0 A_JumpIfInventory("SawedOffCheck",1,"Select1ShellFired")
		Goto Super::SelectLoop
	Select1ShellFired:
		BSWN B 0
		Goto Super::SelectLoop
	Select2ShellsFired:
		BSWN C 0
		Goto Super::SelectLoop
	AkimboSelect:
		BSW2 G 0
		Goto Super::SelectLoop
	Fire:
		TNT1 A 0 A_JumpIfInventory("PowerSamsaraBloodGunsAkimbo",1,"AkimboFire")
		TNT1 A 0 A_JumpIfInventory("SamsaraBloodGunsAkimboToggled",1,"AkimboFire")
		TNT1 A 0 A_JumpIfInventory("SawedOffCheck",2,"Reload")
		TNT1 A 0 A_JumpIfInventory("SawedOffCheck",1,"Fire2")
		TNT1 A 0 A_GunFlash("Flash")
		TNT1 A 0 A_Light2
		BSWF AA 1 bright A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		TNT1 A 0 A_Light1
		BSWF BB 1 bright A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		TNT1 A 0 A_Light0
		BSWF CC 1 bright A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		TNT1 AA 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		TNT1 A 0 A_GiveInventory("SawedOffCheck",1)
		Goto Ready
	Fire2:
		TNT1 A 0 A_GunFlash("Flash2")
		TNT1 A 0 A_Light2
		BSWF DD 1 bright A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		TNT1 A 0 A_Light1
		BSWF EE 1 bright A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		TNT1 A 0 A_Light0
		BSWF FF 1 bright A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		TNT1 AA 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		BSWN CC 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		TNT1 A 0 A_GiveInventory("SawedOffCheck",1)
		Goto Reload
	Flash:
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_PlayWeaponSound("SawnOff/Fire")
		TNT1 A 0 A_JumpIfInventory("CoopModeOn", 1, 2)
		TNT1 A 0 A_FireBullets(4.1, 1.8, 15, 5, "ZBBulletPuff", FBF_NORANDOM|FBF_USEAMMO)
		Goto FlashEnd
		TNT1 A 0 A_FireBullets(4.1, 1.8, 15, 5, "ZBBulletPuffCoop", FBF_NORANDOM|FBF_USEAMMO)
		Goto FlashEnd
	FlashEnd:
		TNT1 A 0 A_JumpIfInventory("SamsaraRecoilIsDisabled", 1, "FlashEndNoPitch")
		BSWN D 1 A_SetPitch(pitch-2.25)
		BSWN EFG 1 A_SetPitch(pitch+0.25)
		BSWN H 1 A_SetPitch(pitch+0.5)
		BSWN I 1 A_SetPitch(pitch+0.25)
		BSWN J 1 A_SetPitch(pitch+0.75)
		BSWN B 1 A_SetPitch(pitch+0.25)
		Stop
	FlashEndNoPitch:
		BSWN DEFGHIJB 1
		Stop
	Flash2:
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_PlayWeaponSound("SawnOff/Fire")
		TNT1 A 0 A_JumpIfInventory("CoopModeOn", 1, 2)
		TNT1 A 0 A_FireBullets(4.1, 1.8, 15, 5, "ZBBulletPuff", FBF_NORANDOM|FBF_USEAMMO)
		Goto Flash2End
		TNT1 A 0 A_FireBullets(4.1, 1.8, 15, 5, "ZBBulletPuffCoop", FBF_NORANDOM|FBF_USEAMMO)
		Goto Flash2End
	Flash2End:
		TNT1 A 0 A_JumpIfInventory("SamsaraRecoilIsDisabled", 1, "Flash2EndNoPitch")
		BSWN K 1 A_SetPitch(pitch-2.25)
		BSWN EFG 1 A_SetPitch(pitch+0.25)
		BSWN L 1 A_SetPitch(pitch+0.5)
		BSWN MNC 1 A_SetPitch(pitch+0.25)
		TNT1 AA 1 A_SetPitch(pitch+0.25)
		Stop
	Flash2EndNoPitch:
		BSWN KEFGLMNC 1
		Stop
	AltFire:
		TNT1 A 0 A_JumpIfInventory("PowerSamsaraBloodGunsAkimbo",1,"AkimboAltFire")
		TNT1 A 0 A_JumpIfInventory("SamsaraBloodGunsAkimboToggled",1,"AkimboAltFire")
		TNT1 A 0 A_JumpIfInventory("SawedOffCheck",2,"Reload")
		TNT1 A 0 A_JumpIfInventory("SawedOffCheck",1,"Fire2")
		TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
		TNT1 A 0 A_JumpIfInventory("Shell", 2, 1)
		Goto Fire+4
		TNT1 A 0 A_GunFlash("AltFlash")
		TNT1 A 0 A_Light(3)
		BSWF GG 1 bright A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		TNT1 A 0 A_Light2
		BSWF HH 1 bright A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		TNT1 A 0 A_Light0
		BSWF II 1 bright A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		TNT1 A 0 A_GiveInventory("SawedOffCheck",2)
		TNT1 AA 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		Goto Reload
	AltFlash:
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_PlayWeaponSound("SawnOff/AltFire")
		TNT1 A 0 A_TakeInventory("Shell", 2, TIF_NOTAKEINFINITE)
		TNT1 A 0 A_JumpIfInventory("CoopModeOn", 1, 2)
		TNT1 A 0 A_FireBullets(5.8, 5.2, 30, 5, "ZBBulletPuff", FBF_NORANDOM)
		Goto AltFlashEnd
		TNT1 A 0 A_FireBullets(5.8, 5.2, 30, 5, "ZBBulletPuffCoop", FBF_NORANDOM)
		Goto AltFlashEnd
	AltFlashEnd:
		TNT1 A 0 A_JumpIfInventory("SamsaraRecoilIsDisabled", 1, "AltFlashEndNoPitch")
		BSWN K 1 A_SetPitch(pitch-4.5)
		BSWN OPQRSTC 1 A_SetPitch(pitch+0.5)
		TNT1 AA 1 A_SetPitch(pitch+0.5)
		Stop
	AltFlashEndNoPitch:
		BSWN KOPQRSTC 1
		Stop
	NoAmmo:
		WP01 AAAAAAA 3 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		Stop
	AkimboFire:
		BSW2 G 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		TNT1 A 0 A_GunFlash("AkimboFlash")
		TNT1 A 0 A_Light2
		BSWF J 2 bright A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		TNT1 A 0 A_Light1
		BSWF K 2 bright A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		TNT1 A 0 A_Light0
		BSWF L 2 bright A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"AkimboFire2")
		TNT1 A 0 A_JumpIfInventory("Shell", 1, "AkimboFire2")
		Goto AkimboFireNoAmmo // Go back if no ammo, this is needed since this works with flash state.
	AkimboFire2:
		TNT1 A 0 A_GunFlash("AkimboFlash2")
		TNT1 A 0 A_Light2
		BSWF M 2 bright A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		TNT1 A 0 A_Light1
		BSWF N 2 bright A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		TNT1 A 0 A_Light0
		BSWF O 2 bright A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		BSW2 N 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		Goto Ready
	AkimboFireNoAmmo:
		BSW2 UVWX 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		Goto Ready
	AkimboFlash:
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_PlaySound("SawnOff/Fire")
		TNT1 A 0 A_JumpIfInventory("CoopModeOn", 1, 2)
		TNT1 A 0 A_FireBullets(4.3, 2.2, 15, 5, "ZBBulletPuff", FBF_NORANDOM|FBF_USEAMMO)
		Goto AkimboFlashEnd
		TNT1 A 0 A_FireBullets(4.3, 2.2, 15, 5, "ZBBulletPuffCoop", FBF_NORANDOM|FBF_USEAMMO)
		Goto AkimboFlashEnd
	AkimboFlashEnd:
		TNT1 A 0 A_JumpIfInventory("SamsaraRecoilIsDisabled", 1, "AkimboFlashEndNoPitch")
		BSW2 H 2 A_SetPitch(pitch-0.95)
		BSW2 IJ 2 A_SetPitch(pitch+0.475)
		Stop
	AkimboFlashEndNoPitch:
		BSW2 HIJ 2
		Stop
	AkimboFlash2:
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_PlaySound("SawnOff/Fire")
		TNT1 A 0 A_JumpIfInventory("CoopModeOn", 1, 2)
		TNT1 A 0 A_FireBullets(4.3, 2.2, 15, 5, "ZBBulletPuff", FBF_NORANDOM|FBF_USEAMMO)
		Goto AkimboFlash2End
		TNT1 A 0 A_FireBullets(4.3, 2.2, 15, 5, "ZBBulletPuffCoop", FBF_NORANDOM|FBF_USEAMMO)
		Goto AkimboFlash2End
	AkimboFlash2End:
		TNT1 A 0 A_JumpIfInventory("SamsaraRecoilIsDisabled", 1, "AkimboFlash2EndNoPitch")
		BSW2 K 2 A_SetPitch(pitch-0.95)
		BSW2 LM 2 A_SetPitch(pitch+0.475)
		Stop
	AkimboFlash2EndNoPitch:
		BSW2 KLM 2
		Stop
	AkimboAltFire:
		TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
		TNT1 A 0 A_JumpIfInventory("Shell", 4, 1)
		Goto AkimboFire
		BSW2 G 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		TNT1 A 0 A_GunFlash("AkimboAltFlash")
		TNT1 A 0 A_Light(4)
		BSWF P 2 bright A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		TNT1 A 0 A_Light2
		BSWF Q 2 bright A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		TNT1 A 0 A_Light0
		BSWF R 2 bright A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		TNT1 AAAAAAAA 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		BSW2 QRST 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		Goto Ready
	AkimboAltFlash:
		TNT1 A 0 A_AlertMonsters
		TNT1 AA 0 A_PlaySound("SawnOff/AltFire")
		TNT1 A 0 A_TakeInventory("Shell", 4, TIF_NOTAKEINFINITE)
		TNT1 A 0 A_JumpIfInventory("CoopModeOn", 1, 2)
		TNT1 A 0 A_FireBullets(6.1, 5.6, 60, 5, "ZBBulletPuff", FBF_NORANDOM)
		Goto AkimboAltFlashEnd
		TNT1 A 0 A_FireBullets(6.1, 5.6, 60, 5, "ZBBulletPuffCoop", FBF_NORANDOM)
		Goto AkimboAltFlashEnd
	AkimboAltFlashEnd:
		TNT1 A 0 A_JumpIfInventory("SamsaraRecoilIsDisabled", 1, "AkimboAltFlashEndNoPitch")
		BSW2 O 2 A_SetPitch(pitch-5.75)
		BSW2 P 2 A_SetPitch(pitch-0.6)
		TNT1 AAA 1 A_SetPitch(pitch-0.5)
		TNT1 A 1 A_SetPitch(pitch+0.75)
		TNT1 A 1 A_SetPitch(pitch+0.6)
		TNT1 AAA 1 A_SetPitch(pitch+0.5)
		TNT1 AA 1 A_SetPitch(pitch+2.5)
		Stop
	AkimboAltFlashEndNoPitch:
		BSW2 OP 2
		Stop
	Reload:
		TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
		TNT1 A 0 A_JumpIfInventory("Shell",1,1)
		Goto Ready
		BSWN U 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		BSWN V 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		BSWN W 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		BSWN X 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		BSWN Y 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		TNT1 A 0 A_PlaySound("SawnOff/Load",CHAN_6)
		TNT1 A 0 A_FireCustomMissile("ZBShotgunCasing",-45+random(-8,8),false,1,8)
		TNT1 A 0 A_FireCustomMissile("ZBShotgunCasing",-45+random(-8,8),false,0,8)
		TNT1 A 0 A_TakeInventory("SawedOffCheck",2)
		BSWN Z 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		BSWN "[]" 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		BSW2 ABBCC 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		BSW2 D 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		BSW2 E 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		BSW2 F 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		Goto Ready
	}
}

Actor SawedOffCheck : Counter { Inventory.MaxAmount 2 }
