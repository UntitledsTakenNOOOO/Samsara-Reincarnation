actor SamsaraCorridor7CAWAttackSilentSound : Boolean {}

actor "M24CAW" : Corridor7WeaponBase
{
    Inventory.PickupMessage "You got the M-24 C.A.W.!"
    Weapon.SlotNumber 1
    Weapon.SlotPriority 1
    Weapon.SelectionOrder 1100
    Weapon.YAdjust 13
    Weapon.AmmoType1 "Clip"
    Weapon.AmmoGive1 30
    Weapon.AmmoUse1 1
    Obituary "%k shot %o to pieces with the M-24 C.A.W.."
    Tag "M-24 Close Assault Weapon"
    States
    {
      Spawn:
        7MG2 A -1
        stop

      Ready:
        C7MG A 1 A_WeaponReady
        loop

      Fire:
        TNT1 A 0 A_AlertMonsters
        C7MG B 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("Clip",1,1)
        goto HoldStart
        TNT1 A 0 A_GunFlash
        goto HoldStart

      Hold:
        TNT1 A 0 A_AlertMonsters
        TNT1 A 0 A_GunFlash
      HoldStart:
        TNT1 A 0 A_TakeInventory("SamsaraCorridor7CAWAttackSilentSound")
        TNT1 A 0 A_GiveInventory("SamsaraCorridor7CAWAttackHandler")
        C7MG C 3 Bright A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("Clip",1,1)
        goto Hold2Start
        TNT1 A 0 A_GunFlash
        goto Hold2Start

      Hold2Start:
        TNT1 A 0 A_GiveInventory("SamsaraCorridor7CAWAttackSilentSound")
        TNT1 A 0 A_GiveInventory("SamsaraCorridor7CAWAttackHandler")
        C7MG D 3 Bright A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        TNT1 A 0 A_ReFire
        C7MG HGFE 3 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        goto Ready

      AltFire:
        C7MG A 0
        goto Super::AltFire
    }
}

actor SamsaraCorridor7CAWAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Clip",1,1)
        stop
        TNT1 A 0 A_TakeInventory("Clip",1)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_JumpIfInventory("SamsaraCorridor7CAWAttackSilentSound",1,2)
        TNT1 A 0 A_PlaySound("C7/CAW",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireBullets(3,3,-1,random(5,18),"BlankPuff",FBF_NORANDOM|FBF_NOFLASH)
        TNT1 A 0 A_FireBullets(3,3,-1,random(3,10),"BlankPuffBloodless",FBF_NORANDOM|FBF_NOFLASH,256)
        TNT1 A 0 A_FireBullets(3,3,-1,random(3,10),"BlankPuffBloodless",FBF_NORANDOM|FBF_NOFLASH,64)
        stop

      PickupAttackCoop:
        TNT1 A 0 A_FireBullets(3,3,-1,random(4,18),"BlankPuffCoop",FBF_NORANDOM|FBF_NOFLASH)
        TNT1 A 0 A_FireBullets(3,3,-1,random(2,9),"BlankPuffBloodlessCoop",FBF_NORANDOM|FBF_NOFLASH,256)
        TNT1 A 0 A_FireBullets(3,3,-1,random(2,9),"BlankPuffBloodlessCoop",FBF_NORANDOM|FBF_NOFLASH,64)
        stop
    }
}
