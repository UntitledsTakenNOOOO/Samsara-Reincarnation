actor DGHasGrenadeLauncher : Boolean {}

ACTOR " GrenadeLauncher " : Weapon
{
	Radius 20
	Height 16
	Weapon.Selectionorder 2500
	+WEAPON.EXPLOSIVE
	+WEAPON.NOAUTOFIRE
	+WEAPON.NOAUTOAIM
	+UNDROPPABLE
	Weapon.AmmoUse 1
	Weapon.AmmoGive 0
	Weapon.AmmoType "RocketAmmo"
	Weapon.Kickback 100
	Inventory.PickupMessage "You got the grenade launcher!"
	Inventory.RestrictedTo "DoomguyPlayer"
	Tag "Grenade Launcher"
	States
	{
	Spawn:
		WDGL A -1
		stop

	// Original
	Ready:
		TNT1 A 0 A_JumpIfInventory("DoomClassMode",2,"ReadyStr")
		TNT1 A 0 A_JumpIfInventory("DoomClassMode",1,"Ready64")
		DGRL A 1 A_WeaponReady
		loop

	Deselect:
		TNT1 A 0 A_JumpIfInventory("DoomClassMode",2,"DeselectStr")
		TNT1 A 0 A_JumpIfInventory("DoomClassMode",1,"Deselect64")
		DGRL A 1 A_Lower
		loop

	Select:
		TNT1 A 0 A_JumpIfInventory("DoomClassMode",2,"SelectStr")
		TNT1 A 0 A_JumpIfInventory("DoomClassMode",1,"Select64")
		DGRL A 1 A_Raise
		loop

	Fire:
		TNT1 A 0 A_JumpIfInventory("DoomClassMode",2,"FireStr")
		TNT1 A 0 A_JumpIfInventory("DoomClassMode",1,"Fire64")
		TNT1 A 0 A_JumpIfInventory("SamsaraUsingSmoothAnims",1,"FireSmooth")
	FireVanilla:
		DGRL B 8 A_GunFlash
		DGRL B 12 A_GiveInventory("SamsaraDoomguyGrenadeLauncherAttackHandler")
	FireFinish:
		TNT1 A 0 A_ReFire
		goto Ready
		
	FireSmooth:
		PKGL A 2
		PKGL A 4 A_GunFlash("FlashSmooth")
		PKGL B 2
		TNT1 A 0 A_GiveInventory("SamsaraDoomguyGrenadeLauncherAttackHandler")
		TNT1 A 0 A_JumpIfInventory("SamsaraHasDoubleFiringSpeed",1,"FireSmoothRage")
		PKGL CD 3
		PKGL CB 2
		PKGL AA 1
		goto FireFinish

	FireSmoothRage:
		PKGL CD 3
		PKGL CB 2
		goto FireFinish

	Flash:
		DGRF A 3 Bright A_Light1
		DGRF B 4 Bright
		DGRF CD 4 Bright A_Light2
		goto LightDone
	FlashSmooth:
		PKGF AB 2 Bright A_Light1
		PKGF C 2 Bright
		PKGF DE 3 Bright A_Light2
		PKGF F 2 Bright
		goto LightDone

	// Doom 64 (has recoil visual and kickback)
	Ready64:
		64GL A 1 A_WeaponReady
		goto Ready

	Deselect64:
		64GL A 1 A_Lower
		goto Deselect

	Select64:
		64GL A 1 A_Raise
		goto Select

	Fire64:
		64GL B 8 A_GunFlash("Flash64")
		64GL B 13 A_GiveInventory("SamsaraDoomguy64GrenadeLauncherAttackHandler")
		goto FireFinish

	Flash64:
		64GM A 2 Bright A_Light1
		64GM B 4 Bright
		64GM CD 4 Bright A_Light2
		goto LightDone

	// Stronghold
	ReadyStr:
		DGRL C 1 A_WeaponReady
		goto Ready

	// Unpowered
	DeselectStr:
		TNT1 A 0 A_JumpIfInventory("PowerSamsaraDoomguyStrWeaponPower",1,"DeselectStrPowered")
		TNT1 A 0 A_JumpIfInventory("SamsaraHasPermaWeaponLevel2",1,"DeselectStrPowered")
	DeselectStrContinue:
		TNT1 A 0 A_Lower
		DGRL C 1 A_Lower
		goto Deselect

	SelectStr:
		TNT1 A 0 A_JumpIfInventory("PowerSamsaraDoomguyStrWeaponPower",1,"SelectStrPowered")
		TNT1 A 0 A_JumpIfInventory("SamsaraHasPermaWeaponLevel2",1,"SelectStrPowered")
	SelectStrContinue:
		TNT1 A 0 A_Raise
		DGRL C 1 A_Raise
		goto Select

	FireStr:
		DGRF E 4 Bright A_GunFlash("FlashStr")
		TNT1 A 0 A_GiveInventory("SamsaraDoomguyStrGrenadeLauncherAttackHandler")
		DGRF FGH 5 Bright
		goto FireFinish

	FlashStr:
		TNT1 A 4 A_Light1
		TNT1 A 5 A_Light2
		TNT1 A 5 A_Light1
		goto LightDone

	// Powered
	DeselectStrPowered:
		TNT1 A 0 A_Lower
		goto DeselectStrContinue

	SelectStrPowered:
		TNT1 A 0 A_Raise
		goto SelectStrContinue
	}
}

actor SamsaraDoomguyGrenadeLauncherAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("RocketAmmo",1,1)
        stop
        TNT1 A 0 A_TakeInventory("RocketAmmo",1)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_JumpIfInventory("SamsaraImprovedDoomguyBalance",1,"PickupAttackImproved")
      // Vanilla
      PickupAttackVanilla:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackVanillaCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackVanillaCoop")
        TNT1 A 0 A_JumpIfInventory("SamsaraAlternateSoundToken",2,"PickupAttackVanillaDMSoundPerkristan")
        TNT1 A 0 A_JumpIfInventory("SamsaraAlternateSoundToken",1,"PickupAttackVanillaDMSoundPSX")
      PickupAttackVanillaDM:
        TNT1 A 0 A_FireCustomMissile("DoomGrenadeVanilla",0,false,0,0,0,6.328125)
        stop

      PickupAttackVanillaDMSoundPSX:
        TNT1 A 0 A_FireCustomMissile("DoomGrenadeVanillaSoundPSX",0,false,0,0,0,6.328125)
        stop

      PickupAttackVanillaDMSoundPerkristan:
        TNT1 A 0 A_FireCustomMissile("DoomGrenadeVanillaSoundPerkristan",0,false,0,0,0,6.328125)
        stop

      PickupAttackVanillaCoop:
        TNT1 A 0 A_JumpIfInventory("SamsaraAlternateSoundToken",2,"PickupAttackVanillaCoopSoundPerkristan")
        TNT1 A 0 A_JumpIfInventory("SamsaraAlternateSoundToken",1,"PickupAttackVanillaCoopSoundPSX")
        TNT1 A 0 A_FireCustomMissile("DoomGrenadeVanillaCoop",0,false,0,0,0,6.328125)
        stop

      PickupAttackVanillaCoopSoundPSX:
        TNT1 A 0 A_FireCustomMissile("DoomGrenadeVanillaCoopSoundPSX",0,false,0,0,0,6.328125)
        stop

      PickupAttackVanillaCoopSoundPerkristan:
        TNT1 A 0 A_FireCustomMissile("DoomGrenadeVanillaCoopSoundPerkristan",0,false,0,0,0,6.328125)
        stop

      // Improved
      PickupAttackImproved:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackImprovedCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackImprovedCoop")
        TNT1 A 0 A_JumpIfInventory("SamsaraAlternateSoundToken",2,"PickupAttackImprovedDMSoundPerkristan")
        TNT1 A 0 A_JumpIfInventory("SamsaraAlternateSoundToken",1,"PickupAttackImprovedDMSoundPSX")
      PickupAttackImprovedDM:
        TNT1 A 0 A_FireCustomMissile("DoomGrenade",0,false,0,0,0,6.328125)
        stop

      PickupAttackImprovedDMSoundPSX:
        TNT1 A 0 A_FireCustomMissile("DoomGrenadeSoundPSX",0,false,0,0,0,6.328125)
        stop

      PickupAttackImprovedDMSoundPerkristan:
        TNT1 A 0 A_FireCustomMissile("DoomGrenadeSoundPerkristan",0,false,0,0,0,6.328125)
        stop

      PickupAttackImprovedCoop:
        TNT1 A 0 A_JumpIfInventory("SamsaraAlternateSoundToken",2,"PickupAttackImprovedCoopSoundPerkristan")
        TNT1 A 0 A_JumpIfInventory("SamsaraAlternateSoundToken",1,"PickupAttackImprovedCoopSoundPSX")
        TNT1 A 0 A_FireCustomMissile("DoomGrenadeCoop",0,false,0,0,0,6.328125)
        stop

      PickupAttackImprovedCoopSoundPSX:
        TNT1 A 0 A_FireCustomMissile("DoomGrenadeCoopSoundPSX",0,false,0,0,0,6.328125)
        stop

      PickupAttackImprovedCoopSoundPerkristan:
        TNT1 A 0 A_FireCustomMissile("DoomGrenadeCoopSoundPerkristan",0,false,0,0,0,6.328125)
        stop
    }
}

actor DoomGrenade // does not inherit from Grenade for source port equality, uses zdoom's physics
{
	Projectile
	Radius 8
	Height 8
	Speed 25
	Damage (100)
	DamageType "DoomDamageTypeExplosive"
	ReactionTime 87 // zan value, didn't use A_Countdown in zdoom
	Gravity 0.25
	BounceType "Doom" // replaces +DOOMBOUNCE in zan, plays sounds as intended
	SeeSound "doomguy/grenlf"
	BounceSound "doomguy/grbnce" // no longer uses +USESTBOUNCESOUND from zan
	DeathSound "doomguy/grenlx"
	Decal "Scorch"
	Obituary "$SAMSARA_DOOMGUY_OB_SLOT5S"
	//SelfObituary "%o tripped %p own grenade."
	-BLOODSPLATTER
	+DEHEXPLOSION
	+EXPLODEONDEATH
	+GRENADETRAIL
	-NOGRAVITY
	+RANDOMIZE
	States
	{
	Spawn:
		DGRN A 1 Bright A_Countdown
		loop

	Death:
		TNT1 A 0 A_ChangeFlag("NOGRAVITY",true)
		TNT1 A 0 A_Explode
	DeathFinish:
		DMSL B 8 Bright
		DMSL C 6 Bright
		DMSL D 4 Bright
		stop
	}
}

actor DoomGrenadeCoop : DoomGrenade
{
	Species "Player"
	+THRUSPECIES
}

actor DoomGrenadeVanilla : DoomGrenade { Damage 20 }

actor DoomGrenadeVanillaCoop : DoomGrenadeVanilla
{
	Species "Player"
	+THRUSPECIES
}

actor DoomGrenadeSoundPSX : DoomGrenade { SeeSound "doom64guy/grenlf" BounceSound "doom64guy/grbnce" DeathSound "doom64guy/grenlx" }
actor DoomGrenadeCoopSoundPSX : DoomGrenadeCoop { SeeSound "doom64guy/grenlf" BounceSound "doom64guy/grbnce" DeathSound "doom64guy/grenlx" }
actor DoomGrenadeVanillaSoundPSX : DoomGrenadeVanilla { SeeSound "doom64guy/grenlf" BounceSound "doom64guy/grbnce" DeathSound "doom64guy/grenlx" }
actor DoomGrenadeVanillaCoopSoundPSX : DoomGrenadeVanillaCoop { SeeSound "doom64guy/grenlf" BounceSound "doom64guy/grbnce" DeathSound "doom64guy/grenlx" }

actor DoomGrenadeSoundPerkristan : DoomGrenade { SeeSound "doomguy/perkristan/grenlf" DeathSound "doomguy/perkristan/grenlx" }
actor DoomGrenadeCoopSoundPerkristan : DoomGrenadeCoop { SeeSound "doomguy/perkristan/grenlf" DeathSound "doomguy/perkristan/grenlx" }
actor DoomGrenadeVanillaSoundPerkristan : DoomGrenadeVanilla { SeeSound "doomguy/perkristan/grenlf" DeathSound "doomguy/perkristan/grenlx" }
actor DoomGrenadeVanillaCoopSoundPerkristan : DoomGrenadeVanillaCoop { SeeSound "doomguy/perkristan/grenlf" DeathSound "doomguy/perkristan/grenlx" }

actor SamsaraDoomguy64GrenadeLauncherAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("RocketAmmo",1,1)
        stop
        TNT1 A 0 A_TakeInventory("RocketAmmo",1)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_JumpIfInventory("SamsaraImprovedDoomguyBalance",1,"PickupAttackImproved")
      // Vanilla
      PickupAttackVanilla:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackVanillaCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackVanillaCoop")
      PickupAttackVanillaDM:
        TNT1 A 0 A_FireCustomMissile("Doom64GrenadeVanilla",0,false,0,0,0,6.328125)
        goto PickupFinish

      PickupAttackVanillaCoop:
        TNT1 A 0 A_FireCustomMissile("Doom64GrenadeVanillaCoop",0,false,0,0,0,6.328125)
        goto PickupFinish

      // Improved
      PickupAttackImproved:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackImprovedCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackImprovedCoop")
      PickupAttackImprovedDM:
        TNT1 A 0 A_FireCustomMissile("Doom64Grenade",0,false,0,0,0,6.328125)
        goto PickupFinish

      PickupAttackImprovedCoop:
        TNT1 A 0 A_FireCustomMissile("Doom64GrenadeCoop",0,false,0,0,0,6.328125)
        goto PickupFinish

      // Finishing stuff
      PickupFinish:
        TNT1 A 0 A_Recoil(2)
        TNT1 A 0 ACS_NamedExecuteAlways("SamsaraRecoil", 0, 2, 0, 20)
        stop
    }
}

actor Doom64Grenade : DoomGrenade
{
	SeeSound "doom64guy/grenlf"
	BounceSound "doom64guy/grbnce"
	DeathSound "doom64guy/grenlx"
	XScale 0.81
	YScale 0.68
	-DEHEXPLOSION
	-GRENADETRAIL
	States
	{
	Spawn:
	SpawnLoopA:
		64GR A 1 Bright Light("ROCKET64") A_SpawnItemEx("Doom64RocketSmokeTrail")
		TNT1 A 0 A_Countdown
	SpawnLoopB:
		64GR A 1 Bright Light("ROCKET64")
		TNT1 A 0 A_Countdown
		goto SpawnLoopA

	Death:
		TNT1 A 0 A_ChangeFlag("NOGRAVITY",true)
		TNT1 A 0 A_Explode
	DeathFinish:
		64MI B 8 Bright Light("ROCKET64_X1")
		64MI C 6 Bright Light("ROCKET64_X2") A_FadeOut(0.2)
		64MI D 3 Bright Light("ROCKET64_X3") A_FadeOut(0.2)
		64MI E 3 Bright Light("ROCKET64_X4") A_FadeOut(0.2)
		64MI F 3 Bright Light("ROCKET64_X5") A_FadeOut(0.2)
		stop
	}
}

actor Doom64GrenadeCoop : Doom64Grenade
{
	Species "Player"
	+THRUSPECIES
}

actor Doom64GrenadeVanilla : Doom64Grenade { Damage 20 }

actor Doom64GrenadeVanillaCoop : Doom64GrenadeVanilla
{
	Species "Player"
	+THRUSPECIES
}

// Attack handler

actor SamsaraDoomguyStrGrenadeLauncherAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("RocketAmmo",1,1)
        stop
        TNT1 A 0 A_TakeInventory("RocketAmmo",1)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_JumpIfInventory("PowerSamsaraDoomguyStrWeaponPower",1,"PickupAttackPowered")
        TNT1 A 0 A_JumpIfInventory("SamsaraHasPermaWeaponLevel2",1,"PickupAttackPowered")
      // Unpowered
      PickupAttackUnpowered:
        TNT1 A 0 A_JumpIfInventory("SamsaraImprovedDoomguyBalance",1,"PickupAttackUnpoweredImproved")
      // Vanilla
      PickupAttackUnpoweredVanilla:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackUnpoweredVanillaCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackUnpoweredVanillaCoop")
        TNT1 A 0 A_JumpIfInventory("SamsaraAlternateSoundToken",1,"PickupAttackUnpoweredVanillaDMSoundDE")
      PickupAttackUnpoweredVanillaDM:
        TNT1 A 0 A_FireCustomMissile("SamsaraDoomguyStrGrenade",0,false,0,0,0,6.328125)
        stop

      PickupAttackUnpoweredVanillaDMSoundDE:
        TNT1 A 0 A_FireCustomMissile("SamsaraDoomguyStrGrenadeSoundDE",0,false,0,0,0,6.328125)
        stop

      PickupAttackUnpoweredVanillaCoop:
        TNT1 A 0 A_JumpIfInventory("SamsaraAlternateSoundToken",1,"PickupAttackUnpoweredVanillaCoopSoundDE")
        TNT1 A 0 A_FireCustomMissile("SamsaraDoomguyStrGrenadeCoop",0,false,0,0,0,6.328125)
        stop

      PickupAttackUnpoweredVanillaCoopSoundDE:
        TNT1 A 0 A_FireCustomMissile("SamsaraDoomguyStrGrenadeCoopSoundDE",0,false,0,0,0,6.328125)
        stop

      // Improved
      PickupAttackUnpoweredImproved:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackUnpoweredImprovedCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackUnpoweredImprovedCoop")
        TNT1 A 0 A_JumpIfInventory("SamsaraAlternateSoundToken",1,"PickupAttackUnpoweredImprovedDMSoundDE")
      PickupAttackUnpoweredImprovedDM:
        TNT1 A 0 A_FireCustomMissile("SamsaraDoomguyStrGrenadeImproved",0,false,0,0,0,6.328125)
        stop

      PickupAttackUnpoweredImprovedDMSoundDE:
        TNT1 A 0 A_FireCustomMissile("SamsaraDoomguyStrGrenadeImprovedSoundDE",0,false,0,0,0,6.328125)
        stop

      PickupAttackUnpoweredImprovedCoop:
        TNT1 A 0 A_JumpIfInventory("SamsaraAlternateSoundToken",1,"PickupAttackUnpoweredImprovedCoopSoundDE")
        TNT1 A 0 A_FireCustomMissile("SamsaraDoomguyStrGrenadeImprovedCoop",0,false,0,0,0,6.328125)
        stop

      PickupAttackUnpoweredImprovedCoopSoundDE:
        TNT1 A 0 A_FireCustomMissile("SamsaraDoomguyStrGrenadeImprovedCoopSoundDE",0,false,0,0,0,6.328125)
        stop

      // Powered
      PickupAttackPowered:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackPoweredCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackPoweredCoop")
        TNT1 A 0 A_JumpIfInventory("SamsaraAlternateSoundToken",1,"PickupAttackPoweredDMSoundDE")
      PickupAttackPoweredDM:
        TNT1 A 0 A_FireCustomMissile("SamsaraDoomguyStrPowerClusterGrenade",0,false,0,0,0,6.328125)
        stop

      PickupAttackPoweredDMSoundDE:
        TNT1 A 0 A_FireCustomMissile("SamsaraDoomguyStrPowerClusterGrenadeSoundDE",0,false,0,0,0,6.328125)
        stop

      PickupAttackPoweredCoop:
        TNT1 A 0 A_JumpIfInventory("SamsaraAlternateSoundToken",1,"PickupAttackPoweredCoopSoundDE")
        TNT1 A 0 A_FireCustomMissile("SamsaraDoomguyStrPowerClusterGrenadeCoop",0,false,0,0,0,6.328125)
        stop

      PickupAttackPoweredCoopSoundDE:
        TNT1 A 0 A_FireCustomMissile("SamsaraDoomguyStrPowerClusterGrenadeSoundDE",0,false,0,0,0,6.328125)
        stop
    }
}

// Grenade

actor SamsaraDoomguyStrGrenade : DoomGrenadeVanilla
{
	SeeSound "doomguy/stronghold/grenlf"
	BounceSound "doomguy/stronghold/grbnce"
	DeathSound "doomguy/stronghold/grenlx"
	-DEHEXPLOSION
	States
	{
	Spawn:
		DGRN B 1 Bright Light("NEWGRENADE") A_Countdown
		loop

	Death:
		TNT1 A 0 A_ChangeFlag("NOGRAVITY",true)
		TNT1 A 0 A_SetTranslucent(0.67,1)
		TNT1 A 0 A_Explode
	DeathFinish:
		DMSL B 8 Bright Light("ROCKET_X1")
		DMSL C 6 Bright Light("ROCKET_X2")
		DMSL D 4 Bright Light("ROCKET_X3")
		stop
	}
}

actor SamsaraDoomguyStrGrenadeCoop : SamsaraDoomguyStrGrenade
{
	Species "Player"
	+THRUSPECIES
}

actor SamsaraDoomguyStrGrenadeSoundDE : SamsaraDoomguyStrGrenade
{
	SeeSound "doomguy/demoneclipse/grenlf"
	BounceSound "doomguy/demoneclipse/grbnce"
	DeathSound "doomguy/demoneclipse/grenlx"
}

actor SamsaraDoomguyStrGrenadeCoopSoundDE : SamsaraDoomguyStrGrenadeSoundDE
{
	Species "Player"
	+THRUSPECIES
}

actor SamsaraDoomguyStrGrenadeImproved : SamsaraDoomguyStrGrenade { Damage (100) }

actor SamsaraDoomguyStrGrenadeImprovedCoop : SamsaraDoomguyStrGrenadeImproved
{
	Species "Player"
	+THRUSPECIES
}

actor SamsaraDoomguyStrGrenadeImprovedSoundDE : SamsaraDoomguyStrGrenadeImproved
{
	SeeSound "doomguy/demoneclipse/grenlf"
	BounceSound "doomguy/demoneclipse/grbnce"
	DeathSound "doomguy/demoneclipse/grenlx"
}

actor SamsaraDoomguyStrGrenadeImprovedCoopSoundDE : SamsaraDoomguyStrGrenadeImprovedSoundDE
{
	Species "Player"
	+THRUSPECIES
}

// Powered grenade

actor SamsaraDoomguyStrPowerClusterGrenade : SamsaraDoomguyStrGrenade
{
	Speed 32
	Damage 10
	Scale 1.25
	+FORCERADIUSDMG
	States
	{
	Spawn:
		DGRN B 1 Bright Light("POWERGRENADE") A_Countdown
		loop

	Death:
		TNT1 A 0 A_ChangeFlag("NOGRAVITY",true)
		TNT1 A 0 A_SetTranslucent(0.67,1)
		TNT1 A 0 A_CustomMissile("SamsaraDoomguyStrPowerTinyGrenade",0,0,0,CMF_AIMDIRECTION|CMF_TRACKOWNER,random(60,80))
		TNT1 A 0 A_CustomMissile("SamsaraDoomguyStrPowerTinyGrenade",0,0,72,CMF_AIMDIRECTION|CMF_TRACKOWNER,random(60,80))
		TNT1 A 0 A_CustomMissile("SamsaraDoomguyStrPowerTinyGrenade",0,0,144,CMF_AIMDIRECTION|CMF_TRACKOWNER,random(60,80))
		TNT1 A 0 A_CustomMissile("SamsaraDoomguyStrPowerTinyGrenade",0,0,216,CMF_AIMDIRECTION|CMF_TRACKOWNER,random(60,80))
		TNT1 A 0 A_CustomMissile("SamsaraDoomguyStrPowerTinyGrenade",0,0,288,CMF_AIMDIRECTION|CMF_TRACKOWNER,random(60,80))
		TNT1 A 0 A_Explode(32,192,0)
		TNT1 A 0 A_ChangeFlag("FORCERADIUSDMG",false)
		TNT1 A 0 A_Explode(32,128)
	DeathFinish:
		DMSL B 8 Bright Light("POWERHOMROCKET_X1")
		DMSL C 6 Bright Light("POWERHOMROCKET_X2")
		DMSL D 4 Bright Light("POWERHOMROCKET_X3")
		stop
	}
}

actor SamsaraDoomguyStrPowerClusterGrenadeCoop : SamsaraDoomguyStrPowerClusterGrenade
{
	Species "Player"
	+THRUSPECIES
	States
	{
	Death:
		TNT1 A 0 A_ChangeFlag("NOGRAVITY",true)
		TNT1 A 0 A_SetTranslucent(0.67,1)
		TNT1 A 0 A_CustomMissile("SamsaraDoomguyStrPowerTinyGrenadeCoop",0,0,0,CMF_AIMDIRECTION|CMF_TRACKOWNER,random(60,80))
		TNT1 A 0 A_CustomMissile("SamsaraDoomguyStrPowerTinyGrenadeCoop",0,0,72,CMF_AIMDIRECTION|CMF_TRACKOWNER,random(60,80))
		TNT1 A 0 A_CustomMissile("SamsaraDoomguyStrPowerTinyGrenadeCoop",0,0,144,CMF_AIMDIRECTION|CMF_TRACKOWNER,random(60,80))
		TNT1 A 0 A_CustomMissile("SamsaraDoomguyStrPowerTinyGrenadeCoop",0,0,216,CMF_AIMDIRECTION|CMF_TRACKOWNER,random(60,80))
		TNT1 A 0 A_CustomMissile("SamsaraDoomguyStrPowerTinyGrenadeCoop",0,0,288,CMF_AIMDIRECTION|CMF_TRACKOWNER,random(60,80))
		TNT1 A 0 A_Explode(32,192,0)
		TNT1 A 0 A_ChangeFlag("FORCERADIUSDMG",false)
		TNT1 A 0 A_Explode(32,128)
		goto DeathFinish
	}
}

actor SamsaraDoomguyStrPowerClusterGrenadeSoundDE : SamsaraDoomguyStrPowerClusterGrenade
{
	SeeSound "doomguy/demoneclipse/grenlf"
	BounceSound "doomguy/demoneclipse/grbnce"
	DeathSound "doomguy/demoneclipse/grenlx"
	States
	{
	Death:
		TNT1 A 0 A_ChangeFlag("NOGRAVITY",true)
		TNT1 A 0 A_SetTranslucent(0.67,1)
		TNT1 A 0 A_CustomMissile("SamsaraDoomguyStrPowerTinyGrenadeSoundDE",0,0,0,CMF_AIMDIRECTION|CMF_TRACKOWNER,random(60,80))
		TNT1 A 0 A_CustomMissile("SamsaraDoomguyStrPowerTinyGrenadeSoundDE",0,0,72,CMF_AIMDIRECTION|CMF_TRACKOWNER,random(60,80))
		TNT1 A 0 A_CustomMissile("SamsaraDoomguyStrPowerTinyGrenadeSoundDE",0,0,144,CMF_AIMDIRECTION|CMF_TRACKOWNER,random(60,80))
		TNT1 A 0 A_CustomMissile("SamsaraDoomguyStrPowerTinyGrenadeSoundDE",0,0,216,CMF_AIMDIRECTION|CMF_TRACKOWNER,random(60,80))
		TNT1 A 0 A_CustomMissile("SamsaraDoomguyStrPowerTinyGrenadeSoundDE",0,0,288,CMF_AIMDIRECTION|CMF_TRACKOWNER,random(60,80))
		TNT1 A 0 A_Explode(32,192,0)
		TNT1 A 0 A_ChangeFlag("FORCERADIUSDMG",false)
		TNT1 A 0 A_Explode(32,128)
	    goto DeathFinish
	}
}

actor SamsaraDoomguyStrPowerClusterGrenadeCoopSoundDE : SamsaraDoomguyStrPowerClusterGrenadeSoundDE
{
	Species "Player"
	+THRUSPECIES
	States
	{
	Death:
		TNT1 A 0 A_ChangeFlag("NOGRAVITY",true)
		TNT1 A 0 A_SetTranslucent(0.67,1)
		TNT1 A 0 A_CustomMissile("SamsaraDoomguyStrPowerTinyGrenadeCoopSoundDE",0,0,0,CMF_AIMDIRECTION|CMF_TRACKOWNER,random(60,80))
		TNT1 A 0 A_CustomMissile("SamsaraDoomguyStrPowerTinyGrenadeCoopSoundDE",0,0,72,CMF_AIMDIRECTION|CMF_TRACKOWNER,random(60,80))
		TNT1 A 0 A_CustomMissile("SamsaraDoomguyStrPowerTinyGrenadeCoopSoundDE",0,0,144,CMF_AIMDIRECTION|CMF_TRACKOWNER,random(60,80))
		TNT1 A 0 A_CustomMissile("SamsaraDoomguyStrPowerTinyGrenadeCoopSoundDE",0,0,216,CMF_AIMDIRECTION|CMF_TRACKOWNER,random(60,80))
		TNT1 A 0 A_CustomMissile("SamsaraDoomguyStrPowerTinyGrenadeCoopSoundDE",0,0,288,CMF_AIMDIRECTION|CMF_TRACKOWNER,random(60,80))
		TNT1 A 0 A_Explode(32,192,0)
		TNT1 A 0 A_ChangeFlag("FORCERADIUSDMG",false)
		TNT1 A 0 A_Explode(32,128)
		goto DeathFinish
	}
}

// Powered tiny grenade

actor SamsaraDoomguyStrPowerTinyGrenade : SamsaraDoomguyStrPowerClusterGrenade
{
	Radius 4
	Height 4
	Speed 8
	Damage 5
	Gravity 0.75
	BounceFactor 0.3
	Scale 0.75
	SeeSound ""
	BounceSound "doomguy/stronghold/tinygrbnce"
	DeathSound "doomguy/stronghold/tinygrenlx"
	States
	{
	Spawn:
		DGRN B 1 Bright NoDelay Light("POWERGRENADETINY") ThrustThingZ(0,random(0,8),random(0,1),1)
		DGRN B 1 Bright Light("POWERGRENADETINY") A_Countdown
		wait

	Death:
		TNT1 A 0 A_ChangeFlag("NOGRAVITY",true)
		TNT1 A 0 A_SetTranslucent(0.67,1)
		TNT1 A 0 A_Explode(32,128,0)
		TNT1 A 0 A_ChangeFlag("FORCERADIUSDMG",false)
		TNT1 A 0 A_Explode(32,96)
	DeathFinish:
		DMSL B 8 Bright Light("POWERGRENADETINY_X1")
		DMSL C 6 Bright Light("POWERGRENADETINY_X2")
		DMSL D 4 Bright Light("POWERGRENADETINY_X3")
		stop
	}
}

actor SamsaraDoomguyStrPowerTinyGrenadeCoop : SamsaraDoomguyStrPowerTinyGrenade
{
	Species "Player"
	+THRUSPECIES
}

actor SamsaraDoomguyStrPowerTinyGrenadeSoundDE : SamsaraDoomguyStrPowerTinyGrenade
{
	BounceSound "doomguy/demoneclipse/tinygrbnce"
	DeathSound "doomguy/demoneclipse/tinygrenlx"
}

actor SamsaraDoomguyStrPowerTinyGrenadeCoopSoundDE : SamsaraDoomguyStrPowerTinyGrenadeSoundDE
{
	Species "Player"
	+THRUSPECIES
}
