ACTOR SoF_Knife : SoF_Weapon
{
	Weapon.SlotNumber 1
	Weapon.SelectionOrder 2000
	Obituary "%o was slashed open by %k"
	Inventory.Pickupmessage "Combat Knife"
	Inventory.PickupSound "SoF/Weapons/knife/Ready"
	Weapon.AmmoType2 "SoF_Knives"
	Weapon.AmmoUse2 "0"
	Tag "The Pig Sticker"
	States
	{
		Ready:
			TNT1 A 0
			SF12 AB 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			SF12 C 0 A_PlaySound("SoF/Weapons/knife/Ready",CHAN_AUTO)
			SF12 CDE 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)		
		ReadyLoop:
		IDLE:	
			SF0F A 0 A_Jump(40,"IDLEFINGER_1","IDLEFINGER_2")
			SF0F ABCDEFGHIJKLMNOPQRSTUVW 2 A_WeaponReady(WRF_ALLOWRELOAD)
			Loop
		IDLEFINGER_1:
			SF10 ABCDEFG 2 A_WeaponReady(WRF_ALLOWRELOAD)
			SF10 G 0 A_PlaySound("SoF/Weapons/knife/Twirl",CHAN_AUTO,0.75)
			SF10 HIJKLMNOPQRSTUVW 2 A_WeaponReady(WRF_ALLOWRELOAD)
			Goto ReadyLoop
		IDLEFINGER_2:
			SF11 ABCDEFGHIJKLMNOPQRS 2 A_WeaponReady(WRF_ALLOWRELOAD)
			Goto ReadyLoop
		Deselect:
			SF00 A 0 A_StopSound(CHAN_WEAPON)
			SF00 ABCD 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)	
			SF00 H 0 A_PlaySound("SoF/Weapons/knife/Done",CHAN_AUTO)
			Goto DeselectLoop
		Fire:
			"####" "#" 0 A_Jump(192,"FIRE1_2A")
		FIRE1_1A:
			SF01 A 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			SF01 B 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			SF01 C 0 A_GiveInventory("SoF_Knife_FireToken")
			SF01 C 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			SF01 D 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			SF01 E 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			SF01 F 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		FIRE1_1B:
			SF02 A 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			SF02 B 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			SF02 C 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			SF02 D 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			SF02 E 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			TNT1 A 0 A_Refire("HandleLoop")
			Goto ReadyLoop
		FIRE1_2A:
			//SF04 A 0 A_GiveInventory("SoF_Knife_FireToken")
			SF04 A 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			SF04 B 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			SF04 C 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			SF04 D 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			SF04 E 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			SF04 F 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			Goto SlashRight
		HandleLoop:
			TNT1 A 0 A_Jump(170,"SlashRight")
			Goto SlashLeft
		SlashLeft:
			TNT1 A 0 A_Jump(256,"FIRE1_2SL_TO_SR","FIRE1_2DL_TO_UR","FIRE1_2UL_TO_DR")
			Goto FIRE1_2UL_TO_DR
		FIRE1_2SL_TO_SR:
			SF09 A 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			SF09 B 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			SF09 C 0 A_GiveInventory("SoF_Knife_FireToken")
			SF09 C 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			SF09 D 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			SF09 E 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			SF07 A 0 A_Refire("HandleLoop")
			Goto FIRE1_2L_TO_IDLE
		FIRE1_2DL_TO_UR:
			SF05 A 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			SF05 B 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			SF05 C 0 A_GiveInventory("SoF_Knife_FireToken")
			SF05 C 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			SF05 D 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			SF05 E 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			SF07 A 0 A_Refire("HandleLoop")
			Goto FIRE1_2L_TO_IDLE
		FIRE1_2UL_TO_DR:
			SF0B A 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			SF0B B 0 A_GiveInventory("SoF_Knife_FireToken")
			SF0B B 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			SF0B C 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			SF0B D 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			SF0B E 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			SF07 A 0 A_Refire("HandleLoop")
			Goto FIRE1_2L_TO_IDLE
		//Restored animation
		FIRE1_2L_TO_IDLE:
			SF07 A 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			SF07 B 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			SF07 C 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			SF07 D 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			Goto ReadyLoop
		SlashRight:
			TNT1 A 0 A_Jump(256,"FIRE1_2SR_TO_SL","FIRE1_2DR_TO_UL","FIRE1_2UR_TO_DL")
			Goto FIRE1_2SR_TO_SL
		FIRE1_2SR_TO_SL:
			SF0A A 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			SF0A B 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			SF0A C 0 A_GiveInventory("SoF_Knife_FireToken")
			SF0A C 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			SF0A D 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			SF0A E 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			SF08 A 0 A_Refire("HandleLoop")
			Goto FIRE1_2R_TO_IDLE
		FIRE1_2DR_TO_UL:
			SF06 A 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			SF06 B 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			SF06 C 0 A_GiveInventory("SoF_Knife_FireToken")
			SF06 C 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			SF06 D 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			SF06 E 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			SF08 A 0 A_Refire("HandleLoop")
			Goto FIRE1_2R_TO_IDLE
		FIRE1_2UR_TO_DL:
			SF0C A 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			SF0C B 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			SF0C C 0 A_GiveInventory("SoF_Knife_FireToken")
			SF0C C 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			SF0C D 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			SF0C E 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			SF08 A 0 A_Refire("HandleLoop")
			Goto FIRE1_2R_TO_IDLE
		//Restored animation
		FIRE1_2R_TO_IDLE:
			SF08 A 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			SF08 B 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			SF08 C 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			SF08 D 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			Goto ReadyLoop
		
		AltFire:
			"####" "#" 0 A_JumpIfInventory("SoF_Knives",1,1)
			Goto ReadyLoop
		FIRE2_1:
			SF0D ABCDEFGH 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			SF0D F 0 A_GiveInventory("SoF_Knife_AltFireToken",1)
			SF0D FGH 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		FIRE2_2:
			SF0E ABCDEFGHI 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			Goto ReadyLoop
		
		Spawn:
			SFPU A -1
			Stop
	}
}

Actor SoF_Knife_FireToken : Trigger
{
	States
	{
		Pickup:
			TNT1 A 0 A_PlaySound("SoF/Weapons/knife/Swing",CHAN_WEAPON)
			TNT1 A 0 A_FireBullets(0,0,-1,0,"SoF_KnifePuff",FBF_NORANDOM,48)
			Stop
	}
}

Actor SoF_Knife_AltFireToken : Trigger
{
	States
	{
		Pickup:
			TNT1 A 0 A_TakeInventory("SoF_Knives",1,TIF_NOTAKEINFINITE)
			TNT1 A 0 A_FireCustomMissile("SoF_KnifeProjectile",0,0,4,4)
			Stop
	}
}

Actor SoF_Knives : Ammo
{
	Inventory.Amount 10
	Inventory.MaxAmount 10
	Ammo.BackpackAmount 2
}

Actor SoF_KnifePuff : SoF_BulletPuff 
{ 
	args 8 
	DamageType "SoFKnife"
	Decal "SoFKnifeSlash" 
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 A 0 A_PlaySound("SoF/Weapons/knife/Impact",CHAN_5)
			TNT1 A 0 A_Explode(CallACS("SoF_BulletDamage",args[0]),4,!XF_HURTSOURCE,0,4)
			SF00 BBBBBB 0 A_SpawnItemEx("SoF_BulletSpark",0,0,0,frandom(-6,6),0,frandom(-6,6),frandom(0,360))
			SF00 BB 0 A_CustomMissile("SoF_BulletMetalChunkTiny",0,0,frandom(0,360),CMF_AIMDIRECTION,frandom(-90,90))
			SF00 B -1 Bright ACS_NamedExecuteWithResult("SoF_FX",1,2,4096)
			Stop
		Melee:
		Crash:
			TNT1 A 0 A_PlaySound("SoF/Weapons/knife/Impact",CHAN_5)
			TNT1 A 0 A_Explode(CallACS("SoF_BulletDamage",args[0]),4,!XF_HURTSOURCE,0,4)
			SF00 BBBBBB 0 A_SpawnItemEx("SoF_BulletSpark",0,0,0,frandom(-6,6),0,frandom(-6,6),frandom(0,360))
			SF00 BB 0 A_CustomMissile("SoF_BulletWallChunkTiny",0,0,frandom(0,360),CMF_AIMDIRECTION,frandom(-90,90))
			SF00 B -1 Bright ACS_NamedExecuteWithResult("SoF_FX",1,2,4096)
			Stop
		XDeath:
			TNT1 A 0 A_PlaySound("SoF/Impact/Gore/HitFlesh",CHAN_AUTO,0.75)
			TNT1 A 0 ACS_NamedExecuteWithResult("SoF_BloodGenerator_Offsets")
			TNT1 A 0 A_Explode(CallACS("SoF_BulletDamage",args[0]),4,!XF_HURTSOURCE,0,4)
			TNT1 A 1 A_GiveInventory("SoF_BloodKnifeGenerator",1,AAPTR_TRACER)
			Stop
	}
}

Actor SoF_KnifeProjectile
{
	Height 4
	Radius 2
	Speed 24
	args 30
	Projectile
	DamageType "SoFKnifeThrow"
	Scale 0.33
	Gravity 0.33
	+BLOODLESSIMPACT
	-NOGRAVITY
	+NOEXTREMEDEATH
	+FORCERADIUSDMG
	+HITTRACER
	+NODAMAGETHRUST
	+FORCERADIUSDMG
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 A 0 A_PlaySound("SoF/Weapons/knife/ThrowLoop",CHAN_AUTO,1.0,true)
		SpawnLoop:
			SF00 A 1 A_SetPitch(pitch+30)
			Loop
		Melee:
		Death:
			TNT1 A 0 A_SpawnItemEx("SoF_KnifePickup",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
			TNT1 A 0 A_StopSound(CHAN_AUTO)
			TNT1 A 0 A_NoGravity
			TNT1 A 0 A_PlaySound("SoF/Weapons/knife/Impact",CHAN_5)
			TNT1 A 0 A_Explode(CallACS("SoF_BulletDamage",args[0]),4,!XF_HURTSOURCE,0,4)
			SF00 BBBBBB 0 A_SpawnItemEx("SoF_BulletSpark",0,0,0,frandom(-6,6),0,frandom(-6,6),frandom(0,360))
			SF00 BB 0 A_CustomMissile("SoF_BulletMetalChunkTiny",0,0,frandom(0,360),CMF_AIMDIRECTION,frandom(-90,90))
			SF00 B -1 Bright ACS_NamedExecuteWithResult("SoF_FX",1,2,4096)
			Stop
		Crash:
			TNT1 A 0 A_SpawnItemEx("SoF_KnifePickupHitActor",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
			TNT1 A 0 A_StopSound(CHAN_AUTO)
			TNT1 A 0 A_NoGravity
			TNT1 A 0 A_PlaySound("SoF/Weapons/knife/Impact",CHAN_5)
			TNT1 A 0 A_Explode(CallACS("SoF_BulletDamage",args[0]),4,!XF_HURTSOURCE,0,4)
			SF00 BBBBBB 0 A_SpawnItemEx("SoF_BulletSpark",0,0,0,frandom(-6,6),0,frandom(-6,6),frandom(0,360))
			SF00 BB 0 A_CustomMissile("SoF_BulletWallChunkTiny",0,0,frandom(0,360),CMF_AIMDIRECTION,frandom(-90,90))
			SF00 B -1 Bright ACS_NamedExecuteWithResult("SoF_FX",1,2,4096)
			Stop
		XDeath:
			TNT1 A 0 A_SpawnItemEx("SoF_KnifePickupHitActor",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
			TNT1 A 0 A_StopSound(CHAN_AUTO)
			TNT1 A 0 A_NoGravity
			TNT1 A 0 A_PlaySound("SoF/Impact/Gore/HitFlesh",CHAN_AUTO,0.75)
			TNT1 A 0 ACS_NamedExecuteWithResult("SoF_BloodGenerator_Offsets")
			TNT1 A 0 A_Explode(CallACS("SoF_BulletDamage",args[0]),4,!XF_HURTSOURCE,0,4)
			TNT1 A 1 A_GiveInventory("SoF_BloodKnifeGenerator",1,AAPTR_TRACER)
			Stop
	}
}

Actor SoF_KnifePickup : CustomInventory
{
	Height 4
	Radius 2
	Inventory.PickupMessage "Combat Knife"
	Inventory.PickupSound "SoF/Weapons/knife/Ready"
	+NOGRAVITY
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 A 0 A_JumpIf(abs(z - floorz) < 6,"SpawnFloor")
			TNT1 A 0 A_JumpIf(abs(ceilingz - z) < 6, "SpawnCeiling")
			SF00 A 0
		SpawnLoop:
			"####" "#" 1
			Loop
		SpawnFloor:
			SF00 B 0 A_SetPitch(-90)
			Goto SpawnLoop
		SpawnCeiling:
			SF00 C 0 A_SetPitch(90)
			Goto SpawnLoop
		Pickup:
			TNT1 A 0 A_GiveInventory("SoF_Knives",1)
			Stop
	}
}

Actor SoF_KnifePickupHitActor : SoF_KnifePickup
{
	-NOGRAVITY
	States
	{
		Spawn:
			SF00 A 1
			Loop
	}
}