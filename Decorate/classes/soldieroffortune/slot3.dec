ACTOR SoF_Shotgun : SoF_Weapon
{
	Weapon.SlotNumber 3
	Weapon.SelectionOrder 800
	Weapon.AmmoUse1 0
	Weapon.AmmoGive1 0
	Weapon.AmmoType1 "SoF_ShotgunMagazine"
	Weapon.AmmoUse2 0
	Weapon.AmmoGive2 12
	Weapon.AmmoType2 "Shell"
	Obituary "%o was peppered by %k's shotgun"
	Inventory.Pickupmessage "12 Gauge Shotgun"
	Tag "B-42 (BERSERKER)"
	States
	{
		Ready:
			TNT1 A 0
			SF06 A 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			SF06 B 0 A_PlaySound("SoF/Weapons/shotgun/Rattle",CHAN_AUTO)
			SF06 BCDEFGHIJ 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			SF06 K 0 A_PlaySound("SoF/Weapons/assault/Slap",CHAN_AUTO)
			SF06 KLMNOP 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			SF06 Q 0 A_PlaySound("SoF/Weapons/shotgun/Cock",CHAN_AUTO)
			SF06 QRSTUVWXY 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			SF0A A 0 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			Goto ReadyLoop
		ReadyLoop:
		IDLE_A:	
			SF02 A 0 A_Jump(10,"IDLEFINGER_A")
			SF02 ABCDEFGHIJKLMNOPQRSTUVW 2 A_WeaponReady(WRF_ALLOWRELOAD)
			Loop
		IDLEFINGER_A:
			SF03 ABCDEFGHIJKLMNOPQRS 2 A_WeaponReady(WRF_ALLOWRELOAD)
			Goto ReadyLoop
		IDLEFINGER_B:
			SF04 ABCDE 2 A_WeaponReady(WRF_ALLOWRELOAD|WRF_NOSECONDARY)
			SF04 F 0 A_PlaySound("SoF/Weapons/assault/Slap",CHAN_AUTO)
			SF04 FGHIJK 2 A_WeaponReady(WRF_ALLOWRELOAD|WRF_NOSECONDARY)
			SF04 L 0 A_PlaySound("SoF/Weapons/assault/Rattle",CHAN_AUTO)
			SF04 LMNOPQRST 2 A_WeaponReady(WRF_ALLOWRELOAD|WRF_NOSECONDARY)
			SF02 U 4 A_WeaponReady(WRF_ALLOWRELOAD|WRF_NOSECONDARY) //Interpolation fix, looks too wonky without
			SF02 A 0
			Goto ReadyLoop
		IDLEFINGER_C:
			SF05 ABCDEFGHI 2 A_WeaponReady(WRF_ALLOWRELOAD|WRF_NOSECONDARY)
			SF05 J 0 A_PlaySound("SoF/Weapons/assault/Rattle",CHAN_AUTO)
			SF05 JKLMNOPQ 2 A_WeaponReady(WRF_ALLOWRELOAD|WRF_NOSECONDARY)
			SF05 R 0 A_PlaySound("SoF/Weapons/assault/Slap",CHAN_AUTO)
			SF05 RSTUVWXYZ 2 A_WeaponReady(WRF_ALLOWRELOAD|WRF_NOSECONDARY)
			Goto ReadyLoop
		Deselect:
			SF00 ABCDE 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)	
			SF00 F 0 A_PlaySound("SoF/Weapons/shotgun/Done",CHAN_AUTO)
			SF00 FGHIJK 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)	
			Goto DeselectLoop
		Fire:
			"####" "#" 0 A_JumpIfInventory("SoF_ShotgunMagazine",1,1)
			Goto DryFire
		FIRE1:
			SF01 A 0 A_GunFlash("FLASH1",GFF_NOEXTCHANGE)
			SF01 A 0 A_GiveInventory("SoF_Shotgun_FireToken")
			SF01 A 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			SF01 B 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			SF01 C 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			SF01 D 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			SF01 E 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			SF01 F 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			SF01 G 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			SF01 H 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			SF01 J 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			SF01 K 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			SF01 L 4 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			SF02 A 0
			Goto ReadyLoop
		FLASH1:
			SFF0 A 1 Bright
			Stop
		AltFire:
			"####" "#" 0 A_Jump(256,"IDLEFINGER_B","IDLEFINGER_C")
			Goto ReadyLoop	
		Reload:
			"####" "#" 0 A_JumpIfInventory("SoF_ShotgunMagazine",0,"ReadyLoop")
			"####" "#" 0 A_JumpIfInventory("Shell",1,1)
			Goto ReadyLoop
		RELOAD_1:
			SF07 A 0 A_TakeInventory("SoF_ShotgunReloadShellSkip")
			SF07 A 0 A_JumpIfInventory("SoF_ShotgunMagazine",1,1)
			Goto RELOAD_START
			SF07 A 0 A_GiveInventory("SoF_ShotgunReloadShellSkip")
		RELOAD_START:
			SF07 ABCD 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		RELOAD_LOOP:
			SF09 A 0 A_JumpIfInventory("Shell",1,2)
			SF09 A 0 A_JumpIfInventory("SoF_ShotgunReloadShellSkip",1,"ReadyLoop")
			Goto RELOAD2
			SF09 A 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			SF09 B 0 A_PlaySound("SoF/Weapons/shotgun/PutIn",CHAN_AUTO)
			SF09 BCD 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			SF09 A 0 A_GiveInventory("SoF_ShotgunMagazine",1)
			SF09 A 0 A_TakeInventory("Shell",1,TIF_NOTAKEINFINITE)
			SF09 A 0 A_Refire("SKIPRELOAD")
			SF09 A 0 A_JumpIfInventory("SoF_ShotgunMagazine",0,"DECIDERELOADFINISH")
			Loop
		SKIPRELOAD:
			SF09 A 0 A_JumpIfInventory("SoF_ShotgunReloadShellSkip",1,"FIRE")
			Goto RELOAD2
		RELOAD_LOOP_FINISH:
			SF09 A 0 A_JumpIfInventory("SoF_ShotgunMagazine",0,"RELOAD2")
			Goto RELOAD_LOOP
		DECIDERELOADFINISH:
			SF09 A 0 A_JumpIfInventory("SoF_ShotgunReloadShellSkip",1,1)
			Goto RELOAD2
			SF09 A 4 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			Goto ReadyLoop
		RELOAD2:
			SF08 A 0 A_TakeInventory("SoF_ShotgunReloadShellSkip")
			SF08 ABC 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			SF08 D 0 A_PlaySound("SoF/Weapons/shotgun/Cock",CHAN_AUTO)
			SF08 DEFGHIJ 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)	
			Goto ReadyLoop
		DryFire:
			"####" "#" 0 A_PlaySound("SoF/Weapons/gpistol/Empty",CHAN_AUTO)
			SF0A ABC 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			Goto Reload
		Spawn:
			SFPU A -1
			Stop
	}
}

Actor SoF_Shotgun_FireToken : Trigger
{
	States
	{
		Pickup:
			TNT1 A 0 A_SpawnItemEx("SoF_ShellCasing",cos(pitch)*8,3,38-(sin(pitch)*8),0.125*sin(pitch),frandom(0.75,1.25),abs(1*cos(pitch)),0,SXF_TRANSFERPITCH)
			TNT1 A 0 A_SpawnItemEx("SoF_MuzzleSmoke",cos(pitch)*8,1,39-(sin(pitch)*8),0.125*sin(pitch),0,abs(0.125*cos(pitch)))
			TNT1 AAAAAAAAAAAA 0 A_SpawnItemEx("SoF_MuzzleSpark",cos(pitch)*7,1.25,39-(sin(pitch)*7),frandom(-1,1)*sin(pitch)+12*cos(pitch),frandom(-2,2),frandom(-1,1)*cos(pitch)+12*sin(-pitch))
			TNT1 A 0 A_PlaySound("SoF/Weapons/shotgun/Fire",CHAN_WEAPON)
			TNT1 A 0 A_AlertMonsters
			TNT1 AAAAAAAAAAAA 0 A_FireBullets(4,4,-1,0,"SoF_ShotgunBulletPuff",FBF_NORANDOM)
			TNT1 A 0 A_TakeInventory("SoF_ShotgunMagazine",1)
			TNT1 A 0 ACS_NamedExecuteWithResult("GlobalRecoil",6,80)
			Stop
	}
}

Actor SoF_ShotgunBulletPuff : SoF_BulletPuff { DamageType "SoFShotgun"  args 13 }
Actor SoF_ShotgunStomachShot : TemporaryBoolean {}
Actor SoF_ShotgunReloadShellSkip : TemporaryBoolean {}

Actor SoF_ShotgunMagazine : Ammo
{
	Inventory.Amount 8
	Inventory.MaxAmount 8
}
