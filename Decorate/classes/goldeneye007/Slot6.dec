actor Goldeneye_RCP90PickedUpX : MapCoordinate {}
actor Goldeneye_RCP90PickedUpY : MapCoordinate {}
actor Goldeneye_RCP90PickedUpZ : MapCoordinate {}

actor Goldeneye_RCP90 : SamsaraGoldenEyeWeapon
{
    Inventory.PickupMessage "Picked up an RC-P90."
    Weapon.SlotNumber 6
    Weapon.SlotPriority 0
    Weapon.SelectionOrder 6
    Weapon.AmmoType1 "Goldeneye_RCP90Magazine"
    Weapon.AmmoUse1 1
    Weapon.AmmoType2 "Cell"
    Weapon.AmmoGive2 80
    Obituary "%o was drilled by %k's RC-P90."
    Tag "RC-P90"
    States
    {
      Spawn:
        PIST A -1
        stop

      // Single
      Ready:
        TNT1 A 0 A_JumpIfInventory("BondWatchQueue",1,"WatchRaise")
        TNT1 A 0 A_JumpIfInventory("BondWatchToken",1,"WatchReady")
        TNT1 A 0 A_JumpIfInventory("BondAltToggle",1,"DualWield")
      ReadyStart:
      ReadyCenter:
        GE00 A 0 A_JumpIfInventory("SamsaraReloadMode",2,"ReadyCenterReloadable")
      ReadyCenterNonReloadable:
        GE00 A 1 A_WeaponReady
        goto ReadyAmmoCheck

      ReadyCenterReloadable:
        GE00 A 1 A_WeaponReady(WRF_ALLOWRELOAD)
        goto ReadyAmmoCheck

      ReadyAmmoCheck:
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",2,1)
        goto Ready
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"Ready")
        TNT1 A 0 A_JumpIfInventory("Goldeneye_RCP90Magazine",1,"Ready")
        TNT1 A 0 A_JumpIfInventory("BondPrimaryToken",1,"Ready")
        goto ReloadCheck

      Deselect:
        GE00 A 0 A_JumpIfInventory("BondDualWieldToken",1,1)
        goto DeselectLoop
        GE00 A 0 A_GunFlash("DeselectDual",GFF_NOEXTCHANGE)
      DeselectLoop:
        GE00 A 0 A_TakeInventory("BondFireDelay")
        GE00 A 0 A_TakeInventory("BondAlternateFire")
        GE00 A 0 A_TakeInventory("BondDualWieldToken")
        GE00 A 0 A_TakeInventory("BondAltToggle")
        GE00 A 0 A_TakeInventory("BondZoomToken")
        GE00 A 0 A_TakeInventory("BondZoomAmountModifyActive")
        GE00 A 0 A_TakeInventory("BondDualWieldSpamProtector")
        GE00 A 0 A_TakeInventory("BondDryFireSpamProtector1")
        GE00 A 0 A_TakeInventory("BondDryFireSpamProtector2")
        GE00 A 0 A_TakeInventory("BondAltActivationToken")
        GE00 A 1 A_Lower
        loop

      Select:
        GE00 A 0 A_PlaySound("Goldeneye/Weaponup",CHAN_7)
      SelectDualCheck:
        GE00 A 0 A_JumpIfInventory("BondDualWieldToken",1,1)
        goto SelectLoop
        GE00 A 0 A_GunFlash("SelectDual",GFF_NOEXTCHANGE)
      SelectLoop:
        GE00 A 0 A_TakeInventory("Goldeneye_UnarmedToken")
        GE00 A 0 A_TakeInventory("Goldeneye_PP7Token")
        GE00 A 0 A_TakeInventory("Goldeneye_KnifeToken")
        GE00 A 0 A_TakeInventory("Goldeneye_KF7Token")
        GE00 A 0 A_TakeInventory("Goldeneye_AutoShotgunToken")
        GE00 A 0 A_TakeInventory("Goldeneye_AR33Token")
        GE00 A 0 A_TakeInventory("Goldeneye_RocketLauncherToken")
        GE00 A 0 A_GiveInventory("Goldeneye_RCP90Token")
        GE00 A 0 A_TakeInventory("Goldeneye_LaserToken")
        GE00 A 0 A_TakeInventory("Goldeneye_GoldenGunToken")
        GE00 A 0 A_TakeInventory("Goldeneye_SniperToken")
        GE00 A 0 A_TakeInventory("Goldeneye_D5KToken")
        GE00 A 1 A_Raise
        goto SelectDualCheck

      Fire:
        GE01 B 0 A_JumpIfInventory("BondSecondaryToken",1,1)
        goto FireAltDualCheck
        GE01 B 0 A_JumpIfInventory("BondAltActivationToken",1,"FireAltDualCheck")
        GE01 B 0 A_TakeInventory("BondAltToggle")
        GE01 B 0 A_GiveInventory("BondAltActivationToken")
        GE01 B 0 ACS_NamedExecuteAlways("BondAltFire")
        loop

      FireAltDualCheck:
        GE01 B 0 A_JumpIfInventory("BondAltToggle",1,"DualWieldFire")
      FireCheckDualOrder:
        GE01 B 0 A_JumpIfInventory("BondDualWieldToken",1,1)
        goto FireAmmoCheck
        GE01 B 0 A_JumpIfInventory("BondFireDelay",0,"FireAmmoCheck")
        GE01 B 0 A_JumpIfInventory("BondAlternateFire",1,"Ready")
        goto FireAmmoCheck

      FireAmmoCheck:
        GE01 B 0 A_JumpIfInventory("SamsaraReloadMode",2,"FireAmmoCheckReloadable")
      FireAmmoCheckNonReloadable:
        GE01 B 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireStart")
        GE01 B 0 A_JumpIfInventory("Cell",1,"FireStart")
        goto FireDry

      FireAmmoCheckReloadable:
        GE01 B 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"FireStart")
        GE01 B 0 A_JumpIfInventory("Goldeneye_RCP90Magazine",1,"FireStart")
        goto FireDry

      FireDry:
        GE00 A 0 A_JumpIfInventory("BondDryFireSpamProtector1",1,"FireSetDualOrder")
        GE00 A 0 A_PlaySound("Goldeneye/DryFire",CHAN_6)
        GE00 A 0 ACS_NamedExecuteAlways("BondDryFireSpamBlocker_Primary")
        GE00 A 0 A_GiveInventory("BondDryFireSpamProtector1")
        goto FireSetDualOrder

      FireStart:
        GE01 B 0 A_GiveInventory("BondFiringToken")
        GE01 B 0 A_TakeInventory("BondGunAngleFiringSingle")
        GE01 B 0 A_AlertMonsters
      FireCenter:
        GE01 B 0 A_GiveInventory("BondGunAngleFiringSingle",5)
        GE01 B 1 Bright A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        GE01 C 0 A_JumpIfInventory("SamsaraReloadMode",2,"FireCenterReloadable")
      FireCenterNonReloadable:
        GE01 C 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireCenterAttack")
        GE01 C 0 A_JumpIfInventory("Cell",1,"FireCenterAttack")
        goto FireCenterContinue1

      FireCenterReloadable:
        GE01 C 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"FireCenterAttack")
        GE01 C 0 A_JumpIfInventory("Goldeneye_RCP90Magazine",1,"FireCenterAttack")
        goto FireCenterContinue1

      FireCenterAttack:
        GE01 C 0 A_SpawnItemEx("Goldeneye_Cartridge_Standard",cos(pitch)*22,6,42-(sin(pitch)*22),(2*cos(pitch))+(2*sin(pitch)),frandom(2,3),abs(4*cos(pitch)),0,SXF_TRANSFERPITCH)
      FireCenterContinue1:
        GE01 C 0 A_GiveInventory("SamsaraGoldenEyeRCP90SingleAttackHandler")
        GE01 C 1 Bright A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        GE01 E 0 A_TakeInventory("BondFiringToken")
      FireCenterReFireCheck:
        GE01 E 0 A_JumpIfInventory("SamsaraReloadMode",2,"FireCenterReFireCheckReloadable")
      FireCenterReFireCheckNonReloadable:
        GE01 E 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireCenterReFire")
        GE01 E 0 A_JumpIfInventory("Cell",1,"FireCenterReFire")
        goto FireCenterContinue2

      FireCenterReFireCheckReloadable:
        GE01 E 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"FireCenterReFire")
        GE01 E 0 A_JumpIfInventory("Goldeneye_RCP90Magazine",1,"FireCenterReFire")
        goto FireCenterContinue2

      FireCenterReFire:
        GE01 E 0 A_ReFire(1)
        goto FireCenterContinue2
        GE01 E 0 A_GiveInventory("BondFireDelay")
        GE01 E 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        goto Fire

      FireCenterContinue2:
        GE01 EDC 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        goto FireSetDualOrder

      FireSetDualOrder:
        TNT1 A 0 A_JumpIfInventory("BondDualWieldToken",1,1)
        goto FireFinish
        TNT1 A 0 A_GiveInventory("BondAlternateFire")
        goto FireFinish

      FireFinish:
        TNT1 A 0 A_TakeInventory("BondFiringToken")
        TNT1 A 0 A_TakeInventory("BondFireDelay")
        goto Ready

      Reload:
        TNT1 A 0 A_JumpIfInventory("BondPrimaryToken",1,"ReloadCheck")
        TNT1 A 0 A_JumpIfInventory("BondDualWieldToken",1,1)
        goto ReloadCheck
        TNT1 A 0 A_JumpIfInventory("Goldeneye_RCP90DualMagazine",0,"ReloadCheck")
        TNT1 A 0 A_JumpIfInventory("BondDualReloadSpamProtector",1,"ReloadCheck")
        TNT1 A 0 A_GunFlash("ReloadCheckDual",GFF_NOEXTCHANGE)
        goto ReloadCheck

      ReloadCheck:
        TNT1 A 0 A_JumpIfInventory("Goldeneye_RCP90Magazine",0,"Ready")
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"ReloadStart")
        TNT1 A 0 A_JumpIfInventory("Cell",1,"ReloadStart")
        goto Ready

      ReloadStart:
        GE02 A 0 A_PlaySound("Goldeneye/Reload")
        GE02 ABCDEFGHI 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        GE02 I 0 A_JumpIfInventory("SamsaraReloadMode",2,"ReloadAmmo")
      ReloadFinish:
        GE02 IHGFEDCBA 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        goto Ready

      ReloadAmmo:
        GE02 I 0 A_JumpIfInventory("Goldeneye_RCP90Magazine",0,"ReloadFinish")
        GE02 I 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
        GE02 I 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        GE02 I 0 A_JumpIfInventory("Cell",1,1)
        goto ReloadFinish
        GE02 I 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
        GE02 I 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        GE02 I 0 A_TakeInventory("Cell",1)
        GE02 I 0 A_GiveInventory("Goldeneye_RCP90Magazine")
        loop

      // Dual Init
      AltFire:
        TNT1 A 0 A_JumpIfInventory("BondAltActivationToken",1,"Ready")
        TNT1 A 0 A_TakeInventory("BondAltToggle")
        TNT1 A 0 A_GiveInventory("BondAltActivationToken")
        TNT1 A 0 ACS_NamedExecuteAlways("BondAltFire")
        goto Ready

      DualUnwield:
        TNT1 A 0 A_TakeInventory("BondFireDelay")
        TNT1 A 0 A_TakeInventory("BondAlternateFire")
        TNT1 A 0 A_JumpIfInventory("BondDualWieldSpamProtector",1,5)
        TNT1 A 0 A_TakeInventory("BondDualWieldToken")
        TNT1 A 0 A_TakeInventory("BondDryFireSpamProtector1")
        TNT1 A 0 A_TakeInventory("BondDryFireSpamProtector2")
        TNT1 A 0 A_GunFlash("DualLower",GFF_NOEXTCHANGE)
        TNT1 A 0 A_ZoomFactor(1)
        goto ReadyStart

      DualUnwieldFire:
        GE01 B 0 A_TakeInventory("BondFireDelay")
        GE01 B 0 A_TakeInventory("BondAlternateFire")
        GE01 B 0 A_JumpIfInventory("BondDualWieldSpamProtector",1,5)
        GE01 B 0 A_TakeInventory("BondDualWieldToken")
        GE01 B 0 A_TakeInventory("BondDryFireSpamProtector1")
        GE01 B 0 A_TakeInventory("BondDryFireSpamProtector2")
        GE01 B 0 A_GunFlash("DualLower",GFF_NOEXTCHANGE)
        GE01 B 0 A_ZoomFactor(1)
        goto FireCheckDualOrder

      DualLower:
        GE12 A 0 A_TakeInventory("BondDualReloadSpamProtector")
        GE12 A 1 A_GiveInventory("BondDualWieldSpamProtector")
        GE12 BCDEFGHI 1
        TNT1 A 0 A_TakeInventory("BondDualWieldSpamProtector")
        stop

      DualWield:
        TNT1 A 0 A_TakeInventory("BondAltActivationToken")
        TNT1 A 0 A_TakeInventory("BondAltToggle")
        TNT1 A 0 A_TakeInventory("BondZoomToken")
        TNT1 A 0 A_JumpIfInventory("Goldeneye_RCP90DualToken",1,1)
        goto ReadyStart
        TNT1 A 0 A_JumpIfInventory("BondDualWieldToken",1,"DualUnwield")
        TNT1 A 0 A_JumpIfInventory("BondDualWieldSpamProtector",1,3)
        TNT1 A 0 A_GiveInventory("BondDualWieldToken")
        TNT1 A 0 A_GunFlash("DualRaise",GFF_NOEXTCHANGE)
        TNT1 A 0 A_ZoomFactor(1)
        goto ReadyStart

      DualWieldFire:
        GE01 B 0 A_TakeInventory("BondAltActivationToken")
        GE01 B 0 A_TakeInventory("BondAltToggle")
        GE01 B 0 A_TakeInventory("BondZoomToken")
        GE01 B 0 A_JumpIfInventory("Goldeneye_RCP90DualToken",1,1)
        goto FireCheckDualOrder
        GE01 B 0 A_JumpIfInventory("BondDualWieldToken",1,"DualUnwieldFire")
        GE01 B 0 A_JumpIfInventory("BondDualWieldSpamProtector",1,3)
        GE01 B 0 A_GiveInventory("BondDualWieldToken")
        GE01 B 0 A_GunFlash("DualRaise",GFF_NOEXTCHANGE)
        GE01 B 0 A_ZoomFactor(1)
        goto FireCheckDualOrder

      DualRaise:
        GE12 I 0 A_TakeInventory("BondDualReloadSpamProtector")
        GE12 I 0 A_GiveInventory("BondDualWieldSpamProtector")
        GE12 I 1 A_PlaySound("Goldeneye/Weaponup",CHAN_7)
        GE12 HGFEDCBA 1
        TNT1 A 0 A_TakeInventory("BondDualWieldSpamProtector")
        goto ReadyStartDual

      // Dual
      ReadyDual:
        TNT1 A 0 A_JumpIfInventory("BondFireDelay",0,2)
        TNT1 A 0 A_JumpIfInventory("BondAlternateFire",1,1)
        goto ReadyStartDual
        TNT1 A 0 A_JumpIfInventory("BondPrimaryToken",1,"FireDual")
      ReadyStartDual:
      ReadyCenterDual:
        GE10 A 1
        goto ReadyAmmoCheckDual

      ReadyAmmoCheckDual:
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",2,1)
        goto ReadyDual
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"ReadyDual")
        TNT1 A 0 A_JumpIfInventory("Goldeneye_RCP90DualMagazine",1,"ReadyDual")
        TNT1 A 0 A_JumpIfInventory("BondPrimaryToken",1,"ReadyDual")
        goto ReloadCheckDual

      DeselectDual:
        GE12 A 0 A_TakeInventory("BondDualReloadSpamProtector")
        GE12 A 1 A_TakeInventory("BondDualWieldSpamProtector")
        GE12 AAAAAAAAAAAAAAA 1
        stop

      SelectDual:
        GE12 A 0 A_TakeInventory("BondDualReloadSpamProtector")
        GE12 A 1 A_TakeInventory("BondDualWieldSpamProtector")
        goto ReadyStartDual

      FireDual:
        GE11 B 0 A_JumpIfInventory("SamsaraReloadMode",2,"FireDualReloadable")
      FireDualNonReloadable:
        GE11 B 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireStartDual")
        GE11 B 0 A_JumpIfInventory("Cell",1,"FireStartDual")
        goto FireDryDual

      FireDualReloadable:
        GE11 B 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"FireStartDual")
        GE11 B 0 A_JumpIfInventory("Goldeneye_RCP90DualMagazine",1,"FireStartDual")
        goto FireDryDual

      FireDryDual:
        GE10 A 0 A_JumpIfInventory("BondDryFireSpamProtector2",1,"FireSetDualOrderDual")
        GE10 A 0 A_PlaySound("Goldeneye/DryFire",CHAN_6)
        GE10 A 0 ACS_NamedExecuteAlways("BondDryFireSpamBlocker_Secondary")
        GE10 A 0 A_GiveInventory("BondDryFireSpamProtector2")
        goto FireSetDualOrderDual

      FireStartDual:
        GE11 B 0 A_GiveInventory("BondFiringToken")
        GE11 B 0 A_TakeInventory("BondGunAngleFiringDual")
        GE11 B 0 A_AlertMonsters
      FireCenterDual:
        GE11 B 1 Bright A_GiveInventory("BondGunAngleFiringDual",5)
        GE11 C 0 A_JumpIfInventory("SamsaraReloadMode",2,"FireCenterDualReloadable")
      FireCenterDualNonReloadable:
        GE11 C 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireCenterDualAttack")
        GE11 C 0 A_JumpIfInventory("Cell",1,"FireCenterDualAttack")
        goto FireCenterDualContinue1

      FireCenterDualReloadable:
        GE11 C 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"FireCenterDualAttack")
        GE11 C 0 A_JumpIfInventory("Goldeneye_RCP90DualMagazine",1,"FireCenterDualAttack")
        goto FireCenterDualContinue1

      FireCenterDualAttack:
        GE11 C 0 A_SpawnItemEx("Goldeneye_Cartridge_Standard",cos(pitch)*22,-6,42-(sin(pitch)*22),(2*cos(pitch))+(2*sin(pitch)),frandom(2,3),abs(4*cos(pitch)),0,SXF_TRANSFERPITCH)
      FireCenterDualContinue1:
        GE11 C 1 Bright A_GiveInventory("SamsaraGoldenEyeRCP90DualAttackHandler")
        GE11 E 0 A_TakeInventory("BondFiringToken")
      FireCenterDualReFireCheck:
        GE11 E 0 A_JumpIfInventory("SamsaraReloadMode",2,"FireCenterDualReFireCheckReloadable")
      FireCenterDualReFireCheckNonReloadable:
        GE11 E 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireCenterDualReFire")
        GE11 E 0 A_JumpIfInventory("Cell",1,"FireCenterDualReFire")
        goto FireCenterDualContinue2

      FireCenterDualReFireCheckReloadable:
        GE11 E 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"FireCenterDualReFire")
        GE11 E 0 A_JumpIfInventory("Goldeneye_RCP90DualMagazine",1,"FireCenterDualReFire")
        goto FireCenterDualContinue2

      FireCenterDualReFire:
        GE11 E 0 A_JumpIfInventory("BondPrimaryToken",1,1)
        goto FireCenterDualContinue2
        GE11 E 1 A_GiveInventory("BondFireDelay")
        goto FireDual

      FireCenterDualContinue2:
        GE11 EDC 1
        goto FireSetDualOrderDual

      FireSetDualOrderDual:
        TNT1 A 0 A_TakeInventory("BondAlternateFire")
        goto FireFinishDual

      FireFinishDual:
        TNT1 A 0 A_TakeInventory("BondFiringToken")
        TNT1 A 0 A_TakeInventory("BondFireDelay")
        goto ReadyDual

      ReloadCheckDual:
        TNT1 A 0 A_TakeInventory("BondDualWieldSpamProtector")
        TNT1 A 0 A_JumpIfInventory("Goldeneye_RCP90DualMagazine",0,"ReadyDual")
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"ReloadStartDual")
        TNT1 A 0 A_JumpIfInventory("Cell",1,"ReloadStartDual")
        goto ReadyDual

      ReloadStartDual:
        GE12 A 0 A_GiveInventory("BondDualReloadSpamProtector")
        GE12 A 1 A_PlaySound("Goldeneye/Reload")
        GE12 BCDEFGHI 1
        GE12 I 0 A_JumpIfInventory("SamsaraReloadMode",2,"ReloadAmmoDual")
      ReloadFinishDual:
        GE12 IHGFEDCBA 1
        TNT1 A 0 A_TakeInventory("BondDualReloadSpamProtector")
        goto ReadyDual

      ReloadAmmoDual:
        GE12 I 0 A_JumpIfInventory("Goldeneye_RCP90DualMagazine",0,"ReloadFinishDual")
        GE12 I 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
        GE12 I 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        GE12 I 0 A_JumpIfInventory("Cell",1,1)
        goto ReloadFinishDual
        GE12 I 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
        GE12 I 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        GE12 I 0 A_TakeInventory("Cell",1)
        GE12 I 0 A_GiveInventory("Goldeneye_RCP90DualMagazine")
        loop
    }
}

// Attack handlers

actor SamsaraGoldenEyeRCP90SingleAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",2,"PickupReloadable")
      PickupNonReloadable:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Cell",1,1)
        stop
        TNT1 A 0 A_TakeInventory("Cell",1)
        goto PickupAttack

      PickupReloadable:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Goldeneye_RCP90Magazine",1,1)
        stop
        TNT1 A 0 A_TakeInventory("Goldeneye_RCP90Magazine",1)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("Goldeneye/RCPShot",CHAN_WEAPON)
      PickupAttackCenter:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCenterCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCenterCoop")
      PickupAttackCenterDM:
        TNT1 A 0 A_FireCustomMissile("Goldeneye_RCP90Tracer",frandom(-3,3),false,6,0,0,frandom(-3,3))
        stop

      PickupAttackCenterCoop:
        TNT1 A 0 A_FireCustomMissile("Goldeneye_RCP90TracerCoop",frandom(-3,3),false,6,0,0,frandom(-3,3))
        stop
    }
}

actor SamsaraGoldenEyeRCP90DualAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",2,"PickupReloadable")
      PickupNonReloadable:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Cell",1,1)
        stop
        TNT1 A 0 A_TakeInventory("Cell",1)
        goto PickupAttack

      PickupReloadable:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Goldeneye_RCP90DualMagazine",1,1)
        stop
        TNT1 A 0 A_TakeInventory("Goldeneye_RCP90DualMagazine",1)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("Goldeneye/RCPShot",CHAN_WEAPON)
      PickupAttackCenter:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCenterCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCenterCoop")
      PickupAttackCenterDM:
        TNT1 A 0 A_FireCustomMissile("Goldeneye_RCP90Tracer",frandom(-3,3),false,-6,0,0,frandom(-3,3))
        stop

      PickupAttackCenterCoop:
        TNT1 A 0 A_FireCustomMissile("Goldeneye_RCP90TracerCoop",frandom(-3,3),false,-6,0,0,frandom(-3,3))
        stop
    }
}

actor Goldeneye_RCP90Magazine : Ammo
{
    Inventory.MaxAmount 80
    Tag "RC-P90 Magazine (Right)"
    +IGNORESKILL
}

actor Goldeneye_RCP90DualMagazine : Ammo
{
    Inventory.MaxAmount 80
    Tag "RC-P90 Magazine (Left)"
    +IGNORESKILL
}

actor Goldeneye_RCP90Tracer : Goldeneye_BulletTracer
{
    Damage (20)
    States
    {
      XDeath:
        TNT1 A 0 A_SetUserVar("user_damage",20)
        goto Super::XDeath
    }
}

actor Goldeneye_RCP90TracerCoop : Goldeneye_RCP90Tracer
{
    Species "Player"
    +THRUSPECIES
}
