actor freeman_slot1selected : Boolean { Inventory.InterHubAmount 0 }
actor HLHornetGunRegenLock : Boolean {}
actor HLShockRoachRegenLock : Boolean {}

Actor "Hornetgun" : HLWeapon
{
	Inventory.PickupMessage "You got the hornetgun!"
	Weapon.SlotNumber 1
	Weapon.SelectionOrder 6750
	Weapon.AmmoType1 "HornetGunAmmo"
    Weapon.AmmoUse1 1
	Weapon.AmmoType2 "ShockRoachAmmo"
    Weapon.AmmoUse2 1
    Tag "Hivehand"
	States
	{
	Spawn:
		HW10 Z -1
		stop

    // Gordon Freeman
	ReadyActual:
		HW12 A 0 A_TakeInventory("HLHornetGunRegenLock")
		HW12 A 0 A_TakeInventory("HLShockRoachRegenLock")
		HW12 A 0 A_JumpIfInventory("HalfLifeOpposingForce",1,"ReadyActualOpp")
		HW12 A 2 A_WeaponReady
        goto ReadyActualJump

    ReadyActualJump:
		HW12 A 0 A_Jump(4,"ReadyActual")
		HW12 A 0 A_Jump(3,"ReadyActual")
		HW12 A 0 A_Jump(2,"ReadyActual")
		HW12 A 0 A_JumpIfInventory("HalfLifeOpposingForce",1,"ReadyActualJumpOpp")
		HW12 A 0 A_Jump(1,"ReadyActualIdle1","ReadyActualIdle2","ReadyActualIdle3")
		goto ReadyActual

	ReadyActualIdle1:
        HW12 A 1 A_WeaponReady // interpolation hack
		HW12 BCDEFGHIJKLMNOPQRSTUVWXYZ 1 A_WeaponReady
		HW21 ABCDEF 1 A_WeaponReady
		goto ReadyActual

	ReadyActualIdle2:
        HW12 A 1 A_WeaponReady // interpolation hack
		HW21 GHIJKLMNOPQRSTUVWXYZ 1 A_WeaponReady
		HW22 ABCDEFGHIJKLMNOPQRSTU 1 A_WeaponReady
		goto ReadyActual

	ReadyActualIdle3:
        HW12 A 1 A_WeaponReady // interpolation hack
		HW22 VWXYZ 1 A_WeaponReady
		HW23 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1 A_WeaponReady
		HW24 ABCDE 1 A_WeaponReady
		goto ReadyActual

	Deselect:
		TNT1 A 0 A_TakeInventory("HLHornetGunRegenLock")
		TNT1 A 0 A_TakeInventory("HLShockRoachRegenLock")
        goto Super::Deselect

	Select:
		TNT1 A 0 A_TakeInventory("freeman_fistselected")
		TNT1 A 0 A_TakeInventory("freeman_pistolselected")
		TNT1 A 0 A_GiveInventory("freeman_slot1selected")
		TNT1 A 0 A_TakeInventory("freeman_slot2selected")
		TNT1 A 0 A_TakeInventory("freeman_slot3selected")
		TNT1 A 0 A_TakeInventory("freeman_slot4selected")
		TNT1 A 0 A_TakeInventory("freeman_slot5selected")
		TNT1 A 0 A_TakeInventory("freeman_slot6selected")
		TNT1 A 0 A_TakeInventory("freeman_slot7selected")
		TNT1 A 0 A_TakeInventory("freeman_uniqueselected")
		TNT1 A 0 A_TakeInventory("freeman_unique2selected")
		TNT1 A 0 A_TakeInventory("freeman_unique3selected")
        goto Super::Select

	Ready: // our "Select" state
		HW10 A 0 A_TakeInventory("HLHornetGunRegenLock")
		HW10 A 0 A_TakeInventory("HLShockRoachRegenLock")
		HW10 A 0 A_JumpIfInventory("HalfLifeOpposingForce",1,"ReadyOpp")
		HW10 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1 A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
		HW11 ABCD 1 A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
		goto ReadyActual

    Fire:
		HW13 A 0 A_JumpIfInventory("HLOriginalKeySwitcherOpen",1,"ReadyActual")
        HW13 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireStart")
        HW13 A 0 A_JumpIfInventory("HalfLifeOpposingForce",1,"FireOpp")
        HW13 A 0 A_JumpIfInventory("HornetGunAmmo",1,"FireStart")
        goto FireDry

    FireDry:
        HW12 A 0 A_ClearReFire
		HW12 A 0 A_TakeInventory("HLHornetGunRegenLock")
		HW12 A 0 A_TakeInventory("HLShockRoachRegenLock")
        HW12 A 0 A_JumpIfInventory("HalfLifeOpposingForce",1,"FireDryOpp")
        HW12 A 4 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        goto ReadyActual

    Hold:
        HW13 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireStart")
        HW13 A 0 A_JumpIfInventory("HalfLifeOpposingForce",1,"HoldOpp")
        HW13 A 0 A_JumpIfInventory("HornetGunAmmo",1,"FireStart")
        goto HoldDry

    HoldDry:
        HW13 G 0 A_ClearReFire
        // technically still in fire animation, no lock removal is done here
        HW13 G 0 A_JumpIfInventory("HalfLifeOpposingForce",1,"HoldDryOpp")
        HW13 GH 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        HW13 I 2 A_WeaponReady(WRF_NOFIRE)
        goto ReadyActual

    FireStart:
        HW13 A 0 A_AlertMonsters
		HW13 A 0 A_JumpIfInventory("HalfLifeOpposingForce",1,"FireStartOpp")
		HW13 A 0 A_TakeInventory("HLShockRoachRegenLock")
		HW13 A 0 A_GiveInventory("HLHornetGunRegenLock")
        HW13 A 0 A_GiveInventory("SamsaraHalfLifeHornetGunPrimaryAttackHandler")
		HW13 ABC 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
		HW13 DEF 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        HW13 G 0 A_ReFire HW13 G 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        HW13 H 0 A_ReFire HW13 H 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        HW13 I 0 A_ReFire HW13 I 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
		goto ReadyActual

    AltFire:
        HW13 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"AltFireStart")
        HW13 A 0 A_JumpIfInventory("HornetGunAmmo",1,"AltFireStart")
        goto FireDry

    AltHold:
        HW13 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"AltFireStart")
        HW13 A 0 A_JumpIfInventory("HornetGunAmmo",1,"AltFireStart")
        goto AltHoldDry

    AltHoldDry:
        // technically still in fire animation, no lock removal is done here
        HW13 D 0 A_ClearReFire
        HW13 DEFG 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        HW13 HI 1 A_WeaponReady(WRF_NOFIRE)
        goto ReadyActual

    AltFireStart:
        HW13 A 0 A_AlertMonsters
		HW13 A 0 A_TakeInventory("HLShockRoachRegenLock")
		HW13 A 0 A_GiveInventory("HLHornetGunRegenLock")
		HW13 A 0 A_GiveInventory("SamsaraHalfLifeHornetGunSecondaryAttackHandler")
		HW13 ABC 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        HW13 D 0 A_JumpIfInventory("HalfLifeOpposingForce",1,2)
        HW13 D 0 A_ReFire
        HW13 D 0 A_ClearReFire HW13 D 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        HW13 E 0 A_JumpIfInventory("HalfLifeOpposingForce",1,2)
        HW13 E 0 A_ReFire
        HW13 E 0 A_ClearReFire HW13 E 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        HW13 F 0 A_JumpIfInventory("HalfLifeOpposingForce",1,2)
        HW13 F 0 A_ReFire
        HW13 F 0 A_ClearReFire HW13 F 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        HW13 G 0 A_JumpIfInventory("HalfLifeOpposingForce",1,2)
        HW13 G 0 A_ReFire
        HW13 G 0 A_ClearReFire HW13 G 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        HW13 H 0 A_JumpIfInventory("HalfLifeOpposingForce",1,2)
        HW13 H 0 A_ReFire
        HW13 H 0 A_ClearReFire HW13 H 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        HW13 I 0 A_JumpIfInventory("HalfLifeOpposingForce",1,2)
        HW13 I 0 A_ReFire
        HW13 I 0 A_ClearReFire HW13 I 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
		goto ReadyActual

    // Adrian Shephard (Shock Roach)
	ReadyActualOpp:
		OP00 A 2 A_WeaponReady(WRF_NOSECONDARY)
		goto ReadyActualJump

    ReadyActualJumpOpp:
		OP00 A 0 A_Jump(1,"ReadyActualIdle1Opp","ReadyActualIdle2Opp")
		goto ReadyActual

	ReadyActualIdle1Opp:
		OP00 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1 A_WeaponReady(WRF_NOSECONDARY)
		OP01 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1 A_WeaponReady(WRF_NOSECONDARY)
		OP02 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1 A_WeaponReady(WRF_NOSECONDARY)
		OP03 ABCDEFGHIJKLMNOPQRSTUVW 1 A_WeaponReady(WRF_NOSECONDARY)
		goto ReadyActual

	ReadyActualIdle2Opp:
        OP00 A 1 A_WeaponReady(WRF_NOSECONDARY) // interpolation hack
		OP08 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1 A_WeaponReady(WRF_NOSECONDARY)
		OP09 ABCDEFGHIJKLMNOPQRSTUVWX 1 A_WeaponReady(WRF_NOSECONDARY)
		goto ReadyActual

	ReadyOpp:
		OP05 A 0 A_PlaySound("HLShockRoach/Draw")
		OP05 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1 A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
		OP06 ABC 1 A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
		goto ReadyActual

    FireOpp:
        OP04 A 0 A_JumpIfInventory("ShockRoachAmmo",1,"FireStart")
        goto FireDry

    FireDryOpp:
        OP00 A 4 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        goto ReadyActual

    HoldOpp:
        OP04 A 0 A_JumpIfInventory("ShockRoachAmmo",1,"FireStart")
        goto HoldDry

    HoldDryOpp:
        OP04 GHIJ 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        goto ReadyActual

    FireStartOpp:
		OP04 A 0 A_TakeInventory("HLHornetGunRegenLock")
		OP04 A 0 A_GiveInventory("HLShockRoachRegenLock")
        OP04 A 0 A_JumpIfInventory("SamsaraCurrentWaterLevel",0,"FireDischargeOpp")
		OP04 A 0 A_GiveInventory("SamsaraHalfLifeShockRoachAttackHandler")
		OP04 AB 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
		OP04 C 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
		OP04 DEF 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        OP04 G 0 A_ReFire OP04 G 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        OP04 H 0 A_ReFire OP04 H 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        OP04 I 0 A_ReFire OP04 I 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        OP04 J 0 A_ReFire OP04 J 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
		goto ReadyActual

    FireDischargeOpp:
        OP00 A 0 A_ClearReFire
        OP00 A 0 A_GiveInventory("SamsaraHalfLifeShockRoachDischargeAttackHandler")
        OP00 A 55 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        goto ReadyActual
	}
}

actor SamsaraHalfLifeHornetGunPrimaryAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("HornetGunAmmo",1,1)
        stop
        TNT1 A 0 A_TakeInventory("HornetGunAmmo",1)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("halflife/hornet_shoot",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireCustomMissile("HLHornetSeekerDM",0,false,6,4)
        goto PickupFinish

      PickupAttackCoop:
        TNT1 A 0 A_FireCustomMissile("HLHornetSeekerCoop",0,false,6,4)
        goto PickupFinish

      // Finishing stuff
      PickupFinish:
        TNT1 A 0 ACS_NamedExecuteAlways("HalfLifeRecoil", 0, 1)
        stop
    }
}

actor SamsaraHalfLifeHornetGunSecondaryAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("HornetGunAmmo",1,1)
        stop
        TNT1 A 0 A_TakeInventory("HornetGunAmmo",1)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("halflife/hornet_shoot",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireCustomMissile("HLHornetDM",0,false,6,4)
        goto PickupFinish

      PickupAttackCoop:
        TNT1 A 0 A_FireCustomMissile("HLHornetCoop",0,false,6,4)
        goto PickupFinish

      // Finishing stuff
      PickupFinish:
        TNT1 A 0 ACS_NamedExecuteAlways("HalfLifeRecoil", 0, 1)
        stop
    }
}

actor HornetGunAmmo : Ammo
{
    Inventory.MaxAmount 8
    Tag "Hivehand Hornets"
    +IGNORESKILL
}

Actor HLHornet
{
	PROJECTILE
	+NOEXTREMEDEATH
	+NOBOUNCESOUND
	+BOUNCEONWALLS
	+BOUNCEONFLOORS
	+BOUNCEONCEILINGS
	+NOWALLBOUNCESND
	+HITTRACER
	+NODAMAGETHRUST
	+FORCERADIUSDMG
	BounceCount 10
	Speed 30
	Radius 2
	Height 4
	Damagetype "HLHornet"
	Damage (0)
	Scale 0.08
	BounceFactor 1.0
	WallBounceFactor 1.0
	ActiveSound "halflife/hornet_buzz"
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_LoopActiveSound
		Goto SpawnLoop
	SpawnLoop:	
		HP10 A 0 A_SpawnItemEx("HLHornetTrail",(momx*-28)/-32.0,-(momy*-28)/-32.0,(momz*-28)/-32.0,0,0,0,0,SXF_ABSOLUTEANGLE)
		HP10 A 0 A_SpawnItemEx("HLHornetTrail",(momx*-24)/-32.0,-(momy*-24)/-32.0,(momz*-24)/-32.0,0,0,0,0,SXF_ABSOLUTEANGLE)
		HP10 A 0 A_SpawnItemEx("HLHornetTrail",(momx*-20)/-32.0,-(momy*-20)/-32.0,(momz*-20)/-32.0,0,0,0,0,SXF_ABSOLUTEANGLE)
		HP10 A 0 A_SpawnItemEx("HLHornetTrail",(momx*-16)/-32.0,-(momy*-16)/-32.0,(momz*-16)/-32.0,0,0,0,0,SXF_ABSOLUTEANGLE)
		HP10 A 0 A_SpawnItemEx("HLHornetTrail",(momx*-12)/-32.0,-(momy*-12)/-32.0,(momz*-12)/-32.0,0,0,0,0,SXF_ABSOLUTEANGLE)
		HP10 A 0 A_SpawnItemEx("HLHornetTrail",(momx*-8)/-32.0,-(momy*-8)/-32.0,(momz*-8)/-32.0,0,0,0,0,SXF_ABSOLUTEANGLE)
		HP10 A 0 A_SpawnItemEx("HLHornetTrail",(momx*-4)/-32.0,-(momy*-4)/-32.0,(momz*-4)/-32.0,0,0,0,0,SXF_ABSOLUTEANGLE)
		HP11 A 1 A_SpawnItemEx("HLHornetTrail",0,0,0,0,0,0,0)
		HP10 B 0 A_SpawnItemEx("HLHornetTrail",(momx*-28)/-32.0,-(momy*-28)/-32.0,(momz*-28)/-32.0,0,0,0,0,SXF_ABSOLUTEANGLE)
		HP10 B 0 A_SpawnItemEx("HLHornetTrail",(momx*-24)/-32.0,-(momy*-24)/-32.0,(momz*-24)/-32.0,0,0,0,0,SXF_ABSOLUTEANGLE)
		HP10 B 0 A_SpawnItemEx("HLHornetTrail",(momx*-20)/-32.0,-(momy*-20)/-32.0,(momz*-20)/-32.0,0,0,0,0,SXF_ABSOLUTEANGLE)
		HP10 B 0 A_SpawnItemEx("HLHornetTrail",(momx*-16)/-32.0,-(momy*-16)/-32.0,(momz*-16)/-32.0,0,0,0,0,SXF_ABSOLUTEANGLE)
		HP10 B 0 A_SpawnItemEx("HLHornetTrail",(momx*-12)/-32.0,-(momy*-12)/-32.0,(momz*-12)/-32.0,0,0,0,0,SXF_ABSOLUTEANGLE)
		HP10 B 0 A_SpawnItemEx("HLHornetTrail",(momx*-8)/-32.0,-(momy*-8)/-32.0,(momz*-8)/-32.0,0,0,0,0,SXF_ABSOLUTEANGLE)
		HP10 B 0 A_SpawnItemEx("HLHornetTrail",(momx*-4)/-32.0,-(momy*-4)/-32.0,(momz*-4)/-32.0,0,0,0,0,SXF_ABSOLUTEANGLE)
		HP11 B 1 A_SpawnItemEx("HLHornetTrail",0,0,0,0,0,0,0)
		HP10 C 0 A_SpawnItemEx("HLHornetTrail",(momx*-28)/-32.0,-(momy*-28)/-32.0,(momz*-28)/-32.0,0,0,0,0,SXF_ABSOLUTEANGLE)
		HP10 C 0 A_SpawnItemEx("HLHornetTrail",(momx*-24)/-32.0,-(momy*-24)/-32.0,(momz*-24)/-32.0,0,0,0,0,SXF_ABSOLUTEANGLE)
		HP10 C 0 A_SpawnItemEx("HLHornetTrail",(momx*-20)/-32.0,-(momy*-20)/-32.0,(momz*-20)/-32.0,0,0,0,0,SXF_ABSOLUTEANGLE)
		HP10 C 0 A_SpawnItemEx("HLHornetTrail",(momx*-16)/-32.0,-(momy*-16)/-32.0,(momz*-16)/-32.0,0,0,0,0,SXF_ABSOLUTEANGLE)
		HP10 C 0 A_SpawnItemEx("HLHornetTrail",(momx*-12)/-32.0,-(momy*-12)/-32.0,(momz*-12)/-32.0,0,0,0,0,SXF_ABSOLUTEANGLE)
		HP10 C 0 A_SpawnItemEx("HLHornetTrail",(momx*-8)/-32.0,-(momy*-8)/-32.0,(momz*-8)/-32.0,0,0,0,0,SXF_ABSOLUTEANGLE)
		HP10 C 0 A_SpawnItemEx("HLHornetTrail",(momx*-4)/-32.0,-(momy*-4)/-32.0,(momz*-4)/-32.0,0,0,0,0,SXF_ABSOLUTEANGLE)
		HP11 C 1 A_SpawnItemEx("HLHornetTrail",0,0,0,0,0,0,0)
		HP10 D 0 A_SpawnItemEx("HLHornetTrail",(momx*-28)/-32.0,-(momy*-28)/-32.0,(momz*-28)/-32.0,0,0,0,0,SXF_ABSOLUTEANGLE)
		HP10 D 0 A_SpawnItemEx("HLHornetTrail",(momx*-24)/-32.0,-(momy*-24)/-32.0,(momz*-24)/-32.0,0,0,0,0,SXF_ABSOLUTEANGLE)
		HP10 D 0 A_SpawnItemEx("HLHornetTrail",(momx*-20)/-32.0,-(momy*-20)/-32.0,(momz*-20)/-32.0,0,0,0,0,SXF_ABSOLUTEANGLE)
		HP10 D 0 A_SpawnItemEx("HLHornetTrail",(momx*-16)/-32.0,-(momy*-16)/-32.0,(momz*-16)/-32.0,0,0,0,0,SXF_ABSOLUTEANGLE)
		HP10 D 0 A_SpawnItemEx("HLHornetTrail",(momx*-12)/-32.0,-(momy*-12)/-32.0,(momz*-12)/-32.0,0,0,0,0,SXF_ABSOLUTEANGLE)
		HP10 D 0 A_SpawnItemEx("HLHornetTrail",(momx*-8)/-32.0,-(momy*-8)/-32.0,(momz*-8)/-32.0,0,0,0,0,SXF_ABSOLUTEANGLE)
		HP10 D 0 A_SpawnItemEx("HLHornetTrail",(momx*-4)/-32.0,-(momy*-4)/-32.0,(momz*-4)/-32.0,0,0,0,0,SXF_ABSOLUTEANGLE)
		HP11 D 1 A_SpawnItemEx("HLHornetTrail",0,0,0,0,0,0,0)
//		HP11 E 1 A_SpawnItemEx("HLHornetTrail",0,0,0,0,0,0,0,SXF_CLIENTSIDE)
		loop
	Crash:
	XDeath:		
		TNT1 A 0 A_Explode(CallACS("HalfLife_BulletDamage",20),8,!XF_HURTSOURCE,0,8)	
	Death:
		TNT1 A 5 A_SpawnItemEx("HLHornetDeath",0,0,0,0,0,0,0,SXF_CLIENTSIDE)
		stop
	}
}

actor HLHornetCoop : HLHornet { +THRUSPECIES Species "Player" +DONTHARMCLASS +DONTHARMSPECIES +MTHRUSPECIES }
actor HLHornetDM : HLHornet { Speed 18 BounceCount 5 }

Actor HLHornetTrail
{
	+NOGRAVITY
	-SOLID
	+NOINTERACTION
	+CLIENTSIDEONLY
	height 8
	radius 3
	renderstyle add
	alpha 0.65
	scale 0.08
	states
	{
	Spawn:
		TNT1 A 0
		TNT1 A 1
	SpawnLoop:
		HW29 U 1 bright A_Fadeout(0.0275)
		loop
	}
}

Actor HLHornetDeath
{
	+CLIENTSIDEONLY
	+NOGRAVITY
	+NOINTERACTION
	-SOLID
	height 7
	radius 8
	renderstyle add
	alpha 0.9
	scale 0.6
	states
	{
	Spawn:
		TNT1 A 0
		HP11 B 1 bright A_PlaySound("halflife/hornet_hit",CHAN_BODY)
		HP11 CDEFG 1 bright
		stop
	}
}

Actor HLHornetSeeker
{
	PROJECTILE
	+NOEXTREMEDEATH
	+FORCERADIUSDMG
	+NODAMAGETHRUST
	+HITTRACER
	+SEEKERMISSILE
	Speed 25
	Radius 2
	Height 4
	Damagetype "HLHornet"
	Damage (0)
	scale 0.08
	DeathSound ""
	ActiveSound "halflife/hornet_buzz"
	//Obituary "%o died."
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_LoopActiveSound
		Goto SpawnLoop
	SpawnLoop:	
		HP10 A 0 A_SeekerMissile(45,45,SMF_LOOK|SMF_PRECISE,256,10)
		HP10 A 0 A_SpawnItemEx("HLHornetSeekerTrail",(momx*-28)/-32.0,-(momy*-28)/-32.0,(momz*-28)/-32.0,0,0,0,0,SXF_ABSOLUTEANGLE)
		HP10 A 0 A_SpawnItemEx("HLHornetSeekerTrail",(momx*-24)/-32.0,-(momy*-24)/-32.0,(momz*-24)/-32.0,0,0,0,0,SXF_ABSOLUTEANGLE)
		HP10 A 0 A_SpawnItemEx("HLHornetSeekerTrail",(momx*-20)/-32.0,-(momy*-20)/-32.0,(momz*-20)/-32.0,0,0,0,0,SXF_ABSOLUTEANGLE)
		HP10 A 0 A_SpawnItemEx("HLHornetSeekerTrail",(momx*-16)/-32.0,-(momy*-16)/-32.0,(momz*-16)/-32.0,0,0,0,0,SXF_ABSOLUTEANGLE)
		HP10 A 0 A_SpawnItemEx("HLHornetSeekerTrail",(momx*-12)/-32.0,-(momy*-12)/-32.0,(momz*-12)/-32.0,0,0,0,0,SXF_ABSOLUTEANGLE)
		HP10 A 0 A_SpawnItemEx("HLHornetSeekerTrail",(momx*-8)/-32.0,-(momy*-8)/-32.0,(momz*-8)/-32.0,0,0,0,0,SXF_ABSOLUTEANGLE)
		HP10 A 0 A_SpawnItemEx("HLHornetSeekerTrail",(momx*-4)/-32.0,-(momy*-4)/-32.0,(momz*-4)/-32.0,0,0,0,0,SXF_ABSOLUTEANGLE)
		HP10 A 1 bright A_SpawnItemEx("HLHornetSeekerTrail",0,0,0,0,0,0,0)
		HP10 B 0 A_SeekerMissile(45,45,SMF_LOOK|SMF_PRECISE,256,10)
		HP10 A 0 A_SpawnItemEx("HLHornetSeekerTrail",(momx*-28)/-32.0,-(momy*-28)/-32.0,(momz*-28)/-32.0,0,0,0,0,SXF_ABSOLUTEANGLE)
		HP10 A 0 A_SpawnItemEx("HLHornetSeekerTrail",(momx*-24)/-32.0,-(momy*-24)/-32.0,(momz*-24)/-32.0,0,0,0,0,SXF_ABSOLUTEANGLE)
		HP10 A 0 A_SpawnItemEx("HLHornetSeekerTrail",(momx*-20)/-32.0,-(momy*-20)/-32.0,(momz*-20)/-32.0,0,0,0,0,SXF_ABSOLUTEANGLE)
		HP10 A 0 A_SpawnItemEx("HLHornetSeekerTrail",(momx*-16)/-32.0,-(momy*-16)/-32.0,(momz*-16)/-32.0,0,0,0,0,SXF_ABSOLUTEANGLE)
		HP10 A 0 A_SpawnItemEx("HLHornetSeekerTrail",(momx*-12)/-32.0,-(momy*-12)/-32.0,(momz*-12)/-32.0,0,0,0,0,SXF_ABSOLUTEANGLE)
		HP10 A 0 A_SpawnItemEx("HLHornetSeekerTrail",(momx*-8)/-32.0,-(momy*-8)/-32.0,(momz*-8)/-32.0,0,0,0,0,SXF_ABSOLUTEANGLE)
		HP10 A 0 A_SpawnItemEx("HLHornetSeekerTrail",(momx*-4)/-32.0,-(momy*-4)/-32.0,(momz*-4)/-32.0,0,0,0,0,SXF_ABSOLUTEANGLE)
		HP10 B 1 bright A_SpawnItemEx("HLHornetSeekerTrail",0,0,0,0,0,0,0)
		HP10 C 0 A_SeekerMissile(45,45,SMF_LOOK|SMF_PRECISE,256,10)
		HP10 A 0 A_SpawnItemEx("HLHornetSeekerTrail",(momx*-28)/-32.0,-(momy*-28)/-32.0,(momz*-28)/-32.0,0,0,0,0,SXF_ABSOLUTEANGLE)
		HP10 A 0 A_SpawnItemEx("HLHornetSeekerTrail",(momx*-24)/-32.0,-(momy*-24)/-32.0,(momz*-24)/-32.0,0,0,0,0,SXF_ABSOLUTEANGLE)
		HP10 A 0 A_SpawnItemEx("HLHornetSeekerTrail",(momx*-20)/-32.0,-(momy*-20)/-32.0,(momz*-20)/-32.0,0,0,0,0,SXF_ABSOLUTEANGLE)
		HP10 A 0 A_SpawnItemEx("HLHornetSeekerTrail",(momx*-16)/-32.0,-(momy*-16)/-32.0,(momz*-16)/-32.0,0,0,0,0,SXF_ABSOLUTEANGLE)
		HP10 A 0 A_SpawnItemEx("HLHornetSeekerTrail",(momx*-12)/-32.0,-(momy*-12)/-32.0,(momz*-12)/-32.0,0,0,0,0,SXF_ABSOLUTEANGLE)
		HP10 A 0 A_SpawnItemEx("HLHornetSeekerTrail",(momx*-8)/-32.0,-(momy*-8)/-32.0,(momz*-8)/-32.0,0,0,0,0,SXF_ABSOLUTEANGLE)
		HP10 A 0 A_SpawnItemEx("HLHornetSeekerTrail",(momx*-4)/-32.0,-(momy*-4)/-32.0,(momz*-4)/-32.0,0,0,0,0,SXF_ABSOLUTEANGLE)
		HP10 C 1 bright A_SpawnItemEx("HLHornetSeekerTrail",0,0,0,0,0,0,0)
		HP10 D 0 A_SeekerMissile(45,45,SMF_LOOK|SMF_PRECISE,256,10)
		HP10 A 0 A_SpawnItemEx("HLHornetSeekerTrail",(momx*-28)/-32.0,-(momy*-28)/-32.0,(momz*-28)/-32.0,0,0,0,0,SXF_ABSOLUTEANGLE)
		HP10 A 0 A_SpawnItemEx("HLHornetSeekerTrail",(momx*-24)/-32.0,-(momy*-24)/-32.0,(momz*-24)/-32.0,0,0,0,0,SXF_ABSOLUTEANGLE)
		HP10 A 0 A_SpawnItemEx("HLHornetSeekerTrail",(momx*-20)/-32.0,-(momy*-20)/-32.0,(momz*-20)/-32.0,0,0,0,0,SXF_ABSOLUTEANGLE)
		HP10 A 0 A_SpawnItemEx("HLHornetSeekerTrail",(momx*-16)/-32.0,-(momy*-16)/-32.0,(momz*-16)/-32.0,0,0,0,0,SXF_ABSOLUTEANGLE)
		HP10 A 0 A_SpawnItemEx("HLHornetSeekerTrail",(momx*-12)/-32.0,-(momy*-12)/-32.0,(momz*-12)/-32.0,0,0,0,0,SXF_ABSOLUTEANGLE)
		HP10 A 0 A_SpawnItemEx("HLHornetSeekerTrail",(momx*-8)/-32.0,-(momy*-8)/-32.0,(momz*-8)/-32.0,0,0,0,0,SXF_ABSOLUTEANGLE)
		HP10 A 0 A_SpawnItemEx("HLHornetSeekerTrail",(momx*-4)/-32.0,-(momy*-4)/-32.0,(momz*-4)/-32.0,0,0,0,0,SXF_ABSOLUTEANGLE)
		HP10 D 1 bright A_SpawnItemEx("HLHornetSeekerTrail",0,0,0,0,0,0,0)
/*		HP10 A 0 A_SeekerMissile(45,45,SMF_LOOK|SMF_PRECISE,256,10)
		HP10 E 1 bright A_SpawnItemEx("HLHornetSeekerTrail",0,0,0,0,0,0,0,SXF_CLIENTSIDE)*/
		loop
	Crash:
	XDeath:		
		TNT1 A 0 A_Explode(CallACS("HalfLife_BulletDamage",24),8,!XF_HURTSOURCE,0,8)	
	Death:
		TNT1 A 5 A_SpawnItemEx("HLHornetSeekerDeath",0,0,0,0,0,0,0)
		stop
	}
}

actor HLHornetSeekerCoop : HLHornetSeeker { +THRUSPECIES Species "Player" +DONTHARMCLASS +DONTHARMSPECIES +MTHRUSPECIES }
actor HLHornetSeekerDM : HLHornetSeeker { Speed 12 }

Actor HLHornetSeekerTrail
{
	+NOGRAVITY
	-SOLID
	+NOINTERACTION
	+CLIENTSIDEONLY
	height 8
	radius 3
	renderstyle add
	alpha 0.65
	scale 0.08
	states
	{
	Spawn:
		TNT1 A 0 
		TNT1 A 1
	SpawnLoop:
		HW29 T 1 bright A_Fadeout(0.0275)
		loop
	}
}

Actor HLHornetSeekerDeath
{
	+CLIENTSIDEONLY
	+NOGRAVITY
	+NOINTERACTION
	-SOLID
	height 7
	radius 8
	renderstyle add
	alpha 0.9
	scale 0.7
	states
	{
	Spawn:
		TNT1 A 0
		HP10 B 1 bright A_PlaySound("halflife/hornet_hit",CHAN_BODY)
		HP10 CDEFG 1 bright
		stop
	}
}

actor SamsaraHalfLifeShockRoachAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("ShockRoachAmmo",1,1)
        stop
        TNT1 A 0 A_TakeInventory("ShockRoachAmmo",1)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("HLShockRoach/Fire",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireCustomMissile("HLShockProjectile",0,false,5,3)
        stop

      PickupAttackCoop:
        TNT1 A 0 A_FireCustomMissile("HLShockProjectileCoop",0,false,5,3)
        stop
    }
}

actor SamsaraHalfLifeShockRoachDischargeAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 ACS_NamedExecuteWithResult("SamsaraDecorate", 5, 0, 2)
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("ShockRoachAmmo",1,1)
        stop
        TNT1 A 0 A_TakeInventory("ShockRoachAmmo")
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("HLShockRoach/Discharge",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireCustomMissile("HLShockRoachDischarge",0,false)
        stop

      PickupAttackCoop:
        TNT1 A 0 A_FireCustomMissile("HLShockRoachDischargeCoop",0,false)
        stop
    }
}

actor ShockRoachAmmo : Ammo
{
    Inventory.MaxAmount 10
    Tag "Shock Roach Plasma"
    +IGNORESKILL
}

Actor HLShockProjectile : FastProjectile
{
	+EXTREMEDEATH
	+FORCERADIUSDMG
	+HITTRACER
	Speed 125
	Radius 2
	Height 4
	Damagetype "HLLaser"
	Damage (0)
	scale 0.7
	Renderstyle add
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_Jump(128, 2)
		TNT1 A 0 A_SetDamageType("HLEgon")
	SpawnLoop:
		TNT1 A 0
		HPI0 E 1 bright A_JumpIf(waterlevel >= 3, "FuckWater")
		loop

	Crash:
	XDeath:	
		TNT1 A 0 A_Explode(CallACS("HalfLife_BulletDamage",25),8,!XF_HURTSOURCE,0,8)	
		TNT1 A 0 A_PlaySound("HLShockRoach/Impact",CHAN_BODY)
		HWX4 A 2 bright
		TNT1 A 0 A_Jump(128,2)
		TNT1 A 0 A_SpawnItemEx("HLPuffSpark",0,0,0,FRandom(1,4),0,Random(0,3),Random(-90,90))
		TNT1 A 0 A_Jump(128,2)
		TNT1 A 0 A_SpawnItemEx("HLPuffSpark",0,0,0,FRandom(1,4),0,Random(0,3),Random(-90,90))
		TNT1 A 0 A_SpawnItemEx("HLPuffSpark",0,0,0,FRandom(1,4),0,Random(0,3),Random(-90,90))
		stop		

	Death:
		TNT1 A 0 A_PlaySound("HLShockRoach/Impact",CHAN_BODY)
		HWX4 A 2 bright
		TNT1 A 0 A_Jump(128,2)
		TNT1 A 0 A_SpawnItemEx("HLPuffSpark",0,0,0,FRandom(1,4),0,Random(0,3),Random(-90,90))
		TNT1 A 0 A_Jump(128,2)
		TNT1 A 0 A_SpawnItemEx("HLPuffSpark",0,0,0,FRandom(1,4),0,Random(0,3),Random(-90,90))
		TNT1 A 0 A_SpawnItemEx("HLPuffSpark",0,0,0,FRandom(1,4),0,Random(0,3),Random(-90,90))
		stop

    FuckWater:
        TNT1 A 0 A_Stop
        TNT1 A 0 A_ChangeFlag("NODAMAGETHRUST",true)
        TNT1 A 0 A_Explode(100,150)
        TNT1 A 0 A_ChangeFlag("NODAMAGETHRUST",false)
        TNT1 A 0 A_Die
        goto Death
	}
}

Actor HLShockProjectileCoop : HLShockProjectile { +THRUSPECIES Species "Player" +DONTHARMCLASS +DONTHARMSPECIES +MTHRUSPECIES }

actor HLShockRoachDischarge
{
    Projectile
    Radius 8
    Height 8
    DamageType "HLLaser"
    ReactionTime 1
    +NODAMAGETHRUST
    +FORCERADIUSDMG
    +NOTIMEFREEZE
    +DONTBLAST
    +DONTREFLECT
    +DONTSPLASH
    +NOTRIGGER
    +NOTONAUTOMAP
    +SKYEXPLODE
    +BLOODLESSIMPACT
    +EXTREMEDEATH
    States
    {
      Spawn:
        TNT1 A 0
        TNT1 A 0 A_Jump(128,2)
        TNT1 A 0 A_SetDamageType("HLEgon")
        TNT1 A 1 A_Countdown
        wait

      Death:
        TNT1 A 1 A_Explode(100*(CallACS("SamsaraDecorate", 5, 1)),150*(CallACS("SamsaraDecorate", 5, 1)))
        stop
    }
}

actor HLShockRoachDischargeCoop : HLShockRoachDischarge
{
    Species "Player"
    +THRUSPECIES
}
