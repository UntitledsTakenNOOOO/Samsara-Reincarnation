actor SamsaraHereticMaceAttackCycle : Counter { Inventory.MaxAmount 8 }

actor " Firemace " : Weapon
{
    Weapon.SelectionOrder 1400
    Inventory.PickUpSound "heretic/weaponget"
    Weapon.AmmoUse 1
    Weapon.AmmoGive1 10
    Weapon.SlotNumber 2
    Weapon.YAdjust 16
    Weapon.AmmoType "Shell"
    //+AMMO_OPTIONAL
    +INVENTORY.UNDROPPABLE
    +NOALERT
    Inventory.PickupMessage "You got the Firemace!"
    Obituary "$SAMSARA_CORVUS_OB_SLOT2_1"
    Inventory.RestrictedTo "CorvusPlayer"
    Tag "Firemace"
    States
    {
      Spawn:
        WMCE A -1
        stop

      Ready:
		TNT1 A 0 A_TakeInventory("CorvusHoldingSlot")
		TNT1 A 0 A_GiveInventory("CorvusHoldingSlot")
        MACE A 1 A_WeaponReady
        loop

      Deselect:
		MACE A 1 A_Lower
        loop

      Select:
        MACE A 1 A_Raise
        loop

      // Unpowered
      Fire:
        TNT1 A 0 A_JumpIfInventory("PowerHereticTome",1,"FirePowered")
		TNT1 A 0 A_JumpIfInventory("SamsaraHasPermaWeaponLevel2",1,"FirePowered")
      FireUnpowered:
        TNT1 A 0 A_AlertMonsters
        MACE B 4 A_GiveInventory("CorvusShootCloneTrigger")
        goto HoldStartUnpowered

      Hold:
        TNT1 A 0 A_JumpIfInventory("PowerHereticTome",1,"FirePowered")
		TNT1 A 0 A_JumpIfInventory("SamsaraHasPermaWeaponLevel2",1,"FirePowered")
      HoldUnpowered:
        TNT1 A 0 A_AlertMonsters
      HoldStartUnpowered:
        TNT1 A 0 A_TakeInventory("SamsaraTakingAmmo")
        TNT1 A 0 A_TakeInventory("SamsaraHereticMaceAttackCycle") // reset
      HoldCycleStartUnpowered:
        TNT1 A 0 A_GiveInventory("SamsaraHereticMaceAttackCycle")
        MACE F 0 A_JumpIfInventory("SamsaraHereticMaceAttackCycle",0,"HoldCycleTakeAmmoUnpowered")
        MACE E 0 A_JumpIfInventory("SamsaraHereticMaceAttackCycle",7,"HoldCycleAttackCheckUnpowered")
        MACE D 0 A_JumpIfInventory("SamsaraHereticMaceAttackCycle",6,"HoldCycleAttackCheckUnpowered")
        MACE C 0 A_JumpIfInventory("SamsaraHereticMaceAttackCycle",5,"HoldCycleAttackCheckUnpowered")
        MACE F 0 A_JumpIfInventory("SamsaraHereticMaceAttackCycle",4,"HoldCycleAttackCheckUnpowered")
        MACE E 0 A_JumpIfInventory("SamsaraHereticMaceAttackCycle",3,"HoldCycleAttackCheckUnpowered")
        MACE D 0 A_JumpIfInventory("SamsaraHereticMaceAttackCycle",2,"HoldCycleAttackCheckUnpowered")
        MACE C 0
        goto HoldCycleAttackCheckUnpowered

      HoldCycleTakeAmmoUnpowered:
        "####" "#" 0 A_GiveInventory("SamsaraTakingAmmo")
      HoldCycleAttackCheckUnpowered:
        "####" "#" 0 A_Jump(28,2) // Has about an 11% chance to fire a larger ball up.
        "####" "#" 0 A_GiveInventory("SamsaraHereticMaceAttackHandler1")
        goto HoldCycleJumpUnpowered
        "####" "#" 0 A_GiveInventory("SamsaraHereticMaceAttackHandler2") // this part of the code had no offset changes
        goto HoldCycleFinishUnpowered

      HoldCycleJumpUnpowered:
        "####" "#" 0 A_Jump(256,"HoldCycleUnpowered_N2_32","HoldCycleUnpowered_N2_33","HoldCycleUnpowered_N2_34","HoldCycleUnpowered_N2_35","HoldCycleUnpowered_N1_32","HoldCycleUnpowered_N1_33","HoldCycleUnpowered_N1_34","HoldCycleUnpowered_N1_35","HoldCycleUnpowered_0_32","HoldCycleUnpowered_0_33","HoldCycleUnpowered_0_34","HoldCycleUnpowered_0_35","HoldCycleUnpowered_1_32","HoldCycleUnpowered_1_33","HoldCycleUnpowered_1_34","HoldCycleUnpowered_1_35")
      HoldCycleUnpowered_N2_32:
        "####" "#" 0 Offset(-2,32)
        goto HoldCycleFinishUnpowered
      HoldCycleUnpowered_N2_33:
        "####" "#" 0 Offset(-2,33)
        goto HoldCycleFinishUnpowered
      HoldCycleUnpowered_N2_34:
        "####" "#" 0 Offset(-2,34)
        goto HoldCycleFinishUnpowered
      HoldCycleUnpowered_N2_35:
        "####" "#" 0 Offset(-2,35)
        goto HoldCycleFinishUnpowered
      HoldCycleUnpowered_N1_32:
        "####" "#" 0 Offset(-1,32)
        goto HoldCycleFinishUnpowered
      HoldCycleUnpowered_N1_33:
        "####" "#" 0 Offset(-1,33)
        goto HoldCycleFinishUnpowered
      HoldCycleUnpowered_N1_34:
        "####" "#" 0 Offset(-1,34)
        goto HoldCycleFinishUnpowered
      HoldCycleUnpowered_N1_35:
        "####" "#" 0 Offset(-1,35)
        goto HoldCycleFinishUnpowered
      HoldCycleUnpowered_0_32:
        "####" "#" 0 Offset(0,32)
        goto HoldCycleFinishUnpowered
      HoldCycleUnpowered_0_33:
        "####" "#" 0 Offset(0,33)
        goto HoldCycleFinishUnpowered
      HoldCycleUnpowered_0_34:
        "####" "#" 0 Offset(0,34)
        goto HoldCycleFinishUnpowered
      HoldCycleUnpowered_0_35:
        "####" "#" 0 Offset(0,35)
        goto HoldCycleFinishUnpowered
      HoldCycleUnpowered_1_32:
        "####" "#" 0 Offset(1,32)
        goto HoldCycleFinishUnpowered
      HoldCycleUnpowered_1_33:
        "####" "#" 0 Offset(1,33)
        goto HoldCycleFinishUnpowered
      HoldCycleUnpowered_1_34:
        "####" "#" 0 Offset(1,34)
        goto HoldCycleFinishUnpowered
      HoldCycleUnpowered_1_35:
        "####" "#" 0 Offset(1,35)
        goto HoldCycleFinishUnpowered

      HoldCycleFinishUnpowered:
        "####" "#" 3
        TNT1 A 0 A_JumpIfInventory("SamsaraHereticMaceAttackCycle",0,1)
        goto HoldCycleStartUnpowered
        MACE C 4 A_ReFire
        MACE DEFB 4
        goto Ready

      // Powered
      FirePowered:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("Shell",5,1)
        goto NoAmmoSwitch
        TNT1 A 0 A_AlertMonsters
        MACE B 4 A_GiveInventory("CorvusShootCloneTrigger")
        MACE D 4 A_GiveInventory("SamsaraHereticMacePoweredAttackHandler")
        MACE B 4
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("Shell",5,1)
        goto NoAmmoSwitch
        MACE A 8 A_ReFire
        goto Ready

      // "No ammo" switch
      NoAmmoSwitch:
        TNT1 A 0 A_ClearReFire
        TNT1 A 0 A_SelectWeapon("Elven Wand")
        goto Deselect
    }
}

// Unpowered

actor SamsaraHereticMaceAttackHandler1 : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Shell",1,1)
        stop
        TNT1 A 0 A_JumpIfInventory("SamsaraTakingAmmo",1,1)
        goto PickupAttack
        TNT1 A 0 A_TakeInventory("Shell",1)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireCustomMissile("MaceFX12",random(-5,5),false,0,0,0,2) //A_FireMacePL1
        stop

      PickupAttackCoop:
        TNT1 A 0 A_FireCustomMissile("MaceFX1Coop",random(-5,5),false,0,0,0,2)
        stop
    }
}

actor SamsaraHereticMaceAttackHandler2 : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Shell",1,1)
        stop
        TNT1 A 0 A_JumpIfInventory("SamsaraTakingAmmo",1,1)
        goto PickupAttack
        TNT1 A 0 A_TakeInventory("Shell",1)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireCustomMissile("MaceFX22",0,false,0,2,0,10)
        stop

      PickupAttackCoop:
        TNT1 A 0 A_FireCustomMissile("MaceFX2Coop",0,false,0,2,0,10)
        stop
    }
}

actor MaceFX12 : MaceFX1 replaces MaceFX1
{
    -NOGRAVITY
    Gravity 0.125
    VSpeed 0.5
    DamageType "Mace"
    Damage 3 // Originally 2
    Decal BaronScorch
    //+STRIFEDAMAGE
    Obituary "$SAMSARA_CORVUS_OB_SLOT2_1"
    +BLOODSPLATTER
}

actor MaceFX1Coop : MaceFX12
{
    Species "Player"
    +THRUSPECIES
}

actor MaceFX22 : MaceFX2 replaces MaceFX2
{
    -NOGRAVITY
    Gravity 0.125
    VSpeed 0.5
    DamageType "Mace"
    Damage 4 // Originally 6
    SeeSound "weapons/maceshoot"
    Decal BaronScorch
    //+STRIFEDAMAGE
    Obituary "$SAMSARA_CORVUS_OB_SLOT2_1"
    +BLOODSPLATTER
}

actor MaceFX2Coop : MaceFX22
{
    Species "Player"
    +THRUSPECIES
}

actor MaceFX32 : MaceFX3 replaces MaceFX3
{
    DamageType "Mace"
    Damage 4
    Decal BaronScorch
    //+STRIFEDAMAGE
    Obituary "$SAMSARA_CORVUS_OB_SLOT2_1"
    Species "Player"
    +BLOODSPLATTER
    +THRUSPECIES
    States
    {
      Spawn: // hack
        FX02 A 0 NoDelay A_JumpIfInventory("SPModeOn",1,"SpawnStart",AAPTR_TARGET)
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"SpawnStart",AAPTR_TARGET)
        TNT1 A 0 A_ChangeFlag("THRUSPECIES",false)
      SpawnStart:
        goto Super::Spawn
    }
}

// Powered

actor SamsaraHereticMacePoweredAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Shell",5,1)
        stop
        TNT1 A 0 A_TakeInventory("Shell",5)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("weapons/maceshoot",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireCustomMissile("MaceFX42",0,false)
        stop

      PickupAttackCoop:
        TNT1 A 0 A_FireCustomMissile("MaceFX43",0,false)
        stop
    }
}

actor MaceFX42 : MaceFX4// replaces MaceFX4
{
    DamageType "Mace"
    Decal BFGScorch
    +FOILINVUL
    Obituary "$SAMSARA_CORVUS_OB_SLOT2_2"
    Radius 12
    Height 8
    Speed 7
    Damage (1000000) //18
    Gravity 0.125
    Projectile
    +BLOODSPLATTER
    -NOGRAVITY
    +TELESTOMP
    +THRUGHOST
    -NOTELEPORT
    BounceType "Hexen" // Oddly enough, not Heretic bounce.
    BounceCount 8
    BounceFactor 1
    BounceSound "weapons/macestop"
    States
    {
      Spawn:
        FX02 E -1
        stop

      Death:
        TNT1 A 0 A_Stop
        TNT1 A 0 A_StopSound(CHAN_VOICE) // would've played the following sound on CHAN_VOICE, but things seem to not work when dying at a wall, huh.
        FX02 C 4 A_PlaySound("weapons/maceexplode") //A_DeathBallImpact
        FX02 GHIJ 4 Bright
        Stop
    }
}

actor MaceFX43 : MaceFX4
{
    +THRUSPECIES
    Species "Player"
    DamageType "Mace"
    Decal BFGScorch
    +FOILINVUL
}

// Lesser powered (unused)

actor MaceFX23 : MaceFX22
{
	Scale 1.5
	Radius 9
	Height 6
	Damage 7
	Speed 9
}

actor MaceFX23_Silent : MaceFX23 { SeeSound "" }
