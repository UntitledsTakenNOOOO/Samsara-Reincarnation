actor UsingPistols: Boolean {}
actor UsingDualPistols: Boolean {}
actor CanDualPistols: Boolean {}

actor ".44 Magnum Mega Class A1" : SamsaraMarathonWeapon
{
    Inventory.PickupMessage "You picked up a .44 Magnum Mega Class A1!"
    Weapon.SlotNumber 1
    Weapon.SelectionOrder 1900
    Weapon.AmmoType "MagnumBullet"
    Weapon.AmmoUse 1
    Tag ".44 Magnum Mega Class A1"
    +WIMPY_WEAPON
    States
    {
      Spawn:
        PSTL A -1
        stop

      // Single
      Ready:
        TNT1 A 0 A_JumpIfInventory("SynthFireActive",1,"LReadyInit")
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",0,"ReadyAllowReload")
      ReadyDisallowReload:
        PIST A 1 A_WeaponReady
        goto ReadyAmmoCheck

      ReadyAllowReload:
        TNT1 A 0 A_JumpIfInventory("MagnumBullet",0,"ReadyDisallowReload")
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto ReadyAllowReloadSuccess
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadOffPlus",1,2)
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"ReadyAllowReloadSuccess")
        TNT1 A 0 A_JumpIfInventory("Clip",1,"ReadyAllowReloadSuccess")
        goto ReadyDisallowReload

      ReadyAllowReloadSuccess:
        PIST A 1 A_WeaponReady(WRF_ALLOWRELOAD)
        goto ReadyAmmoCheck

      ReadyAmmoCheck:
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",1,1)
        goto Ready
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadOffPlus",1,"Ready")
        TNT1 A 0 A_JumpIfInventory("MagnumBullet",1,"Ready")
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto Reload
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"Reload")
        TNT1 A 0 A_JumpIfInventory("Clip",1,"Reload")
        goto Ready

      Deselect:
        TNT1 A 0 A_JumpIfInventory("SynthFireActive",1,"LDeselect")
        TNT1 A 0 A_TakeInventory("SamsaraLevelSwitch_DualWielding")
      DeselectLoop:
        TNT1 A 0 A_JumpIfHealthLower(1,"DeselectInstant")
        TNT1 A 0 A_Lower
        PIST A 1 A_Lower
        goto Deselect

      Select:
        TNT1 A 0 A_TakeInventory("UsingFists")
        //TNT1 A 0 A_TakeInventory("UsingPistols")
        //TNT1 A 0 A_TakeInventory("UsingDualPistols")
        TNT1 A 0 A_TakeInventory("UsingSMG")
        TNT1 A 0 A_TakeInventory("UsingPacifier")
        TNT1 A 0 A_TakeInventory("UsingShotguns")
        TNT1 A 0 A_TakeInventory("UsingDualShotguns")
        TNT1 A 0 A_TakeInventory("UsingFusionPistol")
        TNT1 A 0 A_TakeInventory("UsingAssaultRifle")
        TNT1 A 0 A_TakeInventory("UsingSpanker")
        TNT1 A 0 A_TakeInventory("UsingToasty")
        TNT1 A 0 A_TakeInventory("UsingWMC")
        TNT1 A 0 A_TakeInventory("UsingAlienWeapon")
        TNT1 A 0 A_TakeInventory("UsingAlienWeapon2")
        TNT1 A 0 A_TakeInventory("UsingMarathonInstagib")

        TNT1 A 0 A_GiveInventory("UsingPistols")
        TNT1 A 0 A_JumpIfInventory("SamsaraLevelSwitch_DualWielding",1,"LSelect")
        TNT1 A 0 A_JumpIfInventory("SynthFireActive",1,"LSelect")
      SelectLoop:
        TNT1 A 0 A_TakeInventory("UsingDualPistols")
        TNT1 A 0 A_TakeInventory("SamsaraLevelSwitch_DualWielding")
        TNT1 A 0 A_TakeInventory("SynthFireActive")
        TNT1 A 0 A_TakeInventory("LeftReload")
        TNT1 A 0 A_TakeInventory("RightReload")
        TNT1 A 0 A_Raise
        PIST A 1 A_Raise
        goto Select

      Fire:
        TNT1 A 0 A_JumpIfInventory("SynthFireActive",1,"LReady")
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"FireReloadable")
      FireNonReloadable:
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto FireStart
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireStart")
        TNT1 A 0 A_JumpIfInventory("Clip",1,"FireStart")
        goto Ready

      FireReloadable:
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadOffPlus",1,"FireStart")
        TNT1 A 0 A_JumpIfInventory("MagnumBullet",1,"FireStart")
      FireReloadableDryCheck:
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto Reload
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"Reload")
        TNT1 A 0 A_JumpIfInventory("Clip",1,"Reload")
        goto Ready

      FireStart:
        TNT1 A 0 A_AlertMonsters
        TNT1 A 0 A_FireCustomMissile("44mmCasingSpawner",0,false,8)
        TNT1 A 0 A_GiveInventory("SamsaraMarathonMagnumLeftAttackHandler")
        TNT1 A 0 A_Light2
        PIST A 3 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        TNT1 A 0 A_Light1
        PIST B 3 Bright A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        TNT1 A 0 A_Light0
        PIST C 3 Bright A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        PIST D 3 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        PIST E 4 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        TNT1 A 0 A_ReFire
        goto Ready

      Reload:
        PIST F 11
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"ReloadAmmo")
      ReloadFinish:
        PIST GH 11
        goto Ready

      ReloadAmmo:
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,"ReloadAmmoReserve")
        TNT1 A 0 A_JumpIfInventory("MagnumBullet",0,"ReloadFinish")
        TNT1 A 0 A_GiveInventory("MagnumBullet")
        TNT1 A 0 A_PlaySound("marathon/pistol/reload")
        loop

      ReloadAmmoReserve:
        TNT1 A 0 A_JumpIfInventory("MagnumBullet",0,"ReloadFinish")
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadOffPlus",1,2)
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("Clip",1,1)
        goto ReloadFinish
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadOffPlus",1,2)
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_TakeInventory("Clip",1)
        TNT1 A 0 A_GiveInventory("MagnumBullet")
        TNT1 A 0 A_PlaySound("marathon/pistol/reload")
        goto ReloadAmmo

      // Dual Init
      AltFire:
        TNT1 A 0 A_JumpIfInventory("CanDualPistols",1,1)
        goto Fire
        TNT1 A 0 A_JumpIfInventory("SynthFireActive",1,"LReady")
        TNT1 A 0 A_GiveInventory("UsingDualPistols")
        TNT1 A 0 A_GiveInventory("SamsaraLevelSwitch_DualWielding")
        TNT1 A 0 A_GiveInventory("SynthFireActive")
        PIS3 ABCDEFGHIJK 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        goto Ready

      // Dual
      // Left Magnum
      LReadyInit:
        TNT1 A 0 A_GunFlash("RReadyInit",GFF_NOEXTCHANGE)
        PIS1 A 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
      LReady:
        TNT1 A 0 A_JumpIfInventory("RightReload",1,"LLoadingOther")
        TNT1 A 0 A_JumpIfInventory("CanDualPistols",1,1)
        goto ForceSingleReady
        TNT1 A 0 A_JumpIfInventory("SamsaraReverseDualWieldFiring",1,2)
        TNT1 A 0 A_JumpIfInventory("SynthFireRight",1,"LFire")
        goto LReadyStart
        TNT1 A 0 A_JumpIfInventory("SynthFireLeft",1,"LFire")
        goto LReadyStart

      LReadyStart:
        TNT1 A 0 A_JumpIfInventory("LeftReload",1,2)
        PIS1 A 1 A_WeaponReady(WRF_NOFIRE)
        goto LReadyAmmoCheck
        PIS1 A 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE) // let the other magnum raise from reloading first
        goto LReadyAmmoCheck

      LReadyAmmoCheck:
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",1,1)
        goto LReady
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadOffPlus",1,"LReady")
        TNT1 A 0 A_JumpIfInventory("MagnumBullet",1,"LReady")
        TNT1 A 0 A_JumpIfInventory("LeftReload",1,"LReady")
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto LReload
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"LReload")
        TNT1 A 0 A_JumpIfInventory("Clip",1,"LReload")
        goto LReady

      LLoadingOther:
        PIS1 IJKLMNO 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE) // let the other magnum bob
        TNT1 AAA 11
        PIS1 ONMLKJI 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE) // do it again
        TNT1 A 0 A_TakeInventory("RightReload")
        goto LReady

      LDeselect:
        TNT1 A 0 A_TakeInventory("SamsaraLevelSwitch_DualWielding")
        TNT1 A 0 A_TakeInventory("SynthFireActive")
        TNT1 A 0 A_TakeInventory("LeftReload")
        TNT1 A 0 A_TakeInventory("RightReload")
        TNT1 A 0 A_JumpIfInventory("CanDualPistols",1,"LDeselectLoop")
        TNT1 A 0 A_TakeInventory("UsingDualPistols")
        TNT1 A 0 A_GunFlash("LightDone",GFF_NOEXTCHANGE)
        goto DeselectLoop

      LDeselectLoop:
        TNT1 A 0 A_JumpIfHealthLower(1,"LDeselectInstant")
        TNT1 A 0 A_GunFlash("RDeselect",GFF_NOEXTCHANGE)
        TNT1 A 0 A_Lower
        PIS1 A 1 A_Lower
        goto LDeselect

      LDeselectInstant:
        TNT1 A 0 A_GunFlash("LightDone",GFF_NOEXTCHANGE)
        goto DeselectInstant

      LSelect:
        TNT1 A 0 A_JumpIfInventory("CanDualPistols",1,2)
        TNT1 A 0 A_GunFlash("LightDone",GFF_NOEXTCHANGE)
        goto SelectLoop
        TNT1 A 0 A_GiveInventory("UsingDualPistols")
        TNT1 A 0 A_GiveInventory("SamsaraLevelSwitch_DualWielding")
        TNT1 A 0 A_GiveInventory("SynthFireActive")
        TNT1 A 0 A_GunFlash("RSelect",GFF_NOEXTCHANGE)
        TNT1 A 0 A_Raise
        PIS1 A 1 A_Raise
        goto Select

      LFire:
        TNT1 A 0 A_JumpIfInventory("RightReload",1,"LLoadingOther")
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"LFireReloadableReloadCheck")
      LFireNonReloadable:
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto LFireStart
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"LFireStart")
        TNT1 A 0 A_JumpIfInventory("Clip",1,"LFireStart")
        goto LReadyStart

      LFireReloadable:
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadOffPlus",1,"LFireStart")
        TNT1 A 0 A_JumpIfInventory("MagnumBullet",1,"LFireStart")
      LFireReloadableDryCheck:
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,2)
        TNT1 A 0 A_JumpIfInventory("LeftReload",1,"LReadyStart")
        goto LReload
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("Clip",1,1)
        goto LReadyStart
        TNT1 A 0 A_JumpIfInventory("LeftReload",1,"LReadyStart")
        goto LReload

      LFireReloadableReloadCheck:
        TNT1 A 0 A_JumpIfInventory("LeftReload",1,"LFireReloadable") // ignore if other magnum is doing its reload animation
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",0,1) // only allowed with modern reloading
        goto LFireReloadable
        TNT1 A 0 A_JumpIfInventory("SynthFireReload",1,1) // holding attack + reload?
        goto LFireReloadable
        TNT1 A 0 A_JumpIfInventory("MagnumBullet",0,"LFireReloadable") // ignore if full on magazine or insufficient reserve
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto LReload
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadOffPlus",1,2)
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"LReload")
        TNT1 A 0 A_JumpIfInventory("Clip",1,"LReload")
        goto LFireReloadable

      LFireStart:
        TNT1 A 0 ACS_NamedExecuteWithResult("SamsaraForcePlayerAttackState")
        TNT1 A 0 A_AlertMonsters
        TNT1 A 0 A_FireCustomMissile("44mmCasingSpawner",0,false,8)
        TNT1 A 0 A_GiveInventory("SamsaraMarathonMagnumLeftAttackHandler")
        TNT1 A 0 A_Light2
        PIS1 A 3 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        TNT1 A 0 A_JumpIfInventory("RightReload",1,"LLoadingOther")
        TNT1 A 0 A_Light1
        PIS1 B 3 Bright A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        TNT1 A 0 A_JumpIfInventory("RightReload",1,"LLoadingOther")
        TNT1 A 0 A_Light0
        PIS1 C 3 Bright A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        TNT1 A 0 A_JumpIfInventory("RightReload",1,"LLoadingOther")
        PIS1 D 3 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        TNT1 A 0 A_JumpIfInventory("RightReload",1,"LLoadingOther")
        PIS1 E 4 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        goto LReady // refire stuff technically called in state

      LReload:
        TNT1 A 0 A_JumpIfInventory("RightReload",1,"LLoadingOther")
        TNT1 A 0 A_Light0
        TNT1 A 0 A_GiveInventory("LeftReload")
        PIS1 AAAAAAA 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        PIS1 F 11
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"LReloadAmmo")
      LReloadFinish:
        PIS1 GH 11
        goto LReady

      LReloadAmmo:
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,"LReloadAmmoReserve")
        TNT1 A 0 A_JumpIfInventory("MagnumBullet",0,"LReloadFinish")
        TNT1 A 0 A_GiveInventory("MagnumBullet")
        TNT1 A 0 A_PlaySound("marathon/pistol/reload")
        loop

      LReloadAmmoReserve:
        TNT1 A 0 A_JumpIfInventory("MagnumBullet",0,"LReloadFinish")
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadOffPlus",1,2)
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("Clip",1,1)
        goto LReloadFinish
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadOffPlus",1,2)
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_TakeInventory("Clip",1)
        TNT1 A 0 A_GiveInventory("MagnumBullet")
        TNT1 A 0 A_PlaySound("marathon/pistol/reload")
        goto LReloadAmmo

      // Right Magnum
      RReadyInit:
        PIS2 A 1
      RReady:
        TNT1 A 0 A_JumpIfInventory("LeftReload",1,"RLoadingOther")
        TNT1 A 0 A_JumpIfInventory("CanDualPistols",1,1)
        goto RReadyStart
        TNT1 A 0 A_JumpIfInventory("SamsaraReverseDualWieldFiring",1,2)
        TNT1 A 0 A_JumpIfInventory("SynthFireLeft",1,"RFire")
        goto RReadyStart
        TNT1 A 0 A_JumpIfInventory("SynthFireRight",1,"RFire")
        goto RReadyStart

      RReadyStart:
        PIS2 A 1
        goto RReadyAmmoCheck

      RReadyAmmoCheck:
        TNT1 A 0 A_JumpIfInventory("CanDualPistols",1,1)
        goto RReady
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",1,1)
        goto RReady
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadOffPlus",1,"RReady")
        TNT1 A 0 A_JumpIfInventory("MagnumBulletRight",1,"RReady")
        TNT1 A 0 A_JumpIfInventory("RightReload",1,"RReady")
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto RReload
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"RReload")
        TNT1 A 0 A_JumpIfInventory("Clip",1,"RReload")
        goto RReady

      RLoadingOther:
        PIS2 IJKLMNO 1
        TNT1 AAA 11
        PIS2 ONMLKJI 1
        TNT1 A 0 A_TakeInventory("LeftReload")
        goto RReady

      RDeselect:
        PIS2 A -1 A_Light0
        stop

      RSelect:
        PIS2 A -1
        stop

      RFire:
        TNT1 A 0 A_JumpIfInventory("LeftReload",1,"RLoadingOther")
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"RFireReloadableReloadCheck")
      RFireNonReloadable:
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto RFireStart
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"RFireStart")
        TNT1 A 0 A_JumpIfInventory("Clip",1,"RFireStart")
        goto RReadyStart

      RFireReloadable:
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadOffPlus",1,"RFireStart")
        TNT1 A 0 A_JumpIfInventory("MagnumBulletRight",1,"RFireStart")
      RFireReloadableDryCheck:
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,2)
        TNT1 A 0 A_JumpIfInventory("RightReload",1,"RReadyStart")
        goto RReload
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("Clip",1,1)
        goto RReadyStart
        TNT1 A 0 A_JumpIfInventory("RightReload",1,"RReadyStart")
        goto RReload

      RFireReloadableReloadCheck:
        TNT1 A 0 A_JumpIfInventory("RightReload",1,"RFireReloadable") // ignore if other magnum is doing its reload animation
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",0,1) // only allowed with modern reloading
        goto RFireReloadable
        TNT1 A 0 A_JumpIfInventory("SynthFireReload",1,1) // holding altattack + reload?
        goto RFireReloadable
        TNT1 A 0 A_JumpIfInventory("MagnumBulletRight",0,"RFireReloadable") // ignore if full on magazine or insufficient reserve
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto RReload
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadOffPlus",1,2)
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"RReload")
        TNT1 A 0 A_JumpIfInventory("Clip",1,"RReload")
        goto RFireReloadable

      RFireStart:
        TNT1 A 0 ACS_NamedExecuteWithResult("SamsaraForcePlayerAttackState")
        TNT1 A 0 A_AlertMonsters
        TNT1 A 0 A_FireCustomMissile("44mmCasingSpawner2",0,false,-8)
        TNT1 A 0 A_GiveInventory("SamsaraMarathonMagnumRightAttackHandler")
        PIS2 A 3 A_Light2
        TNT1 A 0 A_JumpIfInventory("LeftReload",1,"RLoadingOther")
        PIS2 B 3 Bright A_Light1
        TNT1 A 0 A_JumpIfInventory("LeftReload",1,"RLoadingOther")
        PIS2 C 3 Bright A_Light0
        PIS2 D 3 A_JumpIfInventory("LeftReload",1,"RLoadingOther")
        PIS2 E 4 A_JumpIfInventory("LeftReload",1,"RLoadingOther")
        goto RReady // refire stuff technically called in state

      RReload:
        TNT1 A 0 A_JumpIfInventory("LeftReload",1,"RLoadingOther")
        TNT1 A 0 A_Light0
        PIS2 A 1 A_GiveInventory("RightReload")
        PIS2 AAAAAA 1
        PIS2 F 11
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"RReloadAmmo")
      RReloadFinish:
        PIS2 GH 11
        goto RReady

      RReloadAmmo:
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,"RReloadAmmoReserve")
        TNT1 A 0 A_JumpIfInventory("MagnumBulletRight",0,"RReloadFinish")
        TNT1 A 0 A_GiveInventory("MagnumBulletRight")
        TNT1 A 0 A_PlaySound("marathon/pistol/reload")
        loop

      RReloadAmmoReserve:
        TNT1 A 0 A_JumpIfInventory("MagnumBulletRight",0,"RReloadFinish")
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadOffPlus",1,2)
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("Clip",1,1)
        goto RReloadFinish
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadOffPlus",1,2)
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_TakeInventory("Clip",1)
        TNT1 A 0 A_GiveInventory("MagnumBulletRight")
        TNT1 A 0 A_PlaySound("marathon/pistol/reload")
        goto RReloadAmmo

      // Dual force remover
      ForceSingleReady:
        TNT1 A 0 A_TakeInventory("UsingDualPistols")
        TNT1 A 0 A_TakeInventory("SamsaraLevelSwitch_DualWielding")
        TNT1 A 0 A_TakeInventory("SynthFireActive")
        TNT1 A 0 A_TakeInventory("LeftReload")
        TNT1 A 0 A_TakeInventory("RightReload")
        TNT1 A 0 A_GunFlash("LightDone",GFF_NOEXTCHANGE)
        goto Ready
    }
}

actor SamsaraMarathonMagnumLeftAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"PickupReloadable")
      PickupNonReloadable:
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto PickupAttack
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Clip",1,1)
        stop
        TNT1 A 0 A_TakeInventory("Clip",1)
        goto PickupAttack

      PickupReloadable:
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadOffPlus",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("MagnumBullet",1,1)
        stop
        TNT1 A 0 A_TakeInventory("MagnumBullet",1)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("marathon/pistol/fire",CHAN_WEAPON)
        TNT1 A 0 A_FireBullets(0,0,1,0,"SamsaraBlankPuff",FBF_NORANDOM|FBF_NOFLASH)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireCustomMissile("MarathonMagnumBullet",random(-1,1),false,0,0,0,random(-1,1))
        stop

      PickupAttackCoop:
        TNT1 A 0 A_FireCustomMissile("MarathonMagnumBulletCoop",random(-1,1),false,0,0,0,random(-1,1))
        stop
    }
}

actor SamsaraMarathonMagnumRightAttackHandler : SamsaraMarathonMagnumLeftAttackHandler
{
    States
    {
      // Ammo checking/taking stuff
      PickupReloadable:
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadOffPlus",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("MagnumBulletRight",1,1)
        stop
        TNT1 A 0 A_TakeInventory("MagnumBulletRight",1)
        goto PickupAttack
    }
}

actor MagnumBullet : Ammo
{
    Inventory.MaxAmount 8
    Tag ".44 Clip (Right)"
    +IGNORESKILL
}

actor MagnumBulletRight : Ammo
{
    Inventory.MaxAmount 8
    Tag ".44 Clip (Left)"
    +IGNORESKILL
}

actor MarathonMagnumBullet : SamsaraMarathonBullet
{
    Damage (random(10,18))
    Obituary "$SAMSARA_MARATHON_OB_PISTOL"
    DamageType "MarathonPistol"
}

actor MarathonMagnumBulletCoop : MarathonMagnumBullet
{
    Radius 1
    Height 1
    Speed 80
    Damage (random(20,28))
    Species "Player"
    +THRUSPECIES
}

actor 44mmCasingSpawner : SamsaraMarathonCasingSpawner
{
    States
    {
      Death:
        TNT1 A 1 A_CustomMissile("44mmCasing",1,5,random(-110,-80),CMF_AIMDIRECTION,random(30,40))
        stop
    }
}

actor 44mmCasingSpawner2 : SamsaraMarathonCasingSpawner
{
    States
    {
      Death:
        TNT1 A 0 A_CustomMissile("44mmCasing",1,-5,random(80,110),CMF_AIMDIRECTION,random(30,40))
        stop
    }
}

actor 44mmCasing : SamsaraMarathonCasing
{ 
    Scale 0.25
    States
    {
      Spawn:
        PBLT ABCDEF 1
        loop
    }
}
