actor "  Dart Cannon  " : Weapon
{
	weapon.selectionorder 1300
	Inventory.PickupSound "EradAmmo/PUP"
	weapon.ammotype "Clip"
	Weapon.BobStyle Smooth
	Weapon.BobRangeX 0.55
	Weapon.BobRangeY 0.55
	Weapon.BobSpeed 1.2  
	weapon.ammouse 1
	Weapon.AmmoGive 18
	Weapon.SlotNumber 4
	+Weapon.NoAlert
	Inventory.PickupMessage "You got the Dart Cannon!"
	Obituary "%o was pierced by %k's death darts."
	Inventory.RestrictedTo "Eleena"
	Tag "Dart Cannon"
	states
	{
		Spawn:
			DZ8P A -1
			Stop
		Ready:
			TNT1 A 0 A_JumpIfInventory("EleenaReady",1,"IntroTaunt")
			ELDC A 1 A_WeaponReady
			Loop
		Deselect:
			TNT1 A 0 A_PlaySound("Erad/wepswitch")
			TNT1 A 0 A_Lower
			ELDC A 1 A_Lower
			Goto Deselect+1
		Select:
			TNT1 A 0 A_Raise
			ELDC A 1 A_Raise
			Loop
		Fire:
			TNT1 A 0 A_AlertMonsters  
			TNT1 A 0 A_PlaySound("eleena/dartfire", CHAN_WEAPON)
			TNT1 A 0 A_GunFlash
			ELDC A 3 bright A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			TNT1 A 0 A_JumpIfInventory("CoopModeOn", 1, "EDartCoopFire")	
			TNT1 A 0 A_FireCustomMissile("ImpalingDart",frandom(3.00,7.00),1,6,0,0)
			TNT1 A 0 A_FireCustomMissile("ImpalingDart",frandom(-3.00,-7.00),0,-6,0,0)
			TNT1 A 0 A_FireCustomMissile("ImpalingDart",frandom(-2.00,2.00),0,0,0,0)
			Goto A
		EDartCoopFire:
			TNT1 A 0 A_FireCustomMissile("CoopImpalingDart",frandom(3.00,7.00),1,6,0,0)
			TNT1 A 0 A_FireCustomMissile("CoopImpalingDart",frandom(-3.00,-7.00),0,-6,0,0)
			TNT1 A 0 A_FireCustomMissile("CoopImpalingDart",frandom(-2.00,2.00),0,0,0,0)
		A:	
			ELDC C 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			ELDC C 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			ELDC D 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			TNT1 A 0 A_Refire
			ELDC E 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			Goto Ready
		AltFire:
			TNT1 A 0 A_JumpIfInventory("SamsaraHasPermaWeaponLevel2",1,2)
			TNT1 A 0 A_JumpIfInventory("PowerEradEnWP",1,1)
			Goto Ready
			TNT1 A 0 A_JumpIfInventory("Clip",1,1)
			Goto Ready	
			TNT1 A 0 A_AlertMonsters	
			TNT1 A 0 A_PlaySound("eleena/dartfire2", CHAN_WEAPON)
			TNT1 A 0 A_GunFlash
			ELDC A 3 bright A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			TNT1 A 0 A_TakeInventory("Clip",1, TIF_NOTAKEINFINITE)
			TNT1 A 0 A_JumpIfInventory("CoopModeOn", 1, "EDartCoopFire2")	
			TNT1 A 0 A_FireCustomMissile("ImpalingDart",0,1,8,0,0)
			ELDC C 0 A_FireCustomMissile("ImpalingDart",0,0,-8,0,0)
			ELDC C 0 A_FireCustomMissile("ImpalingDart",0,0,0,0,0)
			Goto B
		EDartCoopFire2:
			TNT1 A 0 A_FireCustomMissile("CoopImpalingDart",0,1,8,0,0)
			ELDC C 0 A_FireCustomMissile("CoopImpalingDart",0,0,-8,0,0)
			ELDC C 0 A_FireCustomMissile("CoopImpalingDart",0,0,0,0,0)
		B:
			ELDC C 1 Bright A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			ELDC D 1 Bright A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			TNT1 A 0 A_Refire
			ELDC E 1 Bright A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			Goto Ready
		Flash:
			TNT1 A 0 A_Light1
			ELDC B 3 bright A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			TNT1 A 0 A_Light0
			stop
		IntroTaunt:
			TNT1 A 0 A_GiveInventory("EleenaIntroTaunt")
			Goto Ready+1
	}
}

actor ImpalingDart
{
	Speed 40
	Radius 3
	Height 3
	DamageType "EradPiercingDart"
	+BLOODSPLATTER
	+THRUGHOST
	+WINDTHRUST
	+PIERCEARMOR
	Damage (7)
	Projectile
	Renderstyle Normal
	States
	{
		Spawn:
			DS95 J 1
			Loop
		Death:
			DS95 J 1 A_PlaySound("Dart/PierceWall")
			DS95 J 5 
			stop
		XDeath:
		Crash:
			TNT1 A 1
			stop
	}
}

Actor CoopImpalingDart : ImpalingDart
{
	Species "Player"
	+THRUSPECIES
}