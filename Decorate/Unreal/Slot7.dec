actor SamsaraUnrealRedeemerMainRocketControlled : Boolean {} // for spread rune
actor SamsaraUnrealRedeemerRocketDeath : Boolean {}

Actor Unreal_Redeemer : UnrealWeapon
{
	Tag "Redeemer"
	Weapon.SlotNumber 7
	Weapon.AmmoType "Cell"
	Weapon.AmmoGive 60
	Weapon.AmmoUse 15
	Inventory.PickupMessage "You got the Redeemer."
	Obituary "%o was vaporized by %k's Redeemer!!"
	+BFG
	+EXPLOSIVE
    +NOALERT
	States
	{
		Spawn:
			UN00 A 10 Bright
			UN00 A 10
			loop

		ReadyActual:
			//UN01 O 0 A_GunFlash("ReadyActualTarget",GFF_NOEXTCHANGE)
			UN01 O 1 A_WeaponReady
            goto ReadyActualJump

		/*ReadyActualTarget:
			TNT1 A 1 A_GiveInventory("SamsaraUnrealRocketLauncherTargetingHandler")
			stop*/

        ReadyActualJump:
			UN01 O 0 A_Jump(6,"ReadyActualIdle1","ReadyActualIdle2")
			goto ReadyActual

		ReadyActualIdle1:
			//UN01 O 0 A_GunFlash("ReadyActualIdleTarget",GFF_NOEXTCHANGE)
            UN01 O 12 A_WeaponReady // interpolation hack
			UN06 ABCABC 12 A_WeaponReady
			goto ReadyActual

		ReadyActualIdle2:
			//UN01 O 0 A_GunFlash("ReadyActualIdleTarget",GFF_NOEXTCHANGE)
            UN01 O 12 A_WeaponReady // interpolation hack
			UN07 ABABAB 12 A_WeaponReady
			goto ReadyActual

		/*ReadyActualIdleTarget:
			TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 2 A_GiveInventory("SamsaraUnrealRocketLauncherTargetingHandler")
			stop*/

		Deselect:
			UN02 A 0 A_TakeInventory("Unreal_RedeemerSelected")
			UN02 A 1 A_JumpIfHealthLower(1,"DeselectInstant")
			UN02 BCDEFGHIJ 1
			goto DeselectInstant

		Ready: // our "Select" state
			UN05 A 0 A_GiveInventory("Unreal_RedeemerSelected")
            UN05 A 0 A_PlaySound("Unreal/RocketL/Select")
			UN05 ABCDEFG 2 A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
			goto ReadyActual

		Fire:
            UN03 A 0 A_AlertMonsters
			UN03 A 0 A_GiveInventory("SamsaraUnrealRedeemerPrimaryAttackHandler")
			UN03 AB 5 Bright A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			UN03 CDEFGHIJKLM 2 Bright A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
            UN01 O 0 A_CheckReload
			goto ReadyActual

		AltFire:
            UN03 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"AltFireStart")
            UN03 A 0 A_JumpIfInventory("Cell",60,"AltFireStart")
            UN03 A 0 A_JumpIfInventory("Cell",15,"ReadyActual") // can we still use the primary?
            goto NoAmmoSwitch

		AltFireStart:
            UN03 A 0 A_AlertMonsters
			UN03 A 0 A_GiveInventory("SamsaraUnrealRedeemerSecondaryAttackHandler")
			UN03 A 5 Bright A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			//UN03 B 0 A_GunFlash("AltFireTarget",GFF_NOEXTCHANGE)
			UN03 B 5 Bright A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			UN03 CDEFGHIJKLM 2 Bright A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
		AltFireWait:
			UN01 O 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			UN01 O 0 A_JumpIfInventory("SamsaraUnrealRedeemerMainRocketControlled",1,"AltFireWait")
			goto AltFireFinish

        /*AltFireTarget:
            TNT1 A 5 A_GiveInventory("SamsaraUnrealRocketLauncherTargetingHandler")
            TNT1 AAAAAAAAAAA 2 A_GiveInventory("SamsaraUnrealRocketLauncherTargetingHandler")
            stop*/

		AltFireFinish:
            //UN01 A 0 A_GunFlash("AltFireFinishTarget",GFF_NOEXTCHANGE)
			UN01 ABCDEFGHIJKLMN 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
            UN01 O 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"ReadyActual")
            UN01 O 0 A_JumpIfInventory("Cell",15,"ReadyActual")
            goto NoAmmoSwitch

        /*AltFireFinishTarget:
            TNT1 AAAAAAAAAAAAAA 2 A_GiveInventory("SamsaraUnrealRocketLauncherTargetingHandler")
            stop*/

        // "No ammo" switch
        NoAmmoSwitch:
            TNT1 A 0 A_ClearReFire
            TNT1 A 0 A_SelectWeapon("Unreal_AutoMag")
            goto Deselect
	}
}

/*actor SamsaraUnrealRocketLauncherTargetingHandler : Trigger
{
    States
    {
      // Targeting stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupCoop")
      PickupDM:
        TNT1 A 0 A_FireBullets(0,0,1,0,"Unreal_RazorBlankPuff",FBF_NORANDOM|FBF_EXPLICITANGLE|FBF_NOFLASH)
        stop

      PickupCoop:
        TNT1 A 0 A_FireBullets(0,0,1,0,"Unreal_RazorBlankPuffCoop",FBF_NORANDOM|FBF_EXPLICITANGLE|FBF_NOFLASH)
        stop
    }
}*/

actor SamsaraUnrealRedeemerPrimaryAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Cell",15,1)
        stop
        TNT1 A 0 A_TakeInventory("Cell",15)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("Unreal/RocketL/Fire",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireCustomMissile("Unreal_UMSRocket",0,false,13)
        goto PickupFinish

      PickupAttackCoop:
        TNT1 A 0 A_FireCustomMissile("Unreal_UMSRocketCoop",0,false,13)
        goto PickupFinish

      // Finishing stuff
      PickupFinish:
        TNT1 A 0 ACS_NamedExecuteWithResult("Unreal_Recoil")
        stop
    }
}

actor SamsaraUnrealRedeemerSecondaryAttackHandler : Trigger
{
    States
    {
      // Initializing stuff
      Pickup:
        TNT1 A 0 A_TakeInventory("SamsaraUnrealRedeemerMainRocketControlled")
        goto PickupAmmoCheck

      // Ammo checking/taking stuff
      PickupAmmoCheck:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Cell",60,1)
        stop
        TNT1 A 0 A_TakeInventory("Cell",60)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("Unreal/RocketL/Fire",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireCustomMissile("Unreal_UMSRocket2",0,false,13)
        goto PickupFinish

      PickupAttackCoop:
        TNT1 A 0 A_FireCustomMissile("Unreal_UMSRocket2Coop",0,false,13)
        goto PickupFinish

      // Finishing stuff
      PickupFinish:
        TNT1 A 0 ACS_NamedExecuteWithResult("Unreal_Recoil")
        stop
    }
}

Actor Unreal_UMSRocket
{
	Projectile
	Speed 55
	Radius 5
	Height 5
	Scale 2.0
	Damage (300)
	DamageType "UnrealExplosive"
    +DONTBLAST
    +DONTREFLECT
    +FORCERADIUSDMG
	States
	{
		Spawn:
			UN00 A 0 NoDelay ACS_NamedExecuteWithResult("Unreal_RedeemerPitch")
		SpawnStart:
			UN00 A 1 A_PlaySound("Unreal/RocketL/Loop1",CHAN_VOICE,1.0,true)
			UN00 BC 1
		SpawnLoop:
			UN00 D 0 A_PlaySound("Unreal/RocketL/Loop1",CHAN_VOICE,1.0,true)
			UN00 D 0 A_SpawnItemEx("UnrealSmoke",-32*cos(pitch)-32*sin(-pitch),0,32*sin(-pitch)-32*cos(pitch),0,0,0)
			UN00 D 1 A_SpawnItemEx("UnrealSmoke",-64*cos(pitch)-32*sin(-pitch),0,64*sin(-pitch)-32*cos(pitch),0,0,0)
			loop

		Death:
			TNT1 A 0 A_PlaySound("Unreal/Flak/Explode2",CHAN_VOICE)
			TNT1 A 0 A_Explode(300,180)
			TNT1 AAA 0 A_SpawnItemEx("Unreal_RedeemerExplosionEffect", Random(0,4),Random(0,4),Random(0,4),0,0,0,0, SXF_NOCHECKPOSITION)
			TNT1 A 0 A_SpawnItemEx("Unreal_RedeemerExplosionEffect2",0,0,0,0,0,0,0, SXF_NOCHECKPOSITION)
			TNT1 A 5 A_SpawnItemEx("Unreal_YellowShockwave", 0,0,0,0,0,0,0, SXF_NOCHECKPOSITION)
			stop
	}
}

Actor Unreal_UMSRocketCoop : Unreal_UMSRocket { +THRUSPECIES Species "Player" }

Actor Unreal_UMSRocket2 : Unreal_UMSRocket
{
	Speed 16
	Damage (1500)
    ReactionTime 1
    +NOTIMEFREEZE // allow moving controlled missiles during timefreeze. this should suffice for now
	States
	{
        Spawn:
            UN00 A 0 NoDelay ACS_NamedExecuteWithResult("Unreal_RedeemerRocket")
            goto SpawnStart

        ForceDeath:
            UN00 A 0 A_GiveInventory("SamsaraUnrealRedeemerRocketDeath")
            UN00 A 1 A_Countdown
            loop

		Death:
            TNT1 A 0 A_GiveInventory("SamsaraUnrealRedeemerRocketDeath")
			TNT1 A 0 A_PlaySound("Unreal/Flak/Explode2",CHAN_VOICE)
			TNT1 A 0 A_Explode(1500,360)
			TNT1 AAA 0 A_SpawnItemEx("Unreal_EightballExplosiveEffect", Random(0,4),Random(0,4),Random(0,4),0,0,0,0, SXF_NOCHECKPOSITION)
			TNT1 A 0 A_SpawnItemEx("Unreal_RedeemerExplosionEffect2",0,0,0,0,0,0,0, SXF_NOCHECKPOSITION)
			TNT1 A 5 A_SpawnItemEx("Unreal_YellowShockwave2", 0,0,0,0,0,0,0, SXF_NOCHECKPOSITION)
			stop
	}
}

Actor Unreal_UMSRocket2Coop : Unreal_UMSRocket2 { +THRUSPECIES Species "Player" }

Actor Unreal_RedeemerExplosionEffect : Unreal_EightballExplosiveEffect
{
	States
	{
		Spawn:
			UN18 ABCDEFG 2 Bright A_SetScale(scalex+0.5)
			Stop
	}
}

Actor Unreal_RedeemerExplosionEffect2 : Unreal_RedeemerExplosionEffect
{
	Scale 1
	States
	{
		Spawn:
			UN27 AAABBBCCCDDDEEEFFFGGGHHH 1 Bright A_SetScale(scalex+0.333)
			Stop
	}
}

Actor Unreal_YellowShockwave
{
	+CLIENTSIDEONLY
	+NOGRAVITY
	RenderStyle Add
	Alpha 0.5
	Scale 3.5
	States
	{
		Spawn:
			UN00 ABCDEF 1 Bright
			Stop
	}
}

Actor Unreal_YellowShockwave2 : Unreal_YellowShockwave
{ 
	Scale 7
	States
	{
		Spawn:
			UN00 ABCDEF 1 Bright
			Stop
	}
}

actor SamsaraUnrealRedeemerRocketNoTimeFreezeDisabler : Trigger
{
    States
    {
      Pickup:
        TNT1 A 0 A_ChangeFlag("NOTIMEFREEZE",false)
        stop
    }
}
