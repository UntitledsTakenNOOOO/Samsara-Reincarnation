actor "Rocket Launcher" : RocketLauncher // doom64 rocketlauncher has recoil, and pushes the user back
{
    Weapon.SlotNumber 5
    +INVENTORY.UNDROPPABLE
	Inventory.RestrictedTo "DoomguyPlayer"
    States
    {
      Ready:
		TNT1 A 0 A_JumpIfInventory("Doom64Mode", 1, "Ready64")
        DMIS A 1 A_WeaponReady
        Loop
		
      Ready64:
        64ML A 1 A_WeaponReady
        Loop		

      Deselect:
		TNT1 A 0 A_JumpIfInventory("Doom64Mode", 1, "Deselect64")
        DMIS A 1 A_Lower
        Loop
		
      Deselect64:
        64ML A 1 A_Lower
        Loop		

      Select:
		TNT1 A 0 A_JumpIfInventory("Doom64Mode", 1, "Select64")
        DMIS A 1 A_Raise
        Loop
		
      Select64:
        64ML A 1 A_Raise
        Loop		
		
      Fire:
		TNT1 A 0 A_JumpIfInventory("Doom64Mode", 1, "Fire64")
        TNT1 A 0 A_JumpIfInventory("VanillaDoom", 1, "VanillaFire")
        DMIS A 2 
        DMIS A 4 bright A_GunFlash
        DMIS B 2 bright 
        goto FireMid
		
      Fire64:
        64ML B 8 A_GunFlash("Flash64")
        Goto FireMid64

      FireMid64:
        TNT1 A 0 A_JumpIfInventory("CoopModeOn", 1, "CoopMid64")
        TNT1 A 0 A_FireCustomMissile("Doom64Rocket")
        goto Animation64	
      
      VanillaFire:
        DRKL A 3 bright A_Light1
        DRKL B 4 bright
        DRKL C 1 bright A_Light2
        goto FireMid

      FireMid:
        TNT1 A 0 A_JumpIfInventory("CoopModeOn", 1, "CoopMid")
        TNT1 A 0 A_FireCustomMissile("DoomRocket")
        goto FireEnd

      CoopMid:
        TNT1 A 0 A_FireCustomMissile("DoomRocket2")
        goto FireEnd
		
      CoopMid64:
        TNT1 A 0 A_FireCustomMissile("Doom64Rocket2")
        goto Animation64		
      
      FireEnd:
        TNT1 A 0 A_JumpIfInventory("VanillaDoom", 1, "VanillaEnd")
        DMIS D 3 bright
        DMIS C 3 bright
        DMIS BE 2
        DMIS FG 1
        DMIS A 0 A_Refire
        goto Ready
      
      RageEnd:
        DMIS DC 2 bright
        DMIS BFG 2
        DMIS A 0 A_Refire
        goto Ready
        
      VanillaEnd:
        DRKL C 3 bright
        DRKL D 4 bright
        DMIS B 5 A_Light0
        DMIS B 0 A_ReFire // 20 frames total
        Goto Ready
		
      Animation64:
        64ML B 0 A_Recoil(2)
        64ML B 0 ACS_ExecuteAlways(312, 0, 5, 4, 15)
        64ML B 13
        64ML B 0 A_Refire
        Goto Ready
        
      Flash64:
        64MM A 2 bright
        64MM BCD 4 bright
        Goto LightDone		
        
      Flash:
        DMSF A 3 bright A_Light1
        DMSF B 2 bright
        DMSF C 2 bright A_Light2
        DMSF DE 3 bright
        Goto LightDone

      Spawn:
        WDRL A -1
        Stop
    }
}

actor DoomRocket : Rocket
{
    Decal Scorch
    SeeSound "doomguy/rocklf"
    DeathSound "doomguy/rocklx"
    -FIREDAMAGE
    Damagetype "DoomDamageType"
    Obituary "$SAMSARA_DOOMGUY_OB_SLOT5"
    States
    {
      Spawn:
        DMSL A 1 bright
        Loop

      Death:
        DMSL B 8 bright A_Explode
        DMSL C 6 bright
        DMSL D 4 bright
        Stop
    }
}

actor DoomRocket2 : DoomRocket { +THRUSPECIES Species "Player" }


actor Doom64Rocket : Rocket
{
    Decal Scorch
    SeeSound "doom64guy/rocklf"
    DeathSound "doom64guy/rocklx"
    -FIREDAMAGE
    -DEHEXPLOSION
    -ROCKETTRAIL
    +BLOODLESSIMPACT
    DamageType "Explosive"
    Scale 0.75
    Obituary "%k \cpsplattered\c- %o\cp's brains with the Rocket Launcher.\c-"
    States
    {
      Spawn:
        64MI AA 2 bright A_SpawnItemEx("Doom64RocketSmokeTrail")
        Loop

      Death:
        64MI B 8 bright A_Explode
        64MI C 6 bright A_FadeOut(0.2)
        64MI D 3 bright A_FadeOut(0.2)
        64MI EF 3 bright A_FadeOut(0.2)
        Stop
    }
}

actor Doom64Rocket2 : Doom64Rocket { +THRUSPECIES Species "Player" }

actor Doom64RocketSmokeTrail : RocketSmokeTrail
{
    +CLIENTSIDEONLY
    RenderStyle Translucent
    Alpha 0.5
    VSpeed 0
    Scale 0.75
    States
    {
    Spawn:
        64RS ABCDE 2 bright
        stop
    }
}

//=================================================================================================
//
// GrenadeLauncher
//
//=================================================================================================

actor DGHasGrenadeLauncher : Boolean {}
ACTOR " GrenadeLauncher " : Weapon
{
	Radius 20
	Height 16
	Weapon.Selectionorder 2500
	+WEAPON.NOAUTOFIRE
	+WEAPON.NOAUTOAIM
	Weapon.AmmoUse 1
	Weapon.AmmoGive 2
	Weapon.AmmoType "RocketAmmo"
	Weapon.Kickback 100
	Inventory.PickupMessage "$PICKUP_GRENADELAUNCHER"
	States
	{
		Spawn:
			GLAU A -1
			stop
		Ready:
			GRLG A 1 A_WeaponReady
			loop
		Deselect:
			GRLG A 1 A_Lower
			loop
		Select: 
			GRLG A 1 A_Raise
			loop
		Fire: 
			GRLG B 8 A_GunFlash
			GRLG B 12 A_FireCustomMissile ( "Grenade", 0, 1, 0, 0, 0, 6.328125 )
			GRLG B 0 A_ReFire
			goto Ready 
		Flash: 
			GRLF A	3 bright A_Light1
			GRLF B	4 bright
			GRLF C 4 bright A_Light2
			GRLF D 4 bright A_Light2
			Goto LightDone
	}
}