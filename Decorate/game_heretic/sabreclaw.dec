actor Clink2 : SamsaraMonsterRoboticMedium replaces Clink
{
	Game Heretic
	SpawnID 1
	Health 150
	Radius 20
	Height 64
	Mass 75
	Speed 14
	PainChance 32
	Monster
	+NOBLOOD
	+FLOORCLIP
	+NOTDMATCH
	+NOICEDEATH
	SeeSound "clink/sight"
	AttackSound "clink/attack"
	PainSound "clink/pain"
	DeathSound "clink/death"
	ActiveSound "clink/active"
	Obituary "$OB_CLINK"
	DropItem "SkullRodAmmo2", 84
	Tag "Sabreclaw"
	States
	{
	Spawn:
		CLNK AB 10 A_Look
		Loop
	See:
		CLNK ABCD 3 A_Chase
		Loop
	Melee:
		TNT1 A 0 A_ChangeFlag("NOPAIN",0)
		CLNK E 5 A_FaceTarget
		CLNK F 4 A_FaceTarget
		CLNK G 7 A_CustomMeleeAttack(random[ClinkAttack](3,9), "clink/attack", "clink/attack")
		Goto See
	Pain:
		CLNK H 3
		CLNK H 3 A_Pain
		Goto See
	Pain.MightyBoot:
		TNT1 A 0 A_ChangeFlag("NOPAIN",1)
		TNT1 A 0 A_PlaySound("duke/kickhit",CHAN_BODY)
		CLNK H 2 A_FaceTarget
		TNT1 A 0 A_Recoil(14)
		CLNK H 16 A_Pain
		Goto See
	Death:
		CLNK I 6 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)
		CLNK J 6
		CLNK K 5 A_Scream
		CLNK L 5 A_NoBlocking
		CLNK MN 5
		CLNK O -1
		Stop
	}
}