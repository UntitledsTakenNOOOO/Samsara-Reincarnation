Actor " RR Rip Saw " : Weapon
{
	Inventory.PickupMessage "Rip Saw!"
    Inventory.PickupSound "RR/Wpickup"
	Weapon.SelectionOrder 900
    Weapon.BobRangeX 0.75
    Weapon.BobRangeY 0.95
    Weapon.BobSpeed 1.1	
	Weapon.SlotNumber 1
	Weapon.SlotPriority 1
	Weapon.AmmoType "Shell"
	Weapon.AmmoUse 3
	Weapon.AmmoGive 12
	Obituary "%o was ripped by %k's saws."
	Inventory.RestrictedTo "Leonard"	
	Tag "Rip Saw"	
	States
	{
		Spawn:
			R_RS U -1 Bright
			Stop
		Select:
			TNT1 A 0 A_Raise
			R_RS A 1 A_Raise
			Loop
		Deselect:
			TNT1 A 0 A_Lower
			R_RS A 1 A_Lower
			Loop
		Ready:
			TNT1 A 0 A_JumpIfInventory("LeonardWaiting", 15, "WaitingTaunt")
			TNT1 A 0 A_JumpIfInventory("LeonardReady",1,"IntroTaunt")		
			R_RS A 1 A_WeaponReady
			Loop
		Fire:
			TNT1 A 0 A_JumpIfNoAmmo("AltFire")
			TNT1 A 0 A_PlaySound("RR/RIPSAWF",CHAN_WEAPON)
			R_RS A 1 Bright A_JumpIfInventory("CoopModeOn",1,2)
			R_RS B 1 Bright A_FireCustomMissile("RIPSAWRIPPERDISC",0,1,0)
			Goto A
			R_RS B 1 Bright A_FireCustomMissile("RIPSAWRIPPERDISC_2",0,1,0)
		A:	
			R_RS BBBBBBBB 1 Bright
			R_RS CCCCC 1 Bright
			R_RS DDDD 1 Bright
			R_RS AAAAAAAAA 1 Bright
			Goto Ready
		AltFire:
			R_RS E 2 Bright A_Saw("RR/SAWRIP","RR/SAWRIP",5,"RRPuff4", SF_NORANDOM)
			R_RS F 2 Bright A_Saw(0,"RR/SAWRIP",5,"RRPuff4", SF_NORANDOM)
			R_RS G 2 Bright A_Saw("RR/SAWRIP","RR/SAWRIP",5,"RRPuff4", SF_NORANDOM)
			Goto Ready
		IntroTaunt:
			TNT1 A 0 A_JumpIfInventory("LeonardBallgag",1,2)
			TNT1 A 0 A_PlaySoundEx("Leonard/Enter", "SoundSlot7", 0, 0)
			TNT1 A 0 A_TakeInventory("LeonardReady", 1)
			TNT1 A 0 A_GiveInventory("LeonardTauntCooldown",5)
			TNT1 A 0 ACS_ExecuteAlways(706, 0)
			Goto Ready
		WaitingTaunt:
			TNT1 A 0 A_JumpIfInventory("LeonardBallgag",1,2)
			TNT1 A 0 A_PlaySoundEx("Leonard/Bored", "SoundSlot7", 0, 0)
			TNT1 A 0 A_TakeInventory("LeonardWaiting", 15)
			TNT1 A 0 A_GiveInventory("LeonardTauntCooldown",5)
			TNT1 A 0 ACS_ExecuteAlways(706, 0)
			Goto Ready	
	}
}		

Actor RIPSAWRIPPERDISC
{
	PROJECTILE
	+RIPPER
	+BOUNCEONWALLS
	+BOUNCEONFLOORS
	+BOUNCEONCEILINGS
	+NOBOUNCESOUND
	+NOWALLBOUNCESND
	-DONTRIP
	BounceType "Hexen"
	Damage(45)
	Scale 0.4
	Height 4
	Radius 4
	Speed 50
	DamageType "LeonardDamage1"
	BounceFactor 0.93
	BounceCount 10
	States
	{
		Spawn:
			R_RS IIJJKKLLMMNNOOPP 1 Bright
			Loop
		Death:
			TNT1 A 5
			Stop
	}
}

Actor RIPSAWRIPPERDISC_2 : RIPSAWRIPPERDISC { Species "Player" +THRUSPECIES +DONTHARMCLASS +DONTHARMSPECIES +MTHRUSPECIES BounceCount 14 Damage(15*random(3,4)) }