Actor " Plasma Shotgun " : Weapon
{
	Weapon.Slotnumber 2
	Weapon.SelectionOrder 3500
	Weapon.BobStyle Inverse
	Weapon.BobSpeed 0.58
	Weapon.BobRangeX 1.1
	Weapon.BobRangeY 0.1	
	Inventory.Pickupmessage "You got the Energy Shotgun!"
	Inventory.Pickupsound "greed/pickup"
	Weapon.AmmoType "Shell"
    Weapon.AmmoUse 1
    Weapon.AmmoGive 8
	scale 0.65
	Obituary "$SAMSARA_GREED_OB_SLOT_II"
    Inventory.RestrictedTo "POGreedPlayer"	 		
	states
	{
	Spawn:
		GW20 Z -1
		stop
	Ready:
		GW20 A 1 A_WeaponReady
		loop
	Select:
		TNT1 AAA 0 A_Raise
		GW20 A 1 A_Raise
		Loop
	Deselect:
		TNT1 AAA 0 A_Lower
		GW20 A 1 A_Lower
		Loop	
	Fire:
		TNT1 A 0 A_GunFlash("FakeFlash")
		GW20 A 0 A_JumpIfInventory("SPModeOn",1,"SPFire")	
		GW20 A 0 A_JumpIfInventory("CoopModeOn",1,"CoopFire")	
		GW20 A 10 bright A_PlayWeaponSound("greed/plasmashot")
		GW20 A 0 A_FireCustomMissile("PGShotgunPlasmaBallDM",-8,0,-2,0)
		GW20 A 0 A_FireCustomMissile("PGShotgunPlasmaBallDM",-4,0,-1,0)
		GW20 A 0 A_FireCustomMissile("PGShotgunPlasmaBallDM",4,0,1,0)
		GW20 A 0 A_FireCustomMissile("PGShotgunPlasmaBallDM",8,0,2,0)
		GW20 B 0 bright A_FireCustomMissile("PGShotgunPlasmaBallDM",0,1,0,0)
		GW20 B 9 bright
		GW20 A 10
		goto Ready		
	SPFire:
		GW20 A 10 bright A_PlayWeaponSound("greed/plasmashot")
		GW20 A 0 A_FireCustomMissile("PGShotgunPlasmaBall",-8,0,-2,0)
		GW20 A 0 A_FireCustomMissile("PGShotgunPlasmaBall",-4,0,-1,0)
		GW20 A 0 A_FireCustomMissile("PGShotgunPlasmaBall",4,0,1,0)
		GW20 A 0 A_FireCustomMissile("PGShotgunPlasmaBall",8,0,2,0)
		GW20 B 0 bright A_FireCustomMissile("PGShotgunPlasmaBall",0,1,0,0)
		GW20 B 9 bright
		GW20 A 10
		goto Ready			
	CoopFire:
		GW20 A 10 bright A_PlayWeaponSound("greed/plasmashot")
		GW20 A 0 A_FireCustomMissile("PGShotgunPlasmaBallCoop",-8,0,-2,0)
		GW20 A 0 A_FireCustomMissile("PGShotgunPlasmaBallCoop",-4,0,-1,0)
		GW20 A 0 A_FireCustomMissile("PGShotgunPlasmaBallCoop",4,0,1,0)
		GW20 A 0 A_FireCustomMissile("PGShotgunPlasmaBallCoop",8,0,2,0)
		GW20 B 0 bright A_FireCustomMissile("PGShotgunPlasmaBallCoop",0,1,0,0)
		GW20 B 9 bright
		GW20 A 10	
		goto Ready
	FakeFlash:
		TNT1 A 1
		Stop
	}
}

Actor PGShotgunPlasmaBall
{
	Radius 6
	Height 10
	Speed 35
	PROJECTILE
	+NOEXTREMEDEATH
	+THRUGHOST
	+HITTRACER
	+BLOODLESSIMPACT
	Damage (25)	
	//renderstyle add
	DamageType "PGPlasma"
	states
	{
	Spawn:
		GP20 A 2 bright
		loop
	Death:
		TNT1 A 5 A_SpawnItemEx("PGShotgunPlasmaBallPuff",0,0,0,0,0,0,0,SXF_CLIENTSIDE)
		stop
	XDeath:
		TNT1 A 0 A_SpawnItemEx("PGShotgunPlasmaBallPuff",0,0,0,0,0,0,0,SXF_CLIENTSIDE)
		TNT1 A 1 A_GiveInventory("IPOGBloodGenerator", 1, AAPTR_TRACER)
		Stop
	}
}

Actor PGShotgunPlasmaBallPuff
{
	+CLIENTSIDEONLY
	+NOINTERACTION
	-SOLID
	+NOBLOCKMAP
	+NOGRAVITY
	scale 0.5
	//renderstyle add
	states
	{
	Spawn:
		GP20 B 0
		GP20 B 0 A_PlaySound("greed/plasmahit",CHAN_BODY)
		GP20 BCDE 6 bright
		stop
	}
}

Actor PGShotgunPlasmaBallCoop : PGShotgunPlasmaBall { +THRUSPECIES Species "Player" }

Actor PGShotgunPlasmaBallDM : PGShotgunPlasmaBall { Damage (14) }