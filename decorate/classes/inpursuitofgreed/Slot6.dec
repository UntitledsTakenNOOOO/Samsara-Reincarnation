Actor " Laser Cannon " : Weapon
{
	Weapon.Slotnumber 6
	Weapon.SelectionOrder 1500
	Weapon.BobStyle Inverse
	Weapon.BobSpeed 0.58
	Weapon.BobRangeX 1.1
	Weapon.BobRangeY 0.1	
	Inventory.Pickupmessage "You got the laser cannon!"
	Inventory.Pickupsound "greed/pickup"
	Weapon.AmmoType "Cell"
    Weapon.AmmoUse 1 //Each laser projectile
    Weapon.AmmoGive 30
	scale 0.6
	Obituary "$SAMSARA_GREED_OB_SLOT_VI"
    Inventory.RestrictedTo "POGreedPlayer"
    Tag "Laser Cannon"
	+WEAPON.NOALERT
	states
	{
	Spawn:
		GW60 Z -1
		stop
	Ready:
		GW60 A 1 A_WeaponReady
		loop
	Select:
		TNT1 AAA 0 A_Raise
		GW60 A 1 A_Raise
		Loop
	Deselect:
		TNT1 AAA 0 A_Lower
		GW60 A 1 A_Lower
		Loop		
	Fire:
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_GunFlash("FakeFlash")
		GW60 A 0 A_JumpIfInventory("SPModeOn",1,"SPFire")	
		GW60 A 0 A_JumpIfInventory("CoopModeOn",1,"CoopFire")	
		GW60 B 1 bright A_PlayWeaponSound("greed/laser")
		GW60 B 0 bright A_FireCustomMissile("PGLaserBeamDM",0,1,-8,0)
		GW60 B 1 bright A_FireCustomMissile("PGLaserBeamDM",0,1,8,0)
		GW60 B 1 bright
		GW60 C 2
		GW60 A 3
		Goto Ready
	SPFire:
		GW60 B 1 bright A_PlayWeaponSound("greed/laser")
		GW60 B 0 bright A_FireCustomMissile("PGLaserBeam",0,1,-8,0)
		GW60 B 1 bright A_FireCustomMissile("PGLaserBeam",0,1,8,0)
		GW60 B 1 bright
		GW60 C 2
		GW60 A 3
		Goto Ready	
	CoopFire:
		GW60 B 1 bright A_PlayWeaponSound("greed/laser")
		GW60 B 0 bright A_FireCustomMissile("PGLaserBeamCoop",0,1,-8,0)
		GW60 B 1 bright A_FireCustomMissile("PGLaserBeamCoop",0,1,8,0)
		GW60 B 1 bright
		GW60 C 2
		GW60 A 3
		Goto Ready
	FakeFlash:
		TNT1 A 1
		Stop
	AltFire:
		TNT1 A 0 A_JumpIfInventory("IPOGGrenadeToken", 1, 1)
		Goto Ready	
		TNT1 A 0 A_GiveInventory("IPOGGrenadeThrower", 1)
		Goto Ready			
	}
}

Actor PGLaserBeam : FastProjectile
{
	Radius 6
	Height 10
	Speed 70
	PROJECTILE
	+NOEXTREMEDEATH
	+HITTRACER
	+BLOODLESSIMPACT
	Damage (30)
	//renderstyle add
	DamageType "PGLaser"
	states
	{
	Spawn:
		GP60 A 1 bright A_SpawnItemEx("PGLaserTrail",0,0,0,momx*0.5,momy*0.5,momz*0.5,0,SXF_ABSOLUTEMOMENTUM|SXF_CLIENTSIDE)
		loop
	Death:
		GP20 A 0 A_PlaySound("greed/laserhit",CHAN_BODY)
		TNT1 A 1 A_SpawnItemEx("PGBulletPuff",0,0,0,0,0,1,0,SXF_CLIENTSIDE)
		stop
	XDeath:
		TNT1 A 1 A_GiveInventory("IPOGBloodGenerator", 1, AAPTR_TRACER)
		Stop
	}
}

Actor PGLaserTrail
{
	PROJECTILE
	Radius 1
	Height 1
	Speed 0
	Damage (0)
	+CLIENTSIDEONLY
	+NOGRAVITY
	+THRUGHOST
	+THRUSPECIES
	Species "Player"
	//renderstyle add
	states
	{
	Spawn:
		TNT1 A 1
	SpawnLoop:
		GP60 A 1 bright A_Fadeout(0.1)
		loop
	}
}

Actor PGLaserBeamCoop : PGLaserBeam { +THRUSPECIES Species "Player" }

Actor PGLaserBeamDM : PGLaserBeam { Damage (17) }