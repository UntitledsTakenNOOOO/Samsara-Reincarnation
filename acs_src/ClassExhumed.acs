script "JonTauntCooldown" (void) //458
{
    while (CheckInventory("JonTauntCooldown") > 0)
    {
        Delay(35);
        TakeInventory("JonTauntCooldown", 1);
    }
}

Script "JonBallGag" (int type) NET //461
{
	if(CheckInventory("JonBallgag"))
	{
		TakeInventory("JonBallgag", 1);
		Print(s:"Ballgag Off");
	}
	else
	{
		GiveInventory("JonBallGag", 1);
		Print(s:"Ballgag On");
	}
}

Script "PSAnubisDrum" (void)
{
	int drumTid = UniqueTid();
	SpawnSpotFacingForced("AnubisDrum",0,drumTid);
	SetPointerExt(AAPTR_Tracer,0,drumTid);
	
	while(ClassifyActor(0) & ACTOR_ALIVE)
	{
		if(GetActorProperty(0,APROP_Health) < GetActorProperty(0,APROP_SpawnHealth) && !CheckFlag(0, "NOPAIN"))
		{
			SetActorState(0, "Pain", 1);
			terminate;
		}
		Delay(1);
	}
}

Script "PSRingofRaRecoil" (void) clientside
{
	int a;
	int weapon = Timer()%2;
	switch(weapon)
	{
		case 0:
			for(a = 1; a < 8; a++) { SetActorAngle(0, GetActorAngle(0) - 12*a); Delay(1); }
			break;
		case 1:
			for(a = 1; a < 8; a++) { SetActorAngle(0, GetActorAngle(0) + 12*a); Delay(1); }
			break;
	}
}

script "PSManacleBoltPitch" (void)
{
	int myTid = UniqueTid();
	int projectilePitch = GetActorPitch(0);
	Thing_ChangeTid(0,myTid);
	SetActivatorToTarget(0);
	SetActorPitch(myTid,-GetActorPitch(0));
	SetActivator(myTid);
	Thing_ChangeTid(myTid,0);
		
	while(ClassifyActor(0) & ACTOR_MISSILE)
	{
		SetActorAngle(0,GetActorAngle(0)+random(-(3*65536)/360,(3*65536)/360));
		SetActorPitch(0,GetActorPitch(0)+random(-(3*65536)/360,(3*65536)/360));
		GiveInventory("PSManacleBoltSeeker",1);
		Delay(1);	
	}
}