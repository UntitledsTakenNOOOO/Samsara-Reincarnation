actor SamsaraDeusExAssaultShotgunAmmoType : Boolean {}

Actor DeusEx_AssaultShotgun : DeusExWeapon
{
	Weapon.SelectionOrder 1500
	Weapon.SlotNumber 3
	Weapon.SlotPriority 1
	Weapon.AmmoType1 "DeusEx_AssShotgunMagazine"
	Weapon.AmmoType2 "Shell"
	Weapon.AmmoUse1 1
	Weapon.AmmoGive2 24
	Tag "Assault Shotgun"
	Inventory.PickupMessage "You found an Assault Shotgun."
	Obituary "%o lives in a pineapple under the sea thanks to %k's Assault Shotgun."
	States	
	{
		Spawn:
			DX99 A -1
			stop

		ReadyActual:
            //DX00 A 0 A_TakeInventory("DeusEx_ShotgunSpread",1)
            DX00 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"ReadyActualReloadable")
        ReadyActualNonReloadable:
			DX00 A 1 A_WeaponReady
            goto ReadyActualJump

        ReadyActualReloadable:
			DX00 A 1 A_WeaponReady(WRF_ALLOWRELOAD)
            goto ReadyActualJump

        ReadyActualJump:
            DX00 A 0 A_Jump(5,"ReadyActualIdle1","ReadyActualIdle2","ReadyActualIdle3")
			goto ReadyActualAmmoCheck

		ReadyActualIdle1:
            DX01 F 0 A_JumpIfInventory("SamsaraReloadMode",1,"ReadyActualIdle1Reloadable")
        ReadyActualIdle1NonReloadable:
			DX01 FGHIJKLM 5 A_WeaponReady
			goto ReadyActual

        ReadyActualIdle1Reloadable:
			DX01 FGHIJKLM 5 A_WeaponReady(WRF_ALLOWRELOAD)
			goto ReadyActual

		ReadyActualIdle2:
            DX01 N 0 A_JumpIfInventory("SamsaraReloadMode",1,"ReadyActualIdle2Reloadable")
        ReadyActualIdle2NonReloadable:
			DX01 NOPQRSTU 5 A_WeaponReady
			goto ReadyActual

        ReadyActualIdle2Reloadable:
			DX01 NOPQRSTU 5 A_WeaponReady(WRF_ALLOWRELOAD)
			goto ReadyActual

		ReadyActualIdle3:
            DX01 V 0 A_JumpIfInventory("SamsaraReloadMode",1,"ReadyActualIdle3Reloadable")
        ReadyActualIdle3NonReloadable:
			DX01 "VWXYZ[\]" 5 A_WeaponReady
			DX02 AB 5 A_WeaponReady
			goto ReadyActual

        ReadyActualIdle3Reloadable:
			DX01 "VWXYZ[\]" 5 A_WeaponReady(WRF_ALLOWRELOAD)
			DX02 AB 5 A_WeaponReady(WRF_ALLOWRELOAD)
			goto ReadyActual

        ReadyActualAmmoCheck:
            DX00 A 0 A_JumpIfInventory("SamsaraReloadMode",1,1)
            goto ReadyActual
            DX00 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"ReadyActual")
            DX00 A 0 A_JumpIfInventory("DeusEx_AssShotgunMagazine",1,"ReadyActual")
            goto ReloadCheck

		Deselect:
			DX00 N 0 A_TakeInventory("DeusEx_ShotgunSpread")
			DX00 N 0 A_GiveInventory("DeusEx_Reloading")
			DX00 N 2 A_JumpIfHealthLower(1,"DeathDeselect")
			DX00 OPQR 2
			DX00 S 0
            goto DeselectFinish

        DeathDeselect:
            TNT1 AAAAA 2
            goto DeselectFinish

        DeselectFinish:
			TNT1 A 0 A_TakeInventory("DeusEx_AssShotgunEquipped")
			goto InstantDeselect

	    Ready: // our "Select" state
            DX00 B 0 A_PlaySound("DeusEx/AssShot/Select")
			DX00 B 3 A_GiveInventory("DeusEx_AssShotgunEquipped")
			DX00 CDEFGHI 3
			DX00 A 0 A_TakeInventory("DeusEx_Reloading")
			goto ReadyActual

		Fire:
            DX00 J 0 A_JumpIfInventory("SamsaraReloadMode",1,"FireReloadable")
        FireNonReloadable:
            DX00 J 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireWaterCheck")
            DX00 J 0 A_JumpIfInventory("Shell",1,"FireWaterCheck")
            goto FireDry

        FireReloadable:
            DX00 J 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"FireWaterCheck")
            DX00 J 0 A_JumpIfInventory("DeusEx_AssShotgunMagazine",1,"FireWaterCheck")
        FireReloadableDryCheck:
            DX00 J 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
            DX00 J 0 A_JumpIfInventory("Shell",1,1)
            goto FireDry
            DX00 T 0 A_TakeInventory("SamsaraDeusExSwappedAmmo")
            goto ReloadStart

        FireWaterCheck:
            DX00 J 0 A_JumpIfInventory("SamsaraCurrentWaterLevel",0,"FireDry")
            goto FireStart

		FireDry:
			DX00 A 0 A_PlaySound("DeusEx/DryFire",CHAN_6)
			DX00 A 0 A_ClearReFire
            DX00 A 4 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
            goto ReadyActualAmmoCheck

		FireStart:
			DX00 J 0 A_GiveInventory("DeusEx_Firing")
            DX00 J 0 A_AlertMonsters(1300)
			DX00 J 0 A_SpawnItemEx("DeusEx_ShellCasing3",8,-16,39,random(1,3),0,3,-90,SXF_NOCHECKPOSITION|SXF_MULTIPLYSPEED|SXF_TRANSFERPITCH)
			DX00 J 0 A_GunFlash
			DX00 J 0 A_GiveInventory("SamsaraDeusExAssaultShotgunAttackHandler")
			DX00 J 1 Bright A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			DX00 K 4 Bright A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			DX00 LM 2 Bright A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			DX00 M 3 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
            DX00 A 0 A_ReFire
			DX00 A 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			goto ReadyActual

		Flash:
			DX02 CD 1 Bright
			stop

		AltFire:
            DX00 T 0 A_JumpIfInventory("SamsaraReloadMode",1,"AltFireReloadable")
        AltFireNonReloadable:
            DX00 T 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"AltFireSwap")
            DX00 T 0 A_JumpIfInventory("Shell",1,"AltFireSwap")
            goto ReadyActual

        AltFireReloadable:
            DX00 T 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"AltFireSwap")
            DX00 T 0 A_JumpIfInventory("DeusEx_AssShotgunMagazine",1,"AltFireSwap")
        AltFireReloadableDryCheck:
            DX00 T 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
            DX00 T 0 A_JumpIfInventory("Shell",1,1)
            goto ReadyActual
            DX00 T 0 A_TakeInventory("SamsaraDeusExSwappedAmmo")
            goto ReloadStart

		AltFireSwap:
            DX00 T 0 A_GiveInventory("SamsaraDeusExSwappedAmmo")
			DX00 T 0 A_JumpIfInventory("SamsaraDeusExAssaultShotgunAmmoType",1,"AltFireSwapBuckshot")
        AltFireSwapSabot:
            DX00 T 0 A_GiveInventory("SamsaraDeusExAssaultShotgunAmmoType")
            DX00 T 0 A_Print("Assault Shotgun now has 12 Gauge Sabot Shells loaded.",2)
            goto ReloadStart

        AltFireSwapBuckshot:
            DX00 T 0 A_TakeInventory("SamsaraDeusExAssaultShotgunAmmoType")
            DX00 T 0 A_Print("Assault Shotgun now has 12 Gauge Buckshot Shells loaded.",2)
            goto ReloadStart

        Reload:
        ReloadCheck:
            DX00 A 0 A_JumpIfInventory("DeusEx_AssShotgunMagazine",0,"ReadyActual")
            DX00 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
            DX00 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
            DX00 A 0 A_JumpIfInventory("Shell",1,1)
            goto ReadyActual
            DX00 T 0 A_TakeInventory("SamsaraDeusExSwappedAmmo")
            goto ReloadStart

		ReloadStart:
			DX00 T 0 A_GiveInventory("DeusEx_Reloading")
			DX00 T 0 A_PlaySound("DeusEx/AssShot/Reload1")
			DX00 TUVWXYZ 2 A_WeaponReady(WRF_NOFIRE)
			DX00 "[" 0 A_JumpIfInventory("SPModeOn",1,2)
			DX00 "[" 0 A_JumpIfInventory("CoopModeOn",1,1)
            goto ReloadContinueAdvanced
			DX00 "[" 0 A_JumpIfInventory("DeusEx_RifleSkill",0,"ReloadContinueMaster")
			DX00 "[" 0 A_JumpIfInventory("DeusEx_RifleSkill",3,"ReloadContinueAdvanced")
			DX00 "[" 0 A_JumpIfInventory("DeusEx_RifleSkill",2,"ReloadContinueProficient")
			DX00 "[" 0 A_JumpIfInventory("DeusEx_RifleSkill",1,"ReloadContinueTrained")
        ReloadContinueUntrained:
			DX00 "[\]" 3 A_WeaponReady(WRF_NOFIRE)
			DX01 AB 3 A_WeaponReady(WRF_NOFIRE)
        ReloadContinueTrained:
			DX00 "[\]" 3 A_WeaponReady(WRF_NOFIRE)
			DX01 AB 3 A_WeaponReady(WRF_NOFIRE)
        ReloadContinueProficient:
			DX00 "[\]" 3 A_WeaponReady(WRF_NOFIRE)
			DX01 AB 3 A_WeaponReady(WRF_NOFIRE)
        ReloadContinueAdvanced:
			DX00 "[\]" 3 A_WeaponReady(WRF_NOFIRE)
			DX01 AB 3 A_WeaponReady(WRF_NOFIRE)
		ReloadContinueMaster:
			DX01 C 0 A_TakeInventory("DeusEx_Reloading")
			DX01 C 0 A_PlaySound("DeusEx/AssShot/Reload2")
			DX01 CDE 3 A_WeaponReady(WRF_NOFIRE)
            DX00 A 0 A_JumpIfInventory("SamsaraDeusExSwappedAmmo",1,"ReloadDone")
            DX00 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"ReloadAmmo")
            goto ReloadDone

        ReloadDone:
            DX00 A 0
			goto ReadyActual

        ReloadAmmo:
            TNT1 A 0 A_JumpIfInventory("DeusEx_AssShotgunMagazine",0,"ReloadDone")
            TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
            TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
            TNT1 A 0 A_JumpIfInventory("Shell",1,1)
            goto ReloadDone
            TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
            TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
            TNT1 A 0 A_TakeInventory("Shell",1)
            TNT1 A 0 A_GiveInventory("DeusEx_AssShotgunMagazine")
            loop
	}
}

actor SamsaraDeusExAssaultShotgunAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"PickupReloadable")
      PickupNonReloadable:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Shell",1,1)
        stop
        TNT1 A 0 A_TakeInventory("Shell",1)
        goto PickupAttack

      PickupReloadable:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("DeusEx_AssShotgunMagazine",1,1)
        stop
        TNT1 A 0 A_TakeInventory("DeusEx_AssShotgunMagazine",1)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("DeusEx/AssShot/Fire",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SamsaraDeusExAssaultShotgunAmmoType",1,"PickupAttackSabot")
      // Buckshot
      PickupAttackBuckshot:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackBuckshotCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackBuckshotCoop")
      PickupAttackBuckshotDM:
        TNT1 AAAAA 0 A_FireCustomMissile("DeusEx_AutoShotgunBullet",frandom(-3.0,3.0)+CallACS("DeusEx_Decorate", 5, 3, 2)*frandom(-1.0,1.0),false,12,16,0,frandom(-2.0,2.0)+CallACS("DeusEx_Decorate", 5, 3, 2)*frandom(-0.5,0.5))
        goto PickupFinish

      PickupAttackBuckshotCoop:
        TNT1 AAAAA 0 A_FireCustomMissile("DeusEx_AutoShotgunBulletCoop",frandom(-3.0,3.0)+CallACS("DeusEx_Decorate", 5, 3, 2)*frandom(-1.0,1.0),false,12,16,0,frandom(-2.0,2.0)+CallACS("DeusEx_Decorate", 5, 3, 2)*frandom(-0.5,0.5))
        goto PickupFinish

      // Sabot
      PickupAttackSabot:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackSabotCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackSabotCoop")
      PickupAttackSabotDM:
        TNT1 AAAAA 0 A_FireCustomMissile("DeusEx_AutoSabotBullet",frandom(-3.0,3.0)+CallACS("DeusEx_Decorate", 5, 3, 2)*frandom(-1.0,1.0),false,12,16,0,frandom(-2.0,2.0)+CallACS("DeusEx_Decorate", 5, 3, 2)*frandom(-0.5,0.5))
        goto PickupFinish

      PickupAttackSabotCoop:
        TNT1 AAAAA 0 A_FireCustomMissile("DeusEx_AutoSabotBulletCoop",frandom(-3.0,3.0)+CallACS("DeusEx_Decorate", 5, 3, 2)*frandom(-1.0,1.0),false,12,16,0,frandom(-2.0,2.0)+CallACS("DeusEx_Decorate", 5, 3, 2)*frandom(-0.5,0.5))
        goto PickupFinish

      // Finishing stuff
      PickupFinish:
        TNT1 A 0 A_GiveInventory("DeusEx_ShotgunSpread",5)
        TNT1 A 0 ACS_NamedExecuteWithResult("DeusExRecoil", 2, 12, 2, 12)
        stop
    }
}

Actor DeusEx_AssShotgunMagazine : Ammo
{
    Inventory.MaxAmount 12
    Tag "Assault Shotgun Magazine"
    +IGNORESKILL
}

Actor DeusEx_ShotgunSpread : Ammo
{
    Inventory.MaxAmount 140
    Inventory.InterHubAmount 0
    +IGNORESKILL
    +UNDROPPABLE
}

Actor DeusEx_AutoShotgunBullet : DeusExBullet
{
	Damage (CallACS("DeusEx_WeaponSkillDamage", 3, 5)) // was 4
	DamageType "DeusExRifleShot"
	States
	{
		Crash:
			TNT1 A 0 A_PlaySound("DeusEx/Bullet/HitRobot", CHAN_BODY)
			TNT1 A 1 A_Explode(CallACS("DeusEx_DamageModifiers",CallACS("DeusEx_WeaponSkillDamage", 3, 5)),8,!XF_HURTSOURCE,0,8) // was 3
			stop
		XDeath:
			//TNT1 A 0 A_PlaySound("DeusEx/Bullet/HitFlesh", CHAN_BODY)
			TNT1 A 0 A_Explode(CallACS("DeusEx_DamageModifiers",CallACS("DeusEx_WeaponSkillDamage", 3, 5)),8,!XF_HURTSOURCE,0,8)
			TNT1 A 0 ACS_NamedExecuteWithResult("DeusEx_BloodGenerator_Offsets")
			TNT1 A 1 A_GiveInventory("DeusEx_BloodGenerator",1,AAPTR_TRACER)
			stop
	}
}

Actor DeusEx_AutoShotgunBulletCoop : DeusEx_AutoShotgunBullet { Species "Player" +THRUSPECIES }

Actor DeusEx_AutoSabotBullet : DeusExBullet
{
	DamageType "DeusExSabot"
	Damage (0)
	States
	{	
		Crash:
			TNT1 A 0 A_PlaySound("DeusEx/Bullet/HitRobot", CHAN_BODY)
			TNT1 A 1 A_Explode(CallACS("DeusEx_WeaponSkillDamage", 3, 15),32,!XF_HURTSOURCE,0,32)
			stop
		XDeath:
			//TNT1 A 0 A_PlaySound("DeusEx/Bullet/HitFlesh", CHAN_BODY)
			TNT1 A 0 A_Explode(CallACS("DeusEx_WeaponSkillDamage", 3, 15),32,!XF_HURTSOURCE,0,32)
			TNT1 A 0 ACS_NamedExecuteWithResult("DeusEx_BloodGenerator_Offsets")
			TNT1 A 1 A_GiveInventory("DeusEx_BloodGenerator",1,AAPTR_TRACER)
			stop
	}
}

Actor DeusEx_AutoSabotBulletCoop : DeusEx_AutoSabotBullet { Species "Player" +THRUSPECIES }
