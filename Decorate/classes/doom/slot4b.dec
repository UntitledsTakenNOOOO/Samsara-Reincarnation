// Tokens

actor DGStrHasFlamethrower : Boolean {}
actor SamsaraDoomguyStrFlamethrowerAttackAmount : Counter { Inventory.MaxAmount 3 }

// Weapon

actor " Flamer " : Weapon
{
	Inventory.PickupMessage "You got the flamethrower! Time for a BBQ!"
	Inventory.RestrictedTo "DoomguyPlayer"
	Weapon.SlotNumber 4
	Weapon.AmmoType "DoomguyStrGas"
	//Weapon.AmmoGive 48
	Weapon.AmmoUse 1
	Tag "Flamethrower"
	+EXPLOSIVE
	+UNDROPPABLE
	States
	{
	Spawn:
		WFLM A -1
		stop

	// Unpowered
	Ready: // Not as messy as the plasma rifle.
		TNT1 A 0 A_JumpIfInventory("PowerSamsaraDoomguyStrWeaponPower",1,"ReadyPowered")
		TNT1 A 0 A_JumpIfInventory("SamsaraHasPermaWeaponLevel2",1,"ReadyPowered")
		FLMG B 1 A_WeaponReady
		TNT1 A 0 A_JumpIfInventory("SamsaraHasDoubleFiringSpeed",1,4)
		TNT1 A 0 A_JumpIfInventory("PowerSamsaraDoomguyStrWeaponPower",1,"ReadyPowered")
		TNT1 A 0 A_JumpIfInventory("SamsaraHasPermaWeaponLevel2",1,"ReadyPowered")
		FLMG B 1 A_WeaponReady
		TNT1 A 0 A_JumpIfInventory("PowerSamsaraDoomguyStrWeaponPower",1,"ReadyPowered")
		TNT1 A 0 A_JumpIfInventory("SamsaraHasPermaWeaponLevel2",1,"ReadyPowered")
		FLMG B 1 A_WeaponReady
		TNT1 A 0 A_JumpIfInventory("PowerSamsaraDoomguyStrWeaponPower",1,"ReadyPowered")
		TNT1 A 0 A_JumpIfInventory("SamsaraHasPermaWeaponLevel2",1,"ReadyPowered")
		FLMG C 1 A_WeaponReady
		TNT1 A 0 A_JumpIfInventory("SamsaraHasDoubleFiringSpeed",1,4)
		TNT1 A 0 A_JumpIfInventory("PowerSamsaraDoomguyStrWeaponPower",1,"ReadyPowered")
		TNT1 A 0 A_JumpIfInventory("SamsaraHasPermaWeaponLevel2",1,"ReadyPowered")
		FLMG C 1 A_WeaponReady
		TNT1 A 0 A_JumpIfInventory("PowerSamsaraDoomguyStrWeaponPower",1,"ReadyPowered")
		TNT1 A 0 A_JumpIfInventory("SamsaraHasPermaWeaponLevel2",1,"ReadyPowered")
		FLMG C 1 A_WeaponReady
		TNT1 A 0 A_JumpIfInventory("PowerSamsaraDoomguyStrWeaponPower",1,"ReadyPowered")
		TNT1 A 0 A_JumpIfInventory("SamsaraHasPermaWeaponLevel2",1,"ReadyPowered")
		FLMG D 1 A_WeaponReady
		TNT1 A 0 A_JumpIfInventory("SamsaraHasDoubleFiringSpeed",1,4)
		TNT1 A 0 A_JumpIfInventory("PowerSamsaraDoomguyStrWeaponPower",1,"ReadyPowered")
		TNT1 A 0 A_JumpIfInventory("SamsaraHasPermaWeaponLevel2",1,"ReadyPowered")
		FLMG D 1 A_WeaponReady
		TNT1 A 0 A_JumpIfInventory("PowerSamsaraDoomguyStrWeaponPower",1,"ReadyPowered")
		TNT1 A 0 A_JumpIfInventory("SamsaraHasPermaWeaponLevel2",1,"ReadyPowered")
		FLMG D 1 A_WeaponReady
		loop

	Deselect:
		TNT1 A 0 A_JumpIfInventory("PowerSamsaraDoomguyStrWeaponPower",1,"DeselectPowered")
		TNT1 A 0 A_JumpIfInventory("SamsaraHasPermaWeaponLevel2",1,"DeselectPowered")
	DeselectContinue:
		TNT1 A 0 A_Lower
		FLMG A 1 A_Lower
		goto Deselect

	Select:
		TNT1 A 0 A_JumpIfInventory("PowerSamsaraDoomguyStrWeaponPower",1,"SelectPowered")
		TNT1 A 0 A_JumpIfInventory("SamsaraHasPermaWeaponLevel2",1,"SelectPowered")
	SelectContinue:
		TNT1 A 0 A_Raise
		FLMG A 1 A_Raise
		goto Select

	Fire:
		TNT1 A 0 A_TakeInventory("SamsaraDoomguyStrFlamethrowerAttackAmount") // reset
	FireLoop:
		TNT1 A 0 A_GiveInventory("SamsaraDoomguyStrFlamethrowerAttackAmount")
		TNT1 A 0 A_JumpIfInventory("PowerSamsaraDoomguyStrWeaponPower",1,"FirePoweredLoop")
		TNT1 A 0 A_JumpIfInventory("SamsaraHasPermaWeaponLevel2",1,"FirePoweredLoop")
		TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireLoopAttack")
		TNT1 A 0 A_JumpIfInventory("DoomguyStrGas",1,"FireLoopAttack")
		goto FireLoopContinue

	FireLoopAttack:
		TNT1 A 0 A_GunFlash
	FireLoopContinue:
		TNT1 A 0 A_GiveInventory("SamsaraTakingAmmo")
		FLMG E 1 Bright A_GiveInventory("SamsaraDoomguyStrFlamethrowerAttackHandler")
		TNT1 A 0 A_TakeInventory("SamsaraTakingAmmo")
		FLMG F 1 Bright A_GiveInventory("SamsaraDoomguyStrFlamethrowerAttackHandler")
		TNT1 A 0 A_GiveInventory("SamsaraTakingAmmo")
		FLMG G 1 Bright A_GiveInventory("SamsaraDoomguyStrFlamethrowerAttackHandler")
		TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrFlamethrowerAttackAmount",0,"FireFinish")
		goto FireLoop

	FireFinish:
		FLMG H 2 Bright A_ReFire
		goto Ready

	Flash:
		TNT1 A 2 A_Light1
		TNT1 A 2 A_Light2
		TNT1 A 1 A_Light1
		goto LightDone

	// Powered
	ReadyPowered:
		FLMG A 1 A_WeaponReady
		goto Ready

	DeselectPowered:
		TNT1 A 0 A_Lower
		goto DeselectContinue

	SelectPowered:
		TNT1 A 0 A_Raise
		goto SelectContinue

	FirePoweredLoop:
		TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FirePoweredLoopAttack")
		TNT1 A 0 A_JumpIfInventory("DoomguyStrGas",1,"FirePoweredLoopAttack")
		goto FirePoweredLoopContinue

	FirePoweredLoopAttack:
		TNT1 A 0 A_GunFlash
	FirePoweredLoopContinue:
		TNT1 A 0 A_GiveInventory("SamsaraTakingAmmo")
		FLMG A 1 A_GiveInventory("SamsaraDoomguyStrFlamethrowerPoweredAttackHandler")
		TNT1 A 0 A_TakeInventory("SamsaraTakingAmmo")
		FLMG A 1 A_GiveInventory("SamsaraDoomguyStrFlamethrowerPoweredAttackHandler")
		TNT1 A 0 A_GiveInventory("SamsaraTakingAmmo")
		FLMG A 1 A_GiveInventory("SamsaraDoomguyStrFlamethrowerPoweredAttackHandler")
		TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrFlamethrowerAttackAmount",0,"FirePoweredFinish")
		goto FireLoop

	FirePoweredFinish:
		FLMG A 2 A_ReFire
		goto Ready
	}
}

// Unpowered attack handler

actor SamsaraDoomguyStrFlamethrowerAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("DoomguyStrGas",1,1)
        stop
        TNT1 A 0 A_JumpIfInventory("SamsaraTakingAmmo",1,1)
        goto PickupAttack
        TNT1 A 0 A_TakeInventory("DoomguyStrGas",1)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_JumpIfInventory("SamsaraImprovedDoomguyBalance",1,"PickupAttackImproved")
      // Vanilla
      PickupAttackVanilla:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackVanillaCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackVanillaCoop")
      PickupAttackVanillaDM:
        TNT1 A 0 A_FireCustomMissile("SamsaraDoomguyStrFTFire",0,false,random(-4,4))
        stop

      PickupAttackVanillaCoop:
        TNT1 A 0 A_FireCustomMissile("SamsaraDoomguyStrFTFireCoop",0,false,random(-4,4))
        stop

      // Improved
      PickupAttackImproved:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackImprovedCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackImprovedCoop")
      PickupAttackImprovedDM:
        TNT1 A 0 A_FireCustomMissile("SamsaraDoomguyStrFTFireImproved",0,false,random(-4,4))
        stop

      PickupAttackImprovedCoop:
        TNT1 A 0 A_FireCustomMissile("SamsaraDoomguyStrFTFireImprovedCoop",0,false,random(-4,4))
        stop
    }
}

// Powered attack handler

actor SamsaraDoomguyStrFlamethrowerPoweredAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("DoomguyStrGas",1,1)
        stop
        TNT1 A 0 A_JumpIfInventory("SamsaraTakingAmmo",1,1)
        goto PickupAttack
        TNT1 A 0 A_TakeInventory("DoomguyStrGas",1)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireCustomMissile("SamsaraDoomguyStrPowerFTFire",0,false,random(-4,4))
        stop

      PickupAttackCoop:
        TNT1 A 0 A_FireCustomMissile("SamsaraDoomguyStrPowerFTFireCoop",0,false,random(-4,4))
        stop
    }
}

// Flamethrower fire

actor SamsaraDoomguyStrFTFire
{
	Projectile
	Radius 2
	Height 4
	Speed 16
	Damage 1
	DamageType "DoomDamageTypeFire"
	RenderStyle "Add"
	Alpha 0.67
	Scale 0.67
	SeeSound "doomguy/stronghold/flamer"
	DeathSound "doomguy/stronghold/scorch"
	Obituary "$SAMSARA_DOOMGUYSTR_OB_SLOT4E"
	-BLOODSPLATTER
	+DONTSPLASH
	States
	{
	Spawn:
		TNT1 A 2
		FRFX ABCD 2 Bright Light("FTFireSpawn") A_Explode(4,8,0)
		TNT1 A 0 A_LowGravity
		FRFX EFG 2 Bright Light("FTFireSpawn") A_Explode(4,16,0)
		FRFX H 2 Bright Light("FTFireDeathH") A_Explode(4,32,0)
		FRFX I 2 Bright Light("FTFireDeathI") A_Explode(4,32,0)
		FRFX J 2 Bright Light("FTFireDeathJ") A_Explode(4,32,0)
	SpawnContinue:
		FRFX K 2 Bright Light("FTFireDeathK") A_Explode(4,64,0)
		FRFX L 2 Bright Light("FTFireDeathL") A_Explode(4,64,0)
		FRFX M 2 Bright Light("FTFireDeathM") A_Explode(4,64,0)
	SpawnFinish:
		FRFX N 2 Bright Light("FTFireDeathN")
		FRFX O 2 Bright Light("FTFireDeathO")
		FRFX P 2 Bright Light("FTFireDeathP")
		stop

	Death:
		TNT1 A 0 A_NoGravity
		FRFX H 2 Bright Light("FTFireDeathH") A_Explode(4,32,0)
		FRFX I 2 Bright Light("FTFireDeathI") A_Explode(4,32,0)
		FRFX J 2 Bright Light("FTFireDeathJ") A_Explode(4,32,0)
		TNT1 A 0 A_CustomMissile("SamsaraDoomguyStrDropFire",0,0,0,CMF_TRACKOWNER)
		goto SpawnContinue
	}
}

actor SamsaraDoomguyStrFTFireCoop : SamsaraDoomguyStrFTFire
{
	Species "Player"
	+THRUSPECIES
}

actor SamsaraDoomguyStrFTFireImproved : SamsaraDoomguyStrFTFire
{
	States
	{
	Spawn:
		TNT1 A 2
		FRFX ABCD 2 Bright Light("FTFireSpawn") A_Explode(5,8,0)
		TNT1 A 0 A_LowGravity
		FRFX EFG 2 Bright Light("FTFireSpawn") A_Explode(5,16,0)
		FRFX H 2 Bright Light("FTFireDeathH") A_Explode(5,32,0)
		FRFX I 2 Bright Light("FTFireDeathI") A_Explode(5,32,0)
		FRFX J 2 Bright Light("FTFireDeathJ") A_Explode(5,32,0)
	SpawnContinue:
		FRFX K 2 Bright Light("FTFireDeathK") A_Explode(5,64,0)
		FRFX L 2 Bright Light("FTFireDeathL") A_Explode(5,64,0)
		FRFX M 2 Bright Light("FTFireDeathM") A_Explode(5,64,0)
		goto SpawnFinish

	Death:
		TNT1 A 0 A_NoGravity
		FRFX H 2 Bright Light("FTFireDeathH") A_Explode(5,32,0)
		FRFX I 2 Bright Light("FTFireDeathI") A_Explode(5,32,0)
		FRFX J 2 Bright Light("FTFireDeathJ") A_Explode(5,32,0)
		TNT1 A 0 A_CustomMissile("SamsaraDoomguyStrDropFireImproved",0,0,0,CMF_TRACKOWNER)
		goto SpawnContinue
	}
}

actor SamsaraDoomguyStrFTFireImprovedCoop : SamsaraDoomguyStrFTFireImproved
{
	Species "Player"
	+THRUSPECIES
}

// Flamethrower lingering fire token

actor SamsaraDoomguyStrDropFireLoopAmount : Counter { Inventory.MaxAmount 4 }

// Flamethrower lingering fire

actor SamsaraDoomguyStrDropFire
{
	Projectile
	Radius 8
	Height 40
	DamageType "DoomDamageTypeFire"
    ReactionTime 1
	Gravity 0.125
	RenderStyle "Add"
	Alpha 0.9
	Obituary "$SAMSARA_DOOMGUYSTR_OB_SLOT4E"
	-BLOODSPLATTER
	+DONTSPLASH
	+NODAMAGETHRUST
	+NOEXPLODEFLOOR
	-NOGRAVITY
	+THRUACTORS
	States
	{
	Spawn:
		FLME A 0 Bright NoDelay Light("FTFireSpawn") A_PlaySound("doomguy/stronghold/onfire",CHAN_VOICE)
        TNT1 A 0 A_ChangeFlag("PAINLESS",false)
		FLME A 2 Bright Light("FTFireSpawn") A_Explode(3,32,0)
        TNT1 A 0 A_ChangeFlag("PAINLESS",true)
		FLME BCDEFGHIJKLMN 2 Bright Light("FTFireSpawn") A_Explode(2,16,0)
	SpawnLoopInit:
		TNT1 A 0 A_TakeInventory("SamsaraDoomguyStrDropFireLoopAmount") // reset
	SpawnLoop:
		TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrDropFireLoopAmount",0,"SpawnFinish")
		TNT1 A 0 A_GiveInventory("SamsaraDoomguyStrDropFireLoopAmount")
		TNT1 A 0 A_PlaySound("doomguy/stronghold/onfire",CHAN_VOICE)
        TNT1 A 0 A_ChangeFlag("PAINLESS",false)
		FLME A 2 Bright Light("FTFireSpawn") A_Explode(3,32,0)
        TNT1 A 0 A_ChangeFlag("PAINLESS",true)
		FLME BCDEFGHIJKLMN 2 Bright Light("FTFireSpawn") A_Explode(2,16,0)
		loop

    SpawnFinish:
        TNT1 A 0 A_Jump(192,1)
        goto Spawn
        TNT1 A 1 A_Countdown
        wait

    Death:
        TNT1 A 1
        stop
	}
}

actor SamsaraDoomguyStrDropFireImproved : SamsaraDoomguyStrDropFire
{
	States
	{
	Spawn:
		FLME A 0 Bright NoDelay Light("FTFireSpawn") A_PlaySound("doomguy/stronghold/onfire",CHAN_VOICE)
		FLME A 2 Bright Light("FTFireSpawn") A_Explode(5,32,0)
		FLME BCDEFGHIJKLMN 2 Bright Light("FTFireSpawn") A_Explode(5,16,0)
	SpawnLoopInit:
		TNT1 A 0 A_TakeInventory("SamsaraDoomguyStrDropFireLoopAmount") // reset
	SpawnLoop:
		TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrDropFireLoopAmount",0,"SpawnFinish")
		TNT1 A 0 A_GiveInventory("SamsaraDoomguyStrDropFireLoopAmount")
		TNT1 A 0 A_PlaySound("doomguy/stronghold/onfire",CHAN_VOICE)
		FLME A 2 Bright Light("FTFireSpawn") A_Explode(5,32,0)
		FLME BCDEFGHIJKLMN 2 Bright Light("FTFireSpawn") A_Explode(5,16,0)
		loop

    SpawnFinish:
        TNT1 A 0 A_Jump(192,1)
        goto Spawn
        TNT1 A 1 A_Countdown
        wait
	}
}

// Powered flamethrower ice

actor SamsaraDoomguyStrPowerFTFire : SamsaraDoomguyStrFTFire
{
	Damage 2
	DamageType "DoomDamageTypeIce"
	Translation "Ice"
	-DONTSPLASH
	+RIPPER
	States
	{
	Spawn:
		TNT1 A 2
		STEM ABCD 2 Bright Light("PowerFTFireBlue") A_Explode(7,8,0)
		TNT1 A 0 A_LowGravity
		STEM EFG 2 Bright Light("PowerFTFireBlue") A_Explode(8,16,0)
		STEM HIJ 2 Bright Light("PowerFTFireBlue") A_Explode(7,32,0)
		STEM KJI 2 Bright Light("PowerFTFireBlue") A_Explode(8,64,0)
	SpawnFinish:
		STEM HGFEDCBA 1 Bright Light("PowerFTFireBlue")
		stop

	Death:
		TNT1 A 0 A_NoGravity
		STEM J 1 Bright Light("PowerFTFireBlue") A_Explode(7,32,0)
		STEM I 1 Bright Light("PowerFTFireBlue") A_Explode(8,64,0)
	DeathFinish:
		STEM HGECA 1 Bright Light("PowerFTFireBlue")
		stop
	}
}

actor SamsaraDoomguyStrPowerFTFireCoop : SamsaraDoomguyStrPowerFTFire
{
	Species "Player"
	+THRUSPECIES
}

// Ammo

actor DoomguyStrGas : Ammo
{
	Inventory.PickupMessage "Picked up a gas canister."
	Inventory.Icon "AMSMT0"
	Inventory.Amount 24
	Inventory.MaxAmount 240
	Ammo.BackpackAmount 24
	Ammo.BackpackMaxAmount 480
	Tag "Gasoline"
	/*States
	{
	Spawn:
		AGAS A -1
		stop
	}*/
}
