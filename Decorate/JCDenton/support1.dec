Actor DeusEx_StealthPistol : DeusExWeapon
{
	Weapon.SelectionOrder 1700
	Weapon.SlotNumber 1
	Weapon.SlotPriority 2
	Weapon.AmmoType "DeusEx_StealthMagazine"
	Weapon.AmmoUse 1
	Tag "Stealth Pistol"
	Inventory.PickupMessage "You found a Stealth Pistol."
	Obituary "%o was wetworked with %k's Stealth Pistol."
    +WIMPY_WEAPON
	States
	{
		Spawn:
			DX99 A -1
			stop

		ReadyActual:
            DX00 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"ReadyActualReloadable")
        ReadyActualNonReloadable:
			DX00 A 1 A_WeaponReady
            goto ReadyActualJump

        ReadyActualReloadable:
			DX00 A 1 A_WeaponReady(WRF_ALLOWRELOAD)
            goto ReadyActualJump

        ReadyActualJump:
            DX00 A 0 A_Jump(4,"ReadyActualIdle1","ReadyActualIdle2","ReadyActualIdle3")
			goto ReadyActualAmmoCheck

		ReadyActualIdle1:
            DX01 C 0 A_JumpIfInventory("SamsaraReloadMode",1,"ReadyActualIdle1Reloadable")
        ReadyActualIdle1NonReloadable:
			DX01 CDEFGHIJ 5 A_WeaponReady
			goto ReadyActual

        ReadyActualIdle1Reloadable:
			DX01 CDEFGHIJ 5 A_WeaponReady(WRF_ALLOWRELOAD)
			goto ReadyActual

		ReadyActualIdle2:
            DX01 K 0 A_JumpIfInventory("SamsaraReloadMode",1,"ReadyActualIdle2Reloadable")
        ReadyActualIdle2NonReloadable:
			DX01 KLMNOPQR 5 A_WeaponReady
			goto ReadyActual

        ReadyActualIdle2Reloadable:
			DX01 KLMNOPQR 5 A_WeaponReady(WRF_ALLOWRELOAD)
			goto ReadyActual

		ReadyActualIdle3:
            DX01 S 0 A_JumpIfInventory("SamsaraReloadMode",1,"ReadyActualIdle3Reloadable")
        ReadyActualIdle3NonReloadable:
			DX01 STUVWXYZ 5 A_WeaponReady
			goto ReadyActual

        ReadyActualIdle3Reloadable:
			DX01 STUVWXYZ 5 A_WeaponReady(WRF_ALLOWRELOAD)
			goto ReadyActual

        ReadyActualAmmoCheck:
            DX00 A 0 A_JumpIfInventory("SamsaraReloadMode",1,1)
            goto ReadyActual
            DX00 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"ReadyActual")
            DX00 A 0 A_JumpIfInventory("DeusEx_StealthMagazine",1,"ReadyActual")
            goto ReloadCheck

		Deselect:
			DX00 "[" 0 A_TakeInventory("DeusEx_StealthSpread")
			DX00 "[" 0 A_GiveInventory("DeusEx_Reloading")
			DX00 "[" 3 A_JumpIfHealthLower(1,"DeathDeselect")
			DX00 "\]" 3
			DX01 AB 3
            goto DeselectFinish

        DeathDeselect:
            TNT1 AAAAA 3
            goto DeselectFinish

        DeselectFinish:
			TNT1 A 0 A_TakeInventory("DeusEx_StealthEquipped")
			goto DeselectInstant

		Ready: // our "Select" state
            DX00 B 0 A_PlaySound("DeusEx/Stealth/Select")
			DX00 B 2 A_GiveInventory("DeusEx_StealthEquipped")
			DX00 CDEFG 2 
			DX00 A 0 A_TakeInventory("DeusEx_Reloading")
			goto ReadyActual

		Fire:
            DX00 H 0 A_JumpIfInventory("SamsaraReloadMode",1,"FireReloadable")
        FireNonReloadable:
            DX00 H 0 A_JumpIfInventory("PistolModeOn",1,1)
            goto FireStart
            DX00 H 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireStart")
            DX00 H 0 A_JumpIfInventory("Clip",1,"FireStart")
            goto FireDry

        FireReloadable:
            DX00 H 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"FireStart")
            DX00 H 0 A_JumpIfInventory("DeusEx_StealthMagazine",1,"FireStart")
        FireReloadableDryCheck:
            DX00 H 0 A_JumpIfInventory("PistolModeOn",1,1)
            goto ReloadStart
            DX00 H 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"ReloadStart")
            DX00 H 0 A_JumpIfInventory("Clip",1,"ReloadStart")
            goto FireDry

		FireDry:
			DX00 A 0 A_PlaySound("DeusEx/DryFire",CHAN_6)
			DX00 A 0 A_ClearReFire
            DX00 A 4 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
            goto ReadyActualAmmoCheck

		FireStart:
			DX00 H 0 A_GiveInventory("DeusEx_Firing")
			DX00 H 0 A_SpawnItemEx("DeusEx_ShellCasing1",8,-16,39,random(1,3),0,3,-90,SXF_NOCHECKPOSITION|SXF_MULTIPLYSPEED|SXF_TRANSFERPITCH)
			DX00 H 0 A_GiveInventory("SamsaraDeusExStealthPistolAttackHandler")
			DX00 H 3 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			DX00 I 0
			DX00 JK 3 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
            DX00 L 0 A_ReFire
			DX00 L 3 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			goto ReadyActual

        Reload:
        ReloadCheck:
            DX00 A 0 A_JumpIfInventory("DeusEx_StealthMagazine",0,"ReadyActual")
            DX00 A 0 A_JumpIfInventory("PistolModeOn",1,1)
            goto ReloadStart
            DX00 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
            DX00 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"ReloadStart")
            DX00 A 0 A_JumpIfInventory("Clip",1,"ReloadStart")
            goto ReadyActual

		ReloadStart:
			DX00 M 0 A_GiveInventory("DeusEx_Reloading")
			DX00 M 0 A_PlaySound("DeusEx/Stealth/Reload1")
			DX00 MNOPQRS 4 A_WeaponReady(WRF_NOFIRE)
			DX00 T 0 A_JumpIfInventory("SPModeOn",1,2)
			DX00 T 0 A_JumpIfInventory("CoopModeOn",1,1)
            goto ReloadContinueAdvanced
			DX00 T 0 A_JumpIfInventory("DeusEx_PistolSkill",0,"ReloadContinueMaster")
			DX00 T 0 A_JumpIfInventory("DeusEx_PistolSkill",3,"ReloadContinueAdvanced")
			DX00 T 0 A_JumpIfInventory("DeusEx_PistolSkill",2,"ReloadContinueProficient")
			DX00 T 0 A_JumpIfInventory("DeusEx_PistolSkill",1,"ReloadContinueTrained")
        ReloadContinueUntrained: // never called, but here just in case
			DX00 TUVWX 4 A_WeaponReady(WRF_NOFIRE)
        ReloadContinueTrained:
			DX00 TUVWX 4 A_WeaponReady(WRF_NOFIRE)
        ReloadContinueProficient:
			DX00 TUVWX 4 A_WeaponReady(WRF_NOFIRE)
        ReloadContinueAdvanced:
			DX00 TUVWX 4 A_WeaponReady(WRF_NOFIRE)
        ReloadContinueMaster:
			DX00 Y 0 A_TakeInventory("DeusEx_Reloading")
			DX00 Y 0 A_PlaySound("DeusEx/Stealth/Reload2")
			DX00 YZ 4 A_WeaponReady(WRF_NOFIRE)
            DX00 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"ReloadAmmo")
            goto ReloadDone

        ReloadDone:
            DX00 A 0
			goto ReadyActual

        ReloadAmmo:
            TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,"ReloadAmmoReserve")
            TNT1 A 0 A_JumpIfInventory("DeusEx_StealthMagazine",0,"ReloadDone")
            TNT1 A 0 A_GiveInventory("DeusEx_StealthMagazine")
            loop

        ReloadAmmoReserve:
            TNT1 A 0 A_JumpIfInventory("DeusEx_StealthMagazine",0,"ReloadDone")
            TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
            TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
            TNT1 A 0 A_JumpIfInventory("Clip",1,1)
            goto ReloadDone
            TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
            TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
            TNT1 A 0 A_TakeInventory("Clip",1)
            TNT1 A 0 A_GiveInventory("DeusEx_StealthMagazine")
            goto ReloadAmmo
	}
}

actor SamsaraDeusExStealthPistolAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"PickupReloadable")
      PickupNonReloadable:
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto PickupAttack
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Clip",1,1)
        stop
        TNT1 A 0 A_TakeInventory("Clip",1)
        goto PickupAttack

      PickupReloadable:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("DeusEx_StealthMagazine",1,1)
        stop
        TNT1 A 0 A_TakeInventory("DeusEx_StealthMagazine",1)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("DeusEx/Stealth/Fire",CHAN_WEAPON)
        TNT1 A 0 A_FireBullets(0,0,1,0,"SamsaraBlankPuff",FBF_NORANDOM|FBF_NOFLASH)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireCustomMissile("DeusEx_StealthPistolBullet",CallACS("DeusEx_Decorate", 5, 5, 1)*frandom(-1.0,1.0),false,12,16,1,CallACS("DeusEx_Decorate", 5, 5, 1)*frandom(-0.5,0.5))
        goto PickupFinish

      PickupAttackCoop:
        TNT1 A 0 A_FireCustomMissile("DeusEx_StealthPistolBulletCoop",CallACS("DeusEx_Decorate", 5, 5, 1)*frandom(-1.0,1.0),false,12,16,1,CallACS("DeusEx_Decorate", 5, 5, 1)*frandom(-0.5,0.5))
        goto PickupFinish

      // Finishing stuff
      PickupFinish:
        TNT1 A 0 A_GiveInventory("DeusEx_StealthSpread",5)
        TNT1 A 0 ACS_NamedExecuteWithResult("DeusExRecoil", 6, 3, 1)
        stop
    }
}

Actor DeusEx_StealthMagazine : Ammo
{
    Inventory.MaxAmount 7
    Tag "Stealth Pistol Magazine"
    +IGNORESKILL
}

Actor DeusEx_StealthSpread : Ammo
{
    Inventory.MaxAmount 115
    Inventory.InterHubAmount 0
    +IGNORESKILL
    +UNDROPPABLE
}

Actor DeusEx_StealthPistolBullet : DeusExBullet
{
	Damage(CallACS("DeusEx_WeaponSkillDamage", 1, 8))
	DamageType "DeusExPistol"
	States
	{
		Spawn:
			TNT1 A 0 NODELAY //A_SetUserVar(user_damage, CallACS("DeusEx_WeaponSkillDamage", 1, 14))
			TNT1 A 1 //A_SetUserVar(user_damage, CallACS("DeusEx_WeaponSkillDamage", 1, 14))
			Goto Spawn2
		Crash:
			TNT1 A 0 A_PlaySound("DeusEx/Bullet/HitRobot", CHAN_BODY)
			TNT1 A 1 A_Explode(CallACS("DeusEx_DamageModifiers",CallACS("DeusEx_WeaponSkillDamage", 1, 8)),8,!XF_HURTSOURCE,0,8)
			stop	
		XDeath:
			//TNT1 A 0 A_PlaySound("DeusEx/Bullet/HitFlesh", CHAN_BODY)
			TNT1 A 0 A_Explode(CallACS("DeusEx_DamageModifiers",CallACS("DeusEx_WeaponSkillDamage", 1, 8)),8,!XF_HURTSOURCE,0,8)
			TNT1 A 0 ACS_NamedExecuteWithResult("DeusEx_BloodGenerator_Offsets")
			TNT1 A 1 A_GiveInventory("DeusEx_BloodGenerator",1,AAPTR_TRACER)
			stop
	}
}

Actor DeusEx_StealthPistolBulletCoop : DeusEx_StealthPistolBullet { +THRUSPECIES Species "Player" }
