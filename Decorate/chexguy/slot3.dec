actor "Super Large Zorcher" : Weapon
{
    Weapon.SelectionOrder 1500
    Weapon.SlotNumber 3
    //Weapon.KickBack 30000000000
    Inventory.PickUpSound "chex/weaponget"
    Weapon.AmmoUse 4
    Weapon.AmmoGive 16
    Weapon.AmmoType "Shell"
    //+AMMO_OPTIONAL
    +INVENTORY.UNDROPPABLE
    DamageType "Zorch"
    Inventory.PickupMessage "You got the Super Large Zorcher!"
    Obituary "$SAMSARA_CHEX_OB_SLOT3"
	Inventory.RestrictedTo "ChexWarrior"
    Tag "Super Large Zorcher"
    States
    {
      Ready:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"ReadyAmmo")
        TNT1 A 0 A_JumpIfInventory("Shell",4,"ReadyAmmo")
        goto ReadyNoAmmo

      ReadyNoAmmo:
        CSLR O 1 A_WeaponReady
        goto ReadyNoAmmoCheck

      ReadyAmmo:
        CSLZ ABCDEDCB 2 A_WeaponReady
        goto Ready

      ReadyNoAmmoCheck:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("Shell",4,1)
        goto Ready
        TNT1 A 0 A_JumpIfInventory("SamsaraImprovedDoomguyBalance",1,"FireFinishImproved")
        goto FireFinishVanilla

      Deselect:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"DeselectAmmo")
        TNT1 A 0 A_JumpIfInventory("Shell",4,"DeselectAmmo")
        goto DeselectNoAmmo

      DeselectNoAmmo:
        CSLR O 1 A_Lower
        goto Deselect

      DeselectAmmo:
        CSLZ AABBCCDDEEDDCCBB 1 A_Lower
        goto Deselect

      Select:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"SelectAmmo")
        TNT1 A 0 A_JumpIfInventory("Shell",4,"SelectAmmo")
        goto SelectNoAmmo

      SelectNoAmmo:
        CSLR O 1 A_Raise
        goto Select

      SelectAmmo:
        CSLZ AABBCCDDEEDDCCBB 1 A_Raise
        goto Select

      Fire:
        TNT1 A 0 A_JumpIfInventory("SamsaraImprovedDoomguyBalance",1,"FireImproved")
      FireVanilla:
        CSLZ A 1 // 1
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireAttackVanilla")
        TNT1 A 0 A_JumpIfInventory("Shell",4,"FireAttackVanilla")
        goto FireContinueVanilla

      FireAttackVanilla:
        TNT1 A 0 A_GunFlash
      FireContinueVanilla:
        CSLZ F 3 Bright A_GiveInventory("SamsaraChexSuperLargeZorcherAttackHandler") // 4
        CSLZ GH 3 Bright // 10
        CSLZ I 3 // 13
        CSLZ J 6 // 19
        CSLR ABCD 2 // 27
        CSLR E 6 // 33
        CSLR F 2 A_PlaySound("chex/slzorchmagunlk") // 35
        CSLR G 2 // 37
        CSLR H 6 // 43
        CSLR I 2 A_PlaySound("chex/slzorchmagdown") // 45
        CSLR JKLMNO 2 // 57
        CSLR O 9 A_CheckReload // 66
      FireFinishVanilla:
        CSLR ONMLK 2 // 76
        CSLR J 2 A_PlaySound("chex/slzorchmagup") // 78
        CSLR I 4 // 82
        CSLR H 6 // 88
        CSLR G 2 A_PlaySound("chex/slzorchmaglock") // 90
        CSLR FE 2 // 94
        CSLZ STUVW 2 // 104
        CSLZ A 2 A_ReFire // 106
        goto Ready

      FireImproved:
        TNT1 A 0 A_GunFlash
        CSLZ F 2 Bright A_GiveInventory("SamsaraChexSuperLargeZorcherAttackHandler") // 2
        CSLZ GH 2 Bright // 6
        CSLZ IJ 2 // 10
        CSLR ABCD 2 // 18
        CSLR E 4 // 22
        CSLR F 2 A_PlaySound("chex/slzorchmagunlk") // 24
        CSLR G 2 // 26
        CSLR H 4 // 30
        CSLR I 2 A_PlaySound("chex/slzorchmagdown") // 32
        CSLR JKLMNO 2 // 44
        CSLR O 2 A_CheckReload // 46
      FireFinishImproved:
        CSLR ONMLK 2 // 56
        CSLR J 2 A_PlaySound("chex/slzorchmagup") // 58
        CSLR IH 2 // 62
        CSLR G 2 A_PlaySound("chex/slzorchmaglock") // 64
        CSLR FE 2 // 68
        CSLZ STUVW 2 // 78
        CSLZ A 2 A_ReFire // 80
        goto Ready

      Flash:
        TNT1 A 2 Bright A_Light1
        TNT1 A 3 Bright A_Light2
        Goto LightDone

      Spawn:
        WSLZ A -1
        Stop
    }
}

actor SamsaraChexSuperLargeZorcherAttackHandler : Trigger
{
    States
    {
      // Ammo check stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupTakeAmmo")
        TNT1 A 0 A_JumpIfInventory("Shell",4,"PickupTakeAmmo")
        stop

      // Ammo taking stuff
      PickupTakeAmmo:
        TNT1 A 0 A_TakeInventory("Shell",4,TIF_NOTAKEINFINITE)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("chex/slzorcher",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireCustomMissile("SLZorcherBoltDM",0,false,4,4)
        TNT1 A 0 A_FireCustomMissile("SLZorcherBoltDM",0,false,4,-4)
        TNT1 A 0 A_FireCustomMissile("SLZorcherBoltDM",0,false,-4,4)
        TNT1 A 0 A_FireCustomMissile("SLZorcherBoltDM",0,false,-4,-4)
        stop

      PickupAttackCoop:
        TNT1 A 0 A_FireCustomMissile("SLZorcherBoltCoop",0,false,4,4)
        TNT1 A 0 A_FireCustomMissile("SLZorcherBoltCoop",0,false,4,-4)
        TNT1 A 0 A_FireCustomMissile("SLZorcherBoltCoop",0,false,-4,4)
        TNT1 A 0 A_FireCustomMissile("SLZorcherBoltCoop",0,false,-4,-4)
        stop
    }
}

actor SLZorcherBolt// : FastProjectile
{
    Radius 15
    Height 10
    Speed 155
    Damage (random(15,35))
    Scale 0.95
    Alpha 0.9
    PROJECTILE
    //+HEXENBOUNCE
    -BLOODSPLATTER
    +NOBLOOD
	+BLOODLESSIMPACT
    +SEEKERMISSILE
    //BounceCount 3
    RenderStyle Add
    DamageType "Zorch"
    SeeSound "chex/pistol"//"chex/slzorchbounce"
    DeathSound "chex/slzorchhit"
    States
    {
      Spawn:
        SLZR AB 1 BRIGHT A_CustomMissile("SLZorchBoltBlur",0,0,180)
        Loop

      Death:
        TNT1 A 0
        TNT1 A 0 A_CustomMissile("SLZorcherBolt2",0,0,0,4,0)
        stop
      
      Crash:
      XDeath:
        TNT1 A 0
        TNT1 A 0 A_CustomMissile("SLZSpatter",0,0,random(-10,10),6,0)
        TNT1 A 0 A_CustomMissile("SLZSpatter",0,0,random(35,55),6,0)
        TNT1 A 0 A_CustomMissile("SLZSpatter",0,0,random(80,100),6,0)
        TNT1 A 0 A_CustomMissile("SLZSpatter",0,0,random(125,145),6,0)
        TNT1 A 0 A_CustomMissile("SLZSpatter",0,0,random(170,190),6,0)
        TNT1 A 0 A_CustomMissile("SLZSpatter",0,0,random(215,235),6,0)
        TNT1 A 0 A_CustomMissile("SLZSpatter",0,0,random(260,280),6,0)
        TNT1 A 0 A_CustomMissile("SLZSpatter",0,0,random(305,325),6,0)
        SLZD ABCDEF 2 BRIGHT
        Stop
    }
}

actor SLZorcherBolt2 : SLZorcherBolt
{
    Speed 125
    States
    {        
      Death:
        TNT1 A 0
        TNT1 A 0 A_CustomMissile("SLZorcherBolt3",0,0,0,4,0)
        stop
    }
}

actor SLZorcherBolt3 : SLZorcherBolt
{
    Speed 100
    States
    {        
      Death:
        TNT1 A 0
        TNT1 A 0 A_CustomMissile("SLZSpatter",0,0,random(-10,10),6,0)
        TNT1 A 0 A_CustomMissile("SLZSpatter",0,0,random(35,55),6,0)
        TNT1 A 0 A_CustomMissile("SLZSpatter",0,0,random(80,100),6,0)
        TNT1 A 0 A_CustomMissile("SLZSpatter",0,0,random(125,145),6,0)
        TNT1 A 0 A_CustomMissile("SLZSpatter",0,0,random(170,190),6,0)
        TNT1 A 0 A_CustomMissile("SLZSpatter",0,0,random(215,235),6,0)
        TNT1 A 0 A_CustomMissile("SLZSpatter",0,0,random(260,280),6,0)
        TNT1 A 0 A_CustomMissile("SLZSpatter",0,0,random(305,325),6,0)
        SLZD ABCDEF 2 BRIGHT
        Stop
    }
}

actor SLZSpatter
{
    Radius 8
    Height 6
    Speed 15
    Damage (random(12,18))
    Scale 0.25
    Alpha 0.75
    PROJECTILE
    -BLOODSPLATTER
	+BLOODLESSIMPACT
    +NOBLOOD
    RenderStyle Add
    DamageType "Zorch"
    States
    {
      Spawn:
        SLZR CDEFCDEF 1 BRIGHT
        goto SpawnDeath

      Death:
        CMIS BCD 5 BRIGHT
        Stop

      SpawnDeath:
        SLZR CDEF 1 A_FadeOut(0.25)
        stop
    }
}

actor SLZSpatter2 : SLZSpatter
{
    Damage (1)
}

actor SLZSpatterDM : SLZSpatter
{
    Damage (random(1,5))
}

actor SLZSpatterCoop : SLZSpatter
{
    +THRUSPECIES
    Species "Player"
}

actor SLZorcherBoltDM : SLZorcherBolt
{
    Damage (random(10,30))
    States
    {
      Death:
        TNT1 A 0
        TNT1 A 0 A_CustomMissile("SLZorcherBoltDM2",0,0,0,4,0)
        stop
        
      XDeath:
      Crash:
        TNT1 A 0
        TNT1 A 0 A_CustomMissile("SLZSpatter2",0,0,random(-10,10),6,0)
        TNT1 A 0 A_CustomMissile("SLZSpatterDM",0,0,random(35,55),6,0)
        TNT1 A 0 A_CustomMissile("SLZSpatterDM",0,0,random(80,100),6,0)
        TNT1 A 0 A_CustomMissile("SLZSpatterDM",0,0,random(125,145),6,0)
        TNT1 A 0 A_CustomMissile("SLZSpatterDM",0,0,random(170,190),6,0)
        TNT1 A 0 A_CustomMissile("SLZSpatterDM",0,0,random(215,235),6,0)
        TNT1 A 0 A_CustomMissile("SLZSpatterDM",0,0,random(260,280),6,0)
        TNT1 A 0 A_CustomMissile("SLZSpatterDM",0,0,random(305,325),6,0)
        SLZD ABCDEF 2 BRIGHT
        Stop
    }
}

actor SLZorcherBoltDM2 : SLZorcherBoltDM
{
    Speed 125
    States
    {        
      Death:
        TNT1 A 0
        TNT1 A 0 A_CustomMissile("SLZorcherBoltDM3",0,0,0,4,0)
        stop
    }
}

actor SLZorcherBoltDM3 : SLZorcherBoltDM
{
    Speed 100
    States
    {        
      Death:
        TNT1 A 0
        TNT1 A 0 A_CustomMissile("SLZSpatter2",0,0,random(-10,10),6,0)
        TNT1 A 0 A_CustomMissile("SLZSpatterDM",0,0,random(35,55),6,0)
        TNT1 A 0 A_CustomMissile("SLZSpatterDM",0,0,random(80,100),6,0)
        TNT1 A 0 A_CustomMissile("SLZSpatterDM",0,0,random(125,145),6,0)
        TNT1 A 0 A_CustomMissile("SLZSpatterDM",0,0,random(170,190),6,0)
        TNT1 A 0 A_CustomMissile("SLZSpatterDM",0,0,random(215,235),6,0)
        TNT1 A 0 A_CustomMissile("SLZSpatterDM",0,0,random(260,280),6,0)
        TNT1 A 0 A_CustomMissile("SLZSpatterDM",0,0,random(305,325),6,0)
        SLZD ABCDEF 2 BRIGHT
        Stop
    }
}

actor SLZorcherBoltCoop : SLZorcherBolt
{
    +THRUSPECIES
    Species "Player"
    States
    {
      Death:
        TNT1 A 0
        TNT1 A 0 A_CustomMissile("SLZorcherBoltCoop2",0,0,0,4,0)
        stop
        
      XDeath:
      Crash:
        TNT1 A 0
        TNT1 A 0 A_CustomMissile("SLZSpatterCoop",0,0,random(-10,10),6,0)
        TNT1 A 0 A_CustomMissile("SLZSpatterCoop",0,0,random(35,55),6,0)
        TNT1 A 0 A_CustomMissile("SLZSpatterCoop",0,0,random(80,100),6,0)
        TNT1 A 0 A_CustomMissile("SLZSpatterCoop",0,0,random(125,145),6,0)
        TNT1 A 0 A_CustomMissile("SLZSpatterCoop",0,0,random(170,190),6,0)
        TNT1 A 0 A_CustomMissile("SLZSpatterCoop",0,0,random(215,235),6,0)
        TNT1 A 0 A_CustomMissile("SLZSpatterCoop",0,0,random(260,280),6,0)
        TNT1 A 0 A_CustomMissile("SLZSpatterCoop",0,0,random(305,325),6,0)
        SLZD ABCDEF 2 BRIGHT
        Stop
    }
}

actor SLZorcherBoltCoop2 : SLZorcherBoltCoop
{
    Speed 125
    States
    {        
      Death:
        TNT1 A 0
        TNT1 A 0 A_CustomMissile("SLZorcherBoltCoop3",0,0,0,4,0)
        stop
    }
}

actor SLZorcherBoltCoop3 : SLZorcherBoltCoop
{
    Speed 100
    States
    {        
      Death:
        TNT1 A 0
        TNT1 A 0 A_CustomMissile("SLZSpatterCoop",0,0,random(-10,10),6,0)
        TNT1 A 0 A_CustomMissile("SLZSpatterCoop",0,0,random(35,55),6,0)
        TNT1 A 0 A_CustomMissile("SLZSpatterCoop",0,0,random(80,100),6,0)
        TNT1 A 0 A_CustomMissile("SLZSpatterCoop",0,0,random(125,145),6,0)
        TNT1 A 0 A_CustomMissile("SLZSpatterCoop",0,0,random(170,190),6,0)
        TNT1 A 0 A_CustomMissile("SLZSpatterCoop",0,0,random(215,235),6,0)
        TNT1 A 0 A_CustomMissile("SLZSpatterCoop",0,0,random(260,280),6,0)
        TNT1 A 0 A_CustomMissile("SLZSpatterCoop",0,0,random(305,325),6,0)
        SLZD ABCDEF 2 BRIGHT
        Stop
    }
}

actor SLZorchBoltBlur
{
    Height 8
    Radius 1
    Damage 0
    Speed 0
    RenderStyle Add
    Alpha 0.7
    Scale 0.7
    +CLIENTSIDEONLY
    +NOINTERACTION
    +RANDOMIZE
    +NOGRAVITY
    +DROPOFF
    States
    {
      Spawn:
        SLZR A 2 BRIGHT A_FadeOut(0.1)
        SLZR B 2 BRIGHT A_FadeOut(0.1)
        Loop
    }
}
