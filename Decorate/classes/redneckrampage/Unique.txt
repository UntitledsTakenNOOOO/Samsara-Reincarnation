Actor " Hunting Rifle With Scope " : Weapon
{
	Inventory.PickupMessage "Hunting Rifle"
	Inventory.PickupSound "RR/Wpickup"
	Weapon.SelectionOrder 1600
	Weapon.SlotNumber 8
	Weapon.BobRangeX 0.75
	Weapon.BobRangeY 0.95
	Weapon.BobSpeed 1.1
	Weapon.SlotPriority 8
	Weapon.AmmoType "Clip"
	Weapon.AmmoUse 5
	Weapon.AmmoGive 25
	Obituary "%o became one of %k's trophies."
	Tag "Hunting Rifle with Scope"
	Inventory.RestrictedTo "Leonard"
	+Weapon.NoAlert
	States
	{
		Spawn:
			R_HC J -1 Bright
			Stop

		Select:
            TNT1 A 0 A_JumpIfInventory("RZoomed",1,1)
            goto SelectStart
            TNT1 A 0 A_ZoomFactor(1,ZOOM_INSTANT) // map change
            TNT1 A 0 A_TakeInventory("RZoomed")
            goto SelectStart

        SelectStart:
			TNT1 A 0 A_Raise
			R_HC A 1 A_Raise
			goto Select

		Deselect:
			TNT1 A 0 A_JumpIfInventory("RZoomed",1,"ZoomDeselect")
			TNT1 A 0 A_Lower
			R_HC A 1 A_Lower
			Goto Deselect+1
		ZoomDeselect:
			TNT1 A 0 A_TakeInventory("RZoomed",2)
			TNT1 A 0 A_ZoomFactor(1.0)
			R_HC BBGG 1 A_Lower
			TNT1 A 0 A_Lower
			R_CH A 1 A_Lower
			Goto ZoomDeselect+6
		Ready:
			TNT1 A 0 A_JumpIfInventory("RRDrunkToken", 1, "SetDrunkView")
			Goto SetSoberView
			TNT1 A 0 A_JumpIfInventory("LeonardWaiting", 15, "WaitingTaunt")
			TNT1 A 0 A_JumpIfInventory("LeonardReady",1,"IntroTaunt")
			TNT1 A 0 A_JumpIfInventory("RZoomed",1,"ZoomReady")
			TNT1 A 0 A_ZoomFactor(1.0)
			R_HC G 1 A_WeaponReady(WRF_ALLOWZOOM)
			Loop
		ZoomReady:
			TNT1 A 0 A_JumpIfInventory("LeonardReady",1,"IntroTaunt")
			TNT1 A 0 A_ZoomFactor(3.5)
			R_HC C 1 A_WeaponReady(WRF_ALLOWZOOM)
			Loop
		Fire:
			TNT1 A 0 A_JumpIfInventory("RRDrunkToken", 1, "SetDrunkViewFire")
			Goto SetSoberViewFire
			TNT1 A 0 A_JumpIfInventory("IMUSTRELOADTHISRIFLEx",1,"Reload")
			TNT1 A 0 A_JumpIfInventory("RZoomed",1,"ZoomFire")
			R_HC GG 1 Bright
			R_HC BBCC 1 Bright
		Hold:
			TNT1 A 0 A_JumpIfInventory("RZoomed",1,"ZoomHold")
			TNT1 A 0 A_PlaySound("RR/RifleF",CHAN_WEAPON)
			TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,2)
			R_HC C 1 Bright A_FireBullets(1.25,1.25,1,100,"RRPuff",FBF_NORANDOM|FBF_USEAMMO)
			Goto A
			R_HC C 1 Bright A_FireBullets(1.25,1.25,1,100,"RRPuffCoop",FBF_NORANDOM|FBF_USEAMMO)
		A:
			TNT1 A 0 A_AlertMonsters
			R_HC CDDDDDDDDDDDDDDDDCCCCCC 1 Bright
			TNT1 A 0 A_GiveInventory("IMUSTRELOADTHISRIFLEx",1)
			R_HC C 1 Bright
		Reload:
			TNT1 A 0 A_JumpIfInventory("Clip",5,1)
			Goto Ready
			TNT1 A 0 A_PlaySound("RR/RifleR",CHAN_BODY)
			R_HC EEE 1 Bright A_WeaponReady(WRF_NOBOB|WRF_NOFIRE|WRF_NOSWITCH)
			R_HC EFF 1 Bright A_WeaponReady(WRF_NOBOB|WRF_NOFIRE|WRF_NOSWITCH)
			R_HC III 1 Bright A_WeaponReady(WRF_NOBOB|WRF_NOFIRE|WRF_NOSWITCH)
			TNT1 A 0 A_TakeInventory("IMUSTRELOADTHISRIFLEx",1)
			R_HC IIIIHHHHHHHHIIIIIIIIIHHHHHHHHHHHHH 1 Bright A_WeaponReady(WRF_NOBOB|WRF_NOFIRE|WRF_NOSWITCH)
			TNT1 A 0 Bright A_Refire //Only chance to hold fire
			Goto Ready
		ZoomFire:
			R_HC CCC 1 Bright
			TNT1 A 0 A_GiveInventory("IMUSTRELOADTHISRIFLEx",1)
			TNT1 A 0 A_PlaySound("RR/RifleF",CHAN_WEAPON)
			TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,2)
			R_HC C 1 Bright A_FireBullets(0,0,1,100,"RRPuff",FBF_NORANDOM|FBF_USEAMMO)
			Goto B
			R_HC C 1 Bright A_FireBullets(0,0,1,100,"RRPuffCoop",FBF_NORANDOM|FBF_USEAMMO)
		B:
			TNT1 A 0 A_AlertMonsters
			R_HC DDDDDDDDDDDDDDDCCCCCC 1 Bright
			R_HC C 1 Bright
			Goto Reload
		ZoomHold:
			TNT1 A 0 A_GiveInventory("IMUSTRELOADTHISRIFLEx",1)
			TNT1 A 0 A_PlaySound("RR/RifleF",CHAN_WEAPON)
			TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,2)
			R_HC C 1 Bright A_FireBullets(0,0,1,100,"RRPuff",FBF_NORANDOM|FBF_USEAMMO)
			Goto B_
			R_HC C 1 Bright A_FireBullets(0,0,1,100,"RRPuffCoop",FBF_NORANDOM|FBF_USEAMMO)
		B_:
			TNT1 A 0 A_AlertMonsters
			R_HC DDDDDDDDDDDDDDDCCCCCC 1 Bright
			R_HC C 1 Bright
			Goto Reload
		AltFire:
        Zoom:
			TNT1 A 0 A_JumpIfInventory("RZoomed",1,"Zoomout")
			TNT1 A 0 A_ZoomFactor(3.5)
			R_HC GGGBBBCCC 1 A_WeaponReady(WRF_NOBOB|WRF_NOFIRE|WRF_NOSWITCH)
			TNT1 A 0
			TNT1 A 0 A_GiveInventory("RZoomed",1)
			Goto ZoomReady
		Zoomout:
			TNT1 A 0 A_ZoomFactor(1.0)
			R_HC CCCBBBGGG 1 A_WeaponReady(WRF_NOBOB|WRF_NOFIRE|WRF_NOSWITCH)
			TNT1 A 0
			TNT1 A 0 A_TakeInventory("RZoomed",2)
			Goto Ready
		IntroTaunt:
			TNT1 A 0 A_JumpIfInventory("LeonardBallgag",1,2)
			TNT1 A 0 A_PlaySoundEx("Leonard/Enter", "SoundSlot7", 0, 0)
			TNT1 A 0 A_TakeInventory("LeonardReady", 1)
			TNT1 A 0 A_GiveInventory("LeonardTauntCooldown",5)
			TNT1 A 0 ACS_NamedExecuteAlways("LeonardTauntCooldown", 0)
			Goto Ready
		WaitingTaunt:
			TNT1 A 0 A_JumpIfInventory("LeonardIdleBallgag",1,"IdleBallgagStuff")
			TNT1 A 0 A_JumpIfInventory("LeonardBallgag",1,2)
			TNT1 A 0 A_PlaySoundEx("Leonard/Bored", "SoundSlot7", 0, 0)
			TNT1 A 0 A_TakeInventory("LeonardWaiting", 15)
			TNT1 A 0 A_GiveInventory("LeonardTauntCooldown",5)
			TNT1 A 0 ACS_NamedExecuteAlways("LeonardTauntCooldown", 0)
			Goto Ready
		IdleBallgagStuff:
			TNT1 A 0 A_TakeInventory("LeonardWaiting", 15)
			goto Ready
		SetDrunkView:
			TNT1 A 0 A_ZoomFactor(0.55)
			Goto Ready+1
		SetDrunkViewFire:
			TNT1 A 0 A_ZoomFactor(0.55)
			Goto Fire+1
		SetSoberView:
			TNT1 A 0 A_ZoomFactor(1.0)
			Goto Ready+1
		SetSoberViewFire:
			TNT1 A 0 A_JumpIfInventory("RZoomed", 1, 2)
			TNT1 A 0 A_ZoomFactor(1.0)
			TNT1 A 0
			Goto Fire+1
	}
}

Actor RRBullet5 : RRBullet1 { Damage (200-random(50,0)) DamageType "LeonardDamage3" }
Actor RZoomed : Counter { Inventory.MaxAmount 2 }
Actor IMUSTRELOADTHISRIFLEx : Boolean {}

Actor " Chicken Crossbow " : Weapon
{
	Inventory.PickupMessage "Chicken Crossbow"
	Inventory.PickupSound "RR/Wpickup"
	Weapon.SelectionOrder 2500
	Weapon.BobRangeX 0.75
	Weapon.BobRangeY 0.95
	Weapon.BobSpeed 1.1	
	Weapon.SlotNumber 2
	Weapon.SlotPriority 2
	Weapon.AmmoType "ChickenAmmo"
	Weapon.AmmoUse 1
	Weapon.AmmoGive 5
	Obituary "%o didnt expect %k's cock... :^)"
	Inventory.RestrictedTo "Leonard"
	Tag "Chicken Crossbow"
	+WEAPON.EXPLOSIVE
	+WEAPON.NOALERT
	States
	{
		Spawn:
			CH_7 M -1 Bright
			Stop
		Select:
			TNT1 A 0 A_JumpIfnoAmmo("SelectAmmoLess")  
		SelectLoop:
			TNT1 A 0 A_Raise
			CH_R A 1 A_Raise
			Loop
		SelectAmmoLess:
			TNT1 A 0 A_Raise
			CH_R C 1 A_Raise
			Loop
		Deselect:
			TNT1 A 0 A_JumpIfnoAmmo("DeselectAmmoLess")
		DeselectLoop:
			TNT1 A 0 A_Lower
			CH_R A 1 A_Lower
			Loop
		DeselectAmmoLess:
			TNT1 A 0 A_Lower
			CH_R C 1 A_Lower
			Loop
		Ready:
			TNT1 A 0 A_JumpIfInventory("RRDrunkToken", 1, "SetDrunkView")
			Goto SetSoberView
			TNT1 A 0 A_JumpIfInventory("LeonardWaiting", 15, "WaitingTaunt")
			TNT1 A 0 A_JumpIfInventory("LeonardReady",1,"IntroTaunt")
			TNT1 A 0 A_JumpIfNoAmmo("ReadyAmmoless")
			TNT1 A 0 A_Jump(4,"B")
			TNT1 A 0 A_Jump(3,"B")
			TNT1 A 0 A_Jump(2,"B")
			TNT1 A 0 A_Jump(1,"Ready2")
		B:
			CH_R A 1 A_WeaponReady
			Loop
		Ready2:
			TNT1 A 0 A_PlaySound("RR/XBOWCOCKCRY",CHAN_WEAPON)
			CH_R H 8 A_WeaponReady
			Goto Ready
		ReadyAmmoless:
			TNT1 A 0 A_JumpIfInventory("LeonardReady",1,"IntroTaunt")
			TNT1 A 0 A_JumpIfInventory("ChickenAmmo",1,"Reload")
			CH_R C 1 A_WeaponReady
			Loop
		Fire:
			TNT1 A 0 A_JumpIfInventory("RRDrunkToken", 1, "SetDrunkViewFire")
			Goto SetSoberViewFire
			TNT1 A 0 A_AlertMonsters
			CH_R B 2 Bright A_PlaySound("RR/XBOWFire",CHAN_WEAPON)
			TNT1 A 0 A_JumpIfInventory("DMModeOn",1,4)
			TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,2)
			TNT1 A 0 A_FireCustomMissile("RRCHICKENBOMB",0,1,8,0)
			Goto A
			TNT1 A 0 A_FireCustomMissile("RRCHICKENBOMB_2",0,1,8,0)
			Goto A
			TNT1 A 0 A_FireCustomMissile("RRCHICKENBOMB_3",0,1,8,0)
		A:
			CH_R CCC 1 Bright
			CH_R DDDDDDDD 1 Bright
		Reload:
			TNT1 A 0 A_JumpIfInventory("ChickenAmmo",1,1)
			Goto ReadyAmmoless
			TNT1 A 0 A_PlaySound("RR/XBOWReload",CHAN_BODY)
			CH_R EEEE 1 Bright A_WeaponReady(WRF_NOBOB|WRF_NOFIRE|WRF_NOSWITCH)
			CH_R FFFF 1 Bright A_WeaponReady(WRF_NOBOB|WRF_NOFIRE|WRF_NOSWITCH)
			CH_R GGGG 1 Bright A_WeaponReady(WRF_NOBOB|WRF_NOFIRE|WRF_NOSWITCH)
			CH_R HHHHH 1 Bright A_WeaponReady(WRF_NOBOB|WRF_NOFIRE|WRF_NOSWITCH)
			CH_R AAAAAA 1 Bright A_WeaponReady(WRF_NOBOB|WRF_NOFIRE|WRF_NOSWITCH)
			Goto Ready
		IntroTaunt:
			TNT1 A 0 A_JumpIfInventory("LeonardBallgag",1,2)
			TNT1 A 0 A_PlaySoundEx("Leonard/Enter", "SoundSlot7", 0, 0)
			TNT1 A 0 A_TakeInventory("LeonardReady", 1)
			TNT1 A 0 A_GiveInventory("LeonardTauntCooldown",5)
			TNT1 A 0 ACS_NamedExecuteAlways("LeonardTauntCooldown", 0)
			Goto Ready
		WaitingTaunt:
			TNT1 A 0 A_JumpIfInventory("LeonardIdleBallgag",1,"IdleBallgagStuff")
			TNT1 A 0 A_JumpIfInventory("LeonardBallgag",1,2)
			TNT1 A 0 A_PlaySoundEx("Leonard/Bored", "SoundSlot7", 0, 0)
			TNT1 A 0 A_TakeInventory("LeonardWaiting", 15)
			TNT1 A 0 A_GiveInventory("LeonardTauntCooldown",5)
			TNT1 A 0 ACS_NamedExecuteAlways("LeonardTauntCooldown", 0)
			Goto Ready
		IdleBallgagStuff:
			TNT1 A 0 A_TakeInventory("LeonardWaiting", 15)
			goto Ready
		SetDrunkView:
			TNT1 A 0 A_ZoomFactor(0.55)
			Goto Ready+1
		SetDrunkViewFire:
			TNT1 A 0 A_ZoomFactor(0.55)
			Goto Fire+1
		SetSoberView:
			TNT1 A 0 A_ZoomFactor(1.0)
			Goto Ready+1
		SetSoberViewFire:
			TNT1 A 0 A_ZoomFactor(1.0)
			Goto Fire+1
		AltFire:
			TNT1 A 0 A_JumpIfInventory(" RR Hunting Crossbow ",1,"SelectNormal")
			TNT1 A 0 A_JumpIfInventory(" RR Dynomite Crossbow ",1,"SelectDynomite")
			Goto Ready
		SelectNormal:
			TNT1 A 0 A_JumpIfInventory("Shell",1,1)
			Goto AltFire+1
			TNT1 A 0 A_SelectWeapon(" RR Hunting Crossbow ")
			Goto Deselect
		SelectDynomite:
			TNT1 A 0 A_JumpIfInventory("RocketAmmo",1,1)
			Goto Ready
			TNT1 A 0 A_SelectWeapon(" RR Dynomite Crossbow ")
			Goto Deselect
	}
}

actor ChickenAmmo : Ammo
{
    Inventory.MaxAmount 30
    Ammo.BackpackMaxAmount 60
    Inventory.Icon "AMSMS0"
    Tag "Chicken Crossbow Ammo"
    +IGNORESKILL
}

Actor RRCHICKENBOMB
{
	Height 4
	Radius 8
	Decal Sorch
	Speed 50
	Scale 0.35
	Damage (140)
	Projectile
	+NOBLOOD
	+BLOODLESSIMPACT
	+THRUGHOST
	+SEEKERMISSILE
	+SCREENSEEKER
	DamageType "LeonardDamage2"
	States
	{
	  Spawn:
		TNT1 A 0
	  SpawnLoop:
		TNT1 A 0 A_SpawnItemEx("RRCOCKFEATHER",0,0,0,random(-60,60)/10.00,random(-60,60)/10.00,random(5,20)/10.00,SXF_CLIENTSIDE)
		CH_R M 1 Bright A_SeekerMissile(90,90, SMF_LOOK|SMF_PRECISE)
		CH_R M 0 Bright A_SpawnItemEx("RRLEONARDSMOKET",0,0,0,0,0,4,0,0,SXF_CLIENTSIDE)
		CH_R M 1 Bright A_SeekerMissile(90,90, SMF_LOOK|SMF_PRECISE)
		CH_R M 0 Bright A_SpawnItemEx("RRLEONARDSMOKET",0,0,0,0,0,4,0,0,SXF_CLIENTSIDE)
		CH_R M 1 Bright A_SeekerMissile(90,90, SMF_LOOK|SMF_PRECISE)
		CH_R M 0 Bright A_SpawnItemEx("RRLEONARDSMOKET",0,0,0,0,0,4,0,0,SXF_CLIENTSIDE)
		Loop
	  Death:
		CH_R M 10 Bright A_PlaySound("RR/COCKEXP")
		TNT1 A 0 A_Explode(140,178)
		TNT1 A 5 A_SpawnItemEx("RRFAKEEXPLOSION",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_CLIENTSIDE)
		Stop
	 XDeath:
	  Crash:
		TNT1 A 0 A_Explode(140,178)
		TNT1 A 0 A_SpawnItemEx("RRFAKEEXPLOSION",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_CLIENTSIDE)
		TNT1 A 5 A_PlaySound("RR/COCKBOOM")
		Stop
	}
}

Actor RRCHICKENBOMB_2 : RRCHICKENBOMB
{
	Species "Player"
	+THRUSPECIES
	+DONTHARMCLASS
	+DONTHARMSPECIES
	+MTHRUSPECIES
	Damage (140)
	States
	{
	  Death:
		CH_R M 10 Bright A_PlaySound("RR/COCKEXP")
		TNT1 A 0 A_Explode(140,142)
		TNT1 A 5 A_SpawnItemEx("RRFAKEEXPLOSION",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_CLIENTSIDE)
		Stop
	 XDeath:
	  Crash:
		TNT1 A 0 A_Explode(140,142)
		TNT1 A 0 A_SpawnItemEx("RRFAKEEXPLOSION",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_CLIENTSIDE)
		TNT1 A 5 A_PlaySound("RR/COCKBOOM")
		Stop
	}
}	

Actor RRCHICKENBOMB_3 : RRCHICKENBOMB
{
	States
	{
	  Spawn:
		TNT1 A 0
	  SpawnLoop:
		TNT1 A 0 A_SpawnItemEx("RRCOCKFEATHER",0,0,0,random(-60,60)/10.00,random(-60,60)/10.00,random(5,20)/10.00,SXF_CLIENTSIDE)
		CH_R M 1 Bright A_SeekerMissile(10,30, SMF_LOOK|SMF_PRECISE)
		CH_R MM 1 Bright A_SpawnItemEx("RRLEONARDSMOKET",0,0,0,0,0,4,0,0,SXF_CLIENTSIDE)
		CH_R M 0 Bright A_SpawnItemEx("RRLEONARDSMOKET",0,0,0,0,0,4,0,0,SXF_CLIENTSIDE)
		Loop
	  Death:
		CH_R M 10 Bright A_PlaySound("RR/COCKEXP")
		TNT1 A 0 A_Explode(140,142)
		TNT1 A 5 A_SpawnItemEx("RRFAKEEXPLOSION",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_CLIENTSIDE)
		Stop
	 XDeath:
	  Crash:
		TNT1 A 0 A_Explode(140,142)
		TNT1 A 0 A_SpawnItemEx("RRFAKEEXPLOSION",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_CLIENTSIDE)
		TNT1 A 5 A_PlaySound("RR/COCKBOOM")
		Stop
	}
}

Actor RedneckMotorcycle : Weapon
{
	Inventory.PickupMessage "Motogun"
	Inventory.PickupSound "RR/Wpickup"
	Weapon.SelectionOrder 3100
	Weapon.SlotNumber 4
	Weapon.SlotPriority 4
	Weapon.AmmoType "MotoGunAmmo"
	Weapon.AmmoUse 1
	Weapon.BobSpeed 0.0
	Obituary "%o got shredded by %k's motorcycle machine guns."
	Inventory.RestrictedTo "Leonard"
	Tag "Motorcycle"
	+WEAPON.DONTBOB
	+WEAPON.AMMO_OPTIONAL
	+WEAPON.CHEATNOTWEAPON
	States
	{
		Spawn:
			R_MC Z -1
			Stop
		Select:
			TNT1 A 0 A_Raise
			R_MC A 1 A_Raise
			Loop
		Deselect:
			TNT1 A 0 A_Lower
			R_MC A 1 A_Lower
			Loop
		Ready:
			R_MC A 0
			Goto ReadyLoop
		ReadyLoop:
			TNT1 A 0 A_JumpIfInventory("RRDrunkToken", 1, "SetDrunkView")
			Goto SetSoberView
			TNT1 A 0 A_JumpIfInventory("BikeAngle",5,"BikeReady5")
			TNT1 A 0 A_JumpIfInventory("BikeAngle",4,"BikeReady4")
			TNT1 A 0 A_JumpIfInventory("BikeAngle",3,"BikeReady3")
			TNT1 A 0 A_JumpIfInventory("BikeAngle",2,"BikeReady2")
			TNT1 A 0 A_JumpIfInventory("BikeAngle",1,"BikeReady1")
			Goto BikeReady1
		BikeReady1:
			R_MC K 1 A_WeaponReady
			Goto ReadyLoop
		BikeReady2:
			R_MC F 1 A_WeaponReady
			Goto ReadyLoop
		BikeReady3:
			R_MC A 1 A_WeaponReady
			Goto ReadyLoop
		BikeReady4:
			R_MC P 1 A_WeaponReady
			Goto ReadyLoop
		BikeReady5:
			R_MC U 1 A_WeaponReady
			Goto ReadyLoop
		Fire:
			TNT1 A 0 A_JumpIfInventory("RRDrunkToken", 1, "SetDrunkViewFire")
			Goto SetSoberViewFire
			TNT1 A 0 A_JumpIfInventory("MotoGunAmmo", 1, 1)
			Goto ReadyLoop
			TNT1 A 0 A_JumpIfInventory("BikeAngle",5,"BikeFire5")
			TNT1 A 0 A_JumpIfInventory("BikeAngle",4,"BikeFire4")
			TNT1 A 0 A_JumpIfInventory("BikeAngle",3,"BikeFire3")
			TNT1 A 0 A_JumpIfInventory("BikeAngle",2,"BikeFire2")
			TNT1 A 0 A_JumpIfInventory("BikeAngle",1,"BikeFire1")
			Goto BikeFire1
		BikeFire5:
			TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"BikeFire5_Coop")
			R_MC V 1 A_PlaySound("RR/RanchRF",CHAN_WEAPON)
			R_MC W 1 Bright A_FireCustomMissile("RRBullet4_DM",frandom(-2.00,2.00),1,4,0,0,frandom(-2.00,2.00))
			R_MC X 1 A_PlaySound("RR/RanchRF",CHAN_WEAPON)
			R_MC Y 1 Bright A_FireCustomMissile("RRBullet4_DM",frandom(-2.00,2.00),1,-4,0,0,frandom(-2.00,2.00))
			TNT1 A 0 A_ReFire
			goto ReadyLoop
		BikeFire5_Coop:
			R_MC V 1 A_PlaySound("RR/RanchRF",CHAN_WEAPON)
			R_MC W 1 Bright A_FireCustomMissile("RRBullet4",frandom(-2.00,2.00),1,4,0,0,frandom(-2.00,2.00))
			R_MC X 1 A_PlaySound("RR/RanchRF",CHAN_WEAPON)
			R_MC Y 1 Bright A_FireCustomMissile("RRBullet4",frandom(-2.00,2.00),1,-4,0,0,frandom(-2.00,2.00))
			TNT1 A 0 A_ReFire
			goto ReadyLoop
		BikeFire4:
			TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"BikeFire4_Coop")
			R_MC Q 1 A_PlaySound("RR/RanchRF",CHAN_WEAPON)
			R_MC R 1 Bright A_FireCustomMissile("RRBullet4_DM",frandom(-2.00,2.00),1,4,0,0,frandom(-2.00,2.00))
			R_MC S 1 A_PlaySound("RR/RanchRF",CHAN_WEAPON)
			R_MC T 1 Bright A_FireCustomMissile("RRBullet4_DM",frandom(-2.00,2.00),1,-4,0,0,frandom(-2.00,2.00))
			TNT1 A 0 A_ReFire
			goto ReadyLoop
		BikeFire4_Coop:
			R_MC Q 1 A_PlaySound("RR/RanchRF",CHAN_WEAPON)
			R_MC R 1 Bright A_FireCustomMissile("RRBullet4",frandom(-2.00,2.00),1,4,0,0,frandom(-2.00,2.00))
			R_MC S 1 A_PlaySound("RR/RanchRF",CHAN_WEAPON)
			R_MC T 1 Bright A_FireCustomMissile("RRBullet4",frandom(-2.00,2.00),1,-4,0,0,frandom(-2.00,2.00))
			TNT1 A 0 A_ReFire
			goto ReadyLoop
		BikeFire3:
			TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"BikeFire3_Coop")
			R_MC B 1 A_PlaySound("RR/RanchRF",CHAN_WEAPON)
			R_MC C 1 Bright A_FireCustomMissile("RRBullet4_DM",frandom(-2.00,2.00),1,4,0,0,frandom(-2.00,2.00))
			R_MC D 1 A_PlaySound("RR/RanchRF",CHAN_WEAPON)
			R_MC E 1 Bright A_FireCustomMissile("RRBullet4_DM",frandom(-2.00,2.00),1,-4,0,0,frandom(-2.00,2.00))
			TNT1 A 0 A_ReFire
			goto ReadyLoop
		BikeFire3_Coop:
			R_MC B 1 A_PlaySound("RR/RanchRF",CHAN_WEAPON)
			R_MC C 1 Bright A_FireCustomMissile("RRBullet4",frandom(-2.00,2.00),1,4,0,0,frandom(-2.00,2.00))
			R_MC D 1 A_PlaySound("RR/RanchRF",CHAN_WEAPON)
			R_MC E 1 Bright A_FireCustomMissile("RRBullet4",frandom(-2.00,2.00),1,-4,0,0,frandom(-2.00,2.00))
			TNT1 A 0 A_ReFire
			goto ReadyLoop
		BikeFire2:
			TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"BikeFire2_Coop")
			R_MC G 1 A_PlaySound("RR/RanchRF",CHAN_WEAPON)
			R_MC H 1 Bright A_FireCustomMissile("RRBullet4_DM",frandom(-2.00,2.00),1,4,0,0,frandom(-2.00,2.00))
			R_MC I 1 A_PlaySound("RR/RanchRF",CHAN_WEAPON)
			R_MC J 1 Bright A_FireCustomMissile("RRBullet4_DM",frandom(-2.00,2.00),1,-4,0,0,frandom(-2.00,2.00))
			TNT1 A 0 A_ReFire
			goto ReadyLoop
		BikeFire2_Coop:
			R_MC G 1 A_PlaySound("RR/RanchRF",CHAN_WEAPON)
			R_MC H 1 Bright A_FireCustomMissile("RRBullet4",frandom(-2.00,2.00),1,4,0,0,frandom(-2.00,2.00))
			R_MC I 1 A_PlaySound("RR/RanchRF",CHAN_WEAPON)
			R_MC J 1 Bright A_FireCustomMissile("RRBullet4",frandom(-2.00,2.00),1,-4,0,0,frandom(-2.00,2.00))
			TNT1 A 0 A_ReFire
			goto ReadyLoop
		BikeFire1:
			TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"BikeFire1_Coop")
			R_MC L 1 A_PlaySound("RR/RanchRF",CHAN_WEAPON)
			R_MC M 1 Bright A_FireCustomMissile("RRBullet4_DM",frandom(-2.00,2.00),1,4,0,0,frandom(-2.00,2.00))
			R_MC N 1 A_PlaySound("RR/RanchRF",CHAN_WEAPON)
			R_MC O 1 Bright A_FireCustomMissile("RRBullet4_DM",frandom(-2.00,2.00),1,-4,0,0,frandom(-2.00,2.00))
			TNT1 A 0 A_ReFire
			goto ReadyLoop
		BikeFire1_Coop:
			R_MC L 1 A_PlaySound("RR/RanchRF",CHAN_WEAPON)
			R_MC M 1 Bright A_FireCustomMissile("RRBullet4",frandom(-2.00,2.00),1,4,0,0,frandom(-2.00,2.00))
			R_MC N 1 A_PlaySound("RR/RanchRF",CHAN_WEAPON)
			R_MC O 1 Bright A_FireCustomMissile("RRBullet4",frandom(-2.00,2.00),1,-4,0,0,frandom(-2.00,2.00))
			TNT1 A 0 A_ReFire
			goto ReadyLoop
		IntroTaunt:
			TNT1 A 0 A_JumpIfInventory("LeonardBallgag",1,2)
			TNT1 A 0 A_PlaySoundEx("Leonard/Enter", "SoundSlot7", 0, 0)
			TNT1 A 0 A_TakeInventory("LeonardReady", 1)
			TNT1 A 0 A_GiveInventory("LeonardTauntCooldown",5)
			TNT1 A 0 ACS_NamedExecuteAlways("LeonardTauntCooldown", 0)
			Goto Ready	
		SetDrunkView:
			TNT1 A 0 A_ZoomFactor(0.55)
			Goto ReadyLoop+1
		SetDrunkViewFire:
			TNT1 A 0 A_ZoomFactor(0.55)
			Goto Fire+1
		SetSoberView:
			TNT1 A 0 A_ZoomFactor(1.0)
			Goto ReadyLoop+1
		SetSoberViewFire:
			TNT1 A 0 A_ZoomFactor(1.0)
			Goto Fire+1
	}
}

actor MotoGunAmmo : Ammo
{
    Inventory.MaxAmount 300
    Ammo.BackpackAmount 50
    Ammo.BackpackMaxAmount 600
    Inventory.Icon "AMSMN0"
    Tag "Motorcycle Gun Ammo"
    +IGNORESKILL
}

Actor RRBullet4_DM : RRBullet4
{
  Damage (30)
  Species ""  
  -THRUSPECIES
  -DONTHARMCLASS
  DamageType "LeonardDamage3"
  -DONTHARMSPECIES
  -MTHRUSPECIES
}

Actor BikeAngle : Counter { Inventory.MaxAmount 5 }
actor RRBikePuff : BulletPuff
{  
  renderstyle Normal
  DamageType "RRBullet"
  Decal RedneckRampageBulletChip
  +NOBLOCKMAP
  +NOGRAVITY
  +RANDOMIZE
  +THRUSPECIES
  states
  {
  Spawn:
	TNT1 A 0
  Melee:
	TNT1 A 1
	stop
  Crash:
	TNT1 A 1 A_PlaySound("RRBike/Crash", CHAN_6)
	stop
  }
}

actor RRBikePuffCoop : RRBikePuff { +MTHRUSPECIES }

Actor LeonardMotorcycle : SwitchableDecoration
{
	Height 40
	Radius 20
	Activation THINGSPEC_Activate|THINGSPEC_ThingTargets
    Tag "Motorcycle"
	-SOLID //+SOLID
	+USESPECIAL
	-SHOOTABLE //+SHOOTABLE
	+NOBLOOD
    +NOTIMEFREEZE
	Health 100
	Scale 0.3
	Mass 400
	States
	{
	Spawn:
		R_MC Z 10
		Loop
	Active:
		TNT1 A 0 A_JumpIfInventory("RRHasBike", 1, "UseFail", AAPTR_Target)
		TNT1 A 0 A_JumpIfInventory("RRClass", 1, 1, AAPTR_Target)
		Goto UseFail
		TNT1 A 0 A_GiveInventory("RRHasBike", 1, AAPTR_Target)
		TNT1 A 0 ACS_NamedExecuteWithResult("RRMotorcycleHealthCounter", 0, 0, 0, 0)
		R_MC Z 4 A_GiveInventory("RRMotorCycle", 1, AAPTR_Target)
		stop
	UseFail:
		R_MC Z 4
		Goto Spawn
	Death:
		TNT1 A 0 A_JumpIfHealthLower(-50, "XDeath")
		TNT1 A 0 A_NoBlocking
		TNT1 A 0 A_PlaySound("Bike/Death",5,1.0,0,ATTN_NORM)
		JKLP A 0 A_CustomMissile("BikeDebris1_RR",0+Random(30,60),0+Random(35,-35),0+Random(0,370))
		JKLP A 0 A_CustomMissile("BikeDebris2_RR",0+Random(30,60),0+Random(35,-35),0+Random(0,370))
		JKLP A 0 A_CustomMissile("BikeDebris3_RR",0+Random(30,60),0+Random(35,-35),0+Random(0,370))
		TNT1 A 0 A_SpawnItemEx("DeadBike", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
		stop
	XDeath:
		TNT1 A 0 A_NoBlocking
		TNT1 A 0 A_PlaySound("Bike/Death",5,1.0,0,ATTN_NORM)
		TNT1 A 0 A_PlaySound("Hulk/Explode",6,1.0,0,ATTN_NORM)
		TNT1 A 0 A_SpawnItemEx("RRFAKEEXPLOSION")
		TNT1 A 0 A_Explode(24-Random(10,0),128)
		JKLP A 0 A_CustomMissile("BikeDebris1_RR",0+Random(30,60),0+Random(35,-35),0+Random(0,370))
		JKLP A 0 A_CustomMissile("BikeDebris2_RR",0+Random(30,60),0+Random(35,-35),0+Random(0,370))
		JKLP A 0 A_CustomMissile("BikeDebris3_RR",0+Random(30,60),0+Random(35,-35),0+Random(0,370))
		stop
	}
}
ACTOR RRMotorcyclePlayer : Leonard
{
	Player.MorphWeapon "RedneckMotorcycle"
	Player.DisplayName "Leonard"
	YScale 0.30
	XScale 0.361
	Player.SideMove 0.0
	Player.JumpZ 0
	Mass 450
	States
	{
	Spawn:
		LE_M A 1
		Loop
	See:
		LE_M A 1
		Loop
	Missile:
		LE_M A 5 BRIGHT
		Goto Spawn
	Pain:
		LE_M A 1 A_Pain
		Goto Spawn
	Death:
		TNT1 A 0 A_JumpIfHealthLower(-50, "XDeath")
		TNT1 A 0 A_SpawnItemEx("UniversalBloodyHellGoreDeathEffect",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_USEBLOODCOLOR)
		TNT1 A 0 A_PlaySound("Bike/Death",5,1.0,0,ATTN_NORM)
		JKLP A 0 A_CustomMissile("BikeDebris1_RR",0+Random(30,60),0+Random(35,-35),0+Random(0,370))
		JKLP A 0 A_CustomMissile("BikeDebris2_RR",0+Random(30,60),0+Random(35,-35),0+Random(0,370))
		JKLP A 0 A_CustomMissile("BikeDebris3_RR",0+Random(30,60),0+Random(35,-35),0+Random(0,370))
		TNT1 A 0 A_SpawnItemEx("DeadBike", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
		LE_3 A 8 A_NoBlocking
		LE_3 B 8 A_PlayerScream
		LE_3 C 8 
		LE_3 D 8
		LE_3 EFGH 8
		TNT1 A 0 ACS_NamedExecuteAlways("SamsaraDecorate", 0, 37)
		LE_3 I -1
		Stop
	XDeath:
		TNT1 A 0 A_SpawnItemEx("UniversalBloodyHellGoreXDeathEffect",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_USEBLOODCOLOR|1048576)
		TNT1 A 0 A_PlayerScream
		TNT1 A 0 A_NoBlocking
		TNT1 A 0 A_PlaySound("Bike/Death",5,1.0,0,ATTN_NORM)
		TNT1 A 0 A_PlaySound("Hulk/Explode",6,1.0,0,ATTN_NORM)
		TNT1 A 0 A_SpawnItemEx("RRFAKEEXPLOSION")
		TNT1 A 0 A_Explode(24-Random(10,0),128)
		TNT1 A 0 A_SpawnItemEx("LeonardBlood_RR")
		RR20 A 0 A_CustomMissile("LeonardHead_RR",0+Random(30,60),0+Random(35,-35),0+Random(0,370))
		RR20 AA 0 A_CustomMissile("LeonardArm_RR",0+Random(30,60),0+Random(35,-35),0+Random(0,370))
		RR20 AA 0 A_CustomMissile("LeonardLeg_RR",0+Random(30,60),0+Random(35,-35),0+Random(0,370))  
		RR20 A 0 A_CustomMissile("MiscGib1_RR",0+Random(30,60),0+Random(35,-35),0+Random(0,370))
		RR20 A 0 A_CustomMissile("MiscGib2_RR",0+Random(30,60),0+Random(35,-35),0+Random(0,370))
		RR20 A 0 A_CustomMissile("MiscGib3_RR",0+Random(30,60),0+Random(35,-35),0+Random(0,370))
		RR20 A 0 A_CustomMissile("MiscGib4_RR",0+Random(30,60),0+Random(35,-35),0+Random(0,370))
		RR20 A 0 A_CustomMissile("MiscGib5_RR",0+Random(30,60),0+Random(35,-35),0+Random(0,370))
		RR20 A 0 A_CustomMissile("MiscGib6_RR",0+Random(30,60),0+Random(35,-35),0+Random(0,370))	
		RR20 AAAAA 0 A_CustomMissile("MiscGib7_RR",0+Random(30,60),0+Random(35,-35),0+Random(0,370))	
		TNT1 A 0 A_PlaySound("Leonard/XDeath",CHAN_BODY,1.0,0,ATTN_NORM)
		JKLP A 0 A_CustomMissile("BikeDebris1_RR",0+Random(30,60),0+Random(35,-35),0+Random(0,370))
		JKLP A 0 A_CustomMissile("BikeDebris2_RR",0+Random(30,60),0+Random(35,-35),0+Random(0,370))
		JKLP A 0 A_CustomMissile("BikeDebris3_RR",0+Random(30,60),0+Random(35,-35),0+Random(0,370))
		TNT1 A 0 ACS_NamedExecuteAlways("SamsaraDecorate", 0, 37)
		TNT1 A -1 
		Stop
	}
}

ACTOR RRMotorCycleMorph : PowerMorph
{
	PowerMorph.PlayerClass "RRMotorcyclePlayer"
	PowerMorph.MorphFlash "RRBikeMorph"
	PowerMorph.UnMorphFlash "RRBikeUnMorph"
	PowerMorph.MorphStyle MRF_WHENINVULNERABLE|MRF_LOSEACTUALWEAPON|MRF_FULLHEALTH
}

Actor RRBikeMorph
{
	States
	{
	Spawn:
		TNT1 A 1
		Stop
	}
}

Actor RRBikeUnMorph
{
	States
	{
	Spawn:
		TNT1 A 1
		TNT1 A 0 A_TakeInventory("RedneckMotorcycle")
		Stop
	}
}

ACTOR RRMotorCycle : PowerupGiver
{
   	Inventory.MaxAmount 0
	Powerup.Duration -0x7FFFFFFD
	Powerup.Type RRMotorCycleMorph
	+INVENTORY.AUTOACTIVATE
	+INVENTORY.ALWAYSPICKUP
	States
	{
		Spawn:
			TNT1 A 2
			Loop
	}
}
Actor RRHasBike : Inventory { -INVENTORY.INVBAR Inventory.Amount 1 Inventory.MaxAmount 1 }
Actor RRBikeMountCounter : Inventory { -INVENTORY.INVBAR Inventory.Amount 1 Inventory.MaxAmount 8 } //Spam causes issues
Actor BikeDebris1_RR
{
	scale 0.30
	radius 6
	height 5
	speed 10
	+MISSILE
	-SOLID
	+CLIENTSIDEONLY	
	states
	{
	Spawn:
		TNT1 A 0
		G_SC A 0 A_Gravity
	SpawnLoop:
		DL_K ABCDE 4
		loop
	Crash:
	Death:
		TNT1 A 5
		stop
	}
}

Actor BikeDebris2_RR : BikeDebris1_RR
{
	states
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_Gravity
	SpawnLoop:
		DL_K FGHIJ 4
		loop
	}
}
Actor BikeDebris3_RR : BikeDebris1_RR
{
	states
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_Gravity
	SpawnLoop:
		DL_K KLMNO 4
		loop
	}
}
Actor DeadBike
{
	+NOBLOCKMAP
	-SOLID
	Scale 0.375
	States
	{
		Spawn:
			TNT1 A 0
			DL_K P -1
			stop
	}
}