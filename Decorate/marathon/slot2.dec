actor CanDualShotties: Boolean {}
actor LevelLimiter: Boolean {}
actor InvasionDualShottyCheck: Boolean {}
actor UsingShotguns: Boolean {}
actor UsingDualShotguns: Boolean {}

actor "WSTE-M5 Combat Shotgun": Weapon
{
    Weapon.BobRangeX 0
    Weapon.BobStyle Smooth
    Weapon.SelectionOrder 400
    Weapon.SlotNumber 2
    Decal BulletChip
    Inventory.PickupMessage "You picked up a WSTE-M5 Combat Shotgun!"
    Weapon.AmmoType1 "WastemShell"
    Weapon.AmmoGive1 0
    Weapon.AmmoUse1 2
    Weapon.AmmoType2 "Shell"
    Weapon.AmmoGive2 8
    Weapon.AmmoUse2 0
    +NOALERT
    +INVENTORY.UNDROPPABLE
    +AMMO_OPTIONAL
	Inventory.RestrictedTo "MarathonPlayer"
    Tag "WSTE-M5 Combat Shotgun"
    States
    {
      Spawn:
        SHTY A -1
        stop

      // Single
      Ready:
        TNT1 A 0 A_TakeInventory("SamsaraLevelSwitch_DualRaising")
        TNT1 A 0 A_JumpIfInventory("SynthFireActive",1,"ReadyDual")
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",0,"ReadyModernReloadable")
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"ReadyNonReloadableAmmo")
      ReadyNonReloadable:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"ReadyNonReloadableAmmo")
        TNT1 A 0 A_JumpIfInventory("Shell",2,"ReadyNonReloadableAmmo")
      ReadyNonReloadableNoAmmo:
        MSHT A 1 A_WeaponReady
      ReadyAmmoCheckNoAmmo:
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"ReadyAmmoCheckReloadable")
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"Reload")
        TNT1 A 0 A_JumpIfInventory("Shell",2,"Reload")
        goto Ready

      ReadyNonReloadableAmmo:
        MSHT A 1 A_WeaponReady
      ReadyAmmoCheckAmmo:
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"ReadyAmmoCheckReloadable")
        goto Ready

      ReadyModernReloadable:
        TNT1 A 0 A_JumpIfInventory("WastemShell",0,"ReadyNonReloadableAmmo")
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("Shell",2,1)
        goto ReadyNonReloadableAmmo
        MSHT A 1 A_WeaponReady(WRF_ALLOWRELOAD)
        goto ReadyAmmoCheckAmmo

      ReadyAmmoCheckReloadable:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"Ready")
        TNT1 A 0 A_JumpIfInventory("WastemShell",0,"Ready")
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"Reload")
        TNT1 A 0 A_JumpIfInventory("Shell",2,"Reload")
        goto Ready

      Deselect:
        TNT1 A 0 A_TakeInventory("SamsaraLevelSwitch_DualWielding")
        TNT1 A 0 A_JumpIfInventory("SynthFireActive",1,"DeselectDual")
        TNT1 A 0 A_Lower
        MSHT A 1 A_Lower
        loop

      Select:
        TNT1 A 0 A_TakeInventory("UsingFists")
        TNT1 A 0 A_TakeInventory("UsingPistols")
        TNT1 A 0 A_TakeInventory("UsingDualPistols")
        TNT1 A 0 A_TakeInventory("UsingSMG")
        TNT1 A 0 A_TakeInventory("UsingPacifier")
        //TNT1 A 0 A_TakeInventory("UsingShotguns")
        //TNT1 A 0 A_TakeInventory("UsingDualShotguns")
        TNT1 A 0 A_TakeInventory("UsingFusionPistol")
        TNT1 A 0 A_TakeInventory("UsingAssaultRifle")
        TNT1 A 0 A_TakeInventory("UsingSpanker")
        TNT1 A 0 A_TakeInventory("UsingToasty")
        TNT1 A 0 A_TakeInventory("UsingWMC")
        TNT1 A 0 A_TakeInventory("UsingAlienWeapon")
        TNT1 A 0 A_TakeInventory("UsingAlienWeapon2")
        //TNT1 A 0 A_TakeInventory("UsingMarathonInstagib")

        TNT1 A 0 A_GiveInventory("UsingShotguns")
        TNT1 A 0 A_JumpIfInventory("SamsaraLevelSwitch_DualWielding",1,"SelectDual")
        TNT1 A 0 A_JumpIfInventory("SynthFireActive",1,"SelectDual")
        TNT1 A 0 A_TakeInventory("UsingDualShotguns")
        TNT1 A 0 A_Raise
        MSHT A 1 A_Raise
        loop

      Fire:
        TNT1 A 0 A_JumpIfInventory("SynthFireActive",1,"LFireSwapCheck")
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"FireReloadable")
      FireNonReloadable:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireActual")
        TNT1 A 0 A_JumpIfInventory("Shell",2,"FireActual")
        goto Ready

      FireReloadable:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"FireActual")
        TNT1 A 0 A_JumpIfInventory("WastemShell",0,"FireActual")
      FireReloadableDryCheck:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"Reload")
        TNT1 A 0 A_JumpIfInventory("Shell",2,"Reload")
        goto Ready

      FireActual:
        TNT1 A 0 A_JumpIfInventory("SamsaraCurrentWaterLevel",0,"Ready")
        MSHT A 2 A_AlertMonsters
        TNT1 A 0 A_GiveInventory("SamsaraMarathonShotgunLeftAttackHandler")
      FireFinish:
        MSHT A 1
        MSHT A 1 A_Light2
        MSHT B 4 Bright A_Light1 
        MSHT C 5 Bright A_Light0
        MSHT D 5 Bright
        goto ReadyAmmoCheckNoAmmo

      Reload:
        MSHT F 4
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"ReloadAmmo")
      ReloadFinish:
        MSHT G 4 A_PlaySound("marathon/shotgun/reload")
        MSHT H 4
        MSHT IJ 5
        MSHT A 3
        goto Ready

      ReloadAmmo:
        TNT1 A 0 A_JumpIfInventory("WastemShell",0,"ReloadFinish")
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("Shell",2,1)
        goto ReloadFinish
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_TakeInventory("Shell",2)
        TNT1 A 0 A_GiveInventory("WastemShell",2)
        loop

      // Dual Init
      AltFire:
        TNT1 A 0 A_JumpIfInventory("SynthFireActive",1,"RFireSwapCheck")
        TNT1 A 0 A_JumpIfInventory("CanDualShotties",1,1)
        goto Ready
        TNT1 A 0 A_GiveInventory("UsingDualShotguns")
        MIWS B 1 A_GiveInventory("SamsaraLevelSwitch_DualWielding")
        MIWS DFHJLNOPQ 1
        TNT1 A 0 A_GiveInventory("SynthFireActive")
        MIWS R 1 A_GunFlash("Flash",GFF_NOEXTCHANGE)
        goto ReadyDual

      // Dual
      ReadyDual:
        TNT1 A 0 A_GiveInventory("SynthFireActive")
        RSHT A 1 A_GunFlash("Flash",GFF_NOEXTCHANGE)
      // Left Shotgun
      LReady:
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"LReadyNonReloadableAmmo")
      LReadyNonReloadable:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"LReadyNonReloadableAmmo")
        TNT1 A 0 A_JumpIfInventory("Shell",2,"LReadyNonReloadableAmmo")
      LReadyNonReloadableNoAmmo:
        RSHT A 1 A_WeaponReady(WRF_NOFIRE)
      LReadyAmmoCheckNoAmmo:
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"LReadyAmmoCheckReloadable")
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"LReload")
        TNT1 A 0 A_JumpIfInventory("Shell",2,"LReload")
      LReadyFireCheck:
        TNT1 A 0 A_JumpIfInventory("SamsaraReverseDualWieldFiring",1,"LReadyFireCheckSwap")
        TNT1 A 0 A_JumpIfInventory("SynthFireRight",1,"LFire")
        goto LReady

      LReadyNonReloadableAmmo:
        RSHT A 1 A_WeaponReady(WRF_NOFIRE)
      LReadyAmmoCheckAmmo:
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"LReadyAmmoCheckReloadable")
        goto LReadyFireCheck

      LReadyAmmoCheckReloadable:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"LReadyFireCheck")
        TNT1 A 0 A_JumpIfInventory("WastemShell",0,"LReadyFireCheck")
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"LReload")
        TNT1 A 0 A_JumpIfInventory("Shell",2,"LReload")
        goto LReadyFireCheck

      LReadyFireCheckSwap:
        TNT1 A 0 A_JumpIfInventory("SynthFireLeft",1,"LFire")
        goto LReady

      DeselectDual:
        TNT1 A 0 A_TakeInventory("SamsaraLevelSwitch_DualWielding")
        TNT1 A 0 A_TakeInventory("SynthFireActive")
      DeselectDual2:
        TNT1 A 0 A_Lower
        RSHT A 1 A_Lower
        loop

      SelectDual:
        TNT1 A 0 A_GiveInventory("UsingDualShotguns")
        TNT1 A 0 A_GiveInventory("SamsaraLevelSwitch_DualRaising")
        TNT1 A 0 A_GiveInventory("SynthFireActive")
        TNT1 A 0 A_GunFlash("Flash",GFF_NOEXTCHANGE)
        TNT1 A 0 A_Raise
        RSHT A 1 A_Raise
        goto Select

      LFireSwapCheck:
        TNT1 A 0 A_JumpIfInventory("SamsaraReverseDualWieldFiring",1,"RFire")
      LFire:
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",0,"LFireModernReloadable")
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"LFireClassicReloadable")
      LFireNonReloadable:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"LFireActual")
        TNT1 A 0 A_JumpIfInventory("Shell",2,"LFireActual")
        goto LReady

      LFireClassicReloadable:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"LFireActual")
        TNT1 A 0 A_JumpIfInventory("WastemShell",0,"LFireActual")
      LFireClassicReloadableDryCheck:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"LReload")
        TNT1 A 0 A_JumpIfInventory("Shell",2,"LReload")
        goto LReady

      LFireModernReloadable:
        TNT1 A 0 A_JumpIfInventory("SynthFireReload",1,1)
        goto LFireClassicReloadable
        TNT1 A 0 A_JumpIfInventory("WastemShell",0,"LFireClassicReloadable")
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"LReload")
        TNT1 A 0 A_JumpIfInventory("Shell",2,"LReload")
        goto LFireClassicReloadable

      LFireActual:
        TNT1 A 0 A_JumpIfInventory("SamsaraCurrentWaterLevel",0,"LReady")
        TNT1 A 0 ACS_NamedExecuteWithResult("SamsaraForcePlayerAttackState")
        RSHT A 2 A_AlertMonsters
        TNT1 A 0 A_GiveInventory("SamsaraMarathonShotgunLeftAttackHandler")
      LFireFinish:
        RSHT A 1
        RSHT A 1 A_Light2
        RSHT B 4 Bright A_Light1 
        RSHT C 5 Bright A_Light0
        RSHT D 5 Bright
        goto LReadyAmmoCheckNoAmmo

      LReload:
        RSHT F 4
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"LReloadAmmo")
      LReloadFinish:
        RSHT G 4 A_PlaySound("marathon/shotgun/reload")
        RSHT H 4
        RSHT IJ 5
        RSHT A 3
        goto LReady

      LReloadAmmo:
        TNT1 A 0 A_JumpIfInventory("WastemShell",0,"LReloadFinish")
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("Shell",2,1)
        goto LReloadFinish
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_TakeInventory("Shell",2)
        TNT1 A 0 A_GiveInventory("WastemShell",2)
        loop

      // Right Shotgun
      Flash:
        MIW2 A 1
      RReady:
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"RReadyNonReloadableAmmo")
      RReadyNonReloadable:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"RReadyNonReloadableAmmo")
        TNT1 A 0 A_JumpIfInventory("Shell",2,"RReadyNonReloadableAmmo")
      RReadyNonReloadableNoAmmo:
        MIW2 A 1
        TNT1 A 0 A_JumpIfInventory("SamsaraLevelSwitch_DualRaising",1,"RReady")
      RReadyAmmoCheckNoAmmo:
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"RReadyAmmoCheckReloadable")
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"RReload")
        TNT1 A 0 A_JumpIfInventory("Shell",2,"RReload")
      RReadyFireCheck:
        TNT1 A 0 A_JumpIfInventory("SamsaraReverseDualWieldFiring",1,"RReadyFireCheckSwap")
        TNT1 A 0 A_JumpIfInventory("SynthFireLeft",1,"RFire")
        goto RReady

      RReadyNonReloadableAmmo:
        MIW2 A 1
        TNT1 A 0 A_JumpIfInventory("SamsaraLevelSwitch_DualRaising",1,"RReady")
      RReadyAmmoCheckAmmo:
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"RReadyAmmoCheckReloadable")
        goto RReadyFireCheck

      RReadyAmmoCheckReloadable:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"RReadyFireCheck")
        TNT1 A 0 A_JumpIfInventory("WastemShellRight",0,"RReadyFireCheck")
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"RReload")
        TNT1 A 0 A_JumpIfInventory("Shell",2,"RReload")
        goto RReadyFireCheck

      RReadyFireCheckSwap:
        TNT1 A 0 A_JumpIfInventory("SynthFireRight",1,"RFire")
        goto RReady

      RFireSwapCheck:
        TNT1 A 0 A_JumpIfInventory("SamsaraReverseDualWieldFiring",1,"LFire")
      RFire:
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",0,"RFireModernReloadable")
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"RFireClassicReloadable")
      RFireNonReloadable:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"RFireActual")
        TNT1 A 0 A_JumpIfInventory("Shell",2,"RFireActual")
        goto RReady

      RFireClassicReloadable:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"RFireActual")
        TNT1 A 0 A_JumpIfInventory("WastemShellRight",0,"RFireActual")
      RFireClassicReloadableDryCheck:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"RReload")
        TNT1 A 0 A_JumpIfInventory("Shell",2,"RReload")
        goto RReady

      RFireModernReloadable:
        TNT1 A 0 A_JumpIfInventory("SynthFireReload",1,1)
        goto RFireClassicReloadable
        TNT1 A 0 A_JumpIfInventory("WastemShellRight",0,"RFireClassicReloadable")
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"RReload")
        TNT1 A 0 A_JumpIfInventory("Shell",2,"RReload")
        goto RFireClassicReloadable

      RFireActual:
        TNT1 A 0 A_JumpIfInventory("SamsaraCurrentWaterLevel",0,"RReady")
        TNT1 A 0 ACS_NamedExecuteWithResult("SamsaraForcePlayerAttackState")
        MIW2 A 2 A_AlertMonsters
        TNT1 A 0 A_GiveInventory("SamsaraMarathonShotgunRightAttackHandler")
      RFireFinish:
        MIW2 A 1
        MIW2 A 1 A_Light2
        MIW2 B 4 Bright A_Light1 
        MIW2 C 5 Bright A_Light0
        MIW2 D 5 Bright
        goto RReadyAmmoCheckNoAmmo

      RReload:
        MIW2 F 4
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"RReloadAmmo")
      RReloadFinish:
        MIW2 G 4 A_PlaySound("marathon/shotgun/reload")
        MIW2 H 4
        MIW2 IJ 5
        MIW2 A 3
        goto RReady

      RReloadAmmo:
        TNT1 A 0 A_JumpIfInventory("WastemShellRight",0,"RReloadFinish")
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("Shell",2,1)
        goto RReloadFinish
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_TakeInventory("Shell",2)
        TNT1 A 0 A_GiveInventory("WastemShellRight",2)
        loop
    }
}

actor SamsaraMarathonShotgunLeftAttackHandler : Trigger
{
    States
    {
      // Ammo check stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"PickupCheckAmmoReloadable")
      PickupCheckAmmoNonReloadable:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupTakeAmmoNonReloadable")
        TNT1 A 0 A_JumpIfInventory("Shell",2,"PickupTakeAmmoNonReloadable")
        stop

      PickupCheckAmmoReloadable:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("WastemShell",0,"PickupTakeAmmoReloadable")
        stop

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("marathon/shotgun/fire",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 AAAAAAAAAA 0 A_FireCustomMissile("MarathonShotgunBullet",random(-4,4),false,0,0,0,random(-4,4))
        stop

      PickupAttackCoop:
        TNT1 AAAAAAAAAA 0 A_FireCustomMissile("MarathonShotgunBulletCoop",random(-4,4),false,0,0,0,random(-4,4))
        stop

      // Ammo taking stuff
      PickupTakeAmmoNonReloadable:
        TNT1 A 0 A_TakeInventory("Shell",2,TIF_NOTAKEINFINITE)
        goto PickupAttack

      PickupTakeAmmoReloadable:
        TNT1 A 0 A_TakeInventory("WastemShell",2)
        goto PickupAttack
    }
}

actor SamsaraMarathonShotgunRightAttackHandler : SamsaraMarathonShotgunLeftAttackHandler
{
    States
    {
      // Ammo check stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"PickupCheckAmmoReloadable")
        goto PickupCheckAmmoNonReloadable

      PickupCheckAmmoReloadable:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("WastemShellRight",0,"PickupTakeAmmoReloadable")
        stop

      // Ammo taking stuff
      PickupTakeAmmoReloadable:
        TNT1 A 0 A_TakeInventory("WastemShellRight",2)
        goto PickupAttack
    }
}

actor WastemShell : Ammo
{
    Inventory.MaxAmount 2
    +INVENTORY.IGNORESKILL
}

actor WastemShellRight : Ammo
{
    Inventory.MaxAmount 2
    +INVENTORY.IGNORESKILL
}

actor MarathonShotgunBullet: FastProjectile
{
    Speed 100
    Damage (random(9,12))
    Radius 8
    Height 8
    Projectile
    Obituary "$SAMSARA_MARATHON_OB_SLOT2"
    +BLOODSPLATTER
    +NOTIMEFREEZE
    +NOEXTREMEDEATH
    +THRUGHOST
    Alpha 1.0
    Decal Bulletchip
    States
    {
      Spawn:
        TNT1 A 1
        loop

      Crash:
        TNT1 A 0 A_PlaySound("puff/ding")
      Death:
        TNT1 A 1 A_SpawnItem("MarathonPuff")
        stop

      XDeath:
        TNT1 A 1 A_PlaySound("puff/hit")
        stop
    }
}

actor MarathonShotgunBulletCoop: MarathonShotgunBullet
{
    Radius 1
    Height 1
    Speed 80
    Damage (random(10,12))
    +THRUSPECIES
    Species "Player"
}

Actor MarathonShotgunPickedUpX : MapCoordinate {}
Actor MarathonShotgunPickedUpY : MapCoordinate {}
Actor MarathonShotgunPickedUpZ : MapCoordinate {}
