// Tokens

actor Goldeneye_SniperLevelLimiter : Boolean {}
actor SamsaraGoldenEyeSniperRifleConsumeAmmo : Boolean {}

// Weapon

actor Goldeneye_SniperRifle : SamsaraGoldenEyeWeapon
{
    Inventory.PickupMessage "Picked up a sniper rifle."
    Inventory.Icon "GEWSNI12"
    Weapon.SlotNumber 8
    Weapon.SlotPriority 13
    Weapon.SelectionOrder 600
    Weapon.AmmoType1 "Goldeneye_SniperMagazine"
    Weapon.AmmoUse1 1
    Weapon.AmmoType2 "Shell"
    Weapon.AmmoGive2 8
    Obituary "%o was drilled by %k's Sniper Rifle."
    Tag "Sniper Rifle"
    States
    {
      Spawn:
        GE00 Z -1
        stop

      // Single
      Ready:
        TNT1 A 0 A_JumpIfInventory("BondWatchQueue",1,"WatchRaise")
        TNT1 A 0 A_JumpIfInventory("BondWatchToken",1,"WatchReady")
        TNT1 A 0 A_JumpIfInventory("BondAltToggle",2,"ZoomWeapon")
        TNT1 A 0 A_JumpIfInventory("BondAltToggle",1,"DualWield")
        TNT1 A 0 A_JumpIfInventory("BondZoomToken",1,"DecideZoomWeapon")
      ReadyStart:
        GE00 A 0 A_JumpIfInventory("SamsaraReloadMode",2,"ReadyReloadable")
      ReadyNonReloadable:
        GE00 A 1 A_WeaponReady
        goto ReadyAmmoCheck

      ReadyReloadable:
        GE00 A 1 A_WeaponReady(WRF_ALLOWRELOAD)
        goto ReadyAmmoCheck

      ReadyAmmoCheck:
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",2,1)
        goto Ready
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"Ready")
        TNT1 A 0 A_JumpIfInventory("Goldeneye_SniperMagazine",1,"Ready")
        TNT1 A 0 A_JumpIfInventory("SamsaraSynthFireLeft",1,"Ready")
        goto ReloadCheck

      Deselect:
        GE00 A 0 A_JumpIfInventory("BondZoomToken",1,1)
        goto DeselectDualCheck
        GE00 A 0 A_TakeInventory("BondZoomToken")
        GE00 A 0 A_TakeInventory("BondZoomAmountModifyActive")
        GE00 A 0 A_ZoomFactor(1)
        GE00 A 0 SetPlayerProperty(false,false,PROP_FROZEN)
      DeselectDualCheck:
        GE00 A 0 A_JumpIfInventory("Goldeneye_SniperDualToken",1,2)
        GE00 A 0 A_GunFlash("DualForceRemoval",GFF_NOEXTCHANGE)
        goto DeselectLoop
        GE00 A 0 A_JumpIfInventory("BondDualWieldToken",1,1)
        goto DeselectLoop
        GE00 A 0 A_GunFlash("DeselectDual",GFF_NOEXTCHANGE)
      DeselectLoop:
        GE00 A 0 A_TakeInventory("BondFireDelay")
        GE00 A 0 A_TakeInventory("BondAlternateFire")
        GE00 A 0 A_TakeInventory("BondDualWieldToken")
        GE00 A 0 A_TakeInventory("BondAltToggle")
        GE00 A 0 A_TakeInventory("BondZoomToken")
        GE00 A 0 A_TakeInventory("BondZoomAmountModifyActive")
        GE00 A 0 A_TakeInventory("BondDualWieldSpamProtector")
        GE00 A 0 A_TakeInventory("BondDryFireSpamProtector1")
        GE00 A 0 A_TakeInventory("BondDryFireSpamProtector2")
        GE00 A 0 A_TakeInventory("BondAltActivationToken")
        GE00 A 1 A_Lower
        loop

      Select:
        GE00 A 0 A_GiveInventory("SamsaraGoldenEyeWeaponSwitchSoundPlayer")
      SelectZoomCheck:
        GE00 A 0 A_JumpIfInventory("BondZoomToken",1,1)
        goto SelectDualCheck
        GE00 A 0 A_TakeInventory("BondZoomToken")
        GE00 A 0 A_TakeInventory("BondZoomAmountModifyActive")
        GE00 A 0 A_ZoomFactor(1,ZOOM_INSTANT) // map change
        GE00 A 0 SetPlayerProperty(false,false,PROP_FROZEN)
      SelectDualCheck:
        GE00 A 0 A_JumpIfInventory("Goldeneye_SniperDualToken",1,2)
        GE00 A 0 A_GunFlash("DualForceRemoval",GFF_NOEXTCHANGE)
        goto SelectLoop
        GE00 A 0 A_JumpIfInventory("BondDualWieldToken",1,1)
        goto SelectLoop
        GE00 A 0 A_GunFlash("SelectDual",GFF_NOEXTCHANGE)
      SelectLoop:
        GE00 A 0 A_TakeInventory("Goldeneye_UnarmedToken")
        GE00 A 0 A_TakeInventory("Goldeneye_PP7Token")
        GE00 A 0 A_TakeInventory("Goldeneye_KnifeToken")
        GE00 A 0 A_TakeInventory("Goldeneye_KF7Token")
        GE00 A 0 A_TakeInventory("Goldeneye_AutoShotgunToken")
        GE00 A 0 A_TakeInventory("Goldeneye_AR33Token")
        GE00 A 0 A_TakeInventory("Goldeneye_RocketLauncherToken")
        GE00 A 0 A_TakeInventory("Goldeneye_RCP90Token")
        GE00 A 0 A_TakeInventory("Goldeneye_LaserToken")
        GE00 A 0 A_TakeInventory("Goldeneye_GoldenGunToken")
        GE00 A 0 A_GiveInventory("Goldeneye_SniperToken")
        GE00 A 0 A_TakeInventory("Goldeneye_D5KToken")
        GE00 A 1 A_Raise
        goto SelectZoomCheck

      Fire:
        GE01 B 0 A_JumpIfInventory("BondZoomToken",1,"DecideZoomWeaponFire")
      FireAltCheck:
        GE01 B 0 A_JumpIfInventory("SamsaraSynthFireRight",1,1)
        goto FireAltZoomCheck
        GE01 B 0 A_JumpIfInventory("BondZoomToken",1,"FireAltZoomCheck")
        GE01 B 0 A_JumpIfInventory("BondAltActivationToken",1,"FireAltZoomCheck")
        GE01 B 0 A_TakeInventory("BondAltToggle")
        GE01 B 0 A_GiveInventory("BondAltActivationToken")
        GE01 B 0 ACS_NamedExecuteAlways("BondAltFire")
        loop

      FireAltZoomCheck:
        GE01 B 0 A_JumpIfInventory("BondAltToggle",2,"ZoomWeaponFire")
      FireAltDualCheck:
        GE01 B 0 A_JumpIfInventory("BondAltToggle",1,"DualWieldFire")
      FireCheckDualOrder:
        GE01 B 0 A_JumpIfInventory("BondDualWieldToken",1,1)
        goto FireAmmoCheck
        GE01 B 0 A_JumpIfInventory("BondAlternateFire",1,"Ready")
        goto FireAmmoCheck

      FireAmmoCheck:
        GE01 B 0 A_JumpIfInventory("SamsaraReloadMode",2,"FireAmmoCheckReloadable")
      FireAmmoCheckNonReloadable:
        GE01 B 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireStart")
        GE01 B 0 A_JumpIfInventory("Shell",1,"FireStart")
        goto FireDry

      FireAmmoCheckReloadable:
        GE01 B 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"FireStart")
        GE01 B 0 A_JumpIfInventory("Goldeneye_SniperMagazine",1,"FireStart")
        goto FireDry

      FireDry:
        GE00 A 0 A_JumpIfInventory("BondDryFireSpamProtector1",1,"FireDryBlocker")
        GE00 A 0 A_GiveInventory("SamsaraGoldenEyeWeaponDrySoundPlayer")
        GE00 A 0 ACS_NamedExecuteAlways("BondDryFireSpamBlocker_Primary")
        GE00 A 0 A_GiveInventory("BondDryFireSpamProtector1")
	  FireDryBlocker:
		GE00 A 0 A_JumpIfInventory("BondDualWieldToken",1,1)
        goto FireFinish
        GE00 A 0 A_GiveInventory("BondAlternateFire")
        goto FireFinish

      FireStart:
        GE01 B 0 A_GiveInventory("BondFiringToken")
        GE01 B 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        GE01 C 0 A_GiveInventory("SamsaraGoldenEyeSniperRifleSingleAttackHandler")
        GE01 C 0 A_TakeInventory("BondFiringToken")
        GE01 C 0 ACS_NamedExecuteWithResult("SamsaraSemiAuto")
        GE01 CDED 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        GE01 C 0 A_JumpIfInventory("SamsaraFiringFullAuto",1,2)
        GE01 C 0 A_ReFire("FireFinish")
        GE01 C 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        GE00 A 0 A_JumpIfInventory("SamsaraFiringFullAuto",1,2)
        GE00 A 0 A_ReFire("FireFinish")
        GE00 A 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        GE00 A 0 A_JumpIfInventory("SamsaraFiringFullAuto",1,2)
        GE00 A 0 A_ReFire("FireFinish")
        GE00 A 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        GE00 A 0 A_JumpIfInventory("SamsaraFiringFullAuto",1,2)
        GE00 A 0 A_ReFire("FireFinish")
        GE00 A 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        GE00 A 0 A_JumpIfInventory("SamsaraFiringFullAuto",1,2)
        GE00 A 0 A_ReFire("FireFinish")
        GE00 A 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        GE00 A 0 A_JumpIfInventory("SamsaraFiringFullAuto",1,2)
        GE00 A 0 A_ReFire("FireFinish")
        GE00 A 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        GE00 A 0 A_JumpIfInventory("SamsaraFiringFullAuto",1,2)
        GE00 A 0 A_ReFire("FireFinish")
        GE00 A 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        goto FireFinish

      FireFinish:
		TNT1 A 0 A_TakeInventory("SamsaraFiringFullAuto")
        TNT1 A 0 A_TakeInventory("BondFiringToken")
        goto Ready

      Reload:
        TNT1 A 0 A_JumpIfInventory("SamsaraSynthFireLeft",1,"ReloadCheck")
        TNT1 A 0 A_JumpIfInventory("BondDualWieldToken",1,1)
        goto ReloadCheck
        TNT1 A 0 A_JumpIfInventory("Goldeneye_SniperDualMagazine",0,"ReloadCheck")
        TNT1 A 0 A_JumpIfInventory("BondDualReloadSpamProtector",1,"ReloadCheck")
        TNT1 A 0 A_GunFlash("ReloadCheckDual",GFF_NOEXTCHANGE)
        goto ReloadCheck

      ReloadCheck:
        TNT1 A 0 A_JumpIfInventory("Goldeneye_SniperMagazine",0,"Ready")
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"ReloadStart")
        TNT1 A 0 A_JumpIfInventory("Shell",1,"ReloadStart")
        goto Ready

      ReloadStart:
        GE02 A 0 A_GiveInventory("SamsaraGoldenEyeWeaponReloadSoundPlayer")
        GE02 ABCDEFGHI 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        GE02 I 0 A_JumpIfInventory("SamsaraReloadMode",2,"ReloadAmmo")
      ReloadFinish:
        GE02 IHGFEDCBA 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        goto Ready

      ReloadAmmo:
        GE02 I 0 A_JumpIfInventory("Goldeneye_SniperMagazine",0,"ReloadFinish")
        GE02 I 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
        GE02 I 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        GE02 I 0 A_JumpIfInventory("Shell",1,1)
        goto ReloadFinish
        GE02 I 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
        GE02 I 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"ReloadAmmoGiveMagazine")
        GE02 I 0 A_JumpIfInventory("SamsaraGoldenEyeSniperRifleConsumeAmmo",0,2)
        GE02 I 0 A_GiveInventory("SamsaraGoldenEyeSniperRifleConsumeAmmo")
        goto ReloadAmmoGiveMagazine
        GE02 I 0 A_TakeInventory("Shell",1)
        GE02 I 0 A_TakeInventory("SamsaraGoldenEyeSniperRifleConsumeAmmo")
      ReloadAmmoGiveMagazine:
        GE02 I 0 A_GiveInventory("Goldeneye_SniperMagazine")
        goto ReloadAmmo

      // Dual Init
      AltFire:
        TNT1 A 0 A_JumpIfInventory("BondZoomToken",1,"Ready")
        TNT1 A 0 A_JumpIfInventory("BondAltActivationToken",1,"Ready")
        TNT1 A 0 A_TakeInventory("BondAltToggle")
        TNT1 A 0 A_GiveInventory("BondAltActivationToken")
        TNT1 A 0 ACS_NamedExecuteAlways("BondAltFire")
        goto Ready

      DualUnwield:
        TNT1 A 0 A_TakeInventory("BondAlternateFire")
        TNT1 A 0 A_JumpIfInventory("BondDualWieldSpamProtector",1,5)
        TNT1 A 0 A_TakeInventory("BondDualWieldToken")
        TNT1 A 0 A_TakeInventory("BondDryFireSpamProtector1")
        TNT1 A 0 A_TakeInventory("BondDryFireSpamProtector2")
        TNT1 A 0 A_GunFlash("DualLower",GFF_NOEXTCHANGE)
        TNT1 A 0 A_ZoomFactor(1)
        goto ReadyStart

      DualUnwieldFire:
        GE01 B 0 A_TakeInventory("BondAlternateFire")
        GE01 B 0 A_JumpIfInventory("BondDualWieldSpamProtector",1,5)
        GE01 B 0 A_TakeInventory("BondDualWieldToken")
        GE01 B 0 A_TakeInventory("BondDryFireSpamProtector1")
        GE01 B 0 A_TakeInventory("BondDryFireSpamProtector2")
        GE01 B 0 A_GunFlash("DualLower",GFF_NOEXTCHANGE)
        GE01 B 0 A_ZoomFactor(1)
        goto FireCheckDualOrder

      DualLower:
        GE12 A 0 A_TakeInventory("BondDualReloadSpamProtector")
        GE12 A 1 A_GiveInventory("BondDualWieldSpamProtector")
        GE12 BCDEFGHI 1
        TNT1 A 0 A_TakeInventory("BondDualWieldSpamProtector")
        stop

      DualWield:
        TNT1 A 0 A_TakeInventory("BondAltActivationToken")
        TNT1 A 0 A_TakeInventory("BondAltToggle")
        TNT1 A 0 A_TakeInventory("BondZoomToken")
        TNT1 A 0 A_JumpIfInventory("Goldeneye_SniperDualToken",1,2)
        TNT1 A 0 A_ZoomFactor(1)
        goto ReadyStart
        TNT1 A 0 A_JumpIfInventory("BondDualWieldToken",1,"DualUnwield")
        TNT1 A 0 A_JumpIfInventory("BondDualWieldSpamProtector",1,3)
        TNT1 A 0 A_GiveInventory("BondDualWieldToken")
        TNT1 A 0 A_GunFlash("DualRaise",GFF_NOEXTCHANGE)
        TNT1 A 0 A_ZoomFactor(1)
        goto ReadyStart

      DualWieldFire:
        GE01 B 0 A_TakeInventory("BondAltActivationToken")
        GE01 B 0 A_TakeInventory("BondAltToggle")
        GE01 B 0 A_TakeInventory("BondZoomToken")
        GE01 B 0 A_JumpIfInventory("Goldeneye_SniperDualToken",1,2)
        GE01 B 0 A_ZoomFactor(1)
        goto FireCheckDualOrder
        GE01 B 0 A_JumpIfInventory("BondDualWieldToken",1,"DualUnwieldFire")
        GE01 B 0 A_JumpIfInventory("BondDualWieldSpamProtector",1,3)
        GE01 B 0 A_GiveInventory("BondDualWieldToken")
        GE01 B 0 A_GunFlash("DualRaise",GFF_NOEXTCHANGE)
        GE01 B 0 A_ZoomFactor(1)
        goto FireCheckDualOrder

      DualRaise:
        GE12 I 0 A_TakeInventory("BondDualReloadSpamProtector")
        GE12 I 0 A_GiveInventory("BondDualWieldSpamProtector")
        GE12 I 1 A_GiveInventory("SamsaraGoldenEyeWeaponSwitchSoundPlayer")
        GE12 HGFEDCBA 1
        TNT1 A 0 A_TakeInventory("BondDualWieldSpamProtector")
        goto ReadyStartDual

      // Dual
      ReadyDual:
        TNT1 A 0 A_JumpIfInventory("BondAlternateFire",1,1)
        goto ReadyStartDual
        TNT1 A 0 A_JumpIfInventory("SamsaraSynthFireLeft",1,"FireDual")
      ReadyStartDual:
        GE10 A 1
        goto ReadyAmmoCheckDual

      ReadyAmmoCheckDual:
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",2,1)
        goto ReadyDual
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"ReadyDual")
        TNT1 A 0 A_JumpIfInventory("Goldeneye_SniperDualMagazine",1,"ReadyDual")
        TNT1 A 0 A_JumpIfInventory("SamsaraSynthFireLeft",1,"ReadyDual")
        goto ReloadCheckDual

      DeselectDual:
        GE10 A 0 A_TakeInventory("BondDualReloadSpamProtector")
        GE10 A 1 A_TakeInventory("BondDualWieldSpamProtector")
        GE10 AAAAAAAAAAAAAAA 1
        stop

      SelectDual:
        GE10 A 0 A_TakeInventory("BondDualReloadSpamProtector")
        GE10 A 1 A_TakeInventory("BondDualWieldSpamProtector")
        goto ReadyStartDual

      FireDual:
        GE11 B 0 A_JumpIfInventory("BondAlternateFire",1,"FireAmmoCheckDual")
        goto ReadyDual

      FireAmmoCheckDual:
        GE11 B 0 A_JumpIfInventory("SamsaraReloadMode",2,"FireAmmoCheckDualReloadable")
      FireAmmoCheckDualNonReloadable:
        GE11 B 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireStartDual")
        GE11 B 0 A_JumpIfInventory("Shell",1,"FireStartDual")
        goto FireDryDual

      FireAmmoCheckDualReloadable:
        GE11 B 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"FireStartDual")
        GE11 B 0 A_JumpIfInventory("Goldeneye_SniperDualMagazine",1,"FireStartDual")
        goto FireDryDual

      FireDryDual:
        GE10 A 0 A_JumpIfInventory("BondDryFireSpamProtector2",1,"FireDryBlockerDual")
        GE10 A 0 A_GiveInventory("SamsaraGoldenEyeWeaponDrySoundPlayer")
        GE10 A 0 ACS_NamedExecuteAlways("BondDryFireSpamBlocker_Secondary")
        GE10 A 0 A_GiveInventory("BondDryFireSpamProtector2")
	  FireDryBlockerDual:
		GE10 A 0 A_TakeInventory("BondAlternateFire")
        goto FireFinishDual

      FireStartDual:
        GE11 B 1 A_GiveInventory("BondFiringToken")
        GE11 C 0 A_GiveInventory("SamsaraGoldenEyeSniperRifleDualAttackHandler")
        GE11 C 0 A_TakeInventory("BondFiringToken")
        GE11 C 1 ACS_NamedExecuteWithResult("SamsaraSemiAuto2")
        GE11 DED 1
        GE11 C 0 A_JumpIfInventory("SamsaraFiringFullAuto2",1,2)
        GE11 C 0 A_JumpIfInventory("SamsaraSynthFireLeft",1,"FireFinishDual")
        GE11 C 1
        GE10 A 0 A_JumpIfInventory("SamsaraFiringFullAuto2",1,2)
        GE10 A 0 A_JumpIfInventory("SamsaraSynthFireLeft",1,"FireFinishDual")
        GE10 A 1
        GE10 A 0 A_JumpIfInventory("SamsaraFiringFullAuto2",1,2)
        GE10 A 0 A_JumpIfInventory("SamsaraSynthFireLeft",1,"FireFinishDual")
        GE10 A 1
        GE10 A 0 A_JumpIfInventory("SamsaraFiringFullAuto2",1,2)
        GE10 A 0 A_JumpIfInventory("SamsaraSynthFireLeft",1,"FireFinishDual")
        GE10 A 1
        GE10 A 0 A_JumpIfInventory("SamsaraFiringFullAuto2",1,2)
        GE10 A 0 A_JumpIfInventory("SamsaraSynthFireLeft",1,"FireFinishDual")
        GE10 A 1
        GE10 A 0 A_JumpIfInventory("SamsaraFiringFullAuto2",1,2)
        GE10 A 0 A_JumpIfInventory("SamsaraSynthFireLeft",1,"FireFinishDual")
        GE10 A 1
        GE10 A 0 A_JumpIfInventory("SamsaraFiringFullAuto2",1,2)
        GE10 A 0 A_JumpIfInventory("SamsaraSynthFireLeft",1,"FireFinishDual")
        GE10 A 1
        goto FireFinishDual

      FireFinishDual:
		TNT1 A 0 A_TakeInventory("SamsaraFiringFullAuto2")
        TNT1 A 0 A_TakeInventory("BondFiringToken")
        goto ReadyDual

      ReloadCheckDual:
        TNT1 A 0 A_TakeInventory("BondDualWieldSpamProtector")
        TNT1 A 0 A_JumpIfInventory("Goldeneye_SniperDualMagazine",0,"ReadyDual")
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"ReloadStartDual")
        TNT1 A 0 A_JumpIfInventory("Shell",1,"ReloadStartDual")
        goto ReadyDual

      ReloadStartDual:
        GE12 A 0 A_GiveInventory("BondDualReloadSpamProtector")
        GE12 A 1 A_GiveInventory("SamsaraGoldenEyeWeaponReloadSoundPlayer")
        GE12 BCDEFGHI 1
        GE12 I 0 A_JumpIfInventory("SamsaraReloadMode",2,"ReloadAmmoDual")
      ReloadFinishDual:
        GE12 IHGFEDCBA 1
        TNT1 A 0 A_TakeInventory("BondDualReloadSpamProtector")
        goto ReadyDual

      ReloadAmmoDual:
        GE12 I 0 A_JumpIfInventory("Goldeneye_SniperDualMagazine",0,"ReloadFinishDual")
        GE12 I 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
        GE12 I 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        GE12 I 0 A_JumpIfInventory("Shell",1,1)
        goto ReloadFinishDual
        GE12 I 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
        GE12 I 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"ReloadAmmoGiveMagazineDual")
        GE12 I 0 A_JumpIfInventory("SamsaraGoldenEyeSniperRifleConsumeAmmo",0,2)
        GE12 I 0 A_GiveInventory("SamsaraGoldenEyeSniperRifleConsumeAmmo")
        goto ReloadAmmoGiveMagazineDual
        GE12 I 0 A_TakeInventory("Shell",1)
        GE12 I 0 A_TakeInventory("SamsaraGoldenEyeSniperRifleConsumeAmmo")
      ReloadAmmoGiveMagazineDual:
        GE12 I 0 A_GiveInventory("Goldeneye_SniperDualMagazine")
        goto ReloadAmmoDual

      // Zoom
      ZoomWeapon:
        TNT1 A 0 A_TakeInventory("BondAltActivationToken")
        TNT1 A 0 A_TakeInventory("BondAltToggle")
        TNT1 A 0 A_GiveInventory("BondZoomToken")
        TNT1 A 0 A_GiveInventory("BondZoomAmountModifyActive")
        TNT1 A 0 ACS_NamedExecuteAlways("BondZoomAmountModify")
        TNT1 A 0 A_ZoomFactor(1+((CallACS("BondZoomAmountCheck")+1)*0.5))
        TNT1 A 0 SetPlayerProperty(false,true,PROP_FROZEN)
        goto ReadyStart

      DecideZoomWeapon:
        TNT1 A 0 A_JumpIfInventory("SamsaraSynthFireRight",1,1)
        goto UnzoomWeapon
        TNT1 A 0 A_ZoomFactor(1+((CallACS("BondZoomAmountCheck")+1)*0.5))
        goto ReadyStart

      UnzoomWeapon:
        TNT1 A 0 A_TakeInventory("BondZoomToken")
        TNT1 A 0 A_TakeInventory("BondZoomAmountModifyActive")
        TNT1 A 0 A_ZoomFactor(1)
        TNT1 A 0 SetPlayerProperty(false,false,PROP_FROZEN)
        goto ReadyStart

      ZoomWeaponFire:
        GE01 B 0 A_TakeInventory("BondAltActivationToken")
        GE01 B 0 A_TakeInventory("BondAltToggle")
        GE01 B 0 A_GiveInventory("BondZoomToken")
        GE01 B 0 A_GiveInventory("BondZoomAmountModifyActive")
        GE01 B 0 ACS_NamedExecuteAlways("BondZoomAmountModify")
        GE01 B 0 A_ZoomFactor(1+((CallACS("BondZoomAmountCheck")+1)*0.5))
        GE01 B 0 SetPlayerProperty(false,true,PROP_FROZEN)
        goto FireAltDualCheck

      DecideZoomWeaponFire:
        GE01 B 0 A_JumpIfInventory("SamsaraSynthFireRight",1,1)
        goto UnzoomWeaponFire
        GE01 B 0 A_ZoomFactor(1+((CallACS("BondZoomAmountCheck")+1)*0.5))
        goto FireAltCheck

      UnzoomWeaponFire:
        GE01 B 0 A_TakeInventory("BondZoomToken")
        GE01 B 0 A_TakeInventory("BondZoomAmountModifyActive")
        GE01 B 0 A_ZoomFactor(1)
        GE01 B 0 SetPlayerProperty(false,false,PROP_FROZEN)
        goto FireAltCheck
    }
}

// Attack handlers

actor SamsaraGoldenEyeSniperRifleSingleAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",2,"PickupReloadable")
      PickupNonReloadable:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Shell",1,1)
        stop
        TNT1 A 0 A_JumpIfInventory("SamsaraGoldenEyeSniperRifleConsumeAmmo",0,2)
        TNT1 A 0 A_GiveInventory("SamsaraGoldenEyeSniperRifleConsumeAmmo")
        goto PickupAttack
        TNT1 A 0 A_TakeInventory("Shell",1)
        TNT1 A 0 A_TakeInventory("SamsaraGoldenEyeSniperRifleConsumeAmmo")
        goto PickupAttack

      PickupReloadable:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Goldeneye_SniperMagazine",1,1)
        stop
        TNT1 A 0 A_TakeInventory("Goldeneye_SniperMagazine",1)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
		TNT1 A 0 ACS_NamedExecuteWithResult("BondAltFireActivator")
        TNT1 A 0 A_JumpIfInventory("SamsaraAlternateSoundToken",1,"PickupAttackSource")
      // Original sounds
      PickupAttackN64:
        TNT1 A 0 A_PlaySound("Goldeneye/SniperShot",CHAN_WEAPON)
        TNT1 A 0 A_SpawnItemEx("Goldeneye_Cartridge_Rifle",cos(pitch)*22,6,42-(sin(pitch)*22),(2*cos(pitch))+(2*sin(pitch)),frandom(2,3),abs(4*cos(pitch)),0,SXF_TRANSFERPITCH)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackN64Coop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackN64Coop")
      PickupAttackN64DM:
        TNT1 A 0 A_FireCustomMissile("Goldeneye_SniperTracer",frandom(0.75,0.85),false,6,0,0,frandom(0.575,0.675))
        stop

      PickupAttackN64Coop:
        TNT1 A 0 A_FireCustomMissile("Goldeneye_SniperTracerCoop",frandom(0.75,0.85),false,6,0,0,frandom(0.575,0.675))
        stop

      // Source sounds
      PickupAttackSource:
        TNT1 A 0 A_PlaySound("GoldeneyeSource/SniperShot",CHAN_WEAPON)
        TNT1 A 0 A_SpawnItemEx("Goldeneye_Cartridge_RifleSoundSource",cos(pitch)*22,6,42-(sin(pitch)*22),(2*cos(pitch))+(2*sin(pitch)),frandom(2,3),abs(4*cos(pitch)),0,SXF_TRANSFERPITCH)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackSourceCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackSourceCoop")
      PickupAttackSourceDM:
        TNT1 A 0 A_FireCustomMissile("Goldeneye_SniperTracerSoundSource",frandom(0.75,0.85),false,6,0,0,frandom(0.575,0.675))
        stop

      PickupAttackSourceCoop:
        TNT1 A 0 A_FireCustomMissile("Goldeneye_SniperTracerCoopSoundSource",frandom(0.75,0.85),false,6,0,0,frandom(0.575,0.675))
        stop
    }
}

actor SamsaraGoldenEyeSniperRifleDualAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",2,"PickupReloadable")
      PickupNonReloadable:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Shell",1,1)
        stop
        TNT1 A 0 A_JumpIfInventory("SamsaraGoldenEyeSniperRifleConsumeAmmo",0,2)
        TNT1 A 0 A_GiveInventory("SamsaraGoldenEyeSniperRifleConsumeAmmo")
        goto PickupAttack
        TNT1 A 0 A_TakeInventory("Shell",1)
        TNT1 A 0 A_TakeInventory("SamsaraGoldenEyeSniperRifleConsumeAmmo")
        goto PickupAttack

      PickupReloadable:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Goldeneye_SniperDualMagazine",1,1)
        stop
        TNT1 A 0 A_TakeInventory("Goldeneye_SniperDualMagazine",1)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
		TNT1 A 0 ACS_NamedExecuteWithResult("BondAltFireActivator",1)
        TNT1 A 0 A_JumpIfInventory("SamsaraAlternateSoundToken",1,"PickupAttackSource")
      // Original sounds
      PickupAttackN64:
        TNT1 A 0 A_PlaySound("Goldeneye/SniperShot",CHAN_WEAPON)
        TNT1 A 0 A_SpawnItemEx("Goldeneye_Cartridge_Rifle",cos(pitch)*22,-6,42-(sin(pitch)*22),(2*cos(pitch))+(2*sin(pitch)),frandom(2,3),abs(4*cos(pitch)),0,SXF_TRANSFERPITCH)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackN64Coop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackN64Coop")
      PickupAttackN64DM:
        TNT1 A 0 A_FireCustomMissile("Goldeneye_SniperTracer",frandom(0.75,0.85),false,-6,0,0,frandom(0.575,0.675))
        stop

      PickupAttackN64Coop:
        TNT1 A 0 A_FireCustomMissile("Goldeneye_SniperTracerCoop",frandom(0.75,0.85),false,-6,0,0,frandom(0.575,0.675))
        stop

      // Source sounds
      PickupAttackSource:
        TNT1 A 0 A_PlaySound("GoldeneyeSource/SniperShot",CHAN_WEAPON)
        TNT1 A 0 A_SpawnItemEx("Goldeneye_Cartridge_RifleSoundSource",cos(pitch)*22,-6,42-(sin(pitch)*22),(2*cos(pitch))+(2*sin(pitch)),frandom(2,3),abs(4*cos(pitch)),0,SXF_TRANSFERPITCH)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackSourceCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackSourceCoop")
      PickupAttackSourceDM:
        TNT1 A 0 A_FireCustomMissile("Goldeneye_SniperTracerSoundSource",frandom(0.75,0.85),false,-6,0,0,frandom(0.575,0.675))
        stop

      PickupAttackSourceCoop:
        TNT1 A 0 A_FireCustomMissile("Goldeneye_SniperTracerCoopSoundSource",frandom(0.75,0.85),false,-6,0,0,frandom(0.575,0.675))
        stop
    }
}

actor Goldeneye_SniperMagazine : Ammo
{
    Inventory.MaxAmount 8
    Tag "Sniper Rifle Magazine (Right)"
    +IGNORESKILL
}

actor Goldeneye_SniperDualMagazine : Ammo
{
    Inventory.MaxAmount 8
    Tag "Sniper Rifle Magazine (Left)"
    +IGNORESKILL
}

actor Goldeneye_SniperTracer : Goldeneye_BulletTracerThruGhost
{
    States
    {
      XDeath:
        TNT1 A 0 A_SetUserVar("user_damage",25)
        goto Super::XDeath
	  Crash:
        TNT1 A 0 A_SetUserVar("user_damage",25)
        goto Super::Crash
    }
}

actor Goldeneye_SniperTracerCoop : Goldeneye_SniperTracer
{
    Species "Player"
    +THRUSPECIES
}

actor Goldeneye_SniperTracerSoundSource : Goldeneye_BulletTracerThruGhostSoundSource
{
    States
    {
      XDeath:
        TNT1 A 0 A_SetUserVar("user_damage",25)
        goto Super::XDeath
	  Crash:
        TNT1 A 0 A_SetUserVar("user_damage",25)
        goto Super::Crash
    }
}

actor Goldeneye_SniperTracerCoopSoundSource : Goldeneye_SniperTracerSoundSource
{
    Species "Player"
    +THRUSPECIES
}
