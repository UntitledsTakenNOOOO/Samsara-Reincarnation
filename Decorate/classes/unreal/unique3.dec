actor SamsaraUnrealCARifleInFireState : Boolean {}
actor SamsaraUnrealCARiflePrimaryFiringSoundCheck : Boolean {}
actor SamsaraUnrealCARifleCanAltFire : Boolean {}
actor SamsaraUnrealCARifleSecondaryCooldown : Counter {}
actor SamsaraUnrealCARifleSecondaryCooldownEndSoundCheck : Boolean {}

Actor Unreal_Duster : UnrealWeapon
{
	Tag "Assault Rifle"
	Weapon.SlotNumber 9
	Weapon.SlotPriority 0
	Weapon.SelectionOrder 1400
	Weapon.AmmoType "Clip"
	Weapon.AmmoGive 75
	Weapon.AmmoUse 1
	Inventory.PickupMessage "You got the Combat Assault Rifle."
	Obituary "%o was filled with lead by %k's Combat Assault Rifle."
	+NOALERT
	States
	{
		Spawn:
			UN00 A 10 Bright
			UN00 A 10
			loop

		ReadyActual:
			UN04 I 0 A_TakeInventory("SamsaraUnrealCARifleInFireState")
			UN04 I 0 A_TakeInventory("SamsaraUnrealCARiflePrimaryFiringSoundCheck")
			UN04 I 0 A_GiveInventory("SamsaraUnrealCARifleCanAltFire")
			UN04 I 6 A_WeaponReady
			goto ReadyActualJump

		ReadyActualJump:
			UN04 I 0 A_Jump(8,"ReadyActualIdle")
			goto ReadyActual

		ReadyActualIdle:
			UN04 I 12 A_WeaponReady
			UN05 ABAB 12 A_WeaponReady
			goto ReadyActual

		Deselect:
			UN02 A 0 A_JumpIfInventory("SamsaraUnrealCARiflePrimaryFiringSoundCheck",1,1)
			goto DeselectStart
			UN02 A 0 A_PlaySound("silence",CHAN_WEAPON)
			UN02 A 0 A_StopSound(CHAN_WEAPON)
			UN02 A 0 A_TakeInventory("SamsaraUnrealCARiflePrimaryFiringSoundCheck")
			goto DeselectStart

		DeselectStart:
			UN02 A 0 A_TakeInventory("Unreal_CARSelected")
			UN02 A 0 A_TakeInventory("SamsaraUnrealCARifleInFireState")
			UN02 A 0 A_TakeInventory("SamsaraUnrealCARifleCanAltFire")
			UN02 A 0 A_TakeInventory("SamsaraUnrealCARifleSecondaryCooldownEndSoundCheck")
			UN02 A 2 A_JumpIfHealthLower(1,"DeselectInstant")
			UN02 BCD 2
			goto DeselectInstant

		Ready: // our "Select" state
			UN04 A 0 A_GiveInventory("Unreal_CARSelected")
			UN04 A 0 A_PlaySound("Unreal/CAR/Select")
			UN04 A 0 A_TakeInventory("SamsaraUnrealCARifleInFireState") // map change hack
			UN04 A 0 A_TakeInventory("SamsaraUnrealCARiflePrimaryFiringSoundCheck")
			UN04 A 0 A_TakeInventory("SamsaraUnrealCARifleCanAltFire")
			UN04 A 0 A_TakeInventory("SamsaraUnrealCARifleSecondaryCooldownEndSoundCheck")
			UN04 A 0 A_JumpIfInventory("SamsaraHasDoubleFiringSpeed",1,2)
			UN04 A 1 A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
			UN04 A 0 A_TakeInventory("SamsaraUnrealCARifleInFireState") // map change hack
			UN04 A 0 A_TakeInventory("SamsaraUnrealCARiflePrimaryFiringSoundCheck")
			UN04 A 0 A_TakeInventory("SamsaraUnrealCARifleCanAltFire")
			UN04 A 0 A_TakeInventory("SamsaraUnrealCARifleSecondaryCooldownEndSoundCheck")
			UN04 A 1 A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
			UN04 BCDEFGH 2 A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
			goto ReadyActual

		Fire:
			UN03 A 0 A_AlertMonsters
			UN03 A 0 A_GiveInventory("SamsaraUnrealCARifleInFireState")
			UN03 A 0 A_GiveInventory("SamsaraUnrealCARiflePrimaryAttackHandler")
			UN03 ABC 1 Bright A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			UN04 I 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
			UN04 I 0 A_JumpIfInventory("Clip",1,1)
			goto FireContinue
			UN04 I 0 A_ReFire
			goto FireContinue

		FireContinue:
			UN04 I 0 A_ClearReFire
			UN04 I 0 A_JumpIfInventory("SamsaraUnrealCARiflePrimaryFiringSoundCheck",1,1)
			goto FireFinish
			UN04 I 0 A_PlaySound("Unreal/CAR/Stop",CHAN_WEAPON)
			UN04 I 0 A_TakeInventory("SamsaraUnrealCARiflePrimaryFiringSoundCheck")
			goto FireFinish

		FireFinish:
			UN04 I 0 A_CheckReload
			goto ReadyActual

		AltFire:
			UN01 A 0 A_JumpIfInventory("SamsaraUnrealCARifleSecondaryCooldown",1,"ReadyActual")
			UN01 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"AltFireStart")
			UN01 A 0 A_JumpIfInventory("Clip",5,"AltFireStart")
			UN01 A 0 A_JumpIfInventory("Clip",1,"ReadyActual") // can we still use the primary?
			goto NoAmmoSwitch

		AltFireStart:
			UN01 A 0 A_AlertMonsters
			UN01 A 0 A_TakeInventory("SamsaraUnrealCARifleInFireState")
			UN01 A 0 A_TakeInventory("SamsaraUnrealCARifleSecondaryCooldown") // reset
			UN01 A 0 A_GiveInventory("SamsaraUnrealCARifleSecondaryCooldown",29)
			UN01 A 0 A_GiveInventory("SamsaraUnrealCARifleSecondaryAttackHandler")
			UN01 ABCDEFG 2 Bright A_WeaponReady(WRF_NOSECONDARY)
			UN01 H 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"AltFireWait")
			UN01 H 0 A_JumpIfInventory("Clip",1,"AltFireWait") // can we still use the primary?
			goto NoAmmoSwitch

		AltFireWait:
			UN01 H 1 A_WeaponReady(WRF_NOSECONDARY)
			UN01 H 0 A_JumpIfInventory("SamsaraUnrealCARifleSecondaryCooldown",1,"AltFireWait")
			goto AltFireContinue

		AltFireContinue:
			UN01 H 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"AltFireContinueAmmo")
			UN01 H 0 A_JumpIfInventory("Clip",5,"AltFireContinueAmmo")
			goto AltFireContinueNoAmmo

		AltFireContinueNoAmmo:
			UN01 IJ 2 A_WeaponReady(WRF_NOSECONDARY)
			goto AltFireFinish

		AltFireContinueAmmo:
			UN01 IJ 2 A_WeaponReady
			goto AltFireFinish

		AltFireFinish:
			UN04 I 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"ReadyActual")
			UN04 I 0 A_JumpIfInventory("Clip",1,"ReadyActual")
			goto NoAmmoSwitch

		// "No ammo" switch
		NoAmmoSwitch:
			TNT1 A 0 A_ClearReFire
			TNT1 A 0 A_SelectWeapon("Unreal_AutoMag")
			goto Deselect
	}
}

actor SamsaraUnrealCARifleAltFire : Trigger
{
	States
	{
	// Ready
	Pickup:
		TNT1 A 0 A_JumpIfInventory("SamsaraUnrealCARifleCanAltFire",1,1)
		stop
		TNT1 A 0 A_JumpIfInventory("SamsaraUnrealCARifleSecondaryCooldown",1,"PickupCooldown")
		TNT1 A 0 A_JumpIfInventory("SamsaraUnrealCARifleSecondaryCooldownEndSoundCheck",1,"PickupCooldownEndSound")
	PickupAltFireCheck:
		TNT1 A 0 A_JumpIfInventory("SamsaraUnrealCARifleInFireState",1,1)
		stop
		TNT1 A 0 A_JumpIfInventory("SamsaraSynthFireRight",1,"PickupStart")
		stop

	// Cooldown
	PickupCooldown:
		TNT1 A 0 A_GiveInventory("SamsaraUnrealCARifleSecondaryCooldownEndSoundCheck")
		stop

	// Cooldown end sound
	PickupCooldownEndSound:
		TNT1 A 0 A_PlaySound("Unreal/CAR/Load")
		TNT1 A 0 A_TakeInventory("SamsaraUnrealCARifleSecondaryCooldownEndSoundCheck")
		goto PickupAltFireCheck

	// Fire
	PickupStart:
		TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAltFire")
		TNT1 A 0 A_JumpIfInventory("Clip",5,"PickupAltFire")
		stop

	PickupAltFire:
		TNT1 A 0 ACS_NamedExecuteWithResult("SamsaraForcePlayerAttackState")
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_TakeInventory("SamsaraUnrealCARifleSecondaryCooldown") // reset
		TNT1 A 0 A_GiveInventory("SamsaraUnrealCARifleSecondaryCooldown",29)
		TNT1 A 0 A_GiveInventory("SamsaraUnrealCARifleSecondaryAttackHandler")
		stop
	}
}

actor SamsaraUnrealCARiflePrimaryAttackHandler : Trigger
{
	States
	{
	// Ammo checking/taking stuff
	Pickup:
		TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
		TNT1 A 0 A_JumpIfInventory("Clip",1,1)
		stop
		TNT1 A 0 A_TakeInventory("Clip",1)
		goto PickupAttack

	// Attack stuff
	PickupAttack:
		TNT1 A 0 A_PlaySound("Unreal/CAR/Fire",CHAN_WEAPON,1.0,true)
		TNT1 A 0 A_GiveInventory("SamsaraUnrealCARiflePrimaryFiringSoundCheck")
		TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
		TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
	PickupAttackDM:
		TNT1 A 0 A_FireBullets(3,3,-1,12,"UnrealPuff",FBF_NORANDOM|FBF_NOFLASH)
		goto PickupFinish
	PickupAttackCoop:
		TNT1 A 0 A_FireBullets(3,3,-1,12,"UnrealPuffCoop",FBF_NORANDOM|FBF_NOFLASH)
		goto PickupFinish

	// Finishing stuff
	PickupFinish:
		TNT1 A 0 ACS_NamedExecuteWithResult("Unreal_Recoil")
		stop
	}
}

actor SamsaraUnrealCARifleSecondaryAttackHandler : Trigger
{
	States
	{
	// Ammo checking/taking stuff
	Pickup:
		TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
		TNT1 A 0 A_JumpIfInventory("Clip",5,1)
		stop
		TNT1 A 0 A_TakeInventory("Clip",5)
		goto PickupAttack

	// Attack stuff
	PickupAttack:
		TNT1 A 0 A_PlaySound("Unreal/CAR/AltFire",CHAN_5)
		TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
		TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
	PickupAttackDM:
		TNT1 A 0 A_FireCustomMissile("Unreal_DusterShell",0,false,13)
		goto PickupFinish
	PickupAttackCoop:
		TNT1 A 0 A_FireCustomMissile("Unreal_DusterShellCoop",0,false,13)
		goto PickupFinish

	// Finishing stuff
	PickupFinish:
		TNT1 A 0 ACS_NamedExecuteWithResult("Unreal_Recoil")
		stop
	}
}

/*
Actor Unreal_CARAmmo : Ammo
{
	Inventory.Amount 50
	Inventory.MaxAmount 250
	Ammo.BackpackAmount 50
	Ammo.BackpackMaxAmount 500
	Inventory.Icon "AMSMN0"
	Tag "Assault Rifle Bullets"
}
*/

Actor Unreal_DusterShell : FastProjectile
{
	Speed 70
	Radius 1
	Height 1
	Damage (40)
	+FORCERADIUSDMG
	DamageType "UnrealExplosive"
	DeathSound "Unreal/DPistol/ProjDie"
	States
	{
		Spawn:
			UN00 A 2
			Loop
		Death:
			TNT1 A 0 A_Explode(40, 40)
			TNT1 A 5 A_SpawnItemEx("Unreal_SmallExplosion",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_CLIENTSIDE)
			Stop
	}
}

Actor Unreal_DusterShellCoop : Unreal_DusterShell { +THRUSPECIES Species "Player" }
