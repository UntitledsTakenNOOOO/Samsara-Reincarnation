actor "Ethereal Crossbow" : Weapon
{
    Weapon.SelectionOrder 800
    Inventory.PickUpSound "heretic/weaponget"
    Weapon.AmmoUse 1
    Weapon.AmmoGive 10
    Weapon.AmmoType "Shell"
    Weapon.SlotNumber 3
    Weapon.YAdjust 16
    //+AMMO_OPTIONAL
    +INVENTORY.UNDROPPABLE
    Inventory.PickupMessage "You got the Ethereal Crossbow!"
    Obituary "$SAMSARA_CORVUS_OB_SLOT3"
    Inventory.RestrictedTo "CorvusPlayer"
    Tag "Ethereal Crossbow"
    States
    {
      Spawn:
        WCBW A -1
        stop

      Ready:
		TNT1 A 0 A_TakeInventory("CorvusHoldingSlot")
		TNT1 A 0 A_GiveInventory("CorvusHoldingSlot",2)
        CRBW AAAAAABBBBBBCCCCCC 1 A_WeaponReady
        loop

      Deselect:
		CRBW A 1 A_Lower
        loop

      Select:
        CRBW A 1 A_Raise
        loop

      // Unpowered
      Fire:
		TNT1 A 0 ACS_NamedExecuteWithResult("HereticTriggerCloneAttack")
        TNT1 A 0 A_JumpIfInventory("PowerHereticTome",1,"FirePowered")
        TNT1 A 0 A_JumpIfInventory("SamsaraHasPermaWeaponLevel2",1,"FirePowered")
      FireUnpowered:
        CRBW D 6 A_GiveInventory("SamsaraHereticCrossbowAttackHandler")
        CRBW EFGH 3
        CRBW AB 4
        CRBW C 5 A_ReFire
        goto Ready

      // Powered
      FirePowered:
        CRBW D 5 A_GiveInventory("SamsaraHereticCrossbowPoweredAttackHandler")
        CRBW E 3
        CRBW F 2
        CRBW G 3
        CRBW H 2
        CRBW AB 3
        CRBW C 4 A_ReFire
        goto Ready
    }
}

// Unpowered

actor SamsaraHereticCrossbowAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Shell",1,1)
        stop
        TNT1 A 0 A_TakeInventory("Shell",1)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireCustomMissile("CrossbowFX1B",0,false)
        TNT1 A 0 A_FireCustomMissile("CrossbowFX3B",-4.5,false)
        TNT1 A 0 A_FireCustomMissile("CrossbowFX3B",4.5,false)
        stop

      PickupAttackCoop:
        TNT1 A 0 A_FireCustomMissile("CrossbowFX12",0,false)
        TNT1 A 0 A_FireCustomMissile("CrossbowFX32",-4.5,false)
        TNT1 A 0 A_FireCustomMissile("CrossbowFX32",4.5,false)
        stop
    }
}

actor CrossbowFX1B : CrossbowFX1
{
    DamageType "CorvusCrossbow"
    Damage 13 // Originally 10
    Speed 40
    SeeSound "heretic/bowshoot"
    DeathSound "heretic/bowhit"
    Decal CrossbowScorch
    +BLOODSPLATTER
    States
    {
      Spawn:
        FX03 B -1 Bright Light("BIGBOWBOLT")
        stop

      Death:
        FX03 H 8 Bright Light("BIGBOWBOLT_X1")
        FX03 I 8 Bright Light("BIGBOWBOLT_X2")
        FX03 J 8 Bright Light("BIGBOWBOLT_X3")
        stop
    }
}

actor CrossbowFX12 : CrossbowFX1
{
    DamageType "CorvusCrossbow"
    Damage 12
    SeeSound "heretic/bowshoot"
    DeathSound "heretic/bowhit"
    Decal CrossbowScorch
    Species "Player"
    +BLOODSPLATTER
    +THRUSPECIES
    States
    {
      Spawn:
        FX03 B -1 Bright Light("BIGBOWBOLT")
        stop

      Death:
        FX03 H 8 Bright Light("BIGBOWBOLT_X1")
        FX03 I 8 Bright Light("BIGBOWBOLT_X2")
        FX03 J 8 Bright Light("BIGBOWBOLT_X3")
        stop
    }
}

actor CrossbowFX3B : CrossbowFX3
{
    DamageType "CorvusCrossbow"
    Damage 5
    Speed 35
    DeathSound "heretic/bowhit"
    Decal CrossbowScorch2
    +BLOODSPLATTER
    States
    {
      Spawn:
        FX03 A -1 Bright Light("SMALLBOWBOLT")
        stop

      Death:
        FX03 C 8 Bright Light("SMALLBOWBOLT_X1")
        FX03 D 8 Bright Light("SMALLBOWBOLT_X2")
        FX03 E 8 Bright Light("SMALLBOWBOLT_X3")
        stop
    }
}

actor CrossbowFX32 : CrossbowFX3
{
    DamageType "CorvusCrossbow"
    Damage 3
    DeathSound "heretic/bowhit"
    Decal CrossbowScorch2
    Species "Player"
    +BLOODSPLATTER
    +THRUSPECIES
    States
    {
      Spawn:
        FX03 A -1 Bright Light("SMALLBOWBOLT")
        stop

      Death:
        FX03 C 8 Bright Light("SMALLBOWBOLT_X1")
        FX03 D 8 Bright Light("SMALLBOWBOLT_X2")
        FX03 E 8 Bright Light("SMALLBOWBOLT_X3")
        stop
    }
}

// Powered

actor SamsaraHereticCrossbowPoweredAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Shell",1,1)
        stop
        TNT1 A 0 A_TakeInventory("Shell",1)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireCustomMissile("CrossbowFX2B",0,false)
        TNT1 A 0 A_FireCustomMissile("CrossbowFX2B",-4.5,false)
        TNT1 A 0 A_FireCustomMissile("CrossbowFX2B",4.5,false)
        TNT1 A 0 A_FireCustomMissile("CrossbowFX3B",-9,false)
        TNT1 A 0 A_FireCustomMissile("CrossbowFX3B",9,false)
        stop

      PickupAttackCoop:
        TNT1 A 0 A_FireCustomMissile("CrossbowFX22",0,false)
        TNT1 A 0 A_FireCustomMissile("CrossbowFX22",-4.5,false)
        TNT1 A 0 A_FireCustomMissile("CrossbowFX22",4.5,false)
        TNT1 A 0 A_FireCustomMissile("CrossbowFX32",-9,false)
        TNT1 A 0 A_FireCustomMissile("CrossbowFX32",9,false)
        stop
    }
}

actor CrossbowFX2B : CrossbowFX2
{
    DamageType "CorvusCrossbow"
    Damage 9
    Speed 42
    SeeSound "heretic/bowshoot"
    DeathSound "heretic/bowhit"
    Decal CrossbowScorch2
    +BLOODSPLATTER
    States
    {
      Spawn:
        FX03 B 1 Bright Light("BIGBOWBOLT") A_SpawnItemEx("CrossbowFX4",random2[BoltSpark]()*0.015625,random2[BoltSpark]()*0.015625,0,0,0,0,0,SXF_ABSOLUTEPOSITION,50)
        loop

      Death:
        FX03 H 8 Bright Light("BIGBOWBOLT_X1")
        FX03 I 8 Bright Light("BIGBOWBOLT_X2")
        FX03 J 8 Bright Light("BIGBOWBOLT_X3")
        stop
    }
}

actor CrossbowFX22 : CrossbowFX2
{
    DamageType "CorvusCrossbow"
    Damage 8
    SeeSound "heretic/bowshoot"
    DeathSound "heretic/bowhit"
    Decal CrossbowScorch2
    Species "Player"
    +BLOODSPLATTER
    +THRUSPECIES
    States
    {
      Spawn:
        FX03 B 1 Bright Light("BIGBOWBOLT") A_SpawnItemEx("CrossbowFX4",random2[BoltSpark]()*0.015625,random2[BoltSpark]()*0.015625,0,0,0,0,0,SXF_ABSOLUTEPOSITION,50)
        loop

      Death:
        FX03 H 8 Bright Light("BIGBOWBOLT_X1")
        FX03 I 8 Bright Light("BIGBOWBOLT_X2")
        FX03 J 8 Bright Light("BIGBOWBOLT_X3")
        stop
    }
}

actor CrossbowFX5 : CrossbowFX4 replaces CrossbowFX4
{
    DamageType "CorvusCrossbow"
    +CLIENTSIDEONLY
    Alpha 0.75
    Gravity 0.095 //Gravity 0.85 //+LOWGRAVITY //+NOGRAVITY
}
