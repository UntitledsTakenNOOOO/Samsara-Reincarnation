actor "AlienDisintegrator" : Corridor7WeaponBase
{
    Inventory.PickupMessage "You got the Alien Disintegrator!"
    Weapon.SlotNumber 7
    Weapon.SelectionOrder 1000
    Weapon.YAdjust 12
    Weapon.AmmoType1 "Cell"
    Weapon.AmmoGive1 200
    Weapon.AmmoUse1 40
    Tag "Alien Disintegrator"
    +BFG
    States
    {
      Spawn:
        7DN2 A -1
        stop

      Ready:
        C7DN A 1 A_WeaponReady
        loop

      Fire:
        TNT1 A 0 A_AlertMonsters
        C7DN A 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        TNT1 A 0 A_GiveInventory("SamsaraCorridor7AlienDisintegratorAttackHandler")
        C7DN B 5 Bright A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        C7DN CD 4 Bright A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        C7DN A 20 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        TNT1 A 0 A_ReFire
        goto Ready

      AltFire:
        C7DN A 0
        goto Super::AltFire
    }
}

actor SamsaraCorridor7AlienDisintegratorAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Cell",40,1)
        stop
        TNT1 A 0 A_TakeInventory("Cell",40)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("C7/AlienDisintegrator",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireCustomMissile("C7Disint",0,false)
        goto PickupFinish

      PickupAttackCoop:
        TNT1 A 0 A_FireCustomMissile("C7DisintCoop",0,false)
        goto PickupFinish

      // Finishing stuff
      PickupFinish:
        TNT1 A 0 A_JumpIfInventory("WeaponFlashesAreDisabled",1,"PickupStop")
        TNT1 A 0 A_SetBlend("Red",0.7,4)
        stop
    }
}

actor C7Disint
{
    Projectile
    Radius 2
    Height 2
    Speed 12
    DamageType "C7Disintegrate"
    ReactionTime 1
    Obituary "%o's existence was undone by %k's disintegrator."
    +DONTBLAST
    +DONTREFLECT
    +DONTSPLASH
    +NODAMAGETHRUST
    +NOTIMEFREEZE
    +NOTONAUTOMAP
    +NOTRIGGER
    +SKYEXPLODE
    States
    {
      Spawn:
        TNT1 A 2 A_Countdown
        loop

      Death:
        TNT1 A 1 A_BFGSpray("BlankPuff",20,10)
        stop
    }
}

actor C7DisintCoop : C7Disint
{
    Species "Player"
    +THRUSPECIES
    States
    {
      Death:
        TNT1 A 1 A_BFGSpray("BlankPuffCoop",100,16)
        stop
    }
}
