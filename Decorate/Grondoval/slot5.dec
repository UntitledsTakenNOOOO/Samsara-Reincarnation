Actor "Fire Mace" : WTWeapon
{
	Weapon.Slotnumber 5
	Weapon.SelectionOrder 1500
	+WEAPON.MELEEWEAPON
	+WEAPON.AMMO_OPTIONAL
	+THRUGHOST
	+WEAPON.NOALERT	
	Weapon.Ammotype "RocketAmmo"
	Weapon.AmmoUse 1
	Weapon.AmmoGive 30
    Inventory.PickupSound "gpickup/gen"
    Inventory.PickupMessage "You got the fire mace!"
	Inventory.RestrictedTo "GrondovalPlayer"	
	States
	{
	Spawn:
		PWHC A -1
		stop
	Select:
		TNT1 A 0 A_TakeInventory("WTFistActive",1)
		TNT1 A 0 A_GiveInventory("WTWeaponActive",1)
		WW50 A 1 A_Raise
		loop
	Deselect:
		WW50 A 0 A_TakeInventory("WTSlotVClip",999)
		TNT1 A 0 A_TakeInventory("WTWeaponActive",1)
		WW50 A 1 A_Lower
		loop	
	Ready:
		WW50 A 0 A_GunFlash //Call shield statemachine
		WW50 A 0 A_JumpIfInventory("RocketAmmo",1,"MegaWIdle")
	WIdle:
		WW50 A 0 A_JumpIfInventory("WTSlotVClip",1,2)
		WW50 A 0 A_JumpIfInventory("RocketAmmo",1,"MegaWIdle")
		WW50 A 2 A_WeaponReady
		loop
	MegaWIdle:
		WW50 A 0 A_PlayWeaponSound("ggen/buzzloop")
		WW51 ABCB 5 bright A_WeaponReady
		WW50 A 0 A_JumpIfInventory("RocketAmmo",1,"MegaWIdle")
		WW50 A 0 A_PlayWeaponSound("DSNONE")
		goto WIdle
	Altfire:
		WW50 A 0 A_JumpIfInventory("WTSlotVClip",1,"TakeClip")
	GiveClip:
		WW50 A 0 A_PlayWeaponSound("DSNONE")
		WW50 A 12 A_GiveInventory("WTSlotVClip",1)
		goto WIdle
	TakeClip:
		WW50 A 0 A_TakeInventory("WTSlotVClip",999)
		WW50 A 12 A_JumpIfInventory("RocketAmmo",1,1)
		goto WIdle
		WW50 A 0 A_PlayWeaponSound("ggen/buzzloop")
		WW51 CBA 4 bright
		goto MegaWIdle
	Fire:
		WW50 A 0 A_JumpIfInventory("WTSlotVClip",1,"NFire")
		WW50 A 0 A_JumpIfInventory("RocketAmmo",1,"MegaFire")
	NFire:
		WW50 A 0 A_JumpIfInventory("WTAttackClip",1,2)
		WW50 A 0 A_GiveInventory("WTAttackClip",1)
		goto Attack1
		WW50 A 0 A_TakeInventory("WTAttackClip",1)
		goto Attack2
	MegaFire:
		WW51 A 0 A_JumpIfInventory("WTAttackClip",1,2)
		WW51 A 0 A_GiveInventory("WTAttackClip",1)
		goto MegaAttack1
		WW51 A 0 A_TakeInventory("WTAttackClip",1)
		goto MegaAttack2
	Attack1:
		TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"CoopAttack1")
		WW50 BC 3
		WW50 D 3 A_PlayWeaponSound("ggen/swing3")
		WW50 E 3 A_GiveInventory("WTShieldDisable",1) //Disable shield, else the projectile gets blocked
		WW50 F 3 A_FireCustomMissile("WTFireMaceProjectile",0,0,4,8)
		WW50 G 3 A_TakeInventory("WTShieldDisable",999)
		TNT1 A 6
		WW50 A 2 /*offset(0,32)*/ A_Refire
		goto WIdle
	CoopAttack1:
		WW50 BC 3
		WW50 D 3 A_PlayWeaponSound("ggen/swing3")
		WW50 E 3 A_GiveInventory("WTShieldDisable",1) //Disable shield, else the projectile gets blocked
		WW50 F 3 A_FireCustomMissile("WTFireMaceProjectile_2",0,0,4,8)
		WW50 G 3 A_TakeInventory("WTShieldDisable",999)
		TNT1 A 6
		WW50 A 2 /*offset(0,32)*/ A_Refire
		goto WIdle		
	Attack2:
		TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"CoopAttack2")	
		WW50 H 3
		WW50 I 3 A_PlayWeaponSound("ggen/swing3")
		WW50 J 3 A_GiveInventory("WTShieldDisable",1) //Disable shield, else the projectile gets blocked
		WW50 K 3 A_FireCustomMissile("WTFireMaceProjectile_Weak",0,0,4,8)
		WW50 L 3 A_TakeInventory("WTShieldDisable",999)
		TNT1 A 6
		WW50 A 2 /*offset(0,32)*/ A_Refire
		goto WIdle
	CoopAttack2:
		WW50 H 3
		WW50 I 3 A_PlayWeaponSound("ggen/swing3")
		WW50 J 3 A_GiveInventory("WTShieldDisable",1) //Disable shield, else the projectile gets blocked
		WW50 K 3 A_FireCustomMissile("WTFireMaceProjectile_Weak_2",0,0,4,8)
		WW50 L 3 A_TakeInventory("WTShieldDisable",999)
		TNT1 A 6
		WW50 A 2 /*offset(0,32)*/ A_Refire
		goto WIdle
	MegaAttack1:
		TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"CoopMegaAttack1")
		TNT1 A 0 A_AlertMonsters
		WW51 DE 3 bright
		WW51 F 0 bright A_PlayWeaponSound("gspell/fire")
		WW51 F 3 bright A_GiveInventory("WTShieldDisable",1) //Disable shield, else the projectile gets blocked
		WW51 G 3 bright A_FireCustomMissile("WTFireMaceBall",0,1,4,6)
		WW51 H 3 bright A_FireCustomMissile("WTFireMaceBall",0,0,4,6)
		WW51 I 3 bright A_FireCustomMissile("WTFireMaceBall",0,0,4,6)
		goto MA1_Done
	CoopMegaAttack1:
		TNT1 A 0 A_AlertMonsters	
		WW51 DE 3 bright
		WW51 F 0 bright A_PlayWeaponSound("gspell/fire")
		WW51 F 3 bright A_GiveInventory("WTShieldDisable",1) //Disable shield, else the projectile gets blocked	
		WW51 G 3 bright A_FireCustomMissile("WTFireMaceBall_2",0,1,4,6)
		WW51 H 3 bright A_FireCustomMissile("WTFireMaceBall_2",0,0,4,6)
		WW51 I 3 bright A_FireCustomMissile("WTFireMaceBall_2",0,0,4,6)
		goto MA1_Done	
	MA1_Done:
		TNT1 A 6 A_TakeInventory("WTShieldDisable",999)
		WW51 B 2 /*offset(0,32)*/ A_Refire
		goto WIdle
	MegaAttack2:
		TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"CoopMegaAttack2")	
		TNT1 A 0 A_AlertMonsters	
		WW51 J 3
		WW51 K 0 A_PlayWeaponSound("gspell/fire")
		WW51 K 3 A_GiveInventory("WTShieldDisable",1) //Disable shield, else the projectile gets blocked
		WW51 L 3 bright A_FireCustomMissile("WTFireMaceBall",0,1,4,6)
		WW51 M 3 bright A_FireCustomMissile("WTFireMaceBall",0,0,4,6)
		WW51 N 3 bright A_FireCustomMissile("WTFireMaceBall",0,0,4,6)
		goto MA2_Done
	CoopMegaAttack2:
		TNT1 A 0 A_AlertMonsters	
		WW51 J 3
		WW51 K 0 A_PlayWeaponSound("gspell/fire")
		WW51 K 3 A_GiveInventory("WTShieldDisable",1) //Disable shield, else the projectile gets blocked	
		WW51 L 3 bright A_FireCustomMissile("WTFireMaceBall_2",0,1,4,6)
		WW51 M 3 bright A_FireCustomMissile("WTFireMaceBall_2",0,0,4,6)
		WW51 N 3 bright A_FireCustomMissile("WTFireMaceBall_2",0,0,4,6)
		goto MA2_Done	
	MA2_Done:
		TNT1 A 6 A_TakeInventory("WTShieldDisable",999)
		WW51 B 2 /*offset(0,32)*/ A_Refire
		goto WIdle
	}
}

Actor WTFireMaceProjectile : WTMeleeProjectile {
	Speed 51
	Damage (random(45,70))
	Obituary "$SAMSARA_WITCHAVEN_OB_SLOT_V"
	States {
		XDeath: //If we hit a bleeding actor, we spawn some gibs
		TNT1 A 0 A_SpawnItemEx("WTGore",0,0,0,random(-3,3),random(-3,3),random(0,4))
		TNT1 A 1 A_PlaySoundEx("gfist/hit",CHAN_BODY,0,0)
		stop
	}
}

Actor WTFireMaceProjectile_2 : WTFireMaceProjectile { Species "Player" +THRUSPECIES +DONTHARMCLASS +DONTHARMSPECIES +MTHRUSPECIES +FORCEPAIN }

Actor WTFireMaceProjectile_Weak : WTMeleeProjectile { 
	Speed 51 
	Damage (random(50,65)) 
	Obituary "$SAMSARA_WITCHAVEN_OB_SLOT_V"
	States {
		XDeath: //If we hit a bleeding actor, we spawn some gibs
		TNT1 A 0 A_SpawnItemEx("WTGore",0,0,0,random(-3,3),random(-3,3),random(0,4))
		TNT1 A 1 A_PlaySoundEx("gfist/hit",CHAN_BODY,0,0)
		stop
	}
}

Actor WTFireMaceProjectile_Weak_2 : WTFireMaceProjectile_Weak { Species "Player" +THRUSPECIES +DONTHARMCLASS +DONTHARMSPECIES +MTHRUSPECIES +FORCEPAIN }

Actor WTFireMaceBall : FastProjectile
{
	PROJECTILE
//	+SEEKERMISSILE -- OP
	+FORCERADIUSDMG
//	+SCREENSEEKER //ZDoom flag... -- OP
	Speed 50
	Radius 7
	Height 8
	Damagetype "WTRangedFire"
	Damage (25) //(random(25,35))
	Obituary "$SAMSARA_WITCHAVEN_OB_SLOT_V_MEGA"
	scale 0.75
	States
	{
	Spawn:
		WP61 ABCD 4 bright //A_SeekerMissile(3,30,SMF_LOOK|SMF_PRECISE,100)
		WP61 A 0 A_SpawnItemEx("WTFireMaceTrail",0,0,0,random(-1,1),random(-1,1),random(-1,1),SXF_CLIENTSIDE)
		loop
	Death:
		WP50 A 0 A_Explode(random(15,30),128,0)
		TNT1 A 1 A_SpawnItemEx("WTFireMaceDeath")//,0,0,0,0,0,0,0,0,SXF_CLIENTSIDE) SOMETIMES THE PROJECTILE DOESNT SPAWN ITS DEATH ANIMATION WELL OKAY THEN
		stop
	}
}

Actor WTFireMaceBall_2 : WTFireMaceBall 
{ 
	Species "Player" 
	+THRUSPECIES 
	+DONTHARMCLASS 
	+DONTHARMSPECIES 
	+MTHRUSPECIES 
	States
	{
	Spawn:
		WP61 ABCD 4 bright A_SeekerMissile(5,50,SMF_LOOK|SMF_PRECISE,100) //NERFED
		WP61 A 0 A_SpawnItemEx("WTFireMaceTrail",0,0,0,random(-1,1),random(-1,1),random(-1,1),SXF_CLIENTSIDE)
		loop
	}
}
	
Actor WTFireMaceDeath
{
	+NOGRAVITY
	-SOLID
	+NOINTERACTION
	+NOBLOCKMAP
	+ALLOWCLIENTSPAWN
	+CLIENTSIDEONLY
	scale 0.5
	states
	{
	Spawn:
		TNT1 A 0
	Death:
		WP61 AAAAA 0 A_SpawnItemEx("WTFireMaceTrail",0,0,0,random(-1,1),random(-1,1),random(-1,1),0,0,192)
		WP50 ABCDEFGHIJKLM 2 bright
		stop
	}
}

Actor WTFireMaceTrail
{
	+NOGRAVITY
	-SOLID
	+NOINTERACTION
	+NOBLOCKMAP
	+ALLOWCLIENTSPAWN
	+CLIENTSIDEONLY
	renderstyle translucent
	alpha 1.0
	states
	{
	Spawn:
		TNT1 A 0
	Death:
		WP61 FGHI 5 A_FadeOut(0.08)
		loop
	}
}