actor Bazooka : SamsaraROTTWeapon
{
    Inventory.PickupMessage "You bagged a bazooka!"
    Weapon.UpSound "gen/wepswitch"
    Weapon.SlotNumber 1
    Weapon.SelectionOrder 1300
    Weapon.AmmoType "RottMissiles"
    Weapon.AmmoGive 15
    Weapon.AmmoUse 1
    Obituary "%o was on the wrong end of %k's bazooka."
    Tag "Bazooka"
    +EXPLOSIVE
    +IGNORESKILL
    States
    {
      Spawn:
        PBAZ A -1
        stop

      Ready:
        TNT1 A 0 A_TakeInventory("UsingFirearm")
        BAZO A 1 A_WeaponReady
        loop

      Deselect:
        TNT1 A 0 A_JumpIfHealthLower(1,"DeselectInstant")
        BAZO A 1 A_Lower
        loop

      Select:
        BAZO A 1 A_Raise
        loop

      Fire:
        BAZO E 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        TNT1 A 0 A_GiveInventory("SamsaraROTTBazookaAttackHandler")
        BAZO F 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        BAZO G 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        BAZO BC 1 Bright A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        BAZO D 2 Bright A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        BAZO H 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
      FireSemi1Check:
        TNT1 A 0 A_JumpIfInventory("PowerGodProtection",1,"FireForceSwitch1")
        TNT1 A 0 A_ReFire(1)
        goto FireSemi1
        BAZO A 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
      FireSemi2Check:
        TNT1 A 0 A_JumpIfInventory("PowerGodProtection",1,"FireForceSwitch2")
        TNT1 A 0 A_ReFire(1)
        goto FireSemi2
        BAZO A 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
      FireSemi3Check:
        TNT1 A 0 A_JumpIfInventory("PowerGodProtection",1,"FireForceSwitch3")
        TNT1 A 0 A_ReFire(1)
        goto FireSemi3
        BAZO A 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
      FireSemi4Check:
        TNT1 A 0 A_JumpIfInventory("PowerGodProtection",1,"FireForceSwitch4")
        TNT1 A 0 A_ReFire(1)
        goto FireSemi4
        BAZO A 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        goto Ready

      FireSemi1:
        TNT1 A 0 A_JumpIfInventory("PowerGodProtection",1,"FireForceSwitch1")
        BAZO A 2 A_WeaponReady(WRF_NOSWITCH)
      FireSemi2:
        TNT1 A 0 A_JumpIfInventory("PowerGodProtection",1,"FireForceSwitch2")
        BAZO A 2 A_WeaponReady(WRF_NOSWITCH)
      FireSemi3:
        TNT1 A 0 A_JumpIfInventory("PowerGodProtection",1,"FireForceSwitch3")
        BAZO A 2 A_WeaponReady(WRF_NOSWITCH)
      FireSemi4:
        TNT1 A 0 A_JumpIfInventory("PowerGodProtection",1,"FireForceSwitch4")
        BAZO A 1 A_WeaponReady(WRF_NOSWITCH)
        goto Ready

      FireForceSwitch1:
        TNT1 A 0 A_ClearReFire
        BAZO A 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
      FireForceSwitch2:
        TNT1 A 0 A_ClearReFire
        BAZO A 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
      FireForceSwitch3:
        TNT1 A 0 A_ClearReFire
        BAZO A 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
      FireForceSwitch4:
        TNT1 A 0 A_ClearReFire
        BAZO A 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        goto Ready
    }
}

actor SamsaraROTTBazookaAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("RottMissiles",1,1)
        stop
        TNT1 A 0 A_TakeInventory("RottMissiles",1)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("bazooka/shoot",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireCustomMissile("RottMissile",0,false,0,-8) // -16
        stop

      PickupAttackCoop:
        TNT1 A 0 A_FireCustomMissile("RottMissileCoop",0,false,0,-8) // -16
        stop
    }
}

actor RottMissiles : Ammo
{
    Inventory.MaxAmount 100
    Ammo.BackpackAmount 5 // skills with different AmmoFactor values and sv_doubleammo can still give extra regardless of +IGNORESKILL
    Ammo.BackpackMaxAmount 200
    Inventory.Icon "AMSMA0"
    Tag "Bazooka Missiles"
    +IGNORESKILL
}

actor RottMissile
{
    Projectile
    Radius 11
    Height 8
    Speed 42
    Damage (0)
    DamageType "RottExplosive"
    Scale 0.5
    DeathSound "god/bang"
    Decal "Scorch"
    Obituary "%o was on the wrong end of %k's bazooka."
    +BLOODLESSIMPACT
    +EXTREMEDEATH
    +FORCERADIUSDMG
    +WINDTHRUST
    States
    {
      Spawn:
        M1SS A 0 Bright Light("ROTTROCKET")
      SpawnLoop:
        TNT1 A 0 A_PlaySound("missile/fly",CHAN_BODY,1,true)
        M1SS A 1 Bright Light("ROTTROCKET") A_CustomMissile("MissileTrail",0)
        loop

      Death:
        TNT1 A 0 A_PlaySound("silence")
        TNT1 A 0 A_StopSound(CHAN_BODY)
        TNT1 A 0 A_PlaySound("gen/expl2")
        TNT1 A 0 A_Explode(55,48,XF_HURTSOURCE,false,24)
      DeathFinish:
        EXPL ABCDEFGHIJKLMNOPQRST 2 Bright Light("ROTTROCKETEXP")
        stop

      Crash:
        TNT1 A 0 A_Jump(250,"Death")
        TNT1 A 0 A_PlaySound("rmissile/cook",CHAN_5)
        goto Death
    }
}

actor RottMissileCoop : RottMissile
{
    Speed 50
    Damage (25)
    Species "Player"
    +THRUSPECIES
}

actor MissileTrail
{
    Projectile
    Radius 2
    Height 2
    Scale 0.65
    +CLIENTSIDEONLY
    +NOINTERACTION
    +RANDOMIZE
    States
    {
      Spawn:
        TNT1 A 4
        MTRL A 4 Bright
        MTRL BBCCDD 1 Bright A_FadeOut
        stop
    }
}
