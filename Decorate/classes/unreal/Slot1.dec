Actor Unreal_RazorJack : UnrealWeapon
{
	Tag "RazorJack"
	Weapon.SlotNumber 1
	Weapon.SlotPriority 0
	Weapon.AmmoType "Cell"
	Weapon.AmmoGive 15
	Weapon.AmmoUse 1
	Inventory.PickupMessage "You got the RazorJack."
    Inventory.Icon "UNCMPW03"
	Obituary "%k took a bloody chunk out of %o with the RazorJack."
	+ALT_USES_BOTH // only one ammo type with same ammo amount use, so this makes alt fire behave as intended
	States
	{
		Spawn:
			UN00 A 10
			UN00 A 10 Bright
			loop

		ReadyActual:
			UN04 ABCDEFGHIJKLMNOP 2 A_WeaponReady
			loop

		Deselect:
			UN04 A 0 A_TakeInventory("Unreal_RazorJackSelected")
			UN04 A 2 A_JumpIfHealthLower(1,"DeselectInstant")
			UN08 ABC 2
            UN08 D 0
			goto DeselectInstant

		Ready: // our "Select" state
			UN01 A 0 A_GiveInventory("Unreal_RazorJackSelected")
			UN01 A 0 A_GiveInventory("SamsaraUnrealRazorjackSelectSoundPlayer")
			UN01 A 0 ACS_NamedExecuteWithResult("UnrealWeaponTargeter", 0)
			UN01 ABCDEFGHIJKLMNOP 2 A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
			UN03 ABCDE 1 A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
			goto ReadyActual

		Fire:
			UN02 A 0 A_GiveInventory("SamsaraUnrealRazorjackPrimaryAttackHandler")
			UN02 AB 2 Bright A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			UN02 C 3 Bright A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			UN02 DEF 1 Bright A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			UN03 ABCD 1 Bright A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			UN03 E 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
			UN03 E 0 A_JumpIfInventory("Cell",1,1)
			goto FireFinish
			UN03 E 0 A_ReFire
			goto FireFinish

		FireFinish:
			UN03 E 0 A_ClearReFire
			UN03 E 2 Bright A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			UN04 A 0 A_CheckReload
			goto ReadyActual

		AltFire:
			UN05 ABCDEFGHI 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
		AltHold:
			UN05 J 0 A_GiveInventory("SamsaraUnrealRazorjackSecondaryAttackHandler")
			UN05 JKL 2 Bright A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			UN06 ABC 2 Bright A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			UN06 D 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
			UN06 D 0 A_JumpIfInventory("Cell",1,1)
			goto AltFireFinish
			UN06 D 0 A_ReFire
			goto AltFireFInish

		AltFireFinish:
			UN06 D 0 A_ClearReFire
			UN06 D 2 Bright A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			UN07 ABCDE 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			UN04 A 0 A_CheckReload
			goto ReadyActual
	}
}

actor SamsaraUnrealRazorjackPrimaryAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Cell",1,1)
        stop
        TNT1 A 0 A_TakeInventory("Cell",1)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_JumpIfInventory("SamsaraAlternateSoundToken",1,"PickupAttackUnreal")
      // Unreal Gold sounds
      PickupAttackUnrealGold:
        TNT1 A 0 A_PlaySound("Unreal/RazorJack/Fire",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackUnrealGoldCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackUnrealGoldCoop")
      PickupAttackUnrealGoldDM:
        TNT1 A 0 A_FireCustomMissile("Unreal_Razor1",0,false,0,8)
        goto PickupFinish

      PickupAttackUnrealGoldCoop:
        TNT1 A 0 A_FireCustomMissile("Unreal_Razor1Coop",0,false,0,8)
        goto PickupFinish

      // Unreal sounds
      PickupAttackUnreal:
        TNT1 A 0 A_PlaySound("UnrealOld/RazorJack/Fire",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackUnrealCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackUnrealCoop")
      PickupAttackUnrealDM:
        TNT1 A 0 A_FireCustomMissile("Unreal_Razor1SoundUnreal",0,false,0,8)
        goto PickupFinish

      PickupAttackUnrealCoop:
        TNT1 A 0 A_FireCustomMissile("Unreal_Razor1CoopSoundUnreal",0,false,0,8)
        goto PickupFinish

      // Finishing stuff
      PickupFinish:
        TNT1 A 0 ACS_NamedExecuteWithResult("Unreal_Recoil")
        TNT1 A 0 A_JumpIfInventory("WeaponFlashesAreDisabled",1,"PickupStop")
        TNT1 A 0 A_SetBlend("9F2AE3",0.15,10)
        stop
    }
}

actor SamsaraUnrealRazorjackSecondaryAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Cell",1,1)
        stop
        TNT1 A 0 A_TakeInventory("Cell",1)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_JumpIfInventory("SamsaraAlternateSoundToken",1,"PickupAttackUnreal")
      // Unreal Gold sounds
      PickupAttackUnrealGold:
        TNT1 A 0 A_PlaySound("Unreal/RazorJack/Fire",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackUnrealGoldCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackUnrealGoldCoop")
      PickupAttackUnrealGoldDM:
        TNT1 A 0 A_FireCustomMissile("Unreal_Razor2",0,false,-16,8)
        stop //goto PickupFinish

      PickupAttackUnrealGoldCoop:
        TNT1 A 0 A_FireCustomMissile("Unreal_Razor2Coop",0,false,-16,8)
        stop //goto PickupFinish

      // Unreal sounds
      PickupAttackUnreal:
        TNT1 A 0 A_PlaySound("UnrealOld/RazorJack/Fire",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackUnrealCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackUnrealCoop")
      PickupAttackUnrealDM:
        TNT1 A 0 A_FireCustomMissile("Unreal_Razor2SoundUnreal",0,false,0,8)
        stop //goto PickupFinish

      PickupAttackUnrealCoop:
        TNT1 A 0 A_FireCustomMissile("Unreal_Razor2CoopSoundUnreal",0,false,-16,8)
        stop //goto PickupFinish

      // Finishing stuff
      PickupFinish:
        TNT1 A 0 ACS_NamedExecuteWithResult("Unreal_Recoil")
        TNT1 A 0 A_JumpIfInventory("WeaponFlashesAreDisabled",1,"PickupStop")
        TNT1 A 0 A_SetBlend("9F2AE3",0.15,10)
        stop
    }
}

actor SamsaraUnrealRazorjackSelectSoundPlayer : Trigger
{
    States
    {
      // Sound playing stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraAlternateSoundToken",1,"PickupUnreal")
      // Unreal Gold sounds
      PickupUnrealGold:
        TNT1 A 0 A_PlaySound("Unreal/RazorJack/Select")
        stop

      // Unreal sounds
      PickupUnreal:
        TNT1 A 0 A_PlaySound("UnrealOld/RazorJack/Select")
        stop
    }
}

Actor Unreal_Razor1
{
	Projectile
	Radius 5
	Height 3
	Damage (0)
	Speed 26
	BounceCount 3
	BounceType Doom
	BounceSound "Unreal/RazorJack/Hitwall"
	DamageType "UnrealHeadshot"
	var int user_damage;
	ExplosionDamage 0
	+BOUNCEONFLOORS
	+BOUNCEONCEILINGS
	+BOUNCEONWALLS
	+CANBOUNCEWATER
	+NOEXTREMEDEATH
	+HITTRACER
	+FORCERADIUSDMG
	+NODAMAGETHRUST
	+BRIGHT
	States
	{
		Spawn:
			UN00 A 0
			UN00 A 0 A_PlaySound("Unreal/RazorJack/BladeLoop", CHAN_BODY, 1.0, True)
			Goto SpawnLoop
		SpawnLoop:
			UN00 ABCDEFGH 1
			Loop
		Death:
		Crash:
			TNT1 A 0 A_PlaySound("Unreal/RazorJack/Hitwall", CHAN_BODY)
			TNT1 A 2 A_Explode(CallACS("UnrealHitBox",30, 105),12,0,0,12)
			Stop
		XDeath:
			TNT1 A 1 ACS_NamedExecuteWithResult("Unreal_BloodGenerator_Offsets")
			TNT1 A 0 A_GiveInventory("UnrealBloodyPuffGenerator",1,AAPTR_TRACER)
			TNT1 A 0 A_PlaySound("Unreal/RazorJack/HitFlesh", CHAN_BODY)
			TNT1 A 2 A_Explode(CallACS("UnrealHitBox",30, 105),12,0,0,12)
			Stop
	}
}

Actor Unreal_Razor1Coop : Unreal_Razor1 { +THRUSPECIES Species "Player" }

Actor Unreal_Razor2 : Unreal_Razor1
{
	Speed 24
	States
	{
		Spawn:
			UN00 A 0
			UN00 A 0 ACS_NamedExecuteAlways("GuidedMissileFollow",0,0,0,0)
			Goto Super::Spawn
		Death:
		Crash:
			TNT1 A 0 A_PlaySound("Unreal/RazorJack/Hitwall", CHAN_BODY)
			Goto DeathFinish
		XDeath:
			TNT1 A 1 ACS_NamedExecuteWithResult("Unreal_BloodGenerator_Offsets")
			TNT1 A 0 A_GiveInventory("UnrealBloodyPuffGenerator",1,AAPTR_TRACER)
			TNT1 A 0 A_PlaySound("Unreal/RazorJack/HitFlesh", CHAN_BODY)
			Goto DeathFinish
		DeathFinish:
			TNT1 A 2 A_Explode(CallACS("UnrealHitBox",30, 105),12,0,0,12)
			TNT1 A 0 ACS_NamedExecuteAlways("GuidedMissileCleanup",0,0,0,0)
			Stop
	}
}

Actor Unreal_Razor2Coop : Unreal_Razor2 { +THRUSPECIES Species "Player" }

Actor Unreal_Razor1SoundUnreal : Unreal_Razor1
{
	BounceSound "UnrealOld/RazorJack/Hitwall"
	States
	{
		Spawn:
			UN00 A 0
			UN00 A 0 A_PlaySound("UnrealOld/RazorJack/BladeLoop", CHAN_BODY, 1.0, True)
			Goto SpawnLoop
		Death:
		Crash:
			TNT1 A 0 A_PlaySound("UnrealOld/RazorJack/Hitwall", CHAN_BODY)
			TNT1 A 2 A_Explode(CallACS("UnrealHitBox",30, 105),12,0,0,12)
			Stop
		XDeath:
			TNT1 A 1 ACS_NamedExecuteWithResult("Unreal_BloodGenerator_Offsets")
			TNT1 A 0 A_GiveInventory("UnrealBloodyPuffGenerator",1,AAPTR_TRACER)
			TNT1 A 0 A_PlaySound("UnrealOld/RazorJack/HitFlesh", CHAN_BODY)
			TNT1 A 2 A_Explode(CallACS("UnrealHitBox",30, 105),12,0,0,12)
			Stop
	}
}

Actor Unreal_Razor1CoopSoundUnreal : Unreal_Razor1SoundUnreal { +THRUSPECIES Species "Player" }

Actor Unreal_Razor2SoundUnreal : Unreal_Razor1SoundUnreal
{
	Speed 24
	States
	{
		Spawn:
			UN00 A 0
			UN00 A 0 ACS_NamedExecuteAlways("GuidedMissileFollow",0,0,0,0)
			Goto Super::Spawn
		Death:
		Crash:
			TNT1 A 0 A_PlaySound("UnrealOld/RazorJack/Hitwall", CHAN_BODY)
			Goto DeathFinish
		XDeath:
			TNT1 A 1 ACS_NamedExecuteWithResult("Unreal_BloodGenerator_Offsets")
			TNT1 A 0 A_GiveInventory("UnrealBloodyPuffGenerator",1,AAPTR_TRACER)
			TNT1 A 0 A_PlaySound("UnrealOld/RazorJack/HitFlesh", CHAN_BODY)
			Goto DeathFinish
		DeathFinish:
			TNT1 A 2 A_Explode(CallACS("UnrealHitBox",30, 105),12,0,0,12)
			TNT1 A 0 ACS_NamedExecuteAlways("GuidedMissileCleanup",0,0,0,0)
			Stop
	}
}

Actor Unreal_Razor2CoopSoundUnreal : Unreal_Razor2SoundUnreal { +THRUSPECIES Species "Player" }