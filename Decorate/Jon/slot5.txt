actor PSCobraStaff : Weapon
{
  Inventory.PickupSound "PS/Ipickup2"
  Weapon.SelectionOrder 600
  Weapon.AmmoUse 1
  Weapon.AmmoGive 20
  Weapon.BobStyle Smooth
  Weapon.BobRangeX 0.75
  Weapon.BobRangeY 0.28
  Weapon.BobSpeed 1.4  
  Weapon.AmmoType "Rocketammo"
  Weapon.SlotNumber 5
  Inventory.PickupMessage "You got the Cobra Staff!"
  Obituary "%o was devoured by %k's Cobra."
  Inventory.RestrictedTo "PSPlayer"
  Tag "Cobra Staff"
  States
  {
  Ready:
	C_H_ Z 3 A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
	C_H_ L 1 A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
	TNT1 A 0 A_Playsound("Jon/CobraHI",CHAN_WEAPON)
	C_H_ L 2 A_WeaponReady(WRF_NOBOB|WRF_NOFIRE) 
	C_H_ M 1 A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
	C_H_ M 2 A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)	
	C_H_ N 1 A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
	C_H_ N 2 A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)	
	C_H_ OYXW 4 A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
	Goto LoopReady
  LoopReady:
    C_H_ V 1 A_WeaponReady
    Loop
  Deselect:
    C_H_ U 1 A_Lower
    Loop
  Select:
    C_H_ Z 1 A_Raise
    Loop
  Fire:
    C_H_ U 1 BRIGHT 
  Hold:	
    C_H_ A 1 BRIGHT A_PlaySound("Jon/CobraStart",CHAN_BODY)
	C_H_ B 4 BRIGHT
	C_H_ C 4 BRIGHT
	C_H_ D 3 BRIGHT
	TNT1 A 0 A_PlaySound("Jon/CobraFire",CHAN_WEAPON)	
	C_H_ E 2 BRIGHT
	TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,2)
    C_H_ F 1 BRIGHT A_FireCustomMissile("CobraStaffSnake",0,1,0,0)
	Goto A
    C_H_ F 1 BRIGHT A_FireCustomMissile("CobraStaffSnake_2",0,1,0,0)	
  A:	
	TNT1 A 0 A_Recoil(5)
	TNT1 A 0 A_SpawnItemEx("CobraFakeFlash",cos(pitch)*55,0,40-(sin(pitch)*40),cos(pitch)*0,0,-sin(pitch)*0,0,SXF_CLIENTSIDE|SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH)
	C_H_ F 1 offset(0,38) BRIGHT
	C_H_ F 1 offset(0,39) BRIGHT
    C_H_ G 3 offset(0,37) BRIGHT
	C_H_ G 2 offset(0,40) BRIGHT
	C_H_ G 1 offset(0,38) BRIGHT
	C_H_ G 1 offset(0,36) BRIGHT
	C_H_ G 1 offset(0,30) BRIGHT
	C_H_ G 1 offset(0,26) BRIGHT A_Refire	
    Goto LoopReady
  Spawn:
    WCST AAAAAAAAAAAAAAABBBCCCDDDEEEFFF 1 Bright
    Loop
  }
}

Actor CobraFakeFlash 
{
	Scale 0.45
	Radius 1
	Height 1
	Speed 0
	+NOGRAVITY
	-SOLID
	+NOCLIP
	+CLIENTSIDEONLY
	+NOINTERACTION
	+FORCEXYBILLBOARD
	states
	{
		Spawn:
			C_H2 GGH 1 Bright
			C_H2 H 1 A_FadeOut(1.0/20.5)
			Stop
	}
}
  
Actor CobraStaffSnake
{
	scale 0.5
	radius 5
	height 10
	speed 23
	damage (100)
	deathsound "Jon/CobraDeath"
	activesound "Jon/CobraTrail"
	DamageType "PSExplosive"
	PROJECTILE
	+SEEKERMISSILE
	states
	{
		Spawn:
			TNT1 A 0
			TNT1 A 0 A_LoopActiveSound
			C_H2 AAAAA 1 Bright A_SpawnItemEx("CobraStaffTail",-12,0,0,0,0,0,0,SXF_CLIENTSIDE)
			Goto SpawnLoop
		SpawnLoop:
			C_H2 A 0 A_SpawnItemEx("CobraStaffTail",-12,0,0,0,0,0,0,SXF_CLIENTSIDE)
			C_H2 A 0 A_SeekerMissile(2,3)
			C_H2 A 1 Bright A_Weave(4.5,0,2.0,0.0)
			loop
		Death:
			C_H2 A 1 bright A_Jump(150,"Style2")
			C_H2 C 4 Bright A_Explode(126,90)	
			C_H2 D 4 Bright
			C_H2 E 4 Bright
			C_H2 F 4 Bright
			stop
		Style2:
			C_H2 I 4 Bright A_Explode(150,90)
			C_H2 J 4 Bright
			C_H2 K 4 Bright
			C_H2 L 4 Bright
			C_H2 M 4 Bright A_FadeOut(1.5/30.0)
			Stop
	}
}

Actor CobraStaffSnake_2 : CobraStaffSnake { Species "Player" +THRUSPECIES +DONTHARMCLASS +DONTHARMSPECIES +MTHRUSPECIES -THRUGHOST }

Actor CobraStaffTail
{
	scale 0.5
	speed 0
	PROJECTILE
	+FORCEXYBILLBOARD
	+CLIENTSIDEONLY
	states
	{
		Spawn:
			C_H2 B 2 Bright
			C_H2 B 0 Bright A_SpawnItem("CobraStaffTail2",0,0,0)
			stop
	}
}

Actor CobraStaffTail2 : CobraStaffTail
{
	scale 0.4
	states
	{
		Spawn:
			C_H2 B 2 Bright
			C_H2 B 0 Bright A_SpawnItem("CobraStaffTail3",0,0,0)
			stop
	}
}

Actor CobraStaffTail3 : CobraStaffTail
{
	scale 0.3
	states
	{
		Spawn:
			C_H2 B 2 Bright
			C_H2 B 0 Bright A_SpawnItem("CobraStaffTail4",0,0,0)
			stop
	}
}

Actor CobraStaffTail4 : CobraStaffTail
{
	scale 0.2
	states
	{
		Spawn:
		C_H2 B 2 Bright
		C_H2 B 0 Bright A_SpawnItem("CobraStaffTail5",0,0,0)
		stop
	}
}

Actor CobraStaffTail5 : CobraStaffTail
{
	scale 0.1
	states
	{
		Spawn:
			C_H2 B 2 Bright
			stop
	}
}