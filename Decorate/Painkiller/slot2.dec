ACTOR Painkiller_Shotgun : PainkillerWeapon
{
	Weapon.SelectionOrder 900
	Weapon.SlotNumber 2
	Weapon.AmmoType1 "Shell"
	Weapon.AmmoUse1 2
	Weapon.AmmoGive1 20
	Weapon.AmmoType2 "Painkiller_FreezerAmmo"
	Weapon.AmmoUse2 1
	Weapon.AmmoGive2 0
	Obituary "%o was blasted by %k's shotgun."
	Inventory.Pickupmessage "Shotgun"
	Inventory.PickupSound "Painkiller/Shotgun/Pickup"
	Tag "Shotgun/Freezer"
	-WEAPON.CHEATNOTWEAPON
	States
	{
		Select:
			TNT1 A 0 A_TakeInventory("Painkiller_HoldingStake",1)
			TNT1 A 0 A_TakeInventory("Painkiller_HoldingRifle",1)
			TNT1 A 0 A_TakeInventory("Painkiller_HoldingChaingun",1)
			TNT1 A 0 A_TakeInventory("Painkiller_HoldingElectro",1)
			TNT1 A 0 A_TakeInventory("Painkiller_HoldingBolt",1)
			TNT1 A 0 A_TakeInventory("Painkiller_HoldingDemon",1)
			TNT1 A 0 A_GiveInventory("Painkiller_HoldingShotgun",1)
			Goto Super::Select
		Deselect:
			TNT1 AAAAAAAAAAAAAAAAAAAA 0 A_Lower
			TNT1 A 1 A_Lower
			TNT1 A 0 A_TakeInventory("Painkiller_HoldingShotgun",1)
			Loop
		Ready:
			PK00 ABCDEFGHI 10 A_WeaponReady
			Loop
		Fire:
			PK01 A 0 A_GunFlash
			PK01 A 0 A_PlaySound("Painkiller/Shotgun/Fire",CHAN_WEAPON)
			PK01 A 0 A_SpawnItemEx("Painkiller_Shotgun_FireSmoke",4,4,38,0,-0.1,0.1,0,SXF_NOCHECKPOSITION)
			PK01 A 0 A_SpawnItemEx("Painkiller_Shotgun_FireSmoke",4,4,38,0,0.1,0.1,0,SXF_NOCHECKPOSITION)
			PK01 A 0 A_JumpIfInventory("SamsaraHasPermaWeaponLevel2",1,"FireMod")
			PK01 A 0 A_JumpIfInventory("Painkiller_WeaponModifier_Token",1,"FireMod")
			PK01 A 0 A_JumpIfInventory("CoopModeOn",1,2)
			PK01 A 0 A_FireBullets(8,8,15,8,"Painkiller_Shotgun_Puff",FBF_NORANDOM|FBF_USEAMMO)
			Goto FireEnd
			PK01 A 0 A_FireBullets(8,8,15,8,"Painkiller_Shotgun_PuffCoop",FBF_NORANDOM|FBF_USEAMMO)
			Goto FireEnd
		FireDM:
			PK01 A 0 A_JumpIfInventory("SamsaraHasPermaWeaponLevel2",1,"FireDMMod")
			PK01 A 0 A_JumpIfInventory("Painkiller_WeaponModifier_Token",1,"FireDMMod")
			PK01 A 0 A_FireBullets(8,8,24,4,"Painkiller_Shotgun_Puff",FBF_NORANDOM|FBF_USEAMMO)
			Goto FireEnd
		FireMod:
			PK01 A 0 A_JumpIfInventory("CoopModeOn",1,2)
			PK01 A 0 A_FireBullets(2.5,2.5,24,8,"Painkiller_Shotgun_Puff",FBF_NORANDOM|FBF_USEAMMO)
			Goto FireEnd
			PK01 A 0 A_FireBullets(2.5,2.5,24,8,"Painkiller_Shotgun_PuffCoop",FBF_NORANDOM|FBF_USEAMMO)
			Goto FireEnd
		FireDMMod:
			PK01 A 0 A_FireBullets(2.5,2.5,24,5,"Painkiller_Shotgun_Puff",FBF_NORANDOM|FBF_USEAMMO)
			Goto FireEnd
		FireEnd:
			PK01 A 2 A_Quake(4,5,0,4,"")
			PK01 BCDEFGHJKM 2
			PK01 NP 1
			Goto Ready
		Flash:
			PK03 ABCDE 1 Bright
			Stop
		AltFire:
			PK02 A 0 A_PlaySound("Painkiller/Freezer/Fire",CHAN_WEAPON)
			PK02 A 0 A_JumpIfInventory("CoopModeOn",1,3)
			PK02 A 0 A_FireCustomMissile("Painkiller_Shotgun_IceShot",0,0,-5)
			PK02 A 3 A_FireCustomMissile("Painkiller_Shotgun_IceShot",0,0,5)
			Goto AltFireEnd
			PK02 A 0 A_FireCustomMissile("Painkiller_Shotgun_IceShotCoop",0,0,-5)
			PK02 A 3 A_FireCustomMissile("Painkiller_Shotgun_IceShotCoop",0,0,5)
			Goto AltFireEnd
		AltFireEnd:
			PK02 A 0 A_TakeInventory("Painkiller_FreezerAmmo",1,TIF_NOTAKEINFINITE)
			PK02 BC 3 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			PK02 DEFGHIJKLMNOP 3 A_WeaponReady(WRF_NOSECONDARY|WRF_NOSWITCH)
			Goto Ready
		Spawn:
			PK99 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 1 A_SpawnItemEx("Painkiller_Shotgun_Icon",0,0,40)
			PK99 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 1 A_SpawnItemEx("Painkiller_Shotgun_Icon2",0,0,40)
			Loop
	}
}

Actor Painkiller_Shotgun_Icon : Painkiller_Weapon_Icon
{
	States
	{
		Spawn:
			PK07 R 2
			Stop
	}
}

Actor Painkiller_Shotgun_Icon2 : Painkiller_Weapon_Icon
{
	States
	{
		Spawn:
			PK07 S 2
			Stop
	}
}

Actor Painkiller_Shotgun_Puff : PainkillerPuff
{
	+HITTRACER
	+ALWAYSPUFF
	+PUFFONACTORS
	+PUFFGETSOWNER
	+BLOODLESSIMPACT
	-SKYEXPLODE
	+FORCEXYBILLBOARD
	Decal "PainkillerBulletHole"
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 A 0 ACS_NamedExecuteAlways("Painkiller_ShotgunBlast")
			TNT1 A 0 ACS_NamedExecuteWithResult("Painkiller_BloodGenerator_Offsets")
			TNT1 A 0 A_GiveInventory("Painkiller_BloodGenerator",1,AAPTR_TRACER)
			TNT1 A 0 A_SpawnItemEx("Painkiller_ShotgunSmoke",0,0,0,0,0,frandom(0.5,1.0),0,SXF_NOCHECKPOSITION)
			TNT1 A 2 A_GiveInventory("Painkiller_DamageCredit",1,AAPTR_TRACER)
			Stop
		Melee:
		Crash:
			TNT1 A 0 A_PlaySound("Painkiller/Ricochet",CHAN_7,0.33)
			TNT1 A 0 A_SpawnItemEx("Painkiller_ShotgunSmoke",0,0,0,0,0,frandom(0.5,1.0),0,SXF_NOCHECKPOSITION)
			TNT1 A 0 A_SpawnItemEx("Painkiller_ChaingunHitWallEffect1",0,0,0,0,0,frandom(2.5,3.5),0,SXF_NOCHECKPOSITION)
			TNT1 A 0 A_SpawnItemEx("Painkiller_ChaingunHitWallEffectSpawner",0,0,0,0,0,0,0,SXF_CLIENTSIDE|SXF_NOCHECKPOSITION)
			TNT1 A 1 A_GiveInventory("Painkiller_DamageCredit",1,AAPTR_TRACER)
			Stop
	}
}

Actor Painkiller_Shotgun_PuffCoop : Painkiller_Shotgun_Puff { Species "Player" +THRUSPECIES +MTHRUSPECIES }

Actor Painkiller_Shotgun_IceShot
{
	Projectile
	Speed 32
	Damage (0)
	ExplosionDamage 0
	Scale 0.4
	DamageType "PainkillerIce"
	Alpha 0.8
	Renderstyle Add
	Height 16
	Radius 8
	+FORCERADIUSDMG
	+FORCEPAIN
	+FORCEXYBILLBOARD
	States
	{
		Spawn:
			PK00 J 0
			PK00 J 0 A_SetPitch(-CallACS("Painkiller_ShooterPitch")/182)
		SpawnLoop:
			//PK00 J 0 A_SpawnItemEx("Painkiller_PainkillerLaserSegment",-40,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH)
			PK00 J 0 A_SpawnItemEx("Painkiller_Shotgun_IceParticle",-8,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
			PK00 J 2 Bright Light("PAINKILLER_ICESHOT_X1") A_SpawnItemEx("Painkiller_Trail",-8,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH)
			Loop
		Death:
			PK00 J 0 A_GiveInventory("Painkiller_DamageCredit",1,AAPTR_TRACER)
			PK00 J 0 A_Explode(-1,16,!XF_HURTSOURCE,0,16)
			PK00 J 2 Bright Light("PAINKILLER_ICESHOT_X1") A_Fadeout(0.1) PK00 J 0 A_SetScale(scalex-0.05)
			PK00 J 2 Bright Light("PAINKILLER_ICESHOT_X2") A_Fadeout(0.1) PK00 J 0 A_SetScale(scalex-0.05)
			PK00 J 2 Bright Light("PAINKILLER_ICESHOT_X3") A_Fadeout(0.1) PK00 J 0 A_SetScale(scalex-0.05)
			PK00 J 2 Bright Light("PAINKILLER_ICESHOT_X4") A_Fadeout(0.1) PK00 J 0 A_SetScale(scalex-0.05)
			PK00 H 2 Bright Light("PAINKILLER_ICESHOT_X5") A_Fadeout(0.1) PK00 H 0 A_SetScale(scalex-0.05)
			PK00 H 2 Bright A_Fadeout(0.1) PK00 H 0 A_SetScale(scalex-0.05)
			PK00 H 2 Bright A_Fadeout(0.1) PK00 H 0 A_SetScale(scalex-0.05)
			PK00 H 2 Bright A_Fadeout(0.1) PK00 H 0 A_SetScale(scalex-0.05)
			Stop
	}
}

Actor Painkiller_Shotgun_IceShotCoop : Painkiller_Shotgun_IceShot { +THRUSPECIES Species "Player" }

Actor Painkiller_Shotgun_IceParticle
{
	Scale 0.4
	Alpha 0.8
	Renderstyle Add
	+NOINTERACTION
	+NOGRAVITY
	+FORCEXYBILLBOARD
	States
	{
		Spawn:
			PK00 J 2 Bright A_Fadeout(0.1) PK00 J 0 A_SetScale(scalex-0.05)
			PK00 J 2 Bright A_Fadeout(0.1) PK00 J 0 A_SetScale(scalex-0.05)
			PK00 J 2 Bright A_Fadeout(0.1) PK00 J 0 A_SetScale(scalex-0.05)
			PK00 J 2 Bright A_Fadeout(0.1) PK00 J 0 A_SetScale(scalex-0.05)
			PK00 H 2 Bright A_Fadeout(0.1) PK00 H 0 A_SetScale(scalex-0.05)
			PK00 H 2 Bright A_Fadeout(0.1) PK00 H 0 A_SetScale(scalex-0.05)
			PK00 H 2 Bright A_Fadeout(0.1) PK00 H 0 A_SetScale(scalex-0.05)
			PK00 H 2 Bright A_Fadeout(0.1) PK00 H 0 A_SetScale(scalex-0.05)
			Stop
	}
}

Actor Painkiller_Shotgun_FireSmoke : Painkiller_Smoke
{
	Scale 0.05
	States
	{
		Spawn:
			TNT1 A 0
			PK00 DDDDDDDDDDDDDDDD 3 A_Fadeout(0.05)
			Stop
	}
}

Actor Painkiller_IceChunk : IceChunk { Gravity 0.25 Translation "0:255=%[0,0,0]:[1.6,2.0,2.0]" +FORCEXYBILLBOARD }

Actor Painkiller_IceShatter : CustomInventory
{
	-COUNTITEM
	+INVENTORY.ALWAYSPICKUP
	Inventory.PickupMessage ""
	Inventory.PickupSound ""
	States
	{
		Spawn:
			TNT1 A 0 
			TNT1 A 1
			Stop
		Pickup:
			TNT1 A 0 A_JumpIfInventory("Painkiller_NoBloodFlag",1,"Finish")
			TNT1 A 0 ACS_NamedExecuteWithResult("Painkiller_GibCreator",1)
		Finish:	
			TNT1 A 0 A_PlaySound("Painkiller/Freeze/Gib",CHAN_BODY,1.0)
			TNT1 A 0 ACS_NamedExecuteWithResult("Samsara_KillCount")
			TNT1 A 0 ACS_NamedExecuteWithResult("Painkiller_MonsterKill")
			TNT1 A 0 A_KillMaster
			TNT1 A 0 A_Die
			TNT1 A 1 ACS_NamedExecuteWithResult("Painkiller_FreezeShatter")
			stop
	}
}

Actor Painkiller_IceShatter2 : CustomInventory
{
	-COUNTITEM
	+INVENTORY.ALWAYSPICKUP
	Inventory.PickupMessage ""
	Inventory.PickupSound ""
	States
	{
		Spawn:
			TNT1 A 0 
			TNT1 A 1
			Stop
		Pickup:
			TNT1 A 0 A_JumpIfInventory("Painkiller_NoBloodFlag",1,"Finish")
		Finish:	
			TNT1 A 0 A_PlaySound("Painkiller/Freeze/Gib",CHAN_BODY,1.0)
			TNT1 A 0 ACS_NamedExecuteWithResult("Samsara_KillCount")
			TNT1 A 0 ACS_NamedExecuteWithResult("Painkiller_MonsterKill2")
			TNT1 A 0 A_KillMaster
			TNT1 A 0 A_Die
			TNT1 A 1 ACS_NamedExecuteWithResult("Painkiller_FreezeShatter")
			stop
	}
}

Actor Painkiller_ShotgunSmoke
{
	Radius 2
	Height 4
	Renderstyle Translucent
	Scale 0.1
	Alpha 0.33
	+NOINTERACTION
	+NOTRIGGER
	+THRUACTORS
	+CLIENTSIDEONLY
	+FORCEXYBILLBOARD
	States
	{
		Spawn:
			TNT1 A 0
			PK00 E 0 A_Fadeout(0.033)	PK06 E 2 A_SetScale(scalex+0.01)
			PK00 E 0 A_Fadeout(0.033)	PK06 E 2 A_SetScale(scalex+0.01)
			PK00 E 0 A_Fadeout(0.033)	PK06 E 2 A_SetScale(scalex+0.01)
			PK00 E 0 A_Fadeout(0.033)	PK06 E 2 A_SetScale(scalex+0.01)
			PK00 E 0 A_Fadeout(0.033)	PK06 E 2 A_SetScale(scalex+0.01)
			PK00 F 0 A_Fadeout(0.033)	PK06 F 2 A_SetScale(scalex+0.01)
			PK00 F 0 A_Fadeout(0.033)	PK06 F 2 A_SetScale(scalex+0.01)
			PK00 F 0 A_Fadeout(0.033)	PK06 F 2 A_SetScale(scalex+0.01)
			PK00 F 0 A_Fadeout(0.033)	PK06 F 2 A_SetScale(scalex+0.01)
			PK00 F 0 A_Fadeout(0.033)	PK06 F 2 A_SetScale(scalex+0.01)
			Stop
	}
}