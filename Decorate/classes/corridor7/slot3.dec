actor "AssaultShotgun" : Corridor7WeaponBase
{
    Inventory.PickupMessage "You got the Ithaca Stakeout Assault Shotgun!"
    Weapon.SlotNumber 3
    Weapon.SelectionOrder 1300
    Weapon.AmmoType1 "Shell"
    Weapon.AmmoGive1 30
    Weapon.AmmoUse1 2
    Obituary "%k pumped %o full of cryostate lead."
    Tag "Ithaca Stakeout Assault Shotgun"
    States
    {
      Spawn:
        7SG2 A -1
        stop

      Ready:
        C7SG A 1 A_WeaponReady
        loop

      Fire:
        TNT1 A 0 A_AlertMonsters
        C7SG A 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        TNT1 A 0 A_GiveInventory("SamsaraCorridor7AssaultShotgunAttackHandler")
        C7SG BC 3 Bright A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        C7SG DEFG 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        C7SG HIJKL 3 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        TNT1 A 0 A_ReFire
        goto Ready

      AltFire:
        C7SG A 0
        goto Super::AltFire
    }
}

actor SamsaraCorridor7AssaultShotgunAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Shell",2,1)
        stop
        TNT1 A 0 A_TakeInventory("Shell",2)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("C7/Shotgun",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireBullets(5,5,10,random(2,20),"BlankPuff",FBF_NORANDOM|FBF_NOFLASH)
        stop

      PickupAttackCoop:
        TNT1 A 0 A_FireBullets(5,5,10,random(2,35),"BlankPuffCoop",FBF_NORANDOM|FBF_NOFLASH)
        stop
    }
}
