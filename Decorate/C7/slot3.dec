actor "AssaultShotgun" : InstantSwitchWeapon
{
    Weapon.SelectionOrder 1300
    Weapon.SlotNumber 3
	Weapon.BobStyle InverseAlpha
	Weapon.BobRangeX 0.2
	Weapon.BobRangeY 0.2
	Weapon.BobSpeed 4.0	
    Inventory.PickUpSound "C7/AmmoPickup"
    Weapon.AmmoUse 2
    Weapon.AmmoGive 30
    Weapon.AmmoType "Shell"
	+WEAPON.ALT_AMMO_OPTIONAL
	+WEAPON.NOALERT
	Weapon.AmmoType2 "C7ProxyMineAmmo"
	Weapon.AmmoUse2 1
	Weapon.AmmoGive2 0
	Tag "Assault Shotgun"	
    Inventory.PickupMessage "You got the Ithaca Stakeout Assault Shotgun!"
    Obituary "%k pumped %o full of cryostate lead."
    Weapon.YAdjust 0
	Inventory.RestrictedTo "Corridor7Player"			
    States
    {
      Spawn:
        7SG2 A -1
        Loop
      Ready:
        TNT1 A 0 SetPlayerProperty(0,1,2)
        C7SG A 1 A_WeaponReady
        goto Ready+1
      Fire:
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"CoopFire")
        C7SG A 2 A_AlertMonsters
        TNT1 A 0 A_PlayWeaponSound("C7/Shotgun")
        C7SG B 3 BRIGHT A_FireBullets(5,5,10,Random(2,35),"BlankPuff",FBF_NORANDOM|FBF_USEAMMO)
        C7SG C 3 BRIGHT
        C7SG DEFG 2
		C7SG HIJKL 3
        Goto Ready
      CoopFire:
        C7SG A 2 A_AlertMonsters
        TNT1 A 0 A_PlayWeaponSound("C7/Shotgun")
		C7SG BBBBBBBBB 0 BRIGHT A_FireCustomMissile("C7ShotgunBullet",frandom(fRandom(-2.80,-3.00),fRandom(2.80,3.00)),0,0,1,0,frandom(fRandom(-2.80,-3.00),fRandom(2.80,3.00)))
        C7SG B 3 BRIGHT A_FireCustomMissile("C7ShotgunBullet",frandom(fRandom(-2.80,-3.00),fRandom(2.80,3.00)),1,0,1,0,frandom(fRandom(-2.80,-3.00),fRandom(2.80,3.00)))
        C7SG C 3 BRIGHT
        C7SG DEFG 2
		C7SG HIJKL 3
		Goto Ready
	  AltFire:   	  
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"AltCoopFire")		
		C7SG A 0 A_JumpIfNoAmmo("Ready")
		C7SG A 5 A_SpawnItemEx("C7ProxyMine",0,0,0,0,0,0,0)
		C7SG A 5 A_TakeInventory("C7ProxyMineAmmo", 1)
		C7SG A 5 A_Print("Mine placed")
		Goto Ready
      AltCoopFire:	
		C7SG A 0 A_JumpIfNoAmmo("Ready")
		C7SG A 5 A_SpawnItemEx("C7CoopProxyMine",0,0,0,0,0,0,0,SXF_SETMASTER)
		C7SG A 5 A_TakeInventory("C7ProxyMineAmmo", 1)
		C7SG A 5 A_Print("Mine placed")
		Goto Ready	  
    }
} 

actor C7ShotgunBullet : FastProjectile
{
    Damage (Random(2,35))
    Radius 1
    Height 1
    Speed 350
    Projectile
    Renderstyle "Normal"
    Species "Player"
    +BLOODSPLATTER
    +THRUSPECIES
    +NOTIMEFREEZE
	+NODAMAGETHRUST	
    Alpha 1.0
    States
    {
      Spawn:
        TNT1 A 1
        loop

      Death:
        TNT1 A 1
        stop
    }
}
