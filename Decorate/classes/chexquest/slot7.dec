Actor ChexAmmoTake : Counter { Inventory.MaxAmount 7 }
Actor ChexClassicLaz : Boolean { Inventory.RestrictedTo "ChexWarrior" }

Actor "LAZ Device" : Weapon
{
    Weapon.SelectionOrder 2800
    //Weapon.SlotNumber 7
    Inventory.PickUpSound "chex/weaponget"
    Weapon.AmmoUse 40
    Weapon.AmmoGive 40
    Weapon.AmmoType "Cell"
    +WEAPON.NOAUTOFIRE
    +BLOODLESSIMPACT
    +WEAPON.BFG
    +NOEXTREMEDEATH
    +INVENTORY.UNDROPPABLE
    Inventory.PickupMessage "You got the LAZ Device! Woot!"
    Obituary "$SAMSARA_CHEX_OB_SLOT7_1"
    DamageType "ZorchBfg"
	Inventory.RestrictedTo "ChexWarrior"
    Tag "LAZ Device"
    States
    {
      Spawn:
        CFUG A -1
        stop

      Ready:
        CFGG A 1 A_WeaponReady
        loop

      Deselect:
        CFGG A 1 A_Lower
        loop

      Select:
        CFGG A 1 A_Raise
        loop

      Fire:
        TNT1 A 0 A_TakeInventory("ChexAmmoTake")
        TNT1 A 0 A_JumpIfInventory("ChexClassicLaz",1,"FireClassic")
      // Modern
      FireModern:
        TNT1 A 0 A_PlaySound("chex/bfgfire",CHAN_WEAPON)
        CFGG BBBBB 1 Bright A_SpawnItemEx("LazShield1",0,96,32,0,0,0,-270,SXF_NOCHECKPOSITION)
        CFGG CCCCC 1 Bright A_SpawnItemEx("LazShield2",0,96,32,0,0,0,-270,SXF_NOCHECKPOSITION)
        CFGG DDDDD 1 Bright A_SpawnItemEx("LazShield3",0,96,32,0,0,0,-270,SXF_NOCHECKPOSITION)
        CFGG EEEEE 1 Bright A_SpawnItemEx("LazShield4",0,96,32,0,0,0,-270,SXF_NOCHECKPOSITION)
        CFGG FFFFFGGGGG 1 Bright A_GiveInventory("SamsaraChexLAZDeviceModernShieldAttackHandler")
        TNT1 A 0 A_ReFire("HoldModernTimerCheck")
        CFGG HHH 1 Bright A_SpawnItemEx("LazShield5Useless",0,96,32,0,0,0,-270,SXF_NOCHECKPOSITION)
        TNT1 A 0 A_PlaySound("chex/bfglaunch",CHAN_6)
        CFGG H 20 A_GiveInventory("SamsaraChexLAZDeviceModernAttackHandler")
        goto Ready

      HoldModernTimerCheck:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"HoldModern") // halt timer when infinite ammo is on
        TNT1 A 0 A_JumpIfInventory("ChexAmmoTake",0,2)
        TNT1 A 0 A_GiveInventory("ChexAmmoTake")
        goto HoldModern
        TNT1 A 0 A_TakeInventory("Cell",1)
        TNT1 A 0 A_TakeInventory("ChexAmmoTake",5)
        goto HoldModern

      HoldModern:
        TNT1 A 0 A_PlaySound("chex/bfghold",CHAN_7)
        TNT1 AA 0 A_SpawnItemEx("ChexBFGTrail2",0,96,random(28,34),random(-1,1),random(-1,1),random(-1,1),random(-290,-250),SXF_NOCHECKPOSITION)
        CFGG FF 1 Bright A_GiveInventory("SamsaraChexLAZDeviceModernShieldAttackHandler")
        TNT1 AA 0 A_SpawnItemEx("ChexBFGTrail2",0,96,random(28,34),random(-1,1),random(-1,1),random(-1,1),random(-290,-250),SXF_NOCHECKPOSITION)
        CFGG EE 1 Bright A_GiveInventory("SamsaraChexLAZDeviceModernShieldAttackHandler")
        TNT1 AA 0 A_SpawnItemEx("ChexBFGTrail2",0,96,random(28,34),random(-1,1),random(-1,1),random(-1,1),random(-290,-250),SXF_NOCHECKPOSITION)
        CFGG FF 1 Bright A_GiveInventory("SamsaraChexLAZDeviceModernShieldAttackHandler")
        TNT1 AA 0 A_SpawnItemEx("ChexBFGTrail2",0,96,random(28,34),random(-1,1),random(-1,1),random(-1,1),random(-290,-250),SXF_NOCHECKPOSITION)
        CFGG GG 1 Bright A_GiveInventory("SamsaraChexLAZDeviceModernShieldAttackHandler")
        TNT1 AA 0 A_SpawnItemEx("ChexBFGTrail2",0,96,random(28,34),random(-1,1),random(-1,1),random(-1,1),random(-290,-250),SXF_NOCHECKPOSITION)
        TNT1 A 0 A_ReFire("HoldModernTimerCheck")
        TNT1 A 0 A_PlaySound("chex/bfgquickfire",CHAN_WEAPON)
        CFGG EEEFFFFGGGGG 1 Bright A_SpawnItemEx("LazShield5Useless",0,96,32,0,0,0,-270,SXF_NOCHECKPOSITION)
        TNT1 A 0 A_PlaySound("chex/bfglaunch",CHAN_6)
        CFGG H 20 A_GiveInventory("SamsaraChexLAZDeviceModernAttackHandler")
        goto Ready

      Flash:
        CFGF A 11 Bright A_Light1
        CFGF B 6 Bright A_Light2
        goto LightDone

      // Classic
      FireClassic:
        CFGG A 20 A_PlaySound("chex/bfgf",CHAN_WEAPON)
        CFGG B 10 Bright A_GunFlash("FlashClassic")
        CFGG B 10 Bright A_GiveInventory("SamsaraChexLAZDeviceClassicAttackHandler")
        CFGG H 20 A_ReFire
        goto Ready

      FlashClassic:
        CFGF C 11 Bright A_Light1
        CFGF D 6 Bright A_Light2
        goto LightDone
    }
}

actor SamsaraChexLAZDeviceClassicAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Cell",40,1)
        stop
        TNT1 A 0 A_TakeInventory("Cell",40)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_JumpIfInventory("DoomNoBFGAim",1,"PickupAttackNoAim")
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireCustomMissile("ChexLAZBall",0,false)
        stop

      PickupAttackCoop:
        TNT1 A 0 A_FireCustomMissile("ChexLAZBallCoop",0,false)
        stop

      // No aiming attack stuff
      PickupAttackNoAim:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackNoAimCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackNoAimCoop")
      PickupAttackNoAimDM:
        TNT1 A 0 A_SpawnItemEx("ChexLAZBall",0,0,32,25)
        TNT1 A 0 A_JumpIfInventory("SamsaraHasSpread",1,1)
        stop
        TNT1 A 0 A_SpawnItemEx("ChexLAZBall",0,0,32,25,0,0,-15)
        TNT1 A 0 A_SpawnItemEx("ChexLAZBall",0,0,32,25,0,0,15)
        stop

      PickupAttackNoAimCoop:
        TNT1 A 0 A_SpawnItemEx("ChexLAZBallCoop",0,0,32,25)
        TNT1 A 0 A_JumpIfInventory("SamsaraHasSpread",1,1)
        stop
        TNT1 A 0 A_SpawnItemEx("ChexLAZBallCoop",0,0,32,25,0,0,-15)
        TNT1 A 0 A_SpawnItemEx("ChexLAZBallCoop",0,0,32,25,0,0,15)
        stop
    }
}

actor ChexLAZBall : BFGBall
{
    DamageType "Zorch"
    Decal None
    +BRIGHT
    +DONTREFLECT
	+NOEXTREMEDEATH
	+BLOODLESSIMPACT
    DeathSound "chex/bfgx"
    Obituary "$SAMSARA_CHEX_OB_SLOT7_2"
    States
    {
      Spawn:
        CFS1 AB 4 bright
        loop
        
      Death:
        CFE1 AB 8 bright
        CFE1 C 8 bright A_BFGSpray("ChexLAZSplash")
        CFE1 DEF 8 bright
        stop
    }
}

actor ChexLAZBallCoop : ChexLAZBall
{
    Species "Player"
    +THRUSPECIES
    States
    {
      Death:
        CFE1 AB 8 bright
        CFE1 C 8 bright A_BFGSpray("ChexLAZSplashCoop")
        CFE1 DEF 8 bright
        stop
    }
}

actor ChexLAZSplash : BFGExtra
{
    DamageType "Zorch"
	+NOEXTREMEDEATH
	+BLOODLESSIMPACT
    States
    {
    Spawn:
        CFE2 ABCD 8 Bright
        Stop
    }
}

actor ChexLAZSplashCoop : ChexLAZSplash { +MTHRUSPECIES }

actor SamsaraChexLAZDeviceModernAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Cell",40,1)
        stop
        TNT1 A 0 A_TakeInventory("Cell",40)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_JumpIfInventory("DoomNoBFGAim",1,"PickupAttackNoAim")
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireCustomMissile("ChexBFGBall",0,false)
        stop

      PickupAttackCoop:
        TNT1 A 0 A_FireCustomMissile("ChexBFGBall2",0,false)
        stop

      // No aiming attack stuff
      PickupAttackNoAim:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackNoAimCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackNoAimCoop")
      PickupAttackNoAimDM:
        TNT1 A 0 A_SpawnItemEx("ChexBFGBall",0,0,32,25)
        TNT1 A 0 A_JumpIfInventory("SamsaraHasSpread",1,1)
        stop
        TNT1 A 0 A_SpawnItemEx("ChexBFGBall",0,0,32,25,0,0,-15)
        TNT1 A 0 A_SpawnItemEx("ChexBFGBall",0,0,32,25,0,0,15)
        stop

      PickupAttackNoAimCoop:
        TNT1 A 0 A_SpawnItemEx("ChexBFGBall2",0,0,32,25)
        TNT1 A 0 A_JumpIfInventory("SamsaraHasSpread",1,1)
        stop
        TNT1 A 0 A_SpawnItemEx("ChexBFGBall2",0,0,32,25,0,0,-15)
        TNT1 A 0 A_SpawnItemEx("ChexBFGBall2",0,0,32,25,0,0,15)
        stop
    }
}

actor SamsaraChexLAZDeviceModernShieldAttackHandler : Trigger
{
    States
    {
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupCoop")
      PickupDM:
        TNT1 A 0 A_SpawnItemEx("LazShield5",0,96,32,0,0,0,-270,SXF_NOCHECKPOSITION)
        stop

      PickupCoop:
        TNT1 A 0 A_SpawnItemEx("LazShield5Online",0,96,32,0,0,0,-270,SXF_NOCHECKPOSITION)
        stop
    }
}

actor ChexBFGBall
{
    DamageType "Zorch"
    Radius 13
    Height 8
    Speed 25
    Damage 100
    RenderStyle Translucent
    Obituary "$SAMSARA_CHEX_OB_SLOT7_2"
    Alpha 0.75
    DeathSound "chex/bfgx"
    Projectile
    +RANDOMIZE
    +BRIGHT
    +BLOODLESSIMPACT
    +NOEXTREMEDEATH
    +NODAMAGETHRUST
    +DONTREFLECT
    +REFLECTIVE
    //+DONTRIP
    States
    {
      Spawn:
        CFS1 AAAABBBB 1 bright A_CustomMissile("ChexBFGTrail2",0,0,random(-180,180),2,random(-180,180))
        loop
        
      Death:
        CFE1 A 4 bright
        TNT1 AAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx("ChexBFGTrail",0,0,0,random(-2,2),random(-2,2),random(-2,2),random(-180,180),32)//A_CustomMissile("ChexBFGTrail",0,0,random(-180,180),2,random(-180,180))
        TNT1 A 0 A_Explode(224,320,0)
        //TNT1 AAAAAAAAAA 0 A_SpawnItemEx("ChexBFGExplosion2",random(-30,30),random(-30,30),random(-10,30))
        TNT1 A 0 A_SpawnItemEx("ChexBFGExplosion")
        TNT1 A 0 A_SpawnItemEx("ChexBFGExplosionEffect",0,0,0,0,0,0,0,48)
        CFE1 AAAA 1 bright A_CustomMissile("ChexBFGTrail",0,0,random(-180,180),2,random(-180,180))
        CFE1 BBBBBBBBCCCCCCCCDDDDDDDDEEEEEEEEFFFFFFFF 1 bright A_SpawnItemEx("ChexBFGTrail",0,0,0,random(-2,2),random(-2,2),random(-2,2),random(-180,180),32)//A_CustomMissile("ChexBFGTrail",0,0,random(-180,180),2,random(-180,180))
        TNT1 AAAAAAAAAAAAAAAAAAAA 1 A_SpawnItemEx("ChexBFGTrail",0,0,0,random(-2,2),random(-2,2),random(-2,2),random(-180,180),32)//A_CustomMissile("ChexBFGTrail",0,0,random(-180,180),2,random(-180,180))
        TNT1 AAAAAAAAAAAAAAAAAAAA 1 A_SpawnItemEx("ChexBFGTrail",0,0,0,random(-2,2),random(-2,2),random(-2,2),random(-180,180),32)//A_CustomMissile("ChexBFGTrail",0,0,random(-180,180),2,random(-180,180))
        TNT1 AAAAAAAAAAAAAAAAAAAA 1 A_SpawnItemEx("ChexBFGTrail",0,0,0,random(-2,2),random(-2,2),random(-2,2),random(-180,180),32)//A_CustomMissile("ChexBFGTrail",0,0,random(-180,180),2,random(-180,180))
        stop
    }
}

actor ChexBFGTrail
{
    PROJECTILE
    +DOOMBOUNCE
    +NOGRAVITY   
    +EXPLODEONWATER
    -CANBOUNCEWATER
    +CLIENTSIDEONLY
    +NOINTERACTION
	+NOTIMEFREEZE
    -SOLID
    Scale 0.15
    Speed 2
    RenderStyle Add
    Alpha 0.95
    States
    {
    Spawn:
        SLZR A 4 bright
        SLZR B 4 bright A_FadeOut(0.1)
        loop
    }
}

actor ChexBFGTrail2 : ChexBFGTrail
{
    Alpha 0.55
    Scale 0.075
}

actor ChexBFGExplosion
{
    +NOGRAVITY
    +CLIENTSIDEONLY
    +NOINTERACTION
    -SOLID
    Scale 0.75
    Speed 0
    RenderStyle Add
    Alpha 0.85
    States
    {
    Spawn:
        CFE1 A 4 bright
        CFE1 BCDEF 8 bright
        Stop
    }
}

actor ChexBFGExplosionEffect
{
    +NOGRAVITY
    -SOLID
    +NOEXTREMEDEATH
    +NODAMAGETHRUST
    +FORCERADIUSDMG
	+BLOODLESSIMPACT
    Radius 0
    Height 0
    Scale 0.75
    Speed 0
    RenderStyle Add
    Obituary "$SAMSARA_CHEX_OB_SLOT7_1"
    DamageType "Zorch"
    Alpha 0.1
    States
    {
    Spawn:
        TNT1 A 0 A_SetTranslucent(0.1,1)
        CFS3 EE 5 bright A_Explode(96,112,0)//A_Explode(192,224,0)
        TNT1 A 0 A_SetTranslucent(0.25,1)
        CFS3 EEEE 5 bright A_Explode(80,80,0)//A_Explode(160,160,0)
        TNT1 A 0 A_SetTranslucent(0.4,1)
        CFS2 DDDD 5 bright A_Explode(64,64,0)//A_Explode(128,128,0)
        TNT1 A 0 A_SetTranslucent(0.55,1)
        CFS2 CCCC 5 bright A_Explode(48,48,0)//A_Explode(96,96,0)
        CFS2 BBBB 5 bright A_Explode(32,32,0)//A_Explode(64,64,0)
        CFS2 AAAA 5 bright A_Explode(16,16,0)//A_Explode(32,32,0)
        Stop
    }
}

actor ChexBFGBall2 : ChexBFGBall
{
+THRUSPECIES
Species "Player"
States
{
      Death:
        CFE1 A 4 bright
        TNT1 AAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx("ChexBFGTrail",0,0,0,random(-2,2),random(-2,2),random(-2,2),random(-180,180),32)//A_CustomMissile("ChexBFGTrail",0,0,random(-180,180),2,random(-180,180))
        TNT1 A 0 A_Explode(512,384,0)
        //TNT1 AAAAAAAAAA 0 A_SpawnItemEx("ChexBFGExplosion2",random(-30,30),random(-30,30),random(-10,30))
        TNT1 A 0 A_SpawnItemEx("ChexBFGExplosion")
        TNT1 A 0 A_SpawnItemEx("ChexBFGExplosionEffect",0,0,0,0,0,0,0,48)
        CFE1 AAAA 1 bright A_CustomMissile("ChexBFGTrail",0,0,random(-180,180),2,random(-180,180))
        CFE1 BBBBBBBBCCCCCCCCDDDDDDDDEEEEEEEEFFFFFFFF 1 bright A_SpawnItemEx("ChexBFGTrail",0,0,0,random(-2,2),random(-2,2),random(-2,2),random(-180,180),32)//A_CustomMissile("ChexBFGTrail",0,0,random(-180,180),2,random(-180,180))
        TNT1 AAAAAAAAAAAAAAAAAAAA 1 A_SpawnItemEx("ChexBFGTrail",0,0,0,random(-2,2),random(-2,2),random(-2,2),random(-180,180),32)//A_CustomMissile("ChexBFGTrail",0,0,random(-180,180),2,random(-180,180))
        TNT1 AAAAAAAAAAAAAAAAAAAA 1 A_SpawnItemEx("ChexBFGTrail",0,0,0,random(-2,2),random(-2,2),random(-2,2),random(-180,180),32)//A_CustomMissile("ChexBFGTrail",0,0,random(-180,180),2,random(-180,180))
        TNT1 AAAAAAAAAAAAAAAAAAAA 1 A_SpawnItemEx("ChexBFGTrail",0,0,0,random(-2,2),random(-2,2),random(-2,2),random(-180,180),32)//A_CustomMissile("ChexBFGTrail",0,0,random(-180,180),2,random(-180,180))
        stop
    }
}

actor LazShield1
{
    Radius 32//64
    Height 32
    RenderStyle Add
    Alpha 0.5
    Species "Player"
    Tag "LAZ Device Shield"
    +THRUSPECIES
    //+SOLID
    +SHOOTABLE
    +NOBLOOD
    +NOGRAVITY
    +NOTELEPORT
    +INVULNERABLE
    +DONTRIP
    +NOBLOODDECALS
    +FLOORCLIP
    +REFLECTIVE
	+NOTIMEFREEZE
    States
    {
    Spawn:
        CFS2 A 2 bright
        stop
    }
}

actor LazShield2 : LazShield1
{
    States
    {
    Spawn:
        CFS2 B 2 bright
        stop
    }
}

actor LazShield3 : LazShield1
{
    States
    {
    Spawn:
        CFS2 C 2 bright
        stop
    }
}

actor LazShield4 : LazShield1
{
    States
    {
    Spawn:
        CFS2 E 2 bright
        stop
    }
}

actor LazShield5 : LazShield1
{
    Alpha 0.25
    States
    {
    Spawn:
        TNT1 A 0 A_Jump(256,"Spawn1","Spawn2","Spawn3","Spawn4","Spawn5")
        TNT1 A 1
        loop
    Spawn1:
        CFS3 A 2 bright
        stop
    Spawn2:
        CFS3 B 2 bright
        stop
    Spawn3:
        CFS3 C 2 bright
        stop
    Spawn4:
        CFS3 D 2 bright
        stop
    Spawn5:
        CFS3 E 2 bright
        stop
    }
}

actor LazShield5Useless : LazShield5
{
    Radius 0
    Height 0
    -SOLID
    +NOINTERACTION
    +CLIENTSIDEONLY
    -SHOOTABLE
    -REFLECTIVE
	+NOTIMEFREEZE
}

actor LazShield5Online : LazShield5 { Alpha 0.5 }
