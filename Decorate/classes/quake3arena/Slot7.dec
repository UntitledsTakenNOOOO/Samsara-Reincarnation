actor SamsaraQuake3BFG10KLoopSoundCheck : Boolean {}

Actor Q3BFG10K : Q3Weapon
{
	Weapon.SlotNumber 7
	Weapon.AmmoUse 3
	Weapon.AmmoGive 50
	Weapon.AmmoType "Cell"
	Inventory.PickupMessage "BFG10K"
	Obituary "%o was blasted by %k's BFG."
	Tag "BFG-10K"
	+BFG
	+EXPLOSIVE
	+FLOATBOB
	States
	{
		Spawn:
			Q399 ABCDEFGH 2 Bright
			loop

		ReadyActual:
			Q300 A 0 A_GunFlash("ReadyActualGlow",GFF_NOEXTCHANGE)
			Q300 A 0 A_GiveInventory("SamsaraQuake3BFG10KLoopSoundStarter")
			Q300 AAAAAAAA 3 A_WeaponReady
			loop

		ReadyActualGlow:
			Q305 ABCDEFGH 3 Bright
			stop

		Deselect:
			Q300 A 0 A_JumpIfHealthLower(1,"DeselectInstant")
			Q300 A 0 A_GunFlash("DeselectGlow",GFF_NOEXTCHANGE)
			Q300 A 0 A_PlaySound("Quake3/Weapons/Change",CHAN_5)
			Q300 A 1 A_GiveInventory("SamsaraQuake3BFG10KLoopSoundStarter")
			Q301 ABCD 1
			Q301 E 0 A_GiveInventory("SamsaraQuake3BFG10KLoopSoundStopper")
			goto Super::DeselectInstant

		DeselectGlow:
            TNT1 A 0 // no glow defined for now
            /*Q305 A 1 Bright
			Q306 ABCD 1 Bright
            Q306 E 0 Bright*/
			stop

		DeselectInstant:
			TNT1 A 0 A_GunFlash("DeselectInstantGlow",GFF_NOEXTCHANGE)
			TNT1 A 0 A_GiveInventory("SamsaraQuake3BFG10KLoopSoundStopper")
			goto Super::DeselectInstant

		DeselectInstantGlow:
			TNT1 A 0
			stop

		Ready: // our "Select" state
			Q302 A 0 A_GunFlash("ReadyGlow",GFF_NOEXTCHANGE)
			Q302 AB 1 A_GiveInventory("SamsaraQuake3BFG10KLoopSoundStarter") // map change hack
			Q302 CD 1
			goto ReadyActual

		ReadyGlow:
            TNT1 A 0 // no glow defined for now
			/*Q307 ABCD 1 Bright
            Q305 A 0 Bright*/
			stop

		Fire:
			Q310 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireStart")
			Q310 A 0 A_JumpIfInventory("Cell",3,"FireStart")
			goto FireDry

		FireDry:
			Q300 A 0 A_ClearReFire
			Q300 A 0 A_GunFlash("FireDryGlow",GFF_NOEXTCHANGE)
			Q300 A 0 A_PlaySound("Quake3/Weapons/NoAmmo",CHAN_5)
			Q300 A 0 A_GiveInventory("SamsaraQuake3BFG10KLoopSoundStarter")
			Q300 A 17 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			Q300 A 0 A_SelectWeapon("Q3Machinegun")
			goto Deselect

		FireDryGlow:
			Q305 ABCDEFGH 3 Bright
			stop

		FireStart:
			Q310 A 0 A_AlertMonsters
			Q310 A 0 A_GiveInventory("SamsaraQuake3BFG10KLoopSoundStarter")
			Q310 A 0 A_GunFlash
			Q310 A 0 A_GiveInventory("SamsaraQuake3BFG10KAttackHandler")
			Q310 A 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			Q310 C 3 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			Q310 F 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			Q310 A 0 A_ReFire
			goto FireFinish

		Flash:
			Q309 A 2 Bright
			Q309 B 3 Bright
			Q309 C 2 Bright
			stop

		FireFinish:
			Q303 A 0 A_GunFlash("FireFinishGlow",GFF_NOEXTCHANGE)
			Q303 "ADGJMPSVY\" 1 A_WeaponReady
			Q304 "BEH" 2 A_WeaponReady
			Q303 "DGJMPSVY\" 2 A_WeaponReady
			Q304 "BEHK" 3 A_WeaponReady
			goto ReadyActual

		FireFinishGlow:
			Q305 ABCDEFGH 3 Bright
			loop
	}
}

actor SamsaraQuake3BFG10KAttackHandler : Trigger
{
	States
	{
	// Ammo checking/taking stuff
	Pickup:
		TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupSound")
		TNT1 A 0 A_JumpIfInventory("Cell",3,1)
		stop
		TNT1 A 0 A_TakeInventory("Cell",3)
		goto PickupSound

	// Sound stuff
	PickupSound:
		TNT1 A 0 A_JumpIfInventory("SamsaraAlternateSoundToken",2,"PickupSoundRevolution")
		TNT1 A 0 A_PlaySound("Quake3/BFG/Fire",CHAN_WEAPON)
		goto PickupAttack
	PickupSoundRevolution:
		TNT1 A 0 A_PlaySound("Quake3Revolution/BFG/Fire",CHAN_WEAPON)
		goto PickupAttack

	// Attack stuff
	PickupAttack:
		TNT1 A 0 A_GiveInventory("Q3QuadDamageSound")
		TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
		TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
	PickupAttackDM:
		TNT1 A 0 A_FireCustomMissile("Q3BFGProjectile",0,false,6)
		stop
	PickupAttackCoop:
		TNT1 A 0 A_FireCustomMissile("Q3BFGProjectileCoop",0,false,6)
		stop
	}
}

actor SamsaraQuake3BFG10KLoopSoundStarter : Trigger
{
	States
	{
	// Sound starting stuff
	Pickup:
		TNT1 A 0 A_PlaySound("Quake3/BFG/Idle",CHAN_6,0.75,true)
		TNT1 A 0 A_GiveInventory("SamsaraQuake3BFG10KLoopSoundCheck")
		stop
	}
}

actor SamsaraQuake3BFG10KLoopSoundStopper : Trigger
{
	States
	{
	// Sound stopping stuff
	Pickup:
		TNT1 A 0 A_JumpIfInventory("SamsaraQuake3BFG10KLoopSoundCheck",1,1)
		stop
		TNT1 A 0 A_PlaySound("silence",CHAN_6)
		TNT1 A 0 A_StopSound(CHAN_6)
		TNT1 A 0 A_TakeInventory("SamsaraQuake3BFG10KLoopSoundCheck")
		stop
	}
}

Actor Q3BFGProjectile
{
	Height 12
	Radius 6
	Damage (100)
	Speed 32
	Scale 0.33
	RenderStyle Add
	DamageType "Q3BFG"
	Decal "Q3ExplosiveImpact"
	Projectile
	+FORCEXYBILLBOARD
	+FORCERADIUSDMG
	+HITTRACER
	+BRIGHT
	States
	{
		Spawn:
			Q307 A 0
			Q307 A 0 A_PlaySound("Quake3/BFG/Fly",CHAN_VOICE,1.0,true)
			Q307 A 2 A_SpawnItemEx("Q3BFGRing")
			wait

		XDeath:
			TNT1 A 0 ACS_NamedExecuteWithResult("Q3_BloodGenerator_Offsets")
			TNT1 A 0 A_GiveInventory("Q3_BloodGenerator",1,AAPTR_TRACER)
		Death:
			TNT1 A 0 A_PlaySound("Quake3/BFG/Explode",CHAN_VOICE)
			TNT1 A 0 A_Explode(100,100)
			TNT1 A 0 A_SpawnItemEx("Q3BFGBoom")
			TNT1 A 0 A_JumpIf(abs(z - floorz) <= 4, "HitFloor")
			TNT1 A 1 A_CheckCeiling("HitCeiling")
			stop

		HitFloor:
			TNT1 A 0 ACS_NamedExecuteWithResult("Q3SpawnDecal", 1)
			TNT1 A 0 A_SetPitch(90)
			TNT1 A 0 A_JumpIfCloser(32,"Thrust")
			TNT1 A 1
			stop

		Thrust:
			TNT1 A 1 A_GiveToTarget("Q3BFGJumpModifier")
			stop

		HitCeiling:
			TNT1 A 0 ACS_NamedExecuteWithResult("Q3SpawnDecal", 1, 1)
			TNT1 A 0 A_SetPitch(-90)
			TNT1 A 1
			stop
	}
}

Actor Q3BFGProjectileCoop : Q3BFGProjectile { +THRUSPECIES Species "Player" }

actor Q3BFGJumpModifier : Trigger
{
	States
	{
	Pickup:
		TNT1 A 0 ThrustThingZ(0,28,0,1)
		stop
	}
}

Actor Q3BFGBoom
{
	RenderStyle Add
	Height 4
	Radius 2
	+NOINTERACTION
	+CLIENTSIDEONLY
	+FORCEXYBILLBOARD
	+BRIGHT
	States
	{
		Spawn:
			Q307 A 0 NoDelay ACS_NamedExecuteWithResult("Q3EffectLifeCycle", 0, 50, 0, 9)
			Q307 BCD 3
			Q307 D 100
			Stop
	}
}

Actor Q3BFGRing
{
	RenderStyle Add
	Height 4
	Radius 2
	Scale 0.125
	+NOINTERACTION
	+CLIENTSIDEONLY
	+FORCEXYBILLBOARD
	+BRIGHT
	States
	{
		Spawn:
			Q307 E 0 NoDelay ACS_NamedExecuteWithResult("Q3EffectLifeCycle", 0, 50, -75, 6)
			Q307 E 100
			Stop
	}
}
