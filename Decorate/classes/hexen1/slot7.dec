// Tokens

actor SamsaraHexenHasSlot7 : Boolean {}
actor SamsaraHexenWeaponPaletteBlendCounter : Counter { Inventory.MaxAmount 3 }

// Parias (Reincarnation/Classic)

actor SamsaraHexenWraithvergeGhostTimer : Counter { Inventory.MaxAmount 26 }

actor Wraithverge : CWeapWraithverge // [tv50] my hatred for this weapon has not let up
{
    Inventory.PickupMessage "Wraithverge"
    Inventory.PickupSound "hexen/weaponget"
    Inventory.RestrictedTo "HexenPlayer"
    Weapon.AmmoType1 "Clip"
    Weapon.AmmoGive1 0 // 25
    Weapon.AmmoUse1 0 // 20
    Weapon.AmmoType2 "Cell"
    Weapon.AmmoGive2 0 // 25
    Weapon.AmmoUse2 0 // 20
    Obituary "$SAMSARA_HEXEN_OB_SLOT7"
    Tag "Wraithverge"
    +BFG
    +NOALERT
    +UNDROPPABLE
    States
    {
      Spawn:
        WWRT A -1
        stop

      Ready:
        TNT1 AAA 0 A_CHolyPalette
        TNT1 A 0 A_TakeInventory("SamsaraHexenWeaponPaletteBlendCounter")
        CHLY A 1 A_WeaponReady
        loop

      Deselect:
        TNT1 AAA 0 A_CHolyPalette
        TNT1 A 0 A_TakeInventory("SamsaraHexenWeaponPaletteBlendCounter")
        CHLY A 1 A_Lower
        loop

      Select:
        TNT1 AAA 0 A_CHolyPalette
        TNT1 A 0 A_TakeInventory("SamsaraHexenWeaponPaletteBlendCounter")
        CHLY A 1 A_Raise
        loop

      Fire:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,3)
        TNT1 A 0 A_JumpIfInventory("Clip",20,1)
        goto NoAmmoSwitch
        TNT1 A 0 A_JumpIfInventory("Cell",20,1)
        goto NoAmmoSwitch
        CHLY A 1 Bright Offset(0,40) A_AlertMonsters
        CHLY B 1 Bright Offset(0,40)
        CHLY CD 2 Bright Offset(0,43)
        CHLY E 2 Bright Offset(0,45)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"FireCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"FireCoop")
      FireDM:
        TNT1 A 0 A_GiveInventory("SamsaraHexenWraithvergeAttackHandler")
        goto FireFinish

      FireCoop:
      // start attack handler code
      FireCoopAmmoCheck:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireCoopAttack")
        TNT1 A 0 A_JumpIfInventory("Clip",20,1)
        goto FireFinish
        TNT1 A 0 A_JumpIfInventory("Cell",20,1)
        goto FireFinish
        TNT1 A 0 A_TakeInventory("Clip",20)
        TNT1 A 0 A_TakeInventory("Cell",20)
        goto FireCoopAttack

      FireCoopAttack:
        //TNT1 A 0 A_PlaySound("HolySymbolFire",CHAN_WEAPON) // already called in A_CHolyAttack
        TNT1 A 0 A_JumpIfInventory("WeaponFlashesAreDisabled",1,3)
        TNT1 A 0 A_GiveInventory("SamsaraHexenWeaponPaletteBlendCounter",3)
        TNT1 A 0 ACS_NamedExecuteWithResult("SamsaraHexenWeaponPaletteBlend", 0)
        TNT1 A 0 A_CHolyAttack
        TNT1 AAA 0 A_CHolyPalette
        goto FireFinish
      // end attack handler code

      FireFinish:
        CHLY F 6 Bright Offset(0,48)
        CHLY GG 2 Bright Offset(0,40) A_TakeInventory("SamsaraHexenWeaponPaletteBlendCounter",1)
        CHLY G 2 Offset(0,36) A_TakeInventory("SamsaraHexenWeaponPaletteBlendCounter",1)
        CHLY A 20
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,3)
        TNT1 A 0 A_JumpIfInventory("Clip",20,1)
        goto NoAmmoSwitch
        TNT1 A 0 A_JumpIfInventory("Cell",20,1)
        goto NoAmmoSwitch
        CHLY A 5 A_ReFire
        goto Ready

      // "No ammo" switch
      NoAmmoSwitch:
        TNT1 A 0 A_ClearReFire
        TNT1 A 0 A_JumpIfInventory("HexenClassMode",1,"NoAmmoSwitchClericClassic")
        TNT1 A 0 A_SelectWeapon("Jade Wand")
        goto Deselect

      NoAmmoSwitchClericClassic:
        TNT1 A 0 A_SelectWeapon("Sapphire Wand")
        goto Deselect
    }
}

actor SamsaraHexenWraithvergeAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Clip",20,1)
        stop
        TNT1 A 0 A_JumpIfInventory("Cell",20,1)
        stop
        TNT1 A 0 A_TakeInventory("Clip",20)
        TNT1 A 0 A_TakeInventory("Cell",20)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("HolySymbolFire",CHAN_WEAPON)
        /*TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")*/
      PickupAttackDM:
        TNT1 A 0 A_FireCustomMissile("HolySpiritDM",0,false)
        TNT1 A 0 A_FireCustomMissile("HolySpiritDM2",0,false)
        TNT1 A 0 A_FireCustomMissile("HolySpiritDM3",0,false)
        TNT1 A 0 A_FireCustomMissile("HolySpiritDM4",0,false)
        goto PickupFinish

      /*PickupAttackCoop:
        TNT1 A 0
        goto PickupFinish*/

      // Finishing stuff
      PickupFinish:
        TNT1 A 0 A_JumpIfInventory("WeaponFlashesAreDisabled",1,"PickupStop")
        TNT1 A 0 A_GiveInventory("SamsaraHexenWeaponPaletteBlendCounter",3)
        TNT1 A 0 ACS_NamedExecuteWithResult("SamsaraHexenWeaponPaletteBlend", 0)
        stop
    }
}

actor HolyMissile_Coop : HolyMissile replaces HolyMissile
{
    Species "Player"
    +BLOODSPLATTER
    +THRUSPECIES
}

actor HolyMissilePuff2: HolyMissilePuff replaces HolyMissilePuff
{
    +CLIENTSIDEONLY
    +NOINTERACTION
}

actor HolySpirit2 : HolySpirit replaces HolySpirit
{
    Speed 15 // Previously 12
    DamageType "Wraithverge"
    Alpha 0.5
    Species "Player"
    +BLOODSPLATTER
    +FORCERADIUSDMG
    +THRUSPECIES
    States
    {
      Spawn:
        SPIR A 2 Bright A_CHolySeek
        goto Spawn2

      Spawn1:
        TNT1 A 0 A_ChangeFlag("PAINLESS",false)
        TNT1 A 0 A_Explode(random(1,8),64,0)
        TNT1 A 0 A_SpawnItemEx("HolyMissilePuff2",0,0,-6,0,0,0,0,SXF_CLIENTSIDE) //ThrustThing(angle*256/360,4,0,0)
        SPIR A 2 Bright A_CHolySeek
      Spawn2:
        TNT1 A 0 A_ChangeFlag("PAINLESS",true)
        TNT1 A 0 A_Explode(random(1,8),64,0)
        TNT1 A 0 A_SpawnItemEx("HolyMissilePuff2",0,0,-6,0,0,0,0,SXF_CLIENTSIDE) //ThrustThing(angle*256/360,4,0,0)
        SPIR A 2 Bright A_CHolySeek
      Spawn3:
        TNT1 A 0 A_Explode(random(1,8),64,0)
        TNT1 A 0 A_SpawnItemEx("HolyMissilePuff2",0,0,-6,0,0,0,0,SXF_CLIENTSIDE) //ThrustThing(angle*256/360,4,0,0)
        SPIR B 2 Bright A_CHolySeek
      Spawn4:
        TNT1 A 0 A_Explode(random(1,8),64,0)
        TNT1 A 0 A_SpawnItemEx("HolyMissilePuff2",0,0,-6,0,0,0,0,SXF_CLIENTSIDE) //ThrustThing(angle*256/360,4,0,0)
        SPIR B 2 Bright A_CHolyCheckScream
        goto Spawn1

      Death:
        SPIR D 4 Bright
        SPIR E 4 Bright A_Scream
        SPIR FGHI 4 Bright
        stop
    }
}

actor HolySpiritDM //: HolySpirit
{
    Projectile
    Radius 10
    Height 6
    Speed 20 // Previously 12
    Damage 5
    DamageType "Wraithverge"
    BounceType "Hexen"
    BounceFactor 0.9
    WallBounceFactor 0.9
    //RenderStyle Translucent
    //Alpha 0.5
    DeathSound "SpiritDie"
    Obituary "$SAMSARA_HEXEN_OB_SLOT7"
    -ALLOWBOUNCEONACTORS
    +BLOODSPLATTER
    -BOUNCEONACTORS
    +CANBLAST
    +EXPLODEONWATER
    +EXTREMEDEATH
    +FOILINVUL
    +LOOKALLAROUND
    +NOEXPLODEFLOOR
    +RIPPER
    +SEEKERMISSILE
    //+SKULLFLY
    +SKYEXPLODE
    States
    {
      Spawn:
        SPIR PPPP 3 Bright A_SpawnItemEx("HolyMissilePuff")
        TNT1 A 0 A_Explode(128,128,0)
      SpawnSpiritInit:
        TNT1 A 0 A_SetTranslucent(0.5,0)
        TNT1 A 0 A_SetAngle(-67.5)
        TNT1 A 0 ThrustThing(angle*256/360,6,0,0)
      SpawnSpiritStart:
        SPIR A 2 Bright // do nothing at this state, as per engine behavior of ignoring most code pointers on first tic
        TNT1 A 0 A_Jump(256,"SpawnSpiritLoop2") // for actors inheriting from this actor
        goto SpawnSpiritLoop2

      SpawnSpiritLoop1:
        TNT1 A 0 A_ChangeFlag("PAINLESS",false)
        TNT1 A 0 A_Explode(random(1,8),32,0)
        TNT1 A 0 A_SpawnItemEx("HolyMissilePuff2",0,0,-6,0,0,0,0,SXF_CLIENTSIDE) //ThrustThing(angle*256/360,4,0,0)
        TNT1 A 0 A_Weave(random(0,4),random(0,4),2,1)
        SPIR A 2 Bright A_SeekerMissile(2,10,SMF_LOOK) //A_CHolySeek
        goto SpawnSpiritLoop2

      SpawnSpiritLoop2:
        TNT1 A 0 A_ChangeFlag("PAINLESS",true)
        TNT1 A 0 A_Explode(random(1,8),32,0)
        TNT1 A 0 A_SpawnItemEx("HolyMissilePuff2",0,0,-6,0,0,0,0,SXF_CLIENTSIDE) //ThrustThing(angle*256/360,4,0,0)
        TNT1 A 0 A_Weave(random(0,4),random(0,4),2,1)
        SPIR A 2 Bright A_SeekerMissile(2,10,SMF_LOOK) //A_CHolySeek
        goto SpawnSpiritLoop3

      SpawnSpiritLoop3:
        TNT1 A 0 A_Explode(random(1,8),32,0)
        TNT1 A 0 A_SpawnItemEx("HolyMissilePuff2",0,0,-6,0,0,0,0,SXF_CLIENTSIDE) //ThrustThing(angle*256/360,4,0,0)
        TNT1 A 0 A_Weave(random(0,4),random(0,4),2,1)
        SPIR B 2 Bright A_SeekerMissile(2,10,SMF_LOOK) //A_CHolySeek
        goto SpawnSpiritLoop4

      SpawnSpiritLoop4:
        TNT1 A 0 A_Jump(20,1)
        goto SpawnSpiritLoop4NoclipCheck
        TNT1 A 0 A_PlaySound("SpiritActive",CHAN_VOICE)
        goto SpawnSpiritLoop4NoclipCheck

      SpawnSpiritLoop4NoclipCheck:
        TNT1 A 0 A_JumpIfTargetInLOS(2,180,JLOSF_PROJECTILE)
        TNT1 A 0 A_ChangeFlag("NOCLIP",false)
        goto SpawnSpiritLoop4Start
        TNT1 A 0 A_ChangeFlag("NOCLIP",true)
        goto SpawnSpiritLoop4Start

      SpawnSpiritLoop4Start:
        TNT1 A 0 A_Explode(random(1,8)*random(1,2),32,0)
        TNT1 A 0 A_SpawnItemEx("HolyMissilePuff2",0,0,-6,0,0,0,0,SXF_CLIENTSIDE) //ThrustThing(angle*256/360,4,0,0)
        TNT1 A 0 A_Weave(random(0,4),random(0,4),2,1)
        SPIR B 2 Bright A_SeekerMissile(2,10,SMF_LOOK) //A_CHolyCheckScream
        TNT1 A 0 A_JumpIfInventory("SamsaraHexenWraithvergeGhostTimer",0,"TimeOut")
        TNT1 A 0 A_GiveInventory("SamsaraHexenWraithvergeGhostTimer")
        goto SpawnSpiritLoop1

      TimeOut:
        TNT1 A 0 A_ScaleVelocity(0.25)
        SPIR D 4 Bright A_SetTics(random(1,4))
        goto DeathFinish

      Death:
        SPIR D 4 Bright
      DeathFinish:
        SPIR E 4 Bright A_Scream
        SPIR FGHI 4 Bright
        stop
    }
}

actor HolySpiritDM2 : HolySpiritDM
{
    States
    {
      Spawn:
        TNT1 AAAA 3 //Bright A_SpawnItemEx("HolyMissilePuff")
      SpawnSpiritInit:
        TNT1 A 0 A_SetTranslucent(0.5,0)
        TNT1 A 0 A_SetAngle(67.5)
        TNT1 A 0 ThrustThing(angle*256/360,6,0,0)
        goto SpawnSpiritStart
    }
}

actor HolySpiritDM3 : HolySpiritDM
{
    States
    {
      Spawn:
        TNT1 AAAA 3 //Bright A_SpawnItemEx("HolyMissilePuff")
      SpawnSpiritInit:
        TNT1 A 0 A_SetTranslucent(0.5,0)
        TNT1 A 0 A_SetAngle(-135)
        TNT1 A 0 ThrustThing(angle*256/360,6,0,0)
        goto SpawnSpiritStart
    }
}

actor HolySpiritDM4 : HolySpiritDM
{
    States
    {
      Spawn:
        TNT1 AAAA 3 //Bright A_SpawnItemEx("HolyMissilePuff")
      SpawnSpiritInit:
        TNT1 A 0 A_SetTranslucent(0.5,0)
        TNT1 A 0 A_SetAngle(135)
        TNT1 A 0 ThrustThing(angle*256/360,6,0,0)
        goto SpawnSpiritStart
    }
}

// Daedolon

actor Bloodscourge : SamsaraHexenWeapon
{
    Inventory.PickupMessage "Bloodscourge"
    Weapon.SelectionOrder 3100
    Weapon.YAdjust 20
    Weapon.AmmoType1 "Clip"
    //Weapon.AmmoGive1 25
    Weapon.AmmoUse1 15
    Weapon.AmmoType2 "Cell"
    //Weapon.AmmoGive2 25
    Weapon.AmmoUse2 15
    Obituary "$SAMSARA_HEXEN_OB_SLOT7"
    Tag "Bloodscourge"
    +BFG
    +PRIMARY_USES_BOTH
    States
    {
      Spawn:
        H105 A -1 Bright
        stop

      Ready:
        TNT1 A 0 A_TakeInventory("SamsaraHexenWeaponPaletteBlendCounter")
        MSTF AAAAAABBBBBBCCCCCCDDDDDDEEEEEEFFFFF 1 A_WeaponReady
        loop

      Deselect:
        TNT1 A 0 A_TakeInventory("SamsaraHexenWeaponPaletteBlendCounter")
        MSTF A 1 A_Lower
        loop

      Select:
        TNT1 A 0 A_TakeInventory("SamsaraHexenWeaponPaletteBlendCounter")
        MSTF A 1 A_Raise
        loop

      Fire:
        MSTF G 4 Offset(0,40)
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,3)
        TNT1 A 0 A_JumpIfInventory("Clip",15,1)
        goto FireContinue
        TNT1 A 0 A_JumpIfInventory("Cell",15,1)
        goto FireContinue
        TNT1 A 0 A_GunFlash("Flash",GFF_NOEXTCHANGE)
        goto FireContinue

      FireContinue:
        MSTF H 4 Bright Offset(0,48) A_GiveInventory("SamsaraHexenBloodscourgeAttackHandler")
        MSTF H 2 Bright Offset(0,48) A_TakeInventory("SamsaraHexenWeaponPaletteBlendCounter",1)
        MSTF II 2 Offset(0,48) A_TakeInventory("SamsaraHexenWeaponPaletteBlendCounter",1)
        MSTF I 1 Offset(0,40)
        MSTF J 5 Offset(0,36)
        goto Ready

      Flash:
        TNT1 A 4 Bright A_Light1
        TNT1 A 2 Bright
        goto LightDone
    }
}

actor SamsaraHexenBloodscourgeAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Clip",15,1)
        stop
        TNT1 A 0 A_JumpIfInventory("Cell",15,1)
        stop
        TNT1 A 0 A_TakeInventory("Clip",15)
        TNT1 A 0 A_TakeInventory("Cell",15)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("MageStaffFire",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireCustomMissile("MageStaffFX3",0,false,0,8)
        TNT1 A 0 A_FireCustomMissile("MageStaffFX3",-3,false,0,8)
        TNT1 A 0 A_FireCustomMissile("MageStaffFX3",3,false,0,8)
        goto PickupFinish

      PickupAttackCoop:
        TNT1 A 0 A_FireCustomMissile("MageStaffFX3Coop",0,false,0,8)
        TNT1 A 0 A_FireCustomMissile("MageStaffFX3Coop",-3,false,0,8)
        TNT1 A 0 A_FireCustomMissile("MageStaffFX3Coop",3,false,0,8)
        goto PickupFinish

      // Finishing stuff
      PickupFinish:
        TNT1 A 0 A_JumpIfInventory("WeaponFlashesAreDisabled",1,"PickupStop")
        TNT1 A 0 A_GiveInventory("SamsaraHexenWeaponPaletteBlendCounter",3)
        TNT1 A 0 ACS_NamedExecuteWithResult("SamsaraHexenWeaponPaletteBlend", 1)
        stop
    }
}

actor MageStaffFX3
{
    Projectile
    Radius 2
    Height 2
    Speed 17
    Damage 16 // was 4. buffed because Bloodscourge is a little underwhelming
    DamageType "Fire"
    DeathSound "MageStaffExplode"
    Decal "Scorch"
    Obituary "$SAMSARA_HEXEN_OB_INSTAGIB"
    +BLOODSPLATTER
    +EXTREMEDEATH
    +SCREENSEEKER
    +SEEKERMISSILE
    States
    {
      Spawn:
        MSP2 ABCD 2 Bright A_SeekerMissile(2,10,SMF_LOOK|SMF_PRECISE,51)
        loop

      Death:
        MSP2 E 4 Bright A_SetTranslucent(1,1)
        MSP2 F 5 Bright A_Explode(160,192,0) // was 80 damage
        MSP2 GH 5 Bright
        MSP2 I 4 Bright
        stop
    }
}

actor MageStaffFX3Coop : MageStaffFX3
{
    Species "Player"
    +THRUSPECIES
}

// Baratus

actor SamsaraHexenQuietusMeleePuffSpawned : Boolean {}
actor SamsaraHexenQuietusMeleeAmmoTaken : Boolean {}

actor Quietus : SamsaraHexenWeapon
{
    Inventory.PickupMessage "Quietus"
    Weapon.SelectionOrder 2900
    Weapon.YAdjust 10
    Weapon.AmmoType1 "Clip"
    //Weapon.AmmoGive1 25
    Weapon.AmmoUse1 7
    Weapon.AmmoType2 "Cell"
    //Weapon.AmmoGive2 25
    Weapon.AmmoUse2 7
    Obituary "$SAMSARA_HEXEN_OB_SLOT7"
    Tag "Quietus"
    +ALT_AMMO_OPTIONAL
    +AMMO_OPTIONAL
    +BFG
    +EXPLOSIVE
    +NOALERT
    +PRIMARY_USES_BOTH
    States
    {
      Spawn:
        H102 P -1 Bright
        stop

      Ready:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"ReadyAmmo")
        TNT1 A 0 A_JumpIfInventory("Clip",7,1)
        goto ReadyNoAmmo
        TNT1 A 0 A_JumpIfInventory("Cell",7,"ReadyAmmo")
        goto ReadyNoAmmo

      ReadyNoAmmo:
        H102 J 1 A_WeaponReady
        goto Ready

      ReadyAmmo:
        H102 A 1 Bright A_WeaponReady
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,3)
        TNT1 A 0 A_JumpIfInventory("Clip",7,1)
        goto Ready
        TNT1 A 0 A_JumpIfInventory("Cell",7,1)
        goto Ready
        H102 A 1 Bright A_WeaponReady
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,3)
        TNT1 A 0 A_JumpIfInventory("Clip",7,1)
        goto Ready
        TNT1 A 0 A_JumpIfInventory("Cell",7,1)
        goto Ready
        H102 A 1 Bright A_WeaponReady
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,3)
        TNT1 A 0 A_JumpIfInventory("Clip",7,1)
        goto Ready
        TNT1 A 0 A_JumpIfInventory("Cell",7,1)
        goto Ready
        H102 A 1 Bright A_WeaponReady
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,3)
        TNT1 A 0 A_JumpIfInventory("Clip",7,1)
        goto Ready
        TNT1 A 0 A_JumpIfInventory("Cell",7,1)
        goto Ready
        H102 B 1 Bright A_WeaponReady
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,3)
        TNT1 A 0 A_JumpIfInventory("Clip",7,1)
        goto Ready
        TNT1 A 0 A_JumpIfInventory("Cell",7,1)
        goto Ready
        H102 B 1 Bright A_WeaponReady
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,3)
        TNT1 A 0 A_JumpIfInventory("Clip",7,1)
        goto Ready
        TNT1 A 0 A_JumpIfInventory("Cell",7,1)
        goto Ready
        H102 B 1 Bright A_WeaponReady
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,3)
        TNT1 A 0 A_JumpIfInventory("Clip",7,1)
        goto Ready
        TNT1 A 0 A_JumpIfInventory("Cell",7,1)
        goto Ready
        H102 B 1 Bright A_WeaponReady
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,3)
        TNT1 A 0 A_JumpIfInventory("Clip",7,1)
        goto Ready
        TNT1 A 0 A_JumpIfInventory("Cell",7,1)
        goto Ready
        H102 C 1 Bright A_WeaponReady
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,3)
        TNT1 A 0 A_JumpIfInventory("Clip",7,1)
        goto Ready
        TNT1 A 0 A_JumpIfInventory("Cell",7,1)
        goto Ready
        H102 C 1 Bright A_WeaponReady
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,3)
        TNT1 A 0 A_JumpIfInventory("Clip",7,1)
        goto Ready
        TNT1 A 0 A_JumpIfInventory("Cell",7,1)
        goto Ready
        H102 C 1 Bright A_WeaponReady
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,3)
        TNT1 A 0 A_JumpIfInventory("Clip",7,1)
        goto Ready
        TNT1 A 0 A_JumpIfInventory("Cell",7,1)
        goto Ready
        H102 C 1 Bright A_WeaponReady
        goto Ready

      Deselect:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"DeselectAmmo")
        TNT1 A 0 A_JumpIfInventory("Clip",7,1)
        goto DeselectNoAmmo
        TNT1 A 0 A_JumpIfInventory("Cell",7,"DeselectAmmo")
        goto DeselectNoAmmo

      DeselectNoAmmo:
        H102 J 1 A_Lower
        goto Deselect

      DeselectAmmo:
        H102 A 1 Bright A_Lower
        goto Deselect

      Select:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"SelectAmmo")
        TNT1 A 0 A_JumpIfInventory("Clip",7,1)
        goto SelectNoAmmo
        TNT1 A 0 A_JumpIfInventory("Cell",7,"SelectAmmo")
        goto SelectNoAmmo

      SelectNoAmmo:
        H102 J 1 A_Raise
        goto Select

      SelectAmmo:
        H102 A 1 Bright A_Raise
        goto Select

      Fire:
        TNT1 A 0 A_TakeInventory("SamsaraHexenQuietusMeleePuffSpawned")
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireAmmo")
        TNT1 A 0 A_JumpIfInventory("Clip",7,1)
        goto FireNoAmmo
        TNT1 A 0 A_JumpIfInventory("Cell",7,"FireAmmo")
        goto FireNoAmmo

      FireNoAmmo:
        H102 KL 3 Offset(5,36)
        H102 M 2 Offset(5,36)
        goto FireContinueAttack

      FireAmmo:
        H102 DE 3 Bright Offset(5,36)
        H102 F 2 Bright Offset(5,36)
        goto FireContinueAttack

      FireContinueAttack:
        TNT1 A 0 A_JumpIfCloser(128,"FireContinueAttackMeleeOnly") // imperfect hack for non-bleeding actors
        TNT1 A 0 A_GiveInventory("SamsaraHexenQuietusMeleeAttackHandler")
        TNT1 A 0 A_GiveInventory("SamsaraHexenQuietusRangedAttackHandler")
        goto FireContinue

      FireContinueAttackMeleeOnly:
        TNT1 A 0 A_GiveInventory("SamsaraHexenQuietusMeleeAttackHandler")
        goto FireContinue

      FireContinue:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireContinueAmmo")
        TNT1 A 0 A_JumpIfInventory("Clip",7,1)
        goto FireContinueNoAmmo
        TNT1 A 0 A_JumpIfInventory("Cell",7,"FireContinueAmmo")
        goto FireContinueNoAmmo

      FireContinueNoAmmo:
        H102 N 3 Offset(5,36)
        H102 O 2 Offset(5,36)
        TNT1 A 2 Offset(5,36)
        TNT1 A 10 Offset(5,150)
        goto FireFinish

      FireContinueAmmo:
        H102 G 3 Bright Offset(5,36)
        H102 H 2 Bright Offset(5,36)
        H102 I 2 Bright Offset(5,36)
        H102 I 10 Bright Offset(5,150)
        goto FireFinish

      FireFinish:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireFinishAmmo")
        TNT1 A 0 A_JumpIfInventory("Clip",7,1)
        goto FireFinishNoAmmo
        TNT1 A 0 A_JumpIfInventory("Cell",7,"FireFinishAmmo")
        goto FireFinishNoAmmo

      FireFinishNoAmmo:
        H102 J 1 Offset(5,60)
        H102 J 1 Offset(5,55)
        H102 J 1 Offset(5,50)
        H102 J 1 Offset(5,45)
        H102 J 1 Offset(5,40)
        goto Ready

      FireFinishAmmo:
        H102 A 1 Bright Offset(5,60)
        H102 B 1 Bright Offset(5,55)
        H102 C 1 Bright Offset(5,50)
        H102 A 1 Bright Offset(5,45)
        H102 B 1 Bright Offset(5,40)
        goto Ready
    }
}

// Melee

actor SamsaraHexenQuietusMeleeAttackHandler : Trigger
{
    States
    {
      // Initializing stuff
      Pickup:
        TNT1 A 0 A_TakeInventory("SamsaraHexenQuietusMeleeAmmoTaken")
        goto PickupAmmoCheck

      // Ammo checking stuff
      PickupAmmoCheck:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttackStrong")
        TNT1 A 0 A_JumpIfInventory("Clip",7,1)
        goto PickupAttackWeak
        TNT1 A 0 A_JumpIfInventory("Cell",7,"PickupAttackStrong")
        goto PickupAttackWeak

      // Attack stuff
      // Weak
      PickupAttackWeak:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackWeakCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackWeakCoop")
      PickupAttackWeakDM:
        TNT1 A 0 A_FireBullets(-10,-10,-1,random(30,50),"FSwordPuff",FBF_NORANDOM|FBF_EXPLICITANGLE|FBF_NOFLASH|FBF_NORANDOMPUFFZ,128)
        TNT1 A 0 A_FireBullets(-5,-5,-1,random(30,50),"FSwordPuff",FBF_NORANDOM|FBF_EXPLICITANGLE|FBF_NOFLASH|FBF_NORANDOMPUFFZ,128)
        TNT1 A 0 A_FireBullets(0,0,-1,random(30,50),"FSwordPuff",FBF_NORANDOM|FBF_EXPLICITANGLE|FBF_NOFLASH|FBF_NORANDOMPUFFZ,128)
        TNT1 A 0 A_FireBullets(5,5,-1,random(30,50),"FSwordPuff",FBF_NORANDOM|FBF_EXPLICITANGLE|FBF_NOFLASH|FBF_NORANDOMPUFFZ,128)
        TNT1 A 0 A_FireBullets(10,10,-1,random(30,50),"FSwordPuff",FBF_NORANDOM|FBF_EXPLICITANGLE|FBF_NOFLASH|FBF_NORANDOMPUFFZ,128)
        stop

      PickupAttackWeakCoop:
        TNT1 A 0 A_FireBullets(-10,-10,-1,random(30,50),"FSwordPuffCoop",FBF_NORANDOM|FBF_EXPLICITANGLE|FBF_NOFLASH|FBF_NORANDOMPUFFZ,128)
        TNT1 A 0 A_FireBullets(-5,-5,-1,random(30,50),"FSwordPuffCoop",FBF_NORANDOM|FBF_EXPLICITANGLE|FBF_NOFLASH|FBF_NORANDOMPUFFZ,128)
        TNT1 A 0 A_FireBullets(0,0,-1,random(30,50),"FSwordPuffCoop",FBF_NORANDOM|FBF_EXPLICITANGLE|FBF_NOFLASH|FBF_NORANDOMPUFFZ,128)
        TNT1 A 0 A_FireBullets(5,5,-1,random(30,50),"FSwordPuffCoop",FBF_NORANDOM|FBF_EXPLICITANGLE|FBF_NOFLASH|FBF_NORANDOMPUFFZ,128)
        TNT1 A 0 A_FireBullets(10,10,-1,random(30,50),"FSwordPuffCoop",FBF_NORANDOM|FBF_EXPLICITANGLE|FBF_NOFLASH|FBF_NORANDOMPUFFZ,128)
        stop

      // Strong
      PickupAttackStrong:
        TNT1 A 0 A_AlertMonsters
        TNT1 A 0 A_PlaySound("FighterSwordFire",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttackStrongInfinite")
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackStrongCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackStrongCoop")
      PickupAttackStrongDM:
        TNT1 A 0 A_FireBullets(-10,-10,-1,random(120,200),"FSwordPuffGlow",FBF_NORANDOM|FBF_EXPLICITANGLE|FBF_NOFLASH|FBF_NORANDOMPUFFZ,128)
        TNT1 A 0 A_FireBullets(-5,-5,-1,random(120,200),"FSwordPuffGlow",FBF_NORANDOM|FBF_EXPLICITANGLE|FBF_NOFLASH|FBF_NORANDOMPUFFZ,128)
        TNT1 A 0 A_FireBullets(0,0,-1,random(120,200),"FSwordPuffGlow",FBF_NORANDOM|FBF_EXPLICITANGLE|FBF_NOFLASH|FBF_NORANDOMPUFFZ,128)
        TNT1 A 0 A_FireBullets(5,5,-1,random(120,200),"FSwordPuffGlow",FBF_NORANDOM|FBF_EXPLICITANGLE|FBF_NOFLASH|FBF_NORANDOMPUFFZ,128)
        TNT1 A 0 A_FireBullets(10,10,-1,random(120,200),"FSwordPuffGlow",FBF_NORANDOM|FBF_EXPLICITANGLE|FBF_NOFLASH|FBF_NORANDOMPUFFZ,128)
        stop

      PickupAttackStrongCoop:
        TNT1 A 0 A_FireBullets(-10,-10,-1,random(120,200),"FSwordPuffGlowCoop",FBF_NORANDOM|FBF_EXPLICITANGLE|FBF_NOFLASH|FBF_NORANDOMPUFFZ,128)
        TNT1 A 0 A_FireBullets(-5,-5,-1,random(120,200),"FSwordPuffGlowCoop",FBF_NORANDOM|FBF_EXPLICITANGLE|FBF_NOFLASH|FBF_NORANDOMPUFFZ,128)
        TNT1 A 0 A_FireBullets(0,0,-1,random(120,200),"FSwordPuffGlowCoop",FBF_NORANDOM|FBF_EXPLICITANGLE|FBF_NOFLASH|FBF_NORANDOMPUFFZ,128)
        TNT1 A 0 A_FireBullets(5,5,-1,random(120,200),"FSwordPuffGlowCoop",FBF_NORANDOM|FBF_EXPLICITANGLE|FBF_NOFLASH|FBF_NORANDOMPUFFZ,128)
        TNT1 A 0 A_FireBullets(10,10,-1,random(120,200),"FSwordPuffGlowCoop",FBF_NORANDOM|FBF_EXPLICITANGLE|FBF_NOFLASH|FBF_NORANDOMPUFFZ,128)
        stop

      PickupAttackStrongInfinite:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackStrongInfiniteCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackStrongInfiniteCoop")
      PickupAttackStrongInfiniteDM:
        TNT1 A 0 A_FireBullets(-10,-10,-1,random(120,200),"FSwordPuffGlowInfinite",FBF_NORANDOM|FBF_EXPLICITANGLE|FBF_NOFLASH|FBF_NORANDOMPUFFZ,128)
        TNT1 A 0 A_FireBullets(-5,-5,-1,random(120,200),"FSwordPuffGlowInfinite",FBF_NORANDOM|FBF_EXPLICITANGLE|FBF_NOFLASH|FBF_NORANDOMPUFFZ,128)
        TNT1 A 0 A_FireBullets(0,0,-1,random(120,200),"FSwordPuffGlowInfinite",FBF_NORANDOM|FBF_EXPLICITANGLE|FBF_NOFLASH|FBF_NORANDOMPUFFZ,128)
        TNT1 A 0 A_FireBullets(5,5,-1,random(120,200),"FSwordPuffGlowInfinite",FBF_NORANDOM|FBF_EXPLICITANGLE|FBF_NOFLASH|FBF_NORANDOMPUFFZ,128)
        TNT1 A 0 A_FireBullets(10,10,-1,random(120,200),"FSwordPuffGlowInfinite",FBF_NORANDOM|FBF_EXPLICITANGLE|FBF_NOFLASH|FBF_NORANDOMPUFFZ,128)
        stop

      PickupAttackStrongInfiniteCoop:
        TNT1 A 0 A_FireBullets(-10,-10,-1,random(120,200),"FSwordPuffGlowInfiniteCoop",FBF_NORANDOM|FBF_EXPLICITANGLE|FBF_NOFLASH|FBF_NORANDOMPUFFZ,128)
        TNT1 A 0 A_FireBullets(-5,-5,-1,random(120,200),"FSwordPuffGlowInfiniteCoop",FBF_NORANDOM|FBF_EXPLICITANGLE|FBF_NOFLASH|FBF_NORANDOMPUFFZ,128)
        TNT1 A 0 A_FireBullets(0,0,-1,random(120,200),"FSwordPuffGlowInfiniteCoop",FBF_NORANDOM|FBF_EXPLICITANGLE|FBF_NOFLASH|FBF_NORANDOMPUFFZ,128)
        TNT1 A 0 A_FireBullets(5,5,-1,random(120,200),"FSwordPuffGlowInfiniteCoop",FBF_NORANDOM|FBF_EXPLICITANGLE|FBF_NOFLASH|FBF_NORANDOMPUFFZ,128)
        TNT1 A 0 A_FireBullets(10,10,-1,random(120,200),"FSwordPuffGlowInfiniteCoop",FBF_NORANDOM|FBF_EXPLICITANGLE|FBF_NOFLASH|FBF_NORANDOMPUFFZ,128)
        stop
    }
}

// Weak

actor FSwordPuff : AxePuff
{
    +NOTIMEFREEZE
    +PUFFGETSOWNER
    States
    {
      Spawn:
        FHFX S 0
      XDeath:
      Crash:
        TNT1 A 0 A_GiveInventory("SamsaraHexenQuietusMeleePuffSpawned",1,AAPTR_TARGET)
        TNT1 A 0 A_ChangeFlag("NOTIMEFREEZE",false)
        goto Super::Spawn
    }
}

actor FSwordPuffCoop : FSwordPuff { +MTHRUSPECIES }

// Strong

actor FSwordPuffGlow : AxePuff
{
    DamageType "HexenQuietusFire"
    RenderStyle "Add"
    Alpha 1
    SeeSound "FighterSwordExplode"
    ActiveSound ""
    AttackSound "FighterSwordExplode"
    Decal "SwordLightning"
    +EXTREMEDEATH
    +NOTIMEFREEZE
    +PUFFGETSOWNER
    States
    {
      Spawn:
        FSFX D 0 Bright
      XDeath:
        TNT1 A 0 A_JumpIfInventory("SamsaraHexenQuietusMeleeAmmoTaken",1,"Crash",AAPTR_TARGET)
        TNT1 A 0 A_TakeInventory("Clip",7,0,AAPTR_TARGET)
        TNT1 A 0 A_TakeInventory("Cell",7,0,AAPTR_TARGET)
        TNT1 A 0 A_GiveInventory("SamsaraHexenQuietusMeleeAmmoTaken",1,AAPTR_TARGET)
        goto Crash

      Crash:
        TNT1 A 0 A_GiveInventory("SamsaraHexenQuietusMeleePuffSpawned",1,AAPTR_TARGET)
        FSFX D 4 Bright A_ChangeFlag("NOTIMEFREEZE",false)
        TNT1 A 0 A_Jump(256,1,2,3,4)
        TNT1 AAAA 0 A_SpawnItemEx("FSwordFlame",random(-8,8),random(-8,8),random(-4,4))
        FSFX E 3 Bright
        FSFX F 4 Bright
        FSFX G 3 Bright
        FSFX H 4 Bright
        FSFX I 3 Bright
        FSFX J 4 Bright
        FSFX KLM 3 Bright
        stop
    }
}

actor FSwordPuffGlowCoop : FSwordPuffGlow { +MTHRUSPECIES }

actor FSwordPuffGlowInfinite : FSwordPuffGlow
{
    States
    {
      Spawn:
        FSFX D 0 Bright
      XDeath:
        goto Crash
    }
}

actor FSwordPuffGlowInfiniteCoop : FSwordPuffGlowInfinite { +MTHRUSPECIES }

// Ranged

actor SamsaraHexenQuietusRangedAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHexenQuietusMeleePuffSpawned",1,"PickupStop")
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Clip",14,1)
        stop
        TNT1 A 0 A_JumpIfInventory("Cell",14,1)
        stop
        TNT1 A 0 A_TakeInventory("Clip",14)
        TNT1 A 0 A_TakeInventory("Cell",14)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_AlertMonsters
        TNT1 A 0 A_PlaySound("FighterSwordFire",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireCustomMissile("FSwordMissile3",11.5,false,0,-10)
        TNT1 A 0 A_FireCustomMissile("FSwordMissile3",5.625,false,0,-5)
        TNT1 A 0 A_FireCustomMissile("FSwordMissile3",0,false)
        TNT1 A 0 A_FireCustomMissile("FSwordMissile3",-5.625,false,0,5)
        TNT1 A 0 A_FireCustomMissile("FSwordMissile3",-11.5,false,0,10)
        stop

      PickupAttackCoop:
        TNT1 A 0 A_FireCustomMissile("FSwordMissile3Coop",11.5,false,0,-10)
        TNT1 A 0 A_FireCustomMissile("FSwordMissile3Coop",5.625,false,0,-5)
        TNT1 A 0 A_FireCustomMissile("FSwordMissile3Coop",0,false)
        TNT1 A 0 A_FireCustomMissile("FSwordMissile3Coop",-5.625,false,0,5)
        TNT1 A 0 A_FireCustomMissile("FSwordMissile3Coop",-11.5,false,0,10)
        stop
    }
}

actor FSwordMissile3 : FSwordMissile
{
    Damage 24 // was 8
    DamageType "HexenQuietusFire"
    Decal "SwordLightning"
    +BLOODSPLATTER
    States
    {
      Death:
        FSFX D 4 Bright
        FSFX E 3 Bright A_FSwordFlames
        FSFX F 4 Bright A_Explode(96,128,0) // was 64 damage
        FSFX G 3 Bright
        FSFX H 4 Bright
        FSFX I 3 Bright
        FSFX J 4 Bright
        FSFX KLM 3 Bright
        stop
    }
}

actor FSwordMissile3Coop : FSwordMissile3
{
    Species "Player"
    +THRUSPECIES
}
