Actor "Liquid Zorcher" : Weapon
{
    Weapon.SelectionOrder 2800
    //Weapon.SlotNumber 7
    Inventory.PickUpSound "chex/weaponget"
    Weapon.AmmoUse 5
    Weapon.AmmoGive 40
    Weapon.AmmoType "Cell"
    +WEAPON.NOAUTOFIRE
    +BLOODLESSIMPACT
    +WEAPON.BFG
    +NOEXTREMEDEATH
    +INVENTORY.UNDROPPABLE
    Inventory.PickupMessage "You got the LAZ Device! Woot!"
    Obituary "$SAMSARA_CHEX_OB_SLOT7S"
    DamageType "ZorchBfg"
	Inventory.RestrictedTo "ChexWarrior"
    Tag "Liquid Zorcher"
    States
    {

      Ready:
		TNT1 A 0 A_PlaySound("chex/bfg10kidle")
		CFGG IIIJJJKKKLLL 1 A_WeaponReady
        loop

      Deselect:
        CFGG M 1 A_Lower
        loop

      Select:
        CFGG M 1 A_Raise
        loop

      Fire:
 		CFGG M 20 A_PlaySound("chex/bfg10kfire")
		CFGG N 4
		CFGG OPQR 1
	  Hold:
		CFGG S 2 A_GunFlash
		CFGG T 2 A_GiveInventory("SamsaraChexLiquidZorcherAttackHandler")
		CFGG U 2
		TNT1 A 0 A_ReFire
		CFGG V 35 A_PlaySound("chex/bfg10kcool")
		goto Ready

      Flash:
	    TNT1 A 2 Bright A_Light1
		TNT1 A 3 Bright
        goto LightDone

      Spawn:
        CFUG B -1
        stop
    }
}

actor SamsaraChexLiquidZorcherAttackHandler : Trigger
{
    States
    {
      // Ammo check stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupTakeAmmo")
        TNT1 A 0 A_JumpIfInventory("Cell",5,"PickupTakeAmmo")
        stop

      // Ammo taking stuff
      PickupTakeAmmo:
        TNT1 A 0 A_TakeInventory("Cell",5,TIF_NOTAKEINFINITE)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireBullets(0,0,1,0,"LiquidZorcherShot",FBF_NOFLASH)
        stop

      PickupAttackCoop:
        TNT1 A 0 A_FireBullets(0,0,1,0,"LiquidZorcherShot2",FBF_NOFLASH)
        stop
    }
}

actor LiquidZorcherShot
{
	Radius 11
	Height 8
	Speed 20
	DamageType "Zorch"
	+NOBLOCKMAP
	+NOGRAVITY
	+ACTIVATEIMPACT
	+ACTIVATEPCROSS
	+NOTELEPORT 
	+PUFFONACTORS	
	+PUFFGETSOWNER
	+FORCERADIUSDMG
    +NOTIMEFREEZE
	+BLOODLESSIMPACT
	+NOEXTREMEDEATH
	Renderstyle Add
	Alpha 0.75
	SeeSound "chex/bfg10kx"
	AttackSound "chex/bfg10kx"
    //SelfObituary "%o got reckless with %p Liquid Zorcher."
	States
	{
	Spawn:
		CFE1 A 0 Bright
		TNT1 A 0 A_Explode(160,160,0)
		CFE1 A 3 Bright A_ChangeFlag("NOTIMEFREEZE",false)
		CFE1 BCDEF 3 Bright
		Stop
	}
}

actor LiquidZorcherShot2 : LiquidZorcherShot { +MTHRUSPECIES }
