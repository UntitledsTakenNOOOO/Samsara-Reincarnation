actor SamsaraDescentFlashMissileAttackHandler : Trigger
{
	States
	{
	// Ammo checking/taking stuff
	Pickup:
		TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
		goto PickupAttack
		TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
		TNT1 A 0 A_JumpIfInventory("Shell",1,1)
		stop
		TNT1 A 0 A_TakeInventory("Shell",1)
		goto PickupAttack

	// Attack stuff
	PickupAttack:
		TNT1 A 0 A_PlaySound("Descent/Missile/Fire",CHAN_WEAPON)
		TNT1 A 0 A_JumpIfInventory("DescentMissileSwitcher",1,"PickupAttackLeft")
	// Right
	PickupAttackRight:
		TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackRightCoop")
		TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackRightCoop")
	PickupAttackRightDM:
		TNT1 A 0 A_FireCustomMissile("DescentFlashMissile",0,false,8)
		goto PickupFinishRight
	PickupAttackRightCoop:
		TNT1 A 0 A_FireCustomMissile("DescentFlashMissileCoop",0,false,8)
		goto PickupFinishRight

	// Left
	PickupAttackLeft:
		TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackLeftCoop")
		TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackLeftCoop")
	PickupAttackLeftDM:
		TNT1 A 0 A_FireCustomMissile("DescentFlashMissile",0,false,-8)
		goto PickupFinishLeft
	PickupAttackLeftCoop:
		TNT1 A 0 A_FireCustomMissile("DescentFlashMissileCoop",0,false,-8)
		goto PickupFinishLeft

	// Finishing stuff
	// Right
	PickupFinishRight:
		TNT1 A 0 A_GiveInventory("DescentMissileSwitcher")
		stop

	// Left
	PickupFinishLeft:
		TNT1 A 0 A_TakeInventory("DescentMissileSwitcher")
		stop
	}
}

actor DescentFlashMissile : DescentMissile
{
	States
	{
		Death:
			TNT1 A 0 A_RadiusGive("DescentFlashGiver",60,RGF_PLAYERS)
		DeathContinue:
			TNT1 A 0 A_RemoveMaster
			TNT1 A 0 A_Explode(25,50,0)
			TNT1 A 0 A_GiveToTarget("DescentMissileStaticGiver")
			TNT1 A 0 A_TakeFromTarget("DescentMissileCamera",1)
			TNT1 A 0 A_PlaySound("Descent/Flash/Explode",CHAN_VOICE)
			TNT1 A 1 A_SpawnItemEx("DescentExplosion",0,0,0,0,0,0,0,SXF_CLIENTSIDE)
			stop
	}
}

actor DescentFlashMissileCoop : DescentFlashMissile
{
	Species "Player"
	+THRUSPECIES
	States
	{
		Death:
			TNT1 A 0 A_JumpIfCloser(60,1)
			goto DeathContinue
			TNT1 A 0 A_GiveToTarget("DescentFlashGiver")
			goto DeathContinue
	}
}

actor DescentFlashGiver : Trigger
{
	States
	{
		Pickup:
			TNT1 A 0 A_SetBlend("FFFFFF",0.9,35) //ACS_NamedExecuteWithResult("DescentFlashFade")
			stop
	}
}
