ACTOR " Firebomb " : Weapon
{
     Weapon.SlotNumber 5
     Weapon.SelectionOrder 1100
     Weapon.UpSound "gen/wepswitch"
     Inventory.PickupSound "gen/pickup"
     Inventory.Pickupmessage "You found a Firebomb!"
     Weapon.AmmoGive 5
     Weapon.AmmoUse 1
     Weapon.AmmoType "FBMissiles"
     Obituary "%k firebombed %o. 'oh the humanity!'"
	 Inventory.RestrictedTo "IPFreeley"	 
     +IGNORESKILL
	 States 
     { 
     Spawn: 
        PFBM A -1 
        Stop 
     Ready: 
		TNT1 A 0 A_TakeInventory("UsingFirearm", 1)
        FBOM A 1 A_WeaponReady 
        Goto Ready+1
     Deselect: 
        FBOM A 1 A_Lower 
        Loop
     Select:
        FBOM A 1 A_Raise
        Loop 
     Fire:
        FBOM A 1 Offset(0,36) A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		TNT1 A 0 A_PlaySound("Firebomb/Shoot")
        FBOM A 1 Offset(0,38) A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		TNT1 A 0 A_JumpIfInventory("DMModeOn", 1, "DMFire")
		TNT1 A 0 A_JumpIfInventory("CoopModeOn", 1, "CoopFire")
		TNT1 A 0 A_FireCustomMissile("CompFbomb",0,1,0,-8)
	 Continue:
        FBOM A 2 Offset(0,42) A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
        FBOM B 3 Bright Offset(0,40) A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
        FBOM C 3 Bright Offset(0,38) A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
        FBOM D 3 Bright Offset(0,36) A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
        FBOM D 1 Offset(0,36) A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)		
        FBOM A 1 Offset(0,34) A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
        FBOM A 1 Offset(0,32) A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
        FBOM A 15 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
        FBOM A 0 a_refire
        Goto Ready+1
	 CoopFire:
		TNT1 A 0 A_FireCustomMissile("Fbomb",0,1,0,-8)
		Goto Continue
	 DMFire:
		TNT1 A 0 A_FireCustomMissile("FbombDM",0,1,0,-8)
		Goto Continue		
     } 
}  
  
actor Fbomb
{ 
    Radius 12
    Height 6
    Speed 28
    Damage (100)
    damagetype "RottExplosive"
    SCALE 0.55 
    PROJECTILE
	+WINDTHRUST
	+THRUGHOST
	+THRUSPECIES
	+NOBLOOD
	+BLOODLESSIMPACT
	+FORCERADIUSDMG	
	Species "Player"
    DeathSound "god/bang"
//	ActiveSound "missile/fly"
    Obituary "%k firebombed %o. 'oh the humanity!'"
	Decal Scorch
    States
    {
    Spawn:
		TNT1 A 0
        TNT1 A 0 A_PlaySound("missile/fly",5,1.0,1,ATTN_NORM)
	SpawnLoop:	
        FMIS AAAAAAA 2 Bright A_CustomMissile("MissileTrail",0,0,0,0)    
        Loop
    Death:
	XDeath:
		TNT1 A 0 A_StopSound(5)
		TNT1 A 0 A_PlaySound("gen/expl2",6,1.0,0,ATTN_NORM)	
        TNT1 A 0 A_NoGravity
		TNT1 A 2 A_Explode(50,64, 1, true, 32)
//      TNT1 A 0 Radius_Quake(5, 60,0, 16, 0)
        TNT1 A 0 A_CustomMissile("Fbomber", 0, 0, 0, 6)
        TNT1 A 0 A_CustomMissile("Fbomber", 0, 0, 90, 6)
        TNT1 A 0 A_CustomMissile("Fbomber", 0, 0, 180, 6)
        TNT1 A 0 A_CustomMissile("Fbomber", 0, 0, 270, 6)
		TNT1 A 0 A_CustomMissile("FbombEffect", 0)
		BOMB ABCDEFGHIJKLMNOPQRST 2 Bright
        Stop
	Crash:
		TNT1 A 0 A_StopSound(5)	
		TNT1 A 0 A_PlaySound("gen/expl2",6,1.0,0,ATTN_NORM)
		TNT1 A 0 A_Jump(250,2)
		TNT1 A 0 A_PlaySound("rmissile/cook",5,1.0,0,ATTN_NORM)		
		TNT1 A 0		
        TNT1 A 0 A_NoGravity
		TNT1 A 2 A_Explode(50,64, 1, true, 32)
//      TNT1 A 0 Radius_Quake(5, 60,0, 16, 0)
        TNT1 A 0 A_CustomMissile("Fbomber", 0, 0, 0, 6)
        TNT1 A 0 A_CustomMissile("Fbomber", 0, 0, 90, 6)
        TNT1 A 0 A_CustomMissile("Fbomber", 0, 0, 180, 6)
        TNT1 A 0 A_CustomMissile("Fbomber", 0, 0, 270, 6)
		TNT1 A 0 A_CustomMissile("FbombEffect", 0)
		BOMB ABCDEFGHIJKLMNOPQRST 2 Bright
        Stop		
    }
} 

Actor FbombDM : Fbomb { Damage (0) Speed 32 }

Actor CompFbomb : Fbomb
{
	-THRUSPECIES
	Species "None"
	Decal Scorch
    States
    {
    Death:
	XDeath:
		TNT1 A 0 A_StopSound(5)	
		TNT1 A 0 A_PlaySound("gen/expl2",6,1.0,0,ATTN_NORM)	
        TNT1 A 0 A_NoGravity
		TNT1 A 2 A_Explode(50,64, 1, true, 32)
        TNT1 A 0 Radius_Quake(5,60,0,16,0)
        TNT1 A 0 A_CustomMissile("CompFbomber", 0, 0, 0, 6)
        TNT1 A 0 A_CustomMissile("CompFbomber", 0, 0, 90, 6)
        TNT1 A 0 A_CustomMissile("CompFbomber", 0, 0, 180, 6)
        TNT1 A 0 A_CustomMissile("CompFbomber", 0, 0, 270, 6)
		TNT1 A 0 A_CustomMissile("FbombEffect", 0)
		BOMB ABCDEFGHIJKLMNOPQRST 2 Bright
        Stop
	Crash:
		TNT1 A 0 A_StopSound(5)	
		TNT1 A 0 A_PlaySound("gen/expl2",6,1.0,0,ATTN_NORM)
		TNT1 A 0 A_Jump(250,2)
		TNT1 A 0 A_PlaySound("rmissile/cook",5,1.0,0,ATTN_NORM)		
		TNT1 A 0		
        TNT1 A 0 A_NoGravity
		TNT1 A 2 A_Explode(50,64, 1, true, 32)
        TNT1 A 0 Radius_Quake(5,60,0,16,0)
        TNT1 A 0 A_CustomMissile("CompFbomber", 0, 0, 0, 6)
        TNT1 A 0 A_CustomMissile("CompFbomber", 0, 0, 90, 6)
        TNT1 A 0 A_CustomMissile("CompFbomber", 0, 0, 180, 6)
        TNT1 A 0 A_CustomMissile("CompFbomber", 0, 0, 270, 6)
		TNT1 A 0 A_CustomMissile("FbombEffect", 0)
		BOMB ABCDEFGHIJKLMNOPQRST 2 Bright
        Stop	
    }	
}

Actor FbombEffect
{
	PROJECTILE
	Damage 0
	Scale 0.52
	Speed 0
	Height 1
	Radius 1
	+RIPPER
	+NOBLOOD
	+CLIENTSIDEONLY
	States
	{
		Spawn:
		  BOMB ABCDEFGHIJKLMNOPQRST 2 Bright
		  Stop
	}
}

ACTOR Fbomber
{
     Height 1
	 Radius 1
	 Damage 0
	 DamageType "RottExplosive"
	 PROJECTILE
	 +THRUACTORS
     +NOEXPLODEFLOOR
	 +STEPMISSILE
	 +NOBLOOD
	 +BLOODLESSIMPACT
     Speed 12
	 MaxStepHeight 24 
	 Obituary "%k firebombed %o. 'oh the humanity!'"
     States
     {
		Spawn:
		  TNT1 A 5
          TNT1 A 5 A_CustomMissile("Fboming", 1, 0, 0, 6)
          TNT1 A 5 A_CustomMissile("Fboming", 1, 0, 0, 6)
          TNT1 A 5 A_CustomMissile("Fboming", 1, 0, 0, 6)
          TNT1 A 5 A_CustomMissile("Fboming", 1, 0, 0, 6)
          Stop
     }
}

Actor CompFbomber : FBomber
{
     States
     {
		Spawn:
		  TNT1 A 5
          TNT1 A 5 A_CustomMissile("CompFboming", 1, 0, 0, 6)
          TNT1 A 5 A_CustomMissile("CompFboming", 1, 0, 0, 6)
          TNT1 A 5 A_CustomMissile("CompFboming", 1, 0, 0, 6)
          TNT1 A 5 A_CustomMissile("CompFboming", 1, 0, 0, 6)
		  Stop
     }
}

ACTOR Fboming
{
    Radius 1
    Height 1
    Speed 0
    Scale 0.52
	Damage 0
	PROJECTILE
	DamageType "RottExplosive"
	+THRUACTORS  
	+NOBLOOD
	+BLOODLESSIMPACT
    +NOGRAVITY
	+FORCERADIUSDMG
    +NOEXPLODEFLOOR
    States
    {
     Spawn:
		TNT1 A 0
		TNT1 A 0 A_PlaySound("gen/expl1",CHAN_AUTO,1.0)
     Spawned:
		  TNT1 A 0 A_Quake(3,10,0,256,"")
          TNT1 A 0 A_Explode(50,70, 1, true, 35)
//		  TNT1 A 0 A_Explode(48,80,0)		  
          BOMB ABCDEFGHIJKLMNOPQRST  2 Bright
          Stop
    }
}

Actor CompFboming : Fboming
{
     States
     {
     Spawn:
		TNT1 A 0
		TNT1 A 0 A_PlaySound("gen/expl1",CHAN_AUTO,1.0)
     Spawned:
		  TNT1 A 0 A_Quake(3,10,0,256,"")
          TNT1 A 0 A_Explode(50,70, 1, true, 35)
          BOMB ABCDEFGHIJKLMNOPQRST  2 Bright
          Stop
     }
}