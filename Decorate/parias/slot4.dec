actor "Serpent Staff" : CWeapStaff
{
    Weapon.SelectionOrder 1600
    Weapon.SlotNumber 4
    Weapon.AmmoUse1 0
    Weapon.AmmoGive1 25
    Weapon.KickBack 150
    Weapon.YAdjust 10
    Weapon.AmmoType1 "Clip"
    Inventory.PickupMessage "You got the Serpent Staff!"
    Inventory.PickupSound "hexen/weaponget"
    Obituary "$SAMSARA_HEXEN_OB_SLOT4_1"
	Inventory.RestrictedTo "HexenPlayer"
    +INVENTORY.UNDROPPABLE
    States
    {
      Spawn:
        WCSS A -1
        Stop

      Select:
		TNT1 A 0 A_JumpIfInventory("HexenClassMode", 2, "SelectFighter")
		TNT1 A 0 A_JumpIfInventory("HexenClassMode", 1, "SelectMage")
        CSSF C 1 A_Raise
        Loop		
		
      Deselect:
		TNT1 A 0 A_JumpIfInventory("HexenClassMode", 2, "DeselectFighter")
		TNT1 A 0 A_JumpIfInventory("HexenClassMode", 1, "DeselectMage")
        CSSF B 3
        CSSF C 4
        CSSF C 1 A_Lower
        Wait	
		
	  DeselectMage:
		H110 A 1 A_Lower
		Loop		

	  SelectMage:
		H110 A 1 A_Raise
		Loop		
		
	  DeselectFighter:
		TNT1 A 0 A_JumpIfInventory("Clip",3,"DeselectFighterGlow")
		H122 A 1 A_Lower
		Loop
		
	  DeselectFighterGlow:
		H122 H 1 A_Lower
		Loop
		
	  SelectFighter:
		TNT1 A 0 A_JumpIfInventory("Clip",3,"SelectFighterGlow")
		H122 A 1 A_Raise
		Loop 
		
	  SelectFighterGlow:
		H122 H 1 A_Raise
		Loop
		
      Ready:
		TNT1 A 0 A_JumpIfInventory("HexenClassMode", 2, "ReadyFighter")
		TNT1 A 0 A_JumpIfInventory("HexenClassMode", 1, "ReadyMage")
		Goto ReadyCleric

	  ReadyCleric:  
        CSSF C 4
        CSSF B 3 A_CStaffInitBlink
		TNT1 A 0 A_JumpIfInventory("HexenClassMode", 2, "ReadyFighter")
		TNT1 A 0 A_JumpIfInventory("HexenClassMode", 1, "ReadyMage")	
        CSSF AAAAAAA 1 A_WeaponReady
        CSSF A 1 A_CStaffCheckBlink
        Goto ReadyCleric+3
		
	  ReadyFighter:
		TNT1 A 0 A_JumpIfInventory("Clip",3,"ReadyFighterGlow")
		H122 A 1 A_WeaponReady
		Goto Ready
		
	  ReadyFighterGlow:
		H122 HI 3 A_WeaponReady
		Goto Ready
		
	  ReadyMage:
	    H110 A 1 A_WeaponReady
		Goto Ready			

      Fire:
		TNT1 A 0 A_JumpIfInventory("HexenClassMode", 2, "FireFighter")
		TNT1 A 0 A_JumpIfInventory("HexenClassMode", 1, "FireMage")
		TNT1 A 0 A_JumpIfInventory("Clip", 1, 1)
		Goto SwitchWeapon
        CSSF A 0 A_TakeInventory("DrainCounter")
        goto Hold

      Hold:
		TNT1 A 0 A_JumpIfInventory("HexenClassMode", 2, "FireFighter")
		TNT1 A 0 A_JumpIfInventory("HexenClassMode", 1, "FireMage")
        CSSF A 1 BRIGHT Offset (0, 45) A_JumpIfCloser(128, "Drain")
        CSSF A 0 A_TakeInventory("DrainCounter", 1)
		TNT1 A 0 A_TakeInventory("Clip", 1, TIF_NOTAKEINFINITE)
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"CoopFire")
        CSSF J 1 BRIGHT Offset (0, 50) A_CStaffAttack
        CSSF J 2 BRIGHT Offset (0, 50)
        CSSF J 2 Offset (0, 45)
        CSSF A 2 Offset (0, 40)
        CSSF A 2 Offset (0, 36)
        Goto Ready+4
		
	  FireFighter:
		TNT1 A 0 A_JumpIfInventory("Clip",3,"FireFighterGlow")
		H122 B 4
		H122 C 0 A_Playsound("FighterHammerMiss", CHAN_WEAPON)
		H122 C 4
		H122 D 4 A_FireBullets(-10,-5,-1,random(10,20),"FlailPuff",FBF_NORANDOM|FBF_EXPLICITANGLE|FBF_NORANDOMPUFFZ,128)
		H122 E 4 A_FireBullets(0,0,-1,random(10,20),"FlailPuff",FBF_NORANDOM|FBF_EXPLICITANGLE|FBF_NORANDOMPUFFZ,128)
		H122 F 4 A_FireBullets(10,5,-1,random(10,20),"FlailPuff",FBF_NORANDOM|FBF_EXPLICITANGLE|FBF_NORANDOMPUFFZ,128)
		H122 G 4
		goto Ready	
		
	  FireFighterGlow:
		TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"CoopFireFighterGlow")
		H122 J 4
		H122 K 4
		H122 L 0 A_Playsound("FighterHammerMiss", CHAN_WEAPON)
		H122 L 0 A_JumpIfCloser(128,"FireFighterGlowMelee")
		H122 L 4
		H122 M 0 A_JumpIfCloser(128,"FireFighterGlowMelee")
		H122 M 0 A_TakeInventory("Clip",3,TIF_NOTAKEINFINITE)
		H122 M 0 A_FireCustomMissile("FlailIceSpikeBall",0,0)
		H122 M 4 
		H122 N 4
		H122 O 4
		goto Ready
		
	  FireFighterGlowMelee:
		H122 L 0 A_TakeInventory("Clip",1,TIF_NOTAKEINFINITE)
		H122 L 4 A_FireBullets(-10,-5,-1,random(20,40),"FlailPuffGlow",FBF_NORANDOM|FBF_EXPLICITANGLE|FBF_NORANDOMPUFFZ,128)
		H122 M 4 A_FireBullets(0,0,-1,random(20,40),"FlailPuffGlow",FBF_NORANDOM|FBF_EXPLICITANGLE|FBF_NORANDOMPUFFZ,128)
		H122 N 4 A_FireBullets(10,5,-1,random(20,40),"FlailPuffGlow",FBF_NORANDOM|FBF_EXPLICITANGLE|FBF_NORANDOMPUFFZ,128)
		H122 O 4
		goto Ready
		
	  CoopFireFighterGlow:
		H122 J 4
		H122 K 4
		H122 L 0 A_Playsound("FighterHammerMiss", CHAN_WEAPON)
		H122 L 0 A_JumpIfCloser(128,"FireFighterGlowMelee")
		H122 L 4
		H122 M 0 A_JumpIfCloser(128,"FireFighterGlowMelee")
		H122 M 0 A_TakeInventory("Clip",3,TIF_NOTAKEINFINITE)
		H122 M 0 A_FireCustomMissile("FlailIceSpikeBall2",0,0)
		H122 M 4 
		H122 N 4
		H122 O 4
		goto Ready	  
	
	  FireMage:
		TNT1 A 0 A_JumpIfInventory("Clip", 3, 1)
		Goto SwitchWeapon
		TNT1 A 0 A_playsound("hexen/cult/fire")
		H110 A 1
		TNT1 A 0 A_JumpIfInventory("CoopModeOn", 1, "FireMageCoop")
		TNT1 AAAA 0 A_FireCustomMissile("CultShot1",frandom(-12,12),0,0,0,1,frandom(-4,4))
		H110 B 3 Bright A_FireCustomMissile("CultShot1",0,0,0,0)//,frandom(-12,12),1,0,0,1,frandom(-4,4))
		H110 B 0 Bright A_TakeInventory("Clip", 3, TIF_NOTAKEINFINITE)
		H110 CDE 3 Bright
		H110 FGHAA 3 Bright
		TNT1 A 0 A_Refire("FireMage")
		Goto Ready

      Blink:
        CSSF BBBCCCCCBBB 1 A_WeaponReady
        Goto ReadyCleric+3

      Drain:
        CSSF K 0 A_JumpIfInventory("DrainCounter", 1, "DrainSound0")
		CSSF K 0 A_JumpIfInTargetInventory("Hexen_TargetBlocking", 1, "DrainNothing")
        CSSF K 0 A_GiveInventory("DrainCounter", 3)
        CSSF K 0 A_PlaySound("ClericCStaffHitThing", 6)
        goto Drain2

      DrainSound0:
        CSSF K 0 A_TakeInventory("DrainCounter", 1)
        CSSF K 0 A_JumpIfInventory("DrainCounter", 2, 1)
        goto Drain2
        CSSF K 0 A_PlaySound("ClericCStaffHitThing", 5)
        goto Drain2

      Drain2:
        CSSF K 0 A_CustomPunch(random(20,35), 1, 1, "StaffDrainPuff", 98)
        CSSF A 0 A_JumpIf(ACS_NamedExecuteWithResult("SamsaraDecorate", 22), "InfiniteDrain")
        CSSF K 0 A_GiveInventory("SerpentHealth", random(2,4))
        CSSF K 0 A_GiveInventory("HealthBonus",1)
        goto Drain3
		
	  DrainNothing: //when you drain, but you get jack shit :(	
		CSSF K 0 A_CustomPunch(random(20,35), 1, 1, "StaffDrainPuff", 98)
	    CSSF K 0 A_GiveInventory("SerpentHealth", 0)
        CSSF K 0 A_GiveInventory("HealthBonus",0)
        goto Drain3

      InfiniteDrain:
        CSSF K 0 A_GiveInventory("InfiniteHealth", random(3,5))
        goto Drain3

      Drain3:
        CSSF K 8 BRIGHT Offset (0, 36)
        CSSF K 0 A_Refire
        Goto Ready+4

      CoopFire:
        TNT1 A 0 A_PlayWeaponSound("ClericCStaffFire")
        TNT1 A 0 A_FireCustomMissile("CStaffMissileCoop2",-3,0)
        CSSF J 1 BRIGHT Offset (0, 50) A_FireCustomMissile("CStaffMissileCoop",3,1)//A_CStaffAttack
        CSSF J 2 BRIGHT Offset (0, 50)
        CSSF J 2 Offset (0, 45)
        CSSF A 2 Offset (0, 40)
        CSSF A 2 Offset (0, 36)
        CSSF K 0 A_Refire
        Goto Ready+4
		
	  FireMageCoop:
		TNT1 AAAA 0 A_FireCustomMissile("CultShot1Coop",frandom(-12,12),0,0,0,1,frandom(-4,4))
		H110 B 4 Bright A_FireCustomMissile("CultShot1Coop")//,frandom(-12,12),1,0,0,1,frandom(-4,4))
		H110 CDE 3 Bright
		H110 FGHAA 3 Bright
		TNT1 A 0 A_Refire
		Goto Ready	
		
	  SwitchWeapon:
		TNT1 A 0 A_JumpIf(CallACS("Hexen_SwitchPriority")==1,1)
		TNT1 A 0
		Goto Ready
    }
}

actor DrainCounter: Counter {}

actor SerpentHealth: Health
{
    Inventory.Amount 1
}

actor CStaffMissile2 : CStaffMissile replaces CStaffMissile
{
    Decal DoomImpScorch
    Damage (random(5,25))//5
    Obituary "$SAMSARA_HEXEN_OB_SLOT4_2"
    +BLOODSPLATTER
}

actor CStaffMissileCoop : CStaffMissile2
{
    Species "Player"
    +THRUSPECIES
}

actor CStaffMissileCoop2 : CStaffMissile2
{
    Species "Player"
    +THRUSPECIES
    States
    {
      Spawn:
        /*CSSF D 1 Bright A_SetAngle(4 + angle)
        CSSF D 1 Bright //ThrustThing(angle*256/360+64, 1, 0, 0)
        CSSF E 1 Bright A_SetAngle(2 + angle)//ThrustThing(angle*256/360+192, 1, 0, 0)
        CSSF ED 1 Bright
        CSSF D 1 Bright A_SetAngle(2 - angle)
        CSSF E 1 Bright //ThrustThing(angle*256/360+192, 1, 0, 0)
        CSSF E 1 Bright A_SetAngle(4 - angle)//ThrustThing(angle*256/360+192, 2, 0, 0) //ThrustThing(angle*256/360+64, 1, 0, 0)
        CSSF DD 1 Bright*/
        TNT1 AAAAAAAAAAAA 0 A_CStaffMissileSlither

      Spawn2:
        CSSF EEDD 1 Bright A_CStaffMissileSlither
        Loop
    }
}

actor StaffDrainPuff: HammerPuff
{
    SeeSound ""
    AttackSound ""
    ActiveSound ""
}

ACTOR CultShot1
{
	Radius 6
	Height 16
	Speed 35
	Damage (Random(2,8)) // was 1,6
	DamageType "HexenPoison"
	PoisonDamage 2, 12
	PoisonDamageType "HexenPoison"
	Projectile
	Bouncecount 6
	Wallbouncefactor 2
	+RANDOMIZE
	+RIPPER
	+HEXENBOUNCE
	+CANBOUNCEWATER
	+FORCERADIUSDMG
	+ADDITIVEPOISONDAMAGE
	RenderStyle Add
	scale .75
	SeeSound ""
	DeathSound "hexen/cult/hit"
	Decal "Scorch"
	States
	{
		Spawn:
			H111 A 1 BRIGHT A_SpawnItemEX("CultTrail1") 
			TNT1 AAA 0 A_Cstaffmissileslither
			H111 A 1 BRIGHT A_SpawnItemEX("CultTrail1") 
			TNT1 AAA 0 A_Cstaffmissileslither
			H111 A 1 BRIGHT A_SpawnItemEX("CultTrail1") 
			TNT1 AAA 0 A_Cstaffmissileslither
			H111 B 1 BRIGHT A_SpawnItemEX("CultTrail1") 
			TNT1 AAA 0 A_Cstaffmissileslither
			H111 B 1 BRIGHT A_SpawnItemEX("CultTrail1")
			TNT1 AAA 0 A_Cstaffmissileslither
			H111 B 1 BRIGHT A_SpawnItemEX("CultTrail1") 
			TNT1 AAA 0 A_Cstaffmissileslither
			H111 C 1 BRIGHT A_SpawnItemEX("CultTrail1") 
			TNT1 AAA 0 A_Cstaffmissileslither
			H111 C 1 BRIGHT A_SpawnItemEX("CultTrail1") 
			TNT1 AAA 0 A_Cstaffmissileslither
			H111 C 1 BRIGHT A_SpawnItemEX("CultTrail1") 
			TNT1 AAA 0 A_Cstaffmissileslither
			H111 D 1 BRIGHT A_SpawnItemEX("CultTrail1")
			TNT1 AAA 0 A_Cstaffmissileslither
			H111 D 1 BRIGHT A_SpawnItemEX("CultTrail1") 
			TNT1 AAA 0 A_Cstaffmissileslither
			H111 D 1 BRIGHT A_SpawnItemEX("CultTrail1") 
			TNT1 AAA 0 A_Cstaffmissileslither
			Loop
		Death:
			H111 EEEEFFFGGH 2 BRIGHT
			Stop
	}
}


ACTOR CultTrail1
{
	+NOCLIP
	+NOINTERACTION
	+NOGRAVITY
	renderstyle ADD
	alpha 0.5
	scale .5
	States
	{
		Spawn:
			TNT1 A 0 A_Jump(256,"A")
		A:
			H112 AABBCCDDEE 1 BRIGHT //A_fadeout(0.05)
			Stop
	}
}

Actor CultShot1Coop : CultShot1 { +DONTHURTSPECIES +THRUSPECIES Species "Player" +DONTHARMCLASS }

Actor FlailPuff : AxePuff2
{
	Activesound ""
}

actor FlailPuffGlow : AxePuffGlow2
{
    DamageType "HexenIce"
	Renderstyle Normal
    +PUFFGETSOWNER
    States
    {
    Spawn:
         TNT1 A 0
         Stop
    Melee:
         TNT1 A 1
         TNT1 A 1 A_TakeFromTarget("Clip",1,TIF_NOTAKEINFINITE)
         SHEX ABCDE 4 Bright
         goto Spawn
    Crash:
         SHEX ABCDE 4 Bright
         stop
    }
}

Actor FlailIceSpikeBall
{
	Projectile
	Speed 16
	Height 8
	Radius 4
	Damage 20
	DamageType "HexenIce"
	DeathSound "MageShardsExplode"
	States
	{
		Spawn:
			H122 PQ 2 A_CustomMissile("FlailIceSpikeMissile",0,0,random(0,360),CMF_AIMDIRECTION|CMF_SAVEPITCH,0)
			Loop
		Death:
			TNT1 A 0 A_Explode(32,16)
			SHEX ABCDE 4 Bright
			stop
	}
}

actor FlailIceSpikeMissile : FrostMissileDM
{
    Damage (random(8,16))
}

Actor FlailIceSpikeBall2 : FlailIceSpikeBall
{
	+THRUSPECIES 
	Species "Player"
	+DONTHARMSPECIES 
	States
	{
		Spawn:
			H122 PQ 2 A_CustomMissile("FlailIceSpikeMissile2",0,0,random(0,360),CMF_AIMDIRECTION|CMF_SAVEPITCH,0)
			Loop
	}
}	

actor FlailIceSpikeMissile2 : FrostMissile2
{
    Damage (random(8,16))
}