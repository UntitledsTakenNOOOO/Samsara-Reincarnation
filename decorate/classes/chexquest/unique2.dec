actor ChexWarriorFiringCLZ : Boolean {}

actor "Chain-Lightning Zorcher" : Weapon
{
    Weapon.SelectionOrder 1600
    Weapon.SlotNumber 8
    Inventory.PickUpSound "chex/weaponget"
    Weapon.AmmoUse 2
    Weapon.AmmoGive 20
    Weapon.AmmoType "Cell"
    +BLOODLESSIMPACT
    +PUFFONACTORS
    //+AMMO_OPTIONAL
    +INVENTORY.UNDROPPABLE
    DamageType "Zorch"
    Inventory.PickupMessage "You got the Chain-Lightning Zorcher!"
	Inventory.RestrictedTo "ChexWarrior"
    Tag "Chain-Lightning Zorcher"
    States
    {
      Spawn:
        SHOC C -1
        stop

      Ready:
        PISC S 1 A_WeaponReady
        loop

      Deselect:
        PISC S 1 A_Lower
        loop

      Select:
        PISC S 1 A_Raise
        loop

      Fire:
        TNT1 A 0 A_GunFlash
        PISC U 4 A_GiveInventory("SamsaraChexChainLightningZorcherAttackHandler")
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("Cell",2,1)
        goto Fire2
        TNT1 A 0 A_GunFlash("Flash2")
        goto Fire2

      Fire2:
        PISC U 4 A_GiveInventory("SamsaraChexChainLightningZorcherAttackHandler")
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("Cell",2,1)
        goto Fire3
        TNT1 A 0 A_GunFlash("Flash3")
        goto Fire3

      Fire3:
        PISC U 4 A_GiveInventory("SamsaraChexChainLightningZorcherAttackHandler")
        PISC T 7
        PISC T 7 A_CheckReload
        PISC T 8 A_ReFire
        goto Ready

      Flash:
        CPIS G 2 Bright A_Light1
        CPIS G 2 Bright A_Light0
        goto LightDone

      Flash2:
        CPIS H 2 Bright A_Light2
        CPIS H 2 Bright A_Light0
        goto LightDone

      Flash3:
        CPIS I 2 Bright A_Light1
        CPIS I 2 Bright A_Light0
        goto LightDone
    }
}

actor SamsaraChexChainLightningZorcherAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Cell",2,1)
        stop
        TNT1 A 0 A_TakeInventory("Cell",2)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireCustomMissile("ChexPackChainLightning",-1,false,-8,1)
        stop

      PickupAttackCoop:
        TNT1 A 0 A_FireCustomMissile("ChexPackChainLightning2",-1,false,-8,1)
        stop
    }
}

actor ChexPackChainLightning
{
    Projectile
    Radius 3
    Height 3
    Speed 60
    Damage 5
    DamageType "Zorch"
    BounceType "Hexen"
    BounceCount 3
    SeeSound "chex/clshot"
    BounceSound "chex/clshot"
    DeathSound "chex/icehit"
    Obituary "$SAMSARA_CHEX_OB_UNIQUE2"
    -ALLOWBOUNCEONACTORS
    +BLOODLESSIMPACT
    -BLOODSPLATTER
    -BOUNCEONACTORS
    +RIPPER
    States
    {
      Spawn:
        CMIS J -1 Bright Light("CLLIGHT")
        stop

      Death:
        CMIS K 3 Bright Light("CLEXPLIGHT1")
        CMIS L 3 Bright Light("CLEXPLIGHT2")
        stop
    }
}

actor ChexPackChainLightning2 : ChexPackChainLightning
{
    Species "Player"
    +THRUSPECIES
}
