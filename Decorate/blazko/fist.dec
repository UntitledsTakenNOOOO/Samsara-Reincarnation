actor Knife: InstantSwitchWeapon
{
    Weapon.Kickback 0
    Weapon.SelectionOrder 2200
    Weapon.SlotNumber 0
    Inventory.PickUpSound "wolfen/knifeget"
    Inventory.PickupMessage "You got a Knife! Wait, shouldn't you start with this?"
    Obituary "$SAMSARA_WOLFEN_OB_FIST_1"
    +WEAPON.MELEEWEAPON
    +DONTBOB
    +NOALERT
    +INVENTORY.UNDROPPABLE
    Weapon.YAdjust 16
	Inventory.RestrictedTo "WolfenPlayer"
    States
    {
      Spawn:
        WKNP A -1
        stop

      Ready:
		TNT1 A 0 A_JumpIfInventory("WolfenClassMode", 2, 5)
		TNT1 A 0 A_JumpIfInventory("BJSuperKnife",1,"Ready2")
		TNT1 A 0 A_JumpIfInventory("WolfenClassMode",1,2)
        WKNF F 1 A_WeaponReady
        loop
        WKNF O 1 A_WeaponReady
        loop
        TK01 A 1 A_WeaponReady
        loop

      Ready2:
		TNT1 A 0 A_JumpIfInventory("WolfenLostMode",1,2)
        WKNF A 1 A_WeaponReady
        loop
		WKNF J 1 A_WeaponReady
        loop

      Fire:
		TNT1 A 0 A_JumpIfInventory("WolfenClassMode", 2, "FireCTK")
        TNT1 A 0 A_JumpIfInventory("WolfenClassMode",1,"FireLost")
		WKNF B 0 A_JumpIfInventory("BJUberKnife", 1, "Fire3")
        TNT1 A 0 A_JumpIfInventory("BJSuperKnife",1, "Fire2")
        WKNF GH 3
        WKNF I 3 A_CustomPunch(7,1,0,"BlankPuff2",64)
        WKNF I 0 A_PlayWeaponSound ("wolfen/knife2")
        WKNF H 3
        goto Ready
		
      FireCTK:
        TK01 BC 3
		TK01 C 0 A_FireCustomMissile("Totenkopf_KnifeProjectile")
		TK01 C 0 A_PlaySound("Totenkopf/atkknife", CHAN_WEAPON)
		TK01 DE 3 
        goto Ready			
		
	  FireLost:
        WKNF K 0 A_JumpIfInventory("BJUberKnife", 1, "Fire3Lost")
        TNT1 A 0 A_JumpIfInventory("BJSuperKnife",1, "Fire2Lost")
        WKNF PQ 3
        WKNF R 3 A_CustomPunch(7,1,0,"BlankPuff2",64)
        WKNF R 0 A_PlayWeaponSound ("wolfen/knife2")
        WKNF Q 3
        goto Ready

      Fire2:
		WKNF BC 3 A_CustomPunch(6,1,0,"BlankPuff2",64)
        WKNF C 0 A_JumpIfInventory("PopcornStab", 1, "Popcorn2")
        goto Fire2_2
		
	  Fire2Lost:
		WKNF KL 3 A_CustomPunch(6,1,0,"BlankPuff2",64)
        WKNF L 0 A_JumpIfInventory("PopcornStab", 1, "Popcorn2")
        goto Fire2_2Lost

      Popcorn2:
        WKNF D 0 A_FireBullets(0,0, 1,0, "KnifePopcorn", 0, 64)
        goto Fire2_2

      Fire2_2:
        WKNF D 3 A_CustomPunch(6,1,0,"BlankPuff2",64)
        WKNF D 0 A_PlayWeaponSound ("wolfen/knife")
        WKNF E 3
        goto Ready
		
	  Fire2_2Lost:
        WKNF M 3 A_CustomPunch(6,1,0,"BlankPuff2",64)
        WKNF M 0 A_PlayWeaponSound ("wolfen/knife")
        WKNF N 3
        goto Ready

      Fire3:
        WKNF BC 2 A_CustomPunch(7,1,0,"BlankPuff2",64)
        WKNF C 0 A_JumpIfInventory("PopcornStab", 1, "Popcorn2")
        goto Fire3_2
		
	  Fire3Lost:
        WKNF KL 2 A_CustomPunch(7,1,0,"BlankPuff2",64)
        WKNF L 0 A_JumpIfInventory("PopcornStab", 1, "Popcorn2")
        goto Fire3_2Lost

      Popcorn2:
        WKNF D 0 A_FireBullets(0,0, 1,0, "KnifePopcorn", 0, 64)
        goto Fire3_2

      Fire3_2:
		WKNF D 2 A_CustomPunch(7,1,0,"BlankPuff2",64)
        WKNF D 0 A_PlayWeaponSound ("wolfen/knife")
        WKNF E 2
        goto Ready
		
	  Fire3_2Lost:
		WKNF M 2 A_CustomPunch(7,1,0,"BlankPuff2",64)
        WKNF M 0 A_PlayWeaponSound ("wolfen/knife")
        WKNF N 2
        goto Ready
    }
}

actor KnifePopcorn: BulletPuff
{
    Radius 4
    Height 4
    VSpeed 0

    Obituary "$SAMSARA_WOLFEN_OB_FIST_2" 

    DamageType "Pistol"
    DeathSound "wolfen/spearexplode2"

    Scale 0.35
    Alpha 0.75
    RenderStyle Add

    +PUFFGETSOWNER
    +ALWAYSPUFF
    +PUFFONACTORS

    States
    {
      Spawn:
        TNT1 A 0
        TNT1 A 0 A_Scream
        SPB2 AB 0 BRIGHT A_SpawnItemEx("SpearShotTrail2", 0,0,0, random(-3,3),0,random(-3,3), random(0,360))
        SPX2 AA 2 A_Explode(12, 80, 0)
        SPX2 BCDE 4 BRIGHT
        Stop
    }
}

actor "Spear of Longinus" : InstantSwitchWeapon
{
    Weapon.Kickback 0
    Weapon.SelectionOrder 2200
    Inventory.PickUpSound "wolfen/knifeget"
    Inventory.PickupMessage "You got the Spear of...Destiny?"
    Obituary "$SAMSARA_WOLFEN_OB_PUNCHDRUNKB"
    +WEAPON.MELEEWEAPON
    +DONTBOB
    +NOALERT
    +INVENTORY.UNDROPPABLE
    +CHEATNOTWEAPON

    Weapon.YAdjust 16
    States
    {
      Spawn:
        UNIQ R -1
        stop

      Ready:
        WSPR F 1 A_WeaponReady
        loop

      Fire:
        TNT1 A 0 A_PlayWeaponSound("wolfen/spearstab")
        WSPR G 3 A_CustomPunch(20,1,0,"SpearPopcornPuff",98)
        WSPR HI 3 A_CustomPunch(20,1,0,"BlankPuff2",98)
        WSPR I 5
        WSPR I 0
        WSPR H 1
        goto Ready
    }
}

actor SpearPopcornPuff: BulletPuff
{
    Radius 4
    Height 4
    VSpeed 0

    Scale 0.35
    Alpha 0.75
    RenderStyle Add

    DeathSound "wolfen/spearexplode2"
    +PUFFONACTORS
    +CLIENTSIDEONLY

    States
    {
      Spawn:
      Melee:
      XDeath:
        TNT1 A 0
        TNT1 A 0 A_Scream
        SPB2 AB 0 BRIGHT A_SpawnItemEx("SpearShotTrail2", 0,0,0, random(-3,3),0,random(-3,3), random(0,360))
        SPX2 ABCDE 4 BRIGHT
        Stop
    }
}

Actor Totenkopf_KnifeProjectile : FastProjectile
{
	Projectile
	Speed 32
	Damage (random(0,256)>>3)
	Height 4
	Radius 2
	+NOEXTREMEDEATH
	+HITTRACER
	+FORCERADIUSDMG
	+NODAMAGETHRUST
	-BLOODSPLATTER
	+BLOODLESSIMPACT
	+NODAMAGETHRUST
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 A 1 
			Stop
		Crash:
		Death:
			TNT1 A 1
			Stop
		XDeath:
			TNT1 A 0 A_JumpIf(CallACS("Totenkopf_BloodGenerator_Offsets")==1,1)
			TNT1 A 0 A_Playsound("Totenkopf/hitenemy", CHAN_WEAPON)
			TNT1 A 1
			TNT1 A 0 A_GiveInventory("Totenkopf_BloodGenerator",1,AAPTR_TRACER)
			TNT1 A 1
			Stop
	}
}