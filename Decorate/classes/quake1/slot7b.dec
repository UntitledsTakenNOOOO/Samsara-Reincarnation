actor SamsaraQuakeThunderboltLightningFiringSoundCheck : Boolean {}
actor SamsaraQuakeThunderboltFrame : Counter { Inventory.MaxAmount 3 }

// Lightning mode

actor "Thunderbolt" : SamsaraQuakeWeapon
{
    Inventory.PickupMessage "You got the Thunderbolt."
    Weapon.SlotPriority 0
    Weapon.SelectionOrder 1
    Weapon.KickBack 100
    Weapon.AmmoType "Cell"
    Weapon.AmmoGive 60
    Weapon.AmmoUse 1
    Obituary "$SAMSARA_RANGER_OB_SLOT7_1"
    Tag "Thunderbolt (Lightning Mode)"
    +BFG
    +NOAUTOAIM
    States
    {
      Spawn:
        QLTG Z -1
        stop

      Ready:
        QLTG A 0 A_TakeInventory("SamsaraQuakeThunderboltLightningFiringSoundCheck")
        QLTG A 0 A_JumpIfInventory("Thunderbolt DOE",1,2)
        QLTG A 1 A_WeaponReady(WRF_NOSECONDARY)
        loop
        QLTG A 1 A_WeaponReady
        loop

      Deselect:
        TNT1 A 0 A_GiveInventory("SamsaraQuakeThunderboltLightningFiringSoundStopper")
        goto Super::Deselect

      Select:
        TNT1 A 0 A_TakeInventory("Ranger_AxeSelected")
        TNT1 A 0 A_TakeInventory("Ranger_GunSelected")
        TNT1 A 0 A_TakeInventory("Ranger_ExplosiveSelected")
        TNT1 A 0 A_TakeInventory("Ranger_NailSelected")
        TNT1 A 0 A_GiveInventory("Ranger_PlasmaSelected")
        goto Super::Select

      Fire:
        QLTG B 0 A_TakeInventory("SamsaraQuakeThunderboltFrame")
        QLTG B 0 A_PlaySound("quakeweps/lightningstart",CHAN_WEAPON)
      FireFrameCheck:
        QLTG B 0 A_JumpIfInventory("QuakeInvisTimer",1,2)
        QLTG B 0 A_AlertMonsters
        QLTG B 0 A_JumpIfInventory("SamsaraCurrentWaterLevel",2,"FireDischarge")
        QLTG B 0 A_GunFlash
        QLTG B 0 A_GiveInventory("SamsaraQuakeThunderboltLightningAttackHandler")
        QLTG B 0 A_JumpIfInventory("SamsaraQuakeThunderboltFrame",0,"FireFrame4")
        QLTG B 0 A_JumpIfInventory("SamsaraQuakeThunderboltFrame",2,"FireFrame3")
        QLTG B 0 A_JumpIfInventory("SamsaraQuakeThunderboltFrame",1,"FireFrame2")
      FireFrame1:
        QLTG B 0 A_GiveInventory("SamsaraQuakeThunderboltFrame")
        QLTG B 2 Bright A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        goto FireFinish

      FireFrame2:
        QLTG C 0 A_GiveInventory("SamsaraQuakeThunderboltFrame")
        QLTG C 2 Bright A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        goto FireFinish

      FireFrame3:
        QLTG D 0 A_GiveInventory("SamsaraQuakeThunderboltFrame")
        QLTG D 2 Bright A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        goto FireFinish

      FireFrame4:
        QLTG E 0 A_TakeInventory("SamsaraQuakeThunderboltFrame")
        QLTG E 2 Bright A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        goto FireFinish

      FireFinish:
        QLTG F 0 A_ReFire("FireFrameCheck")
        QLTG F 0 A_GiveInventory("SamsaraQuakeThunderboltLightningFiringSoundStopper")
        QLTG F 0 A_JumpIfInventory("QuakeModeOn",1,"FireFinishNoSecondary")
        QLTG F 2 A_WeaponReady
        QLTG GH 1 A_WeaponReady
        goto Ready

      FireFinishNoSecondary:
        QLTG F 2 A_WeaponReady(WRF_NOSECONDARY)
        QLTG GH 1 A_WeaponReady(WRF_NOSECONDARY)
        goto Ready

      FireDischarge:
        QLTG A 0 A_ClearReFire
        QLTG A 0 A_GiveInventory("SamsaraQuakeThunderboltLightningFiringSoundStopper")
        QLTG A 0 A_GiveInventory("SamsaraQuakeThunderboltLightningDischargeAttackHandler") // you're probably dead now
        QLTG A 55 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        QLTG A 0 A_ReFire
        goto Ready

      Flash:
        TNT1 A 2 A_Light2
        TNT1 A 1 A_Light1
        goto LightDone

      AltFire:
        QLTG A 8 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        TNT1 A 0 A_SelectWeapon("Thunderbolt DOE")
        goto Deselect
    }
}

actor SamsaraQuakeThunderboltLightningAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Cell",1,1)
        stop
        TNT1 A 0 A_TakeInventory("Cell",1)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("quakeweps/lightning",CHAN_6,1.0,true)
        TNT1 A 0 A_GiveInventory("SamsaraQuakeThunderboltLightningFiringSoundCheck")
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireCustomMissile("LGBeamProjectile",0,false)
        goto PickupFinish

      PickupAttackCoop:
        TNT1 A 0 A_FireCustomMissile("LGBeamProjectileCoop",0,false)
        goto PickupFinish

      // Finishing stuff
      PickupFinish:
        TNT1 A 0 ACS_NamedExecuteAlways("SamsaraRecoil", 0, -66, 0, 3)
        stop
    }
}

actor SamsaraQuakeThunderboltLightningDischargeAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 ACS_NamedExecuteWithResult("SamsaraDecorate", 5, 0, 0)
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Cell",1,1)
        stop
        TNT1 A 0 A_TakeInventory("Cell")
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("quakeweps/lightningdischarge",CHAN_WEAPON,1.0,false,ATTN_NONE)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireCustomMissile("LGDischarge",0,false)
        stop

      PickupAttackCoop:
        TNT1 A 0 A_FireCustomMissile("LGDischargeCoop",0,false)
        stop
    }
}

actor SamsaraQuakeThunderboltLightningFiringSoundStopper : Trigger
{
    States
    {
      // Sound stopping stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraQuakeThunderboltLightningFiringSoundCheck",1,1)
        stop
        TNT1 A 0 A_PlaySound("silence",CHAN_6)
        TNT1 A 0 A_StopSound(CHAN_6)
        TNT1 A 0 A_TakeInventory("SamsaraQuakeThunderboltLightningFiringSoundCheck")
        stop
    }
}

actor LGDischarge : MjolnirDischarge
{
    States
    {
      Death:
        TNT1 A 0 A_Explode(18*(ACS_NamedExecuteWithResult("SamsaraDecorate", 5, 1)),4096,0)
        TNT1 A 1 A_Explode(17*(ACS_NamedExecuteWithResult("SamsaraDecorate", 5, 1)),4096)
        stop
    }
}

actor LGDischargeCoop : LGDischarge
{
    Species "Player"
    +THRUSPECIES
}

ACTOR LGBeamProjectile : MjolnirProjectile
{
    Speed 256
    -RIPPER
    States
    {
        Spawn:
            TNT1 A 0
            TNT1 A 1 A_Die
            Goto Death
        }
}

ACTOR LGBeamProjectileCoop : LGBeamProjectile { +THRUSPECIES +MTHRUSPECIES Species "Player" }
ACTOR LGBeamProjectile2 : LGBeamProjectile
{
    Damage (30)
    States
    {
        Spawn:
            TNT1 A 0 NoDelay A_ChangeFlag(PAINLESS,Random(0,128))
            TNT1 A 0 A_ChangeFlag("CLIENTSIDEONLY", true)
            TNT1 A 0 A_Warp(AAPTR_DEFAULT,-velx,-vely,-velz,0,WARPF_ABSOLUTEOFFSET|WARPF_NOCHECKPOSITION)
            TNT1 A 0 A_ChangeFlag("CLIENTSIDEONLY", false)
            TNT1 A 0 A_SpawnItemEx("LGBeam",0,0,0,(velx*0.001), (vely*0.001), (velz*0.001), Angle, SXF_ABSOLUTEPOSITION|SXF_ABSOLUTEMOMENTUM|SXF_ABSOLUTEANGLE)
            TNT1 A 0 A_ChangeFlag("CLIENTSIDEONLY", true)
            TNT1 A 0 A_Warp(AAPTR_DEFAULT,velx,vely,velz,0,WARPF_ABSOLUTEOFFSET|WARPF_NOCHECKPOSITION)
        Moving:
            TNT1 A 0 A_JumpIfCloser(496,1)
            Stop
            TNT1 A 0 A_ChangeFlag("CLIENTSIDEONLY", false)
            TNT1 A 0 A_SpawnItemEx("LGBeam",0,0,0,(velx*0.001), (vely*0.001), (velz*0.001), Angle, SXF_ABSOLUTEPOSITION|SXF_ABSOLUTEMOMENTUM|SXF_ABSOLUTEANGLE)
            TNT1 A 0 A_ChangeFlag("CLIENTSIDEONLY", true)
            TNT1 A 0 A_Warp(AAPTR_DEFAULT,velx/5,vely/5,velz/5,0,WARPF_ABSOLUTEOFFSET,1) stop
            TNT1 A 0 A_Warp(AAPTR_DEFAULT,velx/5,vely/5,velz/5,0,WARPF_ABSOLUTEOFFSET,1) stop
            TNT1 A 0 A_Warp(AAPTR_DEFAULT,velx/5,vely/5,velz/5,0,WARPF_ABSOLUTEOFFSET,1) stop
            TNT1 A 0 A_Warp(AAPTR_DEFAULT,velx/5,vely/5,velz/5,0,WARPF_ABSOLUTEOFFSET,1) stop
            TNT1 A 0 A_Warp(AAPTR_DEFAULT,velx/5,vely/5,velz/5,0,WARPF_ABSOLUTEOFFSET,"Moving")
            stop
        Remove:
            TNT1 A 1
            stop
            }
}

actor LGBeam
{
    +BRIGHT
    +NOINTERACTION
    +NOBLOCKMAP
    +CLIENTSIDEONLY
    +NOTIMEFREEZE
    Height 1
    Radius 1
    States
    {
    Spawn:
        TNT1 A 0
        TNT1 A 0 A_Jump(256,"A","B","C","D","E","F")
    A:
        _BLT A 2
        stop
    B:
        _BLT B 2
        stop
    C:
        _BLT C 2
        stop
    D:
        _BLT D 2
        stop
    E:
        _BLT E 2
        stop
    F:
        _BLT F 2
        stop
    }
}

actor LGPuff: BulletPuff
{
    +ALWAYSPUFF
    +PUFFONACTORS
    +PUFFGETSOWNER
    +BLOODLESSIMPACT
    +BRIGHT
    //+FORCERADIUSDMG
    +NODAMAGETHRUST
    +NOTIMEFREEZE
    VSpeed 0
    Scale 0.2
    RenderStyle Add
    DamageType "QuakeElectric"
    //Obituary "%o \csaccepts\c- %k's \csshaft.\c-"
    States
    {
    Spawn:
        TNT1 A 0
        TNT1 A 0 A_Jump(128,"A","B","C","D","E","F")
    A:
        SPKW A 2
        stop
    B:
        SPKW B 2
        Stop
    C:
        SPKW C 2
        Stop
    D:
        SPKW D 2
        Stop
    E:
        SPKW E 2
        Stop
    F:
        SPKW F 2
        Stop
    }
}

// Plasma mode

actor "Thunderbolt DOE" : "Thunderbolt"
{
    Weapon.SlotPriority 1
    Weapon.AmmoType "PlasmaCell"
    Weapon.AmmoGive 0
    Tag "Thunderbolt (Plasma Mode)"
    +EXPLOSIVE
    States
    {
      Ready:
        PLS2 A 0 A_JumpIfInventory("Thunderbolt",1,2)
        PLS2 A 1 A_WeaponReady(WRF_NOSECONDARY)
        loop
        PLS2 A 1 A_WeaponReady
        loop

      Deselect:
        goto InstantSwitchWeapon::Deselect

      Fire:
        PLS2 B 0 A_JumpIfInventory("QuakeInvisTimer",1,2)
        PLS2 B 0 A_AlertMonsters
        PLS2 B 0 A_JumpIfInventory("SamsaraCurrentWaterLevel",2,"FireDischarge")
        PLS2 B 0 A_GiveInventory("SamsaraQuakeThunderboltPlasmaAttackHandler")
        PLS2 BCDE 3 Bright A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        PLS2 A 16 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        PLS2 A 0 A_ReFire
        PLS2 A 3 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        goto Ready

      FireDischarge:
        PLS2 A 0 A_ClearReFire
        PLS2 A 0 A_GiveInventory("SamsaraQuakeThunderboltPlasmaDischargeAttackHandler") // you're probably dead now
        PLS2 A 55 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        PLS2 A 0 A_ReFire
        goto Ready

      AltFire:
        PLS2 A 8 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        TNT1 A 0 A_SelectWeapon("Thunderbolt")
        goto Deselect
    }
}

actor SamsaraQuakeThunderboltPlasmaAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("PlasmaCell",1,1)
        stop
        TNT1 A 0 A_TakeInventory("PlasmaCell",1)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("plasmacell/fire",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireCustomMissile("PlasmaCellBall",0,false) //A_SpawnItemEx("PlasmaCellBall",cos(pitch)*32,0,40-(sin(pitch)*32),cos(pitch)*24,0,-sin(pitch)*24,0,SXF_SETMASTER|SXF_NOCHECKPOSITION,0,tid)
        goto PickupFinish

      PickupAttackCoop:
        TNT1 A 0 A_FireCustomMissile("PlasmaCellBall_Coop",0,false) //A_SpawnItemEx("PlasmaCellBall_Coop",cos(pitch)*32,0,40-(sin(pitch)*32),cos(pitch)*24,0,-sin(pitch)*24,0,SXF_SETMASTER|SXF_NOCHECKPOSITION,0,tid)
        goto PickupFinish

      // Finishing stuff
      PickupFinish:
        TNT1 A 0 ACS_NamedExecuteAlways("SamsaraRecoil", 0, 5, 4, 15)
        stop
    }
}

actor SamsaraQuakeThunderboltPlasmaDischargeAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 ACS_NamedExecuteWithResult("SamsaraDecorate", 5, 0, 1)
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("PlasmaCell",1,1)
        stop
        TNT1 A 0 A_TakeInventory("PlasmaCell")
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("quakeweps/lightningdischarge",CHAN_WEAPON,1.0,false,ATTN_NONE)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireCustomMissile("LGPlasmaDischarge",0,false)
        stop

      PickupAttackCoop:
        TNT1 A 0 A_FireCustomMissile("LGPlasmaDischargeCoop",0,false)
        stop
    }
}

Actor PlasmaCellBall
{
    Radius 3
    Height 5
    Speed 24
    Damage (150)
    Projectile
    +FORCERADIUSDMG
    +BRIGHT
    DeathSound "plasmacell/hit"
    Obituary "%o was obliterated by %k."
    //SelfObituary "%o becomes bored with life." // shared with rocket launcher in DOE
    DamageType "QuakeElectric"
    States
    {
    Spawn:
        BAL7 A 0 NoDelay A_PlaySound("PlasmaCell/Fly",CHAN_ITEM,1.0,true,ATTN_NORM)
    SpawnLoop:
        BAL7 A 2
        Loop
    Death:
        BAL7 A 0 A_StopSound(CHAN_ITEM)
        TNT1 A 0 A_CustomMissile("PlasmaCellBallZapper",0,0,0,CMF_AIMDIRECTION)
        TNT1 A 0 A_CustomMissile("PlasmaCellBallZapper",0,0,30,CMF_AIMDIRECTION)
        TNT1 A 0 A_CustomMissile("PlasmaCellBallZapper",0,0,60,CMF_AIMDIRECTION)
        TNT1 A 0 A_CustomMissile("PlasmaCellBallZapper",0,0,90,CMF_AIMDIRECTION)
        TNT1 A 0 A_CustomMissile("PlasmaCellBallZapper",0,0,120,CMF_AIMDIRECTION)
        TNT1 A 0 A_CustomMissile("PlasmaCellBallZapper",0,0,150,CMF_AIMDIRECTION)
        TNT1 A 0 A_CustomMissile("PlasmaCellBallZapper",0,0,180,CMF_AIMDIRECTION)
        TNT1 A 0 A_CustomMissile("PlasmaCellBallZapper",0,0,210,CMF_AIMDIRECTION)
        TNT1 A 0 A_CustomMissile("PlasmaCellBallZapper",0,0,240,CMF_AIMDIRECTION)
        TNT1 A 0 A_CustomMissile("PlasmaCellBallZapper",0,0,270,CMF_AIMDIRECTION)
        TNT1 A 0 A_CustomMissile("PlasmaCellBallZapper",0,0,300,CMF_AIMDIRECTION)
        TNT1 A 0 A_CustomMissile("PlasmaCellBallZapper",0,0,330,CMF_AIMDIRECTION)
        BAL7 AAAAAAAAAAAAAAAAA 0 A_SpawnItemEx("PlasmaDebris",Random(-3,3)+1,Random(-3,3)+1,Random(-3,3)+1,Random(-3,3)+1,Random(-3,3)+1,Random(-3,3)+1)
        BAL7 AAAAAAAAAAAAAAAAA 0 A_SpawnItemEx("PlasmaDebris",Random(-3,3)+1,Random(-3,3)+1,Random(-3,3)+1,Random(-3,3)+1,Random(-3,3)+1,Random(-3,3)+1)
        BAL7 AAAAAAAAAAAAAAAAA 0 A_SpawnItemEx("PlasmaDebris",Random(-3,3)+1,Random(-3,3)+1,Random(-3,3)+1,Random(-3,3)+1,Random(-3,3)+1,Random(-3,3)+1)
        BAL7 AAAAAAAAAAAAAAAAA 0 A_SpawnItemEx("PlasmaDebris",Random(-3,3)+1,Random(-3,3)+1,Random(-3,3)+1,Random(-3,3)+1,Random(-3,3)+1,Random(-3,3)+1)
        BAL7 AAAAAAAAAAAAAAAAA 0 A_SpawnItemEx("PlasmaDebris",Random(-3,3)+1,Random(-3,3)+1,Random(-3,3)+1,Random(-3,3)+1,Random(-3,3)+1,Random(-3,3)+1)
        BAL7 AAAAAAAAAAAAAAAAA 0 A_SpawnItemEx("PlasmaDebris",Random(-3,3)+1,Random(-3,3)+1,Random(-3,3)+1,Random(-3,3)+1,Random(-3,3)+1,Random(-3,3)+1)
        BAL7 AAAAAAAAAAAAAAAAA 0 A_SpawnItemEx("PlasmaDebris",Random(-3,3)+1,Random(-3,3)+1,Random(-3,3)+1,Random(-3,3)+1,Random(-3,3)+1,Random(-3,3)+1)
        BAL7 AAAAAAAAAAAAAAAAA 0 A_SpawnItemEx("PlasmaDebris",Random(-3,3)+1,Random(-3,3)+1,Random(-3,3)+1,Random(-3,3)+1,Random(-3,3)+1,Random(-3,3)+1)
        TNT1 A 0 A_Explode(250,150,0)
        BAL7 A 5 A_Explode(100,150)
        Stop
    }
}

Actor PlasmaCellBall_Coop : PlasmaCellBall
{
    +THRUSPECIES
    +DONTHARMCLASS
    +DONTHARMSPECIES
    +MTHRUSPECIES
    Species "Player"
}

Actor PlasmaCellBallZapper
{
    Height 4
    Radius 2
    Projectile
    +ISMONSTER
    +FRIENDLY
    +QUICKTORETALIATE
    +NOTARGET
    +NOCLIP
    -COUNTKILL
    States
    {
    Spawn:
        TNT1 A 0
        TNT1 A 0 A_RearrangePointers(AAPTR_DEFAULT,AAPTR_Target)
        TNT1 A 0 A_ClearTarget
        TNT1 A 0 A_CopyFriendliness
        TNT1 AAAA 0 A_LookEx(0,0,0,0,30)
        Stop
    See:
        TNT1 A 0
        TNT1 A 0 A_JumpIfCloser(256,"Missile")
        Stop
    Missile:
        TNT1 A 0 A_CustomMissile("LGBeamProjectile2",0)
        goto Death
    Death:
        TNT1 A 1
        Stop
    }
}

Actor PlasmaCellBallZapper_Coop : PlasmaCellBallZapper
{
    Species "Player"
    +THRUSPECIES
    +DONTHARMCLASS
    +DONTHARMSPECIES
    +MTHRUSPECIES
}

ACTOR PlasmaDebris
{
    Radius 1
    Height 1
    +NOBLOCKMAP
    +DROPOFF
    +CANNOTPUSH
    +SKYEXPLODE
    +NOBLOCKMONST
    +CLIENTSIDEONLY
    +THRUACTORS
    +NOGRAVITY
    +NOINTERACTION
    +FORCEXYBILLBOARD
    States
    {
    Spawn:
        QPRT FGHIJ 5 BRIGHT
        Stop
    Death:
        TNT1 A 5
        Stop
    }
}

actor LGPlasmaDischarge : MjolnirDischarge
{
    States
    {
      Death:
        TNT1 A 0 A_Explode(18*(ACS_NamedExecuteWithResult("SamsaraDecorate", 5, 1)),4096,0) // currently unsure of how damage is calculated, but this should suffice for now
        TNT1 A 1 A_Explode(17*(ACS_NamedExecuteWithResult("SamsaraDecorate", 5, 1)),4096)
        stop
    }
}

actor LGPlasmaDischargeCoop : LGPlasmaDischarge
{
    Species "Player"
    +THRUSPECIES
}
