actor FlechetteCooldown : inventory { -INVBAR +INVENTORY.UNDROPPABLE inventory.maxamount 15 inventory.interhubamount 0 }

actor PortFlechette : CustomInventory
{
    tag "Poison Cloud Flechette"
    +FLOATBOB
    Inventory.Amount 1
    Inventory.MaxAmount 1
    Inventory.PickupFlash "PickupFlash"
    +INVBAR
    +INVENTORY.UNDROPPABLE
    Inventory.Icon "ARTIPSBG"
    Inventory.PickupSound "hexen/weaponget"
    Inventory.UseSound "hexen/artiuse"
    Inventory.PickupMessage "You picked up the Flechette!"
    Inventory.RestrictedTo "HexenPlayer"
    States
    {
      Spawn:
        PSBG A 1 Bright
        Loop

      Use:
        TNT1 A 0 A_JumpIfInventory("HexenClassMode", 2, "UseFighter")
        TNT1 A 0 A_JumpIfInventory("HexenClassMode", 1, "UseMage")
        TNT1 A 0 A_JumpIfInventory("FlechetteCooldown",1,"UseFail")
        TNT1 A 0 A_SpawnItemEx("PoisonBagCoop",0,0,0,0,0,0,0,28,0)
        TNT1 A 1 ACS_NamedExecuteAlways("FlechetteCooldown")
        TNT1 A 0 A_JumpIfInventory("SamsaraHasDisabledInventoryCooldown", 1, 2)
        TNT1 A 0 A_GiveInventory("FlechetteCooldown",15)
        TNT1 A 0
        Fail
        
      UseMage:
        TNT1 A 0 A_JumpIfInventory("FlechetteCooldown",1,"UseFailMage")
        TNT1 A 0 A_SpawnItemEx("FireBombCoop",0,0,0,0,0,0,0,28,0)
        TNT1 A 1 ACS_NamedExecuteAlways("FlechetteCooldown")
        TNT1 A 0 A_JumpIfInventory("SamsaraHasDisabledInventoryCooldown", 1, 2)
        TNT1 A 0 A_GiveInventory("FlechetteCooldown",12)
        TNT1 A 0
        Fail

      UseFighter:
        TNT1 A 0 A_JumpIfInventory("FlechetteCooldown",1,"UseFailFighter")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"UseFighterCoop")
        TNT1 A 0 A_FireCustomMissile("ThrowingBomb2")
      UseFighterFinish:
        TNT1 A 1 ACS_NamedExecuteAlways("FlechetteCooldown")
        TNT1 A 0 A_JumpIfInventory("SamsaraHasDisabledInventoryCooldown", 1, 2)
        TNT1 A 0 A_GiveInventory("FlechetteCooldown",9)
        TNT1 A 0
        Fail

      UseFighterCoop:
        TNT1 A 0 A_FireCustomMissile("ThrowingBomb2Coop")
        Goto UseFighterFinish

      UseFail:
        TNT1 A 1 A_Print("$FLECCOOLDOWN")
        Fail

      UseFailMage:
        TNT1 A 1 A_Print("$FLECCOOLDOWNMAGE")
        Fail

      UseFailFighter:
        TNT1 A 1 A_Print("$FLECCOOLDOWNFIGHTER")
        Fail
    }
}

actor PoisonBagCoop
{
    Speed 1
    Radius 20
    Height 30
    Mass 0x7fffffff
    DeathSound "PoisonShroomDeath"
    DamageType "PoisonCloud"
    +NOBLOCKMAP
    +NOGRAVITY
    +DROPOFF
    +NODAMAGETHRUST
    +DONTSPLASH
    +FOILINVUL
    +PIERCEARMOR
    +FORCERADIUSDMG
    +BLOODLESSIMPACT
    Obituary "$SAMSARA_HEXEN_OB_SLOT1"
    States
    {
      Spawn:
        PSBG A 20 Bright // Previously 18
        PSBG B 4 Bright
        PSBG C 4 BRIGHT
        TNT1 A 0 A_SetTranslucent(0.7,1)
        PSBG D 1 BRIGHT A_Scream
        PSBG D 1 BRIGHT A_Explode(10,128,0)

      Poison:
        PSBG EEEFFFGGGHHHIII 2 BRIGHT A_Explode(6,128,0)
        PSBG EEEFFFGGGHHHIII 2 BRIGHT A_Explode(6,128,0)
        PSBG EEEFFFGGGHHHIII 2 BRIGHT A_Explode(6,128,0)
        PSBG EEEFFFGGGHHHIII 2 BRIGHT A_Explode(6,128,0)
        PSBG EEEFFFGGGHHHIII 2 BRIGHT A_Explode(6,128,0)
        PSBG EEEFFFGGGHHHIII 2 BRIGHT A_Explode(6,128,0)
        PSBG EEEFFFGGGHHHIII 2 BRIGHT A_Explode(6,128,0)

      Death:
        PSBG HG 7 BRIGHT
        PSBG FD 6 BRIGHT
        Stop
    }
}

actor FireBombCoop : PoisonBagCoop
{
    DeathSound "FighterHammerExplode"
    States
    {
      Spawn:
        PSBG A 20 Bright // Previously 18
        PSBG B 4 Bright
        PSBG C 4 BRIGHT
        TNT1 A 0 A_SetTranslucent(0.7,1)
        PSBG D 2 BRIGHT A_Scream

      Explode:
        XPL1 A 0 Bright A_Explode(128, 128)
        TNT1 A 1 A_SpawnItemEx("HexenFireBombExplosion", 0,0,24,0,0,0,0, SXF_NOCHECKPOSITION)
//        XPL1 BCDEF 4 Bright
        Stop
    }
}

actor HexenFireBombExplosion 
{
    +CLIENTSIDEONLY
    +NOINTERACTION
    Renderstyle Add
    Alpha 0.50
    States
    {
        Spawn:
            XPL1 ABCDEF 4 Bright
            Stop
    }
}

ACTOR ThrowingBomb2 : ThrowingBomb
{
    DeathSound "ClericFlameCircle"
    States
    {
    Spawn:
        H106 A 4 A_CheckThrowBomb
        H106 BCDE 3 A_CheckThrowBomb
        H106 F 3 A_CheckThrowBomb2
        Loop
        H106 G 6 A_CheckThrowBomb
        H106 F 4 A_CheckThrowBomb
        H106 H 6 A_CheckThrowBomb
        H106 F 4 A_CheckThrowBomb
        H106 G 6 A_CheckThrowBomb
        H106 F 3 A_CheckThrowBomb
        Wait
    // Needed for GZDoom compatibility
    Tail:
        H106 G 6 A_CheckThrowBomb
        H106 F 4 A_CheckThrowBomb
        H106 H 6 A_CheckThrowBomb
        H106 F 4 A_CheckThrowBomb
        H106 G 6 A_CheckThrowBomb
        H106 F 3 A_CheckThrowBomb
        Wait
    Death:
        CFCF Q 4 Bright A_NoGravity
        CFCF R 3 Bright A_Scream
        CFCF S 4 Bright A_Explode
        CFCF T 3 Bright
        CFCF U 4 Bright
        CFCF W 3 Bright
        CFCF X 4 Bright
        CFCF Z 3 Bright
        Stop
    }
}

actor ThrowingBomb2Coop : ThrowingBomb2
{
    Species "Player"
    +THRUSPECIES
}
