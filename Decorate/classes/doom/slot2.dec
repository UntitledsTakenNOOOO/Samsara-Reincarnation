actor " Shotgun " : Shotgun
{
	Weapon.AmmoGive 8
	Weapon.SlotNumber 2
	+ALT_USES_BOTH
	+UNDROPPABLE
	AttackSound ""
	Tag "Shotgun"
	Obituary "$SAMSARA_DOOMGUY_OB_SLOT2"
	Inventory.RestrictedTo "DoomguyPlayer"
	States
	{
	Spawn:
		WSHT A -1
		stop

	// Original
	Ready:
		TNT1 A 0 A_JumpIfInventory("DoomClassMode",2,"ReadyStr")
		TNT1 A 0 A_JumpIfInventory("DoomClassMode",1,"Ready64")
		DSHT A 1 A_WeaponReady(WRF_NOSECONDARY)
		loop

	Deselect:
		TNT1 A 0 A_JumpIfInventory("DoomClassMode",2,"DeselectStr")
		TNT1 A 0 A_JumpIfInventory("DoomClassMode",1,"Deselect64")
		DSHT A 1 A_Lower
		loop

	Select:
		TNT1 A 0 A_JumpIfInventory("DoomClassMode",2,"SelectStr")
		TNT1 A 0 A_JumpIfInventory("DoomClassMode",1,"Select64")
		DSHT A 1 A_Raise
		loop

	Fire:
		TNT1 A 0 A_JumpIfInventory("DoomClassMode",2,"FireStr")
		DSHT A 3 A_JumpIfInventory("DoomClassMode",1,"Fire64")
		TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireAttack")
		TNT1 A 0 A_JumpIfInventory("Shell",1,"FireAttack")
		goto FireContinue

	FireAttack:
		TNT1 A 0 A_JumpIfInventory("SamsaraUsingSmoothAnims",1,"FireSmooth")
		TNT1 A 0 A_GunFlash
	FireContinue:
		TNT1 A 0 A_GiveInventory("SamsaraDoomguyShotgunAttackHandler")
		TNT1 A 0 A_JumpIfInventory("SamsaraUsingSmoothAnims",1,"FireSmooth")
	FireVanilla:
		DSHT A 7 Bright
		DSHT IJ 5
		DSHT K 4
		DSHT JI 5
		DSHT A 3
		DSHT A 7 A_ReFire
		goto Ready

	FireSmooth:
		TNT1 A 0 A_GiveInventory("SamsaraDoomguyShotgunAttackHandler")
		TNT1 A 0 A_JumpIfInventory("SamsaraHasDoubleFiringSpeed",1,"FireSmoothRage")
		XTS1 B 3 offset(0,40) Bright
		XTS1 C 2 offset(0,39) Bright
		XTS1 D 1 offset(0,37) Bright
		DSHT A 1 offset(0,35)
		XTS1 E 2 offset(0,32)
		XTS1 FGH 2
		XTS1 I 4
		XTS1 JK 3
		XTS1 LMN 2
		XTS1 OPQ 1
		DSHT A 7 A_ReFire
		goto Ready

	FireSmoothRage:
		XTS1 B 3 offset(0,40) Bright
		XTS1 C 2 offset(0,39) Bright
		XTS1 D 1 offset(0,37) Bright
		DSHT A 1 offset(0,35)
		XTS1 E 1 offset(0,32)
		XTS1 FGH 2
		XTS1 I 4
		XTS1 J 2
		XTS1 K 3
		XTS1 LMN 2
		XTS1 OPQ 1
		DSHT A 7 A_ReFire
		goto Ready

	Flash:
		DSHF A 3 Bright A_Light1
		DSHF B 4 Bright A_Light2
		goto LightDone

	// Doom 64 (has recoil visual)
	Ready64:
		TNT1 A 0 A_JumpIfInventory("SamsaraUsingSmoothAnims",1,"Ready64Smooth")
	Ready64Vanilla:
		64SG A 1 A_WeaponReady(WRF_NOSECONDARY)
		goto Ready

	Ready64Smooth:
		64SG M 1 A_WeaponReady(WRF_NOSECONDARY)
		goto Ready

	Deselect64:
		TNT1 A 0 A_JumpIfInventory("SamsaraUsingSmoothAnims",1,"Deselect64Smooth")
	Deselect64Vanilla:
		64SG A 1 A_Lower
		goto Deselect

	Deselect64Smooth:
		64SG M 1 A_Lower
		goto Deselect

	Select64:
		TNT1 A 0 A_JumpIfInventory("SamsaraUsingSmoothAnims",1,"Select64Smooth")
	Select64Vanilla:
		64SG A 1 A_Raise
		goto Select

	Select64Smooth:
		64SG M 1 A_Raise
		goto Select

	Fire64:
		TNT1 A 0 A_GunFlash("Flash64")
		TNT1 A 0 A_GiveInventory("SamsaraDoomguy64ShotgunAttackHandler")
		TNT1 A 0 A_JumpIfInventory("SamsaraUsingSmoothAnims",1,"Fire64Smooth")
	Fire64Vanilla:
		64SG A 5
		64SG C 21
		64SG B 5
		64SG A 7
		64SG A 7 A_ReFire
		goto Ready

	Fire64Smooth:
		//A 5
		64SG O 2
		64SG P 2
		64SG M 1
		//C 21
		64SG M 2
		64SG EFG 1
		64SG H 2
		64SG IJ 3
		64SG KJ 4
		//B 5
		64SG I 3
		64SG H 3
		//A 7
		64SG G 2
		64SG F 2
		64SG E 2
		//A 7
		64SG M 7 A_ReFire
		goto Ready

	Flash64:
		TNT1 A 0 A_JumpIfInventory("SamsaraUsingSmoothAnims",1,"Flash64Smooth")
		64SG N 2 Bright A_Light1
		64SG N 2 Bright A_Light2
		goto LightDone
	Flash64Smooth:
		64SF A 2 Bright A_Light1
		64SF B 2 Bright A_Light2
		64SF C 1 Bright A_Light0
		goto LightDone

	// Stronghold
	// Unpowered
	ReadyStr:
		TNT1 A 0 A_JumpIfInventory("PowerSamsaraDoomguyStrWeaponPower",1,"ReadyStrPowered")
		TNT1 A 0 A_JumpIfInventory("SamsaraHasPermaWeaponLevel2",1,"ReadyStrPowered")
		DSHT L 1 A_WeaponReady(WRF_NOSECONDARY)
		goto Ready

	DeselectStr:
		TNT1 A 0 A_JumpIfInventory("PowerSamsaraDoomguyStrWeaponPower",1,"DeselectStrPowered")
		TNT1 A 0 A_JumpIfInventory("SamsaraHasPermaWeaponLevel2",1,"DeselectStrPowered")
	DeselectStrContinue:
		TNT1 A 0 A_Lower
		DSHT L 1 A_Lower
		goto Deselect

	SelectStr:
		TNT1 A 0 A_JumpIfInventory("PowerSamsaraDoomguyStrWeaponPower",1,"SelectStrPowered")
		TNT1 A 0 A_JumpIfInventory("SamsaraHasPermaWeaponLevel2",1,"SelectStrPowered")
	SelectStrContinue:
		TNT1 A 0 A_Raise
		DSHT L 1 A_Raise
		goto Select

	FireStr:
		TNT1 A 0 A_JumpIfInventory("PowerSamsaraDoomguyStrWeaponPower",1,"FireStrPowered")
		DSHT L 3 A_JumpIfInventory("SamsaraHasPermaWeaponLevel2",1,"FireStrPowered")
		TNT1 A 0 A_JumpIfInventory("SamsaraImprovedDoomguyBalance",1,"FireStrImproved")
	FireStrVanilla:
		TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireStrAttackVanilla")
		TNT1 A 0 A_JumpIfInventory("Shell",1,"FireStrAttackVanilla")
		goto FireStrContinueVanilla

	FireStrAttackVanilla:
		TNT1 A 0 A_GunFlash("FlashStrVanilla")
	FireStrContinueVanilla:
		DSHT P 2 Bright A_GiveInventory("SamsaraDoomguyStrShotgunAttackHandler")
		DSHT Q 3 Bright
		DSHT R 2 Bright
		DSHT MN 5
		DSHT O 4
		DSHT NM 5
		DSHT L 3
		DSHT L 7 A_ReFire
		goto Ready

	FlashStrVanilla:
		TNT1 A 4 Bright A_Light1
		TNT1 A 3 Bright A_Light2
		goto LightDone

	FireStrImproved:
		TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireStrAttackImproved")
		TNT1 A 0 A_JumpIfInventory("Shell",1,"FireStrAttackImproved")
		goto FireStrContinueImproved

	FireStrAttackImproved:
		TNT1 A 0 A_GunFlash("FlashStrImproved")
	FireStrContinueImproved:
		DSHT P 2 Bright A_GiveInventory("SamsaraDoomguyStrShotgunAttackHandler")
		DSHT QR 2 Bright
		DSHT MNONM 4
		DSHT L 3
		DSHT L 5 A_ReFire
		goto Ready

	FlashStrImproved:
		TNT1 A 3 Bright A_Light1
		TNT1 A 3 Bright A_Light2
		goto LightDone

	// Powered
	ReadyStrPowered:
		DSHT L 1 A_WeaponReady
		goto Ready

	DeselectStrPowered:
		TNT1 A 0 A_Lower
		goto DeselectStrContinue

	SelectStrPowered:
		TNT1 A 0 A_Raise
		goto SelectStrContinue

	FireStrPowered:
		DSHT L 2
		TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireStrPoweredAttack")
		TNT1 A 0 A_JumpIfInventory("Shell",1,"FireStrPoweredAttack")
		goto FireStrPoweredContinue

	FireStrPoweredAttack:
		TNT1 A 0 A_GunFlash("FlashStrPowered")
	FireStrPoweredContinue:
		DSHT P 2 Bright A_GiveInventory("SamsaraDoomguyStrShotgunPoweredPrimaryAttackHandler")
		DSHT QR 2 Bright
		DSHT MNONM 2
		DSHT L 1 A_ReFire
		goto Ready

	FlashStrPowered:
		TNT1 A 3 Bright A_Light1
		TNT1 A 3 Bright A_Light2
		goto LightDone

	AltFire:
		DSHT L 3
		TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"AltFireAttack")
		TNT1 A 0 A_JumpIfInventory("Shell",1,"AltFireAttack")
		goto AltFireContinue

	AltFireAttack:
		TNT1 A 0 A_GunFlash
	AltFireContinue:
		TNT1 A 0 A_GiveInventory("SamsaraDoomguyStrShotgunPoweredSecondaryAttackHandler")
		DSHT PQR 3 Bright
		DSHT MNONM 3
		TNT1 A 0 A_JumpIfInventory("PowerSamsaraDoomguyStrWeaponPower",1,"AltFireFinishReFire")
		TNT1 A 0 A_JumpIfInventory("SamsaraHasPermaWeaponLevel2",1,"AltFireFinishReFire")
		TNT1 A 0 A_CheckReload
	AltFireFinish:
		DSHT L 1
		goto Ready

	AltFireFinishReFire:
		TNT1 A 0 A_JumpIfInventory("DoomClassMode",2,2)
		TNT1 A 0 A_CheckReload
		goto AltFireFinish
		TNT1 A 0 A_ReFire
		goto AltFireFinish

	AltFlash:
		TNT1 A 4 Bright A_Light1
		TNT1 A 5 Bright A_Light2
		goto LightDone
	}
}

actor SamsaraDoomguyShotgunAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupSound")
        TNT1 A 0 A_JumpIfInventory("Shell",1,1)
        stop
        TNT1 A 0 A_TakeInventory("Shell",1)
        goto PickupSound

      // Sound stuff
      PickupSound:
        TNT1 A 0 A_JumpIfInventory("SamsaraAlternateSoundToken",2,"PickupSoundPerkristan")
        TNT1 A 0 A_JumpIfInventory("SamsaraAlternateSoundToken",1,"PickupSoundPSX")
        TNT1 A 0 A_PlaySound("doomguy/shotgf",CHAN_WEAPON)
        goto PickupAttack

      PickupSoundPSX:
        TNT1 A 0 A_PlaySound("doom64guy/shotgf",CHAN_WEAPON)
        goto PickupAttack

      PickupSoundPerkristan:
        TNT1 A 0 A_PlaySound("doomguy/perkristan/shotgf",CHAN_WEAPON)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_JumpIfInventory("SamsaraImprovedDoomguyBalance",1,"PickupAttackImproved")
      // Vanilla
      PickupAttackVanilla:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackVanillaCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackVanillaCoop")
      PickupAttackVanillaDM:
        TNT1 A 0 A_FireBullets(5.6,0,7,5,"DoomBulletPuff2ThruGhost",FBF_NOFLASH)
        stop

      PickupAttackVanillaCoop:
        TNT1 A 0 A_FireBullets(5.6,0,7,5,"DoomBulletPuff2ThruGhost_Coop",FBF_NOFLASH)
        stop

      // Improved
      PickupAttackImproved:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackImprovedCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackImprovedCoop")
      PickupAttackImprovedDM:
        TNT1 A 0 A_FireBullets(5.6,0,7,7,"DoomBulletPuff2ThruGhost",FBF_NOFLASH)
        stop

      PickupAttackImprovedCoop:
        TNT1 A 0 A_FireBullets(5.6,0,7,7,"DoomBulletPuff2ThruGhost_Coop",FBF_NOFLASH)
        stop
    }
}

actor SamsaraDoomguy64ShotgunAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Shell",1,1)
        stop
        TNT1 A 0 A_TakeInventory("Shell",1)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("doom64guy/shotgf",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SamsaraImprovedDoomguyBalance",1,"PickupAttackImproved")
      // Vanilla
      PickupAttackVanilla:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackVanillaCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackVanillaCoop")
      PickupAttackVanillaDM:
        TNT1 A 0 A_FireBullets(5.6,0,7,5,"Doom64BulletPuff2ThruGhost",FBF_NOFLASH)
        goto PickupFinish

      PickupAttackVanillaCoop:
        TNT1 A 0 A_FireBullets(5.6,0,7,5,"Doom64BulletPuff2ThruGhost_Coop",FBF_NOFLASH)
        goto PickupFinish

      // Improved
      PickupAttackImproved:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackImprovedCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackImprovedCoop")
      PickupAttackImprovedDM:
        TNT1 A 0 A_FireBullets(5.6,0,7,7,"Doom64BulletPuff2ThruGhost",FBF_NOFLASH)
        goto PickupFinish

      PickupAttackImprovedCoop:
        TNT1 A 0 A_FireBullets(5.6,0,7,7,"Doom64BulletPuff2ThruGhost_Coop",FBF_NOFLASH)
        goto PickupFinish

      // Finishing stuff
      PickupFinish:
        TNT1 A 0 ACS_NamedExecuteAlways("SamsaraRecoil", 0, 2, 0, 20)
        stop
    }
}

// Unpowered attack handler

actor SamsaraDoomguyStrShotgunAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupSound")
        TNT1 A 0 A_JumpIfInventory("Shell",1,1)
        stop
        TNT1 A 0 A_TakeInventory("Shell",1)
        goto PickupSound

      // Sound stuff
      PickupSound:
        TNT1 A 0 A_JumpIfInventory("SamsaraAlternateSoundToken",1,"PickupSoundDE")
        TNT1 A 0 A_PlaySound("doomguy/stronghold/shotgf",CHAN_WEAPON)
        goto PickupAttack

      PickupSoundDE:
        TNT1 A 0 A_PlaySound("doomguy/demoneclipse/shotgf",CHAN_WEAPON)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_JumpIfInventory("SamsaraImprovedDoomguyBalance",1,"PickupAttackImproved")
      // Vanilla
      PickupAttackVanilla:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackVanillaCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackVanillaCoop")
      PickupAttackVanillaDM:
        TNT1 A 0 A_FireBullets(4,3,7,5,"SamsaraDoomguyStrBulletPuffBallisticThruGhost",FBF_NOFLASH)
        stop

      PickupAttackVanillaCoop:
        TNT1 A 0 A_FireBullets(4,3,7,5,"SamsaraDoomguyStrBulletPuffBallisticThruGhostCoop",FBF_NOFLASH)
        stop

      // Improved
      PickupAttackImproved:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackImprovedCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackImprovedCoop")
      PickupAttackImprovedDM:
        TNT1 A 0 A_FireBullets(4,3,8,5,"SamsaraDoomguyStrBulletPuffBallisticThruGhost",FBF_NOFLASH)
        stop

      PickupAttackImprovedCoop:
        TNT1 A 0 A_FireBullets(4,3,8,5,"SamsaraDoomguyStrBulletPuffBallisticThruGhostCoop",FBF_NOFLASH)
        stop
    }
}

// Powered attack handlers

actor SamsaraDoomguyStrShotgunPoweredPrimaryAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Shell",1,1)
        stop
        TNT1 A 0 A_TakeInventory("Shell",1)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_JumpIfInventory("SamsaraAlternateSoundToken",1,"PickupAttackDE")
      PickupAttackStr:
        TNT1 A 0 A_PlaySound("doomguy/stronghold/shotgf",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackStrCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackStrCoop")
      PickupAttackStrDM:
        TNT1 A 0 A_FireBullets(3,2.5,10,12,"SamsaraDoomguyStrElectricalPuff",FBF_NOFLASH)
        stop

      PickupAttackStrCoop:
        TNT1 A 0 A_FireBullets(3,2.5,10,12,"SamsaraDoomguyStrElectricalPuffCoop",FBF_NOFLASH)
        stop

      PickupAttackDE:
        TNT1 A 0 A_PlaySound("doomguy/demoneclipse/shotgf",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackDECoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackDECoop")
      PickupAttackDEDM:
        TNT1 A 0 A_FireBullets(3,2.5,10,12,"SamsaraDoomguyStrElectricalPuffSoundDE",FBF_NOFLASH)
        stop

      PickupAttackDECoop:
        TNT1 A 0 A_FireBullets(3,2.5,10,12,"SamsaraDoomguyStrElectricalPuffCoopSoundDE",FBF_NOFLASH)
        stop
    }
}

actor SamsaraDoomguyStrShotgunPoweredSecondaryAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupSound")
        TNT1 A 0 A_JumpIfInventory("Shell",1,1)
        stop
        TNT1 A 0 A_TakeInventory("Shell",1)
        goto PickupSound

      // Sound stuff
      PickupSound:
        TNT1 A 0 A_JumpIfInventory("SamsaraAlternateSoundToken",1,"PickupSoundDE")
        TNT1 A 0 A_PlaySound("doomguy/stronghold/shotgf",CHAN_WEAPON)
        goto PickupAttack

      PickupSoundDE:
        TNT1 A 0 A_PlaySound("doomguy/demoneclipse/shotgf",CHAN_WEAPON)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireBullets(12,10,8,9,"SamsaraDoomguyStrRepPuffNoDamage",FBF_NOFLASH)
        stop

      PickupAttackCoop:
        TNT1 A 0 A_FireBullets(12,10,8,9,"SamsaraDoomguyStrRepPuffNoDamageCoop",FBF_NOFLASH)
        stop
    }
}
